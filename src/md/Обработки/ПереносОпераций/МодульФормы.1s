Перем ВыбрПланСчетов;
//_____________________________________________________________________________
Процедура УдалитьДокументы(ДатаС,ДатаПо,Вар,ТолькоОперации=0,СтрСодержания="")
	НепосрУдал=0;
	Док=СоздатьОбъект("Документ");
	Обр=0;
	Док.ВыбратьДокументы(ДатаС,ДатаПо);
	НачатьТранзакцию();
	Пока Док.ПолучитьДокумент()=1 Цикл
	    Если ПустаяСтрока(СтрСодержания)=0 Тогда
		    Если Док.СуществуетОперация()=0 Тогда
		    	Продолжить;
	    	КонецЕсли;
	    	Если СокрП(СтрСодержания)<>СокрП(Док.Операция.Содержание) Тогда
		    	Продолжить;
	    	КонецЕсли;
		Иначе    
	        Продолжить;
		КонецЕсли;    
	    Если Вар=1 Тогда
		    Сообщить("Пометка на удаление: "+Док,"i");
			Док.Удалить(НепосрУдал);	        
		ИначеЕсли Вар=0 Тогда	
		    Сообщить("Снята пометка на удаление: "+Док,"i");
			Док.СнятьПометкуУдаления();	        
	    КонецЕсли;
	    Обр=Обр+1;
	КонецЦикла;                                      
	ЗафиксироватьТранзакцию();
	Сообщить("Обработано "+Обр+" документов(операций)");
КонецПроцедуры

Процедура ВыбратьЖурнал()
	Журналы=СоздатьОбъект("СписокЗначений");
	ПолучитьЗначенияОтбора("НомерЖурнала",Журналы);
	Журналы.ВыбратьЗначение(ВыбЖурнал,"Выбор журнала",,,?(Журналы.РазмерСписка()<20,1,0));
КонецПроцедуры      

Процедура ВыбрФайл(ИмяФайла)
	Перем Каталог;
	Если ФС.ВыбратьФайл(0, ИмяФайла, Каталог, "Выберите файл", "Текстовые (*.txt) |*.txt|Все файлы (*.*) |*.*")=1 Тогда
		ИмяФайла=Каталог+ИмяФайла;
	КонецЕсли;
КонецПроцедуры      

Процедура ОткрФайл(ИмяФайла)
	Текст=СоздатьОбъект("Текст");
	Текст.КодоваяСтраница(Кодировка.ПолучитьЗначение(Кодировка.ТекущаяСтрока()));
	Если ФС.СуществуетФайл(ИмяФайла) = 1 Тогда
		Текст.Открыть(ИмяФайла);
		Текст.Показать("Файл",ИмяФайла);
	Иначе
		Предупреждение("Указанный файл не существует");
	КонецЕсли;
КонецПроцедуры

Процедура ВСтроку(Стр,Пар)         
	Если ТипЗначенияСтр(Пар)="Число" Тогда
		Стр=Стр+?(Стр="","",",")+Пар;
    Иначе
		Стр=Стр+?(Стр="","""",",""")+Пар+"""";
	КонецЕсли;
КонецПРоцедуры
	           
Процедура СчетВСтроку(Стр,Сч)         
	Если Найти(Сч.Код,".")>0 Тогда
		Стр=Стр+?(Стр="","""",",""")+СтрЗаменить(Сч.Код,".",""",""")+"""";
	Иначе	
		Стр=Стр+?(Стр="","""",",""")+Сч.Код+""",""""";
	КонецЕсли;
КонецПРоцедуры

Процедура ВыгрузитьПроводки()                   
	Если ПустаяСтрока(ФайлВыгрузки)=1 Тогда
	    Предупреждение("Не указан файл");
	    Возврат;
	КонецЕсли;
	Периодичность=Период.ПолучитьЗначение(Период.ТекущаяСтрока());              
	Текст=СоздатьОбъект("Текст");
	Текст.КодоваяСтраница(Кодировка.ПолучитьЗначение(Кодировка.ТекущаяСтрока()));
	
	Стр="";
	ВСтроку(Стр,ДатаВ1);
	ВСтроку(Стр,ДатаВ2);
	ВСтроку(Стр,ВыбЖурнал);  
	Текст.ДобавитьСтроку(Стр);  
	
	Если Периодичность="Проводка" Тогда
		Опер=СоздатьОбъект("Операция");
		Опер.ВыбратьОперацииСПроводками(ДатаВ1,ДатаВ2,,,,,ВыбранныйПланСчетов());
		Пока Опер.ПолучитьПроводку()=1 Цикл
		    Если Опер.ВключитьПроводки()=0 Тогда
		    	Продолжить;
		    КонецЕсли;
	    	Состояние("Выгрузка за "+Опер.ДатаОперации);
			Если ПустаяСтрока(ВыбЖурнал)=0 Тогда
				Если ВыбЖурнал<>Опер.НомерЖурнала Тогда
			    	Продолжить;
				КонецЕсли;
			КонецЕсли;
			Стр="";
			ВСтроку(Стр,Опер.НомерЖурнала);  //НомерЖурнала
			ВСтроку(Стр,Опер.ДатаОперации);
			СчетВСтроку(Стр,Опер.Дебет.Счет);
			СчетВСтроку(Стр,Опер.Кредит.Счет);  
			ВСтроку(Стр,Опер.Сумма);  
			ВСтроку(Стр,СокрП(Опер.СодержаниеПроводки));  
			Стр=Стр+",,,";
			Если (Опер.Дебет.Счет.Валютный = 1) или (Опер.Кредит.Счет.Валютный = 1) Тогда 
				ВСтроку(Стр,Опер.Валюта.Код);  
				ВСтроку(Стр,Опер.ВалСумма);  
			Иначе	
				Стр=Стр+",,";
			КонецЕсли;
			Стр=Стр+",";
			Текст.ДобавитьСтроку(Стр);
		КонецЦикла;
	Иначе
		Запрос = СоздатьОбъект("Запрос");
		ТекстЗапроса ="
		|Период с ДатаВ1 по ДатаВ2;
		|ДСчет = Операция.Дебет.Счет;
		|КСчет = Операция.Кредит.Счет;
		|Вал = Операция.Валюта;
		|ОперСум = Операция.Сумма;
		|ОперВалСум = Операция.ВалСумма;
		|ПланСчетов = Операция.ПланСчетов;
		|НомерЖурнала = Операция.НомерЖурнала;
		|";
		ТекстЗапроса=ТекстЗапроса+" 
		|Группировка "+Периодичность+";";
		ТекстЗапроса=ТекстЗапроса+" 
		|Группировка ДСчет;
		|Группировка КСчет;
		|Группировка Вал;
		|Функция СумД = ДО(ОперСум);
		|Функция ВалСумД = ДО(ОперВалСум);
		|Условие(ПланСчетов=ВыбрПланСчетов);";
		Если ПустаяСтрока(ВыбЖурнал)=0 Тогда
			ТекстЗапроса=ТекстЗапроса+" 
			|Условие(НомерЖурнала=ВыбЖурнал);"
		КонецЕсли;
        ВыбрПланСчетов = ВыбранныйПланСчетов();
		// Если ошибка в запросе, то выход из процедуры
		Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
			Возврат;
		КонецЕсли;
	
		Пока Запрос.Группировка(Периодичность) = 1 Цикл
	    	Состояние("Выгрузка за "+Запрос.НачалоПериода());
			Пока Запрос.Группировка("ДСчет") = 1 Цикл
				Пока Запрос.Группировка("КСчет") = 1 Цикл
					Пока Запрос.Группировка("Вал") = 1 Цикл
						Стр="";
						ВСтроку(Стр,ВыбЖурнал);  //НомерЖурнала
						ВСтроку(Стр,Запрос.НачалоПериода());
						СчетВСтроку(Стр,Запрос.ДСчет);
						СчетВСтроку(Стр,Запрос.КСчет);  
						ВСтроку(Стр,Запрос.СумД);  
						ВСтроку(Стр,"Сводная проводка");  
						Стр=Стр+",,,";
						Если Запрос.Вал.Выбран()=1 Тогда
							ВСтроку(Стр,Запрос.Вал.Код);  
						Иначе	
							ВСтроку(Стр,"");  
						КонецЕсли;
						ВСтроку(Стр,Формат(Запрос.ВалСумД,"Ч0"));
						Стр=Стр+",";
						Текст.ДобавитьСтроку(Стр);
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	Текст.Записать(ФайлВыгрузки);
	Сообщить("Выгружено "+(Текст.КоличествоСтрок()-1)+" проводок");
КонецПроцедуры                                 

Функция ЗаполнитьСчет(КодСч1,КодСч2,Новый)
	Если ((СтрДлина(СокрЛП(КодСч1)) < 4) И (СтрДлина(СокрЛП(КодСч1)) > 0) И (СтрДлина(СокрЛП(КодСч2)) < 4)) Тогда
	    КодСч=СокрЛП(КодСч1)+"."+СокрЛП(КодСч2);
		Сч=СчетПоКоду(КодСч);
		Если (Сч.Выбран()=1) И(Сч.ЭтоГруппа()=1) Тогда
		    КодСч=СокрЛП(КодСч1)+".0";
			Сч=СчетПоКоду(КодСч);
		КонецЕсли;
		Если Сч.Выбран()=0 Тогда
			Счет=СоздатьОбъект("Счет");
			Счет.Новый();
			Счет.Код=КодСч;
			Счет.Наименование="Создан переносом операций";
			Счет.Записать();
			Новый = 1;
			Сч=СчетПоКоду(КодСч);
		КонецЕсли;
		Возврат Сч;
	Иначе
		Возврат ПолучитьПустоеЗначение("Счет");
	КонецЕсли;
КонецФункции

Процедура СделатьЗабалансовым(Б)
	Сч = СоздатьОбъект("Счет");
	Сч.НайтиСчет(Б);
	Если Сч.Уровень() > 1 Тогда
		А = Сч.ТекущийСчет();
		Сч.НайтиСчет(А.Родитель());	    
	КонецЕсли;
	Сч.Забалансовый = 1;
	Сч.Записать();
КонецПроцедуры

Процедура ЗагрузитьПроводки()              
	Если ПустаяСтрока(ФайлЗагрузки)=1 Тогда
	    Предупреждение("Не указан файл");
	    Возврат;
	КонецЕсли;
	ПреупрСубк=0;
	СтрСодержания="Перенос операций";
	Вал=СоздатьОбъект("Справочник.Валюты");
	Опер=СоздатьОбъект("Операция");
	Спис=СоздатьОбъект("СписокЗначений");
	Текст=СоздатьОбъект("Текст");
	Текст.КодоваяСтраница(Кодировка.ПолучитьЗначение(Кодировка.ТекущаяСтрока()));
	Текст.Открыть(ФайлЗагрузки);
	Обр=0;             
	Ошибка = 0;
	НачатьТранзакцию();
	Для С=1 По Текст.КоличествоСтрок() Цикл
		Стр=Текст.ПолучитьСтроку(С);
	    Спис.ИзСтрокиСРазделителями(Стр);

	    Если (С=1)И(Спис.РазмерСписка()=3) Тогда
	    	ДатаУ1=Спис.ПолучитьЗначение(1);
	    	ДатаУ2=Спис.ПолучитьЗначение(2);
	    	ЖурналЗагр=Спис.ПолучитьЗначение(3);
	    	Если Вопрос("Загрузка "+ДатаУ1+"-"+ДатаУ2+" Журнал:"
	    				+ЖурналЗагр+РазделительСтрок+
	    				"Продолжить ?","Да+Нет")="Нет" Тогда
	    	    Возврат;
	    	КонецЕсли;
	    	Если ПустаяСтрока(ЖурналЗагр)=0 Тогда
	    	    СтрСодержания=СтрСодержания+":"+ЖурналЗагр;
	    	КонецЕсли;
	    	Если УдалятьОперации=1 Тогда
	    		УдалитьДокументы(ДатаУ1,ДатаУ2,1,1,СтрСодержания);
	    	КонецЕсли;
	        Продолжить;
	    КонецЕсли;
	    
	    Если  Спис.РазмерСписка()<2 Тогда
	        Продолжить;
	    КонецЕсли;
	    
	    ТЖурнал=Спис.ПолучитьЗначение(1);
	    ТДата=Дата(Спис.ПолучитьЗначение(2));
	    Если ТЖурнал="Subconto" Тогда
	    	Если ПреупрСубк=0 Тогда
		    	Сообщить("Данные по субконто не загружаются!","!!!");
				ПреупрСубк=1;	    	
	    	КонецЕсли;
	        Продолжить;
	    КонецЕсли;         
	    
	    Если  Спис.РазмерСписка()<12 Тогда
	        Продолжить;
	    КонецЕсли;
	    
	    Сообщить("Загрузка "+Стр,"I");
	    
		Если ТДата=Дата(0) Тогда
	        Продолжить;
		КонецЕсли;	    
	    Если ТДата<>Опер.ДатаОперации Тогда
	        Если Опер.ДатаОперации<>Дата(0) Тогда
	        	Если Опер.КоличествоПроводок()>0 Тогда
		            Опер.Записать();
	        	КонецЕсли;
	        КонецЕсли;
	        Опер.Новая();
	        Опер.ДатаОперации=ТДата;
			Опер.Документ.УстановитьНовыйНомер("");
	        Опер.Содержание=СтрСодержания;;
	    КонецЕсли;    
	    Опер.НоваяПроводка(); Опер.РазделительУчета = Константа.ОсновноеЮрЛицо;
	    Опер.НомерЖурнала=ТЖурнал;
	    
		Новый = 0;
	    Опер.Дебет.Счет=ЗаполнитьСчет(Спис.ПолучитьЗначение(3),Спис.ПолучитьЗначение(4),Новый);
	    Опер.Кредит.Счет=ЗаполнитьСчет(Спис.ПолучитьЗначение(5),Спис.ПолучитьЗначение(6),Новый);
		
		Если ((Опер.Дебет.Счет.Выбран()=0) И (Опер.Кредит.Счет.Выбран()=0)) Тогда
			Сообщить("Проводка не загружена. Не выбраны оба счета.","!");
			Опер.УдалитьПроводку();
			Ошибка = Ошибка + 1;
			Продолжить;
		ИначеЕсли Опер.Дебет.Счет.Выбран()=0 Тогда
			Если Новый = 1 Тогда
				СделатьЗабалансовым(Опер.Кредит.Счет);
			Иначе
				Если Опер.Кредит.Счет.Забалансовый = 0 Тогда
				    Сообщить("Проводка не загружена. Балансовый счет не может корреспондировать с пустым счетом.","!");
					Опер.УдалитьПроводку();
					Ошибка = Ошибка + 1;
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Опер.Кредит.Счет.Выбран()=0 Тогда
			Если Новый = 1 Тогда
				СделатьЗабалансовым(Опер.Дебет.Счет);
			Иначе
				Если Опер.Дебет.Счет.Забалансовый = 0 Тогда
				    Сообщить("Проводка не загружена. Балансовый счет не может корреспондировать с пустым счетом.","!");
					Опер.УдалитьПроводку();
					Ошибка = Ошибка + 1;
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли (Опер.Дебет.Счет.Забалансовый = 0) И (Опер.Кредит.Счет.Забалансовый = 1) Тогда
			Если Новый = 1 Тогда
			    СделатьЗабалансовым(Опер.Дебет.Счет);
			Иначе
				Сообщить("Проводка не загружена. Балансовый счет не может корреспондировать с забалансовым счетом.","!");
				Опер.УдалитьПроводку();
				Ошибка = Ошибка + 1;
				Продолжить;	
			КонецЕсли;
		ИначеЕсли (Опер.Кредит.Счет.Забалансовый = 0) И (Опер.Дебет.Счет.Забалансовый = 1) Тогда
			Если Новый = 1 Тогда
			    СделатьЗабалансовым(Опер.Кредит.Счет);
			Иначе
				Сообщить("Проводка не загружена. Балансовый счет не может корреспондировать с забалансовым счетом.","!");
				Опер.УдалитьПроводку();
				Ошибка = Ошибка + 1;
				Продолжить;	
			КонецЕсли;
		КонецЕсли;	    
		
	    Опер.Сумма=Спис.ПолучитьЗначение(7);
	    Опер.СодержаниеПроводки=Спис.ПолучитьЗначение(8);
	    КодВал=Спис.ПолучитьЗначение(12);
	    Если ПустаяСтрока(КодВал)=0 Тогда
		    Если Вал.НайтиПоКоду(КодВал)=1 Тогда
			    Опер.Валюта=Вал.ТекущийЭлемент();
		    КонецЕсли;
	    КонецЕсли;
	    Опер.ВалСумма=Спис.ПолучитьЗначение(13);
		Обр=Обр+1;
	КонецЦикла;
    Если Опер.ДатаОперации<>Дата(0) Тогда
        Опер.Записать();
    КонецЕсли;
	ЗафиксироватьТранзакцию();
	Сообщить("Загружено "+Обр+" проводок");
	Если Ошибка > 0 Тогда
	    Предупреждение("При загрузке проводок обнаружены недопустимые корреспонденции!
		               |Не загружено "+Строка(Ошибка)+" проводок.");
	КонецЕсли;
КонецПроцедуры                                 

Процедура ПриОткрытии(Флаг)         
    ДатаВ1 = НачалоПериодаБИ();
    ДатаВ2 = КонецПериодаБИ();
	Если Флаг = 0 Тогда
	    Период.УдалитьВсе();
		Период.ДобавитьЗначение("Проводка");
		Период.ДобавитьЗначение("День");
		Период.ДобавитьЗначение("Неделя");
		Период.ДобавитьЗначение("Месяц");
		Период.ДобавитьЗначение("Квартал");
		Период.ДобавитьЗначение("Год");
		Период.ТекущаяСтрока(1);
	
		Кодировка.УдалитьВсе();
		Кодировка.ДобавитьЗначение(0,"Windows");
		Кодировка.ДобавитьЗначение(1,"DOS");
		Кодировка.ТекущаяСтрока(1);
		
		ФайлВыгрузки = КаталогПользователя() + "1sbtrans.txt";
		ФайлЗагрузки = КаталогПользователя() + "1sbtrans.txt";
	КонецЕсли;
    
КонецПроцедуры