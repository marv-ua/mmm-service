Процедура ОК()
	СохранитьЗначение("ОСВРазворотСубконто", тРазворотСубконто);
	Форма.Закрыть();
КонецПроцедуры

//----------------------------------------------------------------------
Процедура Изменить(Колонка)
	Перем Счет, Вариант;

	Если тРазворотСубконто.КоличествоСтрок() = 0 Тогда
		Возврат;
	КонецЕсли;

	Если Колонка = "Счет" Тогда
		Счет = тРазворотСубконто.Счет;
		Если ВвестиЗначение (Счет, "Введите счет", "Счет",,) = 0 Тогда
			Если Счет = "<Счет>" Тогда
				тРазворотСубконто.УдалитьСтроку(тРазворотСубконто.ТекущаяСтрока());
			КонецЕсли;
			Возврат;
		КонецЕсли;
		тРазворотСубконто.Счет = Счет;

		// Заполним список субконто для счета...
		тРазворотСубконто.Список = СоздатьОбъект("СписокЗначений");
		Для Индекс=1 По Счет.КоличествоСубконто() Цикл
			тРазворотСубконто.Список.ДобавитьЗначение(Счет.ВидСубконто(Индекс));
			тРазворотСубконто.Список.Пометка(Индекс, 1);
		КонецЦикла;
		                  
		Если Счет.КоличествоСубконто() > 0 Тогда
			тРазворотСубконто.Субконто = "Х";
		Иначе
			тРазворотСубконто.Субконто = "-";
		КонецЕсли;

		Если Счет.ЭтоГруппа() = 0 Тогда
			тРазворотСубконто.Субсчета = "-";
		Иначе
			тРазворотСубконто.Субсчета = " ";
		КонецЕсли;
		
		тРазворотСубконто.Сортировать("Счет");
		НомСтр = 0;
		Если тРазворотСубконто.НайтиЗначение(Счет, НомСтр, "Счет") = 1 Тогда
		    тРазворотСубконто.ТекущаяСтрока(НомСтр);
		КонецЕсли;

	ИначеЕсли (Колонка = "Субсчета") И (тРазворотСубконто.Субсчета <> "-") Тогда
		Если тРазворотСубконто.Субсчета = "Х" Тогда
			тРазворотСубконто.Субсчета = " ";
		Иначе
			тРазворотСубконто.Субсчета = "Х";
		КонецЕсли;
	ИначеЕсли (Колонка = "Субконто") И (тРазворотСубконто.Субконто <> "-") Тогда
		// Выберем субконто...
		тРазворотСубконто.Список.ОтметитьЗначения(,"Разворачивать счета по:");
		тРазворотСубконто.Субконто = " ";
		Для Индекс=1 По тРазворотСубконто.Список.РазмерСписка() Цикл
			Если тРазворотСубконто.Список.Пометка(Индекс) = 1 Тогда
				тРазворотСубконто.Субконто = "Х";
			    Прервать;
			КонецЕсли;
	    КонецЦикла;
	КонецЕсли;
КонецПроцедуры


//----------------------------------------------------------------------
Процедура Добавить()
	тРазворотСубконто.НоваяСтрока();
	тРазворотСубконто.Счет = "<Счет>";
	тРазворотСубконто.ТекущаяКолонка(1);
	тРазворотСубконто.ТекущаяСтрока(тРазворотСубконто.НомерСтроки);

	Изменить("Счет");
КонецПроцедуры

//----------------------------------------------------------------------
Процедура Удалить()
	Если тРазворотСубконто.КоличествоСтрок() > 0 Тогда
		тРазворотСубконто.УдалитьСтроку(тРазворотСубконто.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

//----------------------------------------------------------------------
Процедура Сохранить()   
	ИмяФайла = "SbcRazv.Dat";     
	ИмяНачКаталога = КаталогИБ();
	Если ФС.ВыбратьФайл(1, ИмяФайла, ИмяНачКаталога, "Выберите файл для сохранения настройки", "Файлы настройки (*.Dat) |*.Dat|Все файлы (*.*) |*.*", "Dat") = 1 Тогда
		ЗначениеВФайл(ИмяНачКаталога+ИмяФайла, тРазворотСубконто);
	КонецЕсли;
КонецПроцедуры

//----------------------------------------------------------------------
Процедура Загрузить()
	ИмяФайла = "";     
	ИмяНачКаталога = КаталогИБ();
	Если ФС.ВыбратьФайл(1, ИмяФайла, ИмяНачКаталога, "Выберите файл для загрузки настройки", "Файлы настройки (*.Dat) |*.Dat|Все файлы (*.*) |*.*", "Dat") = 1 Тогда
		ЗначениеИзФайла(ИмяНачКаталога+ИмяФайла, тРазворотСубконто);
	КонецЕсли;
КонецПроцедуры

//----------------------------------------------------------------------
Процедура ПриОткрытии()
	тРазворотСубконто.Очистить();
	тРазворотСубконто.НоваяКолонка("Счет",,,, "Счет", 16);
	тРазворотСубконто.НоваяКолонка("Субсчета",,,, "Субсчета", 9);
	тРазворотСубконто.НоваяКолонка("Субконто",,,, "Субконто", 9);
	тРазворотСубконто.НоваяКолонка("Список");
	ВосстановитьЗначение("ОСВРазворотСубконто", тРазворотСубконто);
	тРазворотСубконто.ВидимостьКолонки("Список", 0);
КонецПроцедуры