Перем ТекстПериодПлатежа;
Перем НадписьДатаПлатежа;
Перем ВидДатыОснованияПлатежа;
Перем ВидНомераДокументаПлатежа;
Перем ВидДатыДокументаПлатежа;
Перем НадписьДатаДокумента;                                                                                                               
Перем НадписьНомерДокумента;
Перем СписокПараметров;
Перем Конт;
Перем П101;
Перем П104;
Перем П105;
Перем П106;
Перем П107;
Перем П108;
Перем П109;
Перем П110;

//******************************************************************************
// Функция Сформировать() - формирует стоковые значения полей, для последующей 
// передачи в документ "Платежное поручение"
//
Функция Сформировать()
	
	П101 = СтатусСоставителя.ПолучитьЗначение(СтатусСоставителя.ТекущаяСтрока());
	П104 = КодБК;
	П105 = КодОКАТО;
	
	Если П101 = "15" Тогда
		П106 = "0";
		П107 = "0";
		П108 = "0";
		П109 = "0";
		П110 = ТипПлатежа.ПолучитьЗначение(ТипПлатежа.ТекущаяСтрока());	
		Если П110 <> "НС" Тогда
		    П110 = "0";
		КонецЕсли;
	
	Иначе
		П106 = ОснованиеПлатежа.ПолучитьЗначение(ОснованиеПлатежа.ТекущаяСтрока()); 
		Если ПустоеЗначение(П106) = 1 Тогда
			П106 = "0";
			П107 = "0";
			П108 = "0";
			П109 = "0";
			П110 = "0";
			
		ИначеЕсли Найти("ТП,БФ", П106) > 0 Тогда
			Если ПериодичностьУплаты.ПолучитьЗначение(ПериодичностьУплаты.ТекущаяСтрока())="-" Тогда
			    П107 = Строка(Формат(ДатаПоказателя,"Д ДДММГГГГ"));
			
			Иначе
				П107 = ПериодичностьУплаты.ПолучитьЗначение(ПериодичностьУплаты.ТекущаяСтрока())+"."+Формат(ПериодПлатежа,"Ч(0)2")+"."+Формат(ГодПлатежа,"Ч(0)4");
			КонецЕсли;
			
			Если П106 = "БФ" Тогда
			    П108 = Строка(НомерДокумента);
				Если ПустоеЗначение(П108) = 1 Тогда
				    П108 = "0";
				КонецЕсли;
				
			Иначе
				П108 = "0";
			КонецЕсли;
			
			П109 = Строка(Формат(ДатаДокумента,"Д ДДММГГГГ"));
			П110 = ТипПлатежа.ПолучитьЗначение(ТипПлатежа.ТекущаяСтрока());	
			
		ИначеЕсли П106 = "ЗД" Тогда
			Если ПериодичностьУплаты.ПолучитьЗначение(ПериодичностьУплаты.ТекущаяСтрока())="-" Тогда
			    П107 = Строка(Формат(ДатаПоказателя,"Д ДДММГГГГ"));
			
			Иначе
				П107 = ПериодичностьУплаты.ПолучитьЗначение(ПериодичностьУплаты.ТекущаяСтрока())+"."+Формат(ПериодПлатежа,"Ч(0)2")+"."+Формат(ГодПлатежа,"Ч(0)4");
			КонецЕсли;
			
			П108 = "0";
			П109 = "0";
			П110 = ТипПлатежа.ПолучитьЗначение(ТипПлатежа.ТекущаяСтрока());	
			
		ИначеЕсли Найти("АП,АР", П106) > 0 Тогда
			П107 = "0";
			П108 = Строка(НомерДокумента);
			П109 = Строка(Формат(ДатаДокумента,"Д ДДММГГГГ"));
			П110 = ТипПлатежа.ПолучитьЗначение(ТипПлатежа.ТекущаяСтрока());	
			
		Иначе
			П107 = Строка(Формат(ДатаПлатежа,"Д ДДММГГГГ"));
			П108 = Строка(НомерДокумента); 
			П109 = Строка(Формат(ДатаДокумента,"Д ДДММГГГГ"));		
			П110 = ТипПлатежа.ПолучитьЗначение(ТипПлатежа.ТекущаяСтрока());	
		КонецЕсли;
	КонецЕсли;
	
	Возврат П107;
	
КонецФункции //Сформировать()

//*****************************************************************************
// Функция Проверить() - осуществляет проверку корректности заполнения
// реквизитов платежного поручения перед печатью
//
Функция Проверить()
	Ошибка = СоздатьОбъект("СписокЗначений");
	
	П101 = СокрЛП(П101);
	П104 = СокрЛП(П104);
	П105 = СокрЛП(П105);
	П106 = СокрЛП(П106);
	П107 = ?(ПустаяСтрока(СокрЛП(СтрЗаменить(СтрЗаменить(П107,"0",""),".","")))=1,"0", П107);
	П108 = СокрЛП(П108);
	П109 = СокрЛП(П109);
	П110 = СокрЛП(П110);
	
	Если (Найти("09,10,11,12,13", П101) = 0) и (П106 = "БФ") Тогда
		
		Ошибка.ДобавитьЗначение("Возможно, неверно заполнено поле ""Основание платежа"".");
	КонецЕсли;
	
	Если (Найти("01,02,03,04,05,06,07,08,09,10,11,12,13,14,15", П101) =0) или
		 (ПустаяСтрока(СокрЛП(П101)) = 1) Тогда
		
		Ошибка.ДобавитьЗначение("Неверное значение поля ""Статус составителя"".");
	КонецЕсли;
	
	Если (СтрЗаменить(П104,"0","")="")и(Найти("06,07", П101) =0) Тогда
		
		Ошибка.ДобавитьЗначение("Необходимо заполнить поле ""Код БК"".");
	КонецЕсли;
	
	Если (СтрЗаменить(П105,"0","")="")и(Найти("06,07", П101) =0) Тогда
		
		Ошибка.ДобавитьЗначение("Необходимо заполнить поле ""Код ОКАТО"".");
	КонецЕсли;
	
	// Проверяем в зависимости от статуса составителя
	Если П101 = "15" Тогда
		Если СтрЗаменить(П106,"0","")<>"" Тогда 
			
			Ошибка.ДобавитьЗначение("При статусе составителя ""15"" не следует заполнять поле ""Основание платежа"".");
		КонецЕсли;
		Если СтрЗаменить(СтрЗаменить(П107,"0",""),".","") <>"" Тогда
			
			Ошибка.ДобавитьЗначение("При статусе составителя ""15"" не следует заполнять поле ""Период"".");
		КонецЕсли;
		Если СтрЗаменить(П108,"0","")<>"" Тогда
			
			Ошибка.ДобавитьЗначение("При статусе составителя ""15"" не следует заполнять поле ""Номер"".");
		КонецЕсли;
		Если СтрЗаменить(П109,"0","")<>"" Тогда
			
			Ошибка.ДобавитьЗначение("При статусе составителя ""15"" не следует заполнять поле ""Дата"".");
		КонецЕсли;
		Если (П110 <> "") и (П110 <> "НС") Тогда
			
			Ошибка.ДобавитьЗначение("При статусе составителя ""15"" следует указать в поле ""Тип платежа"" значение ""НС"" или оставить данное поле незаполненным.");			
		КонецЕсли;
		
	ИначеЕсли СтрЗаменить(П106,"0","") = "" Тогда
		Если СтрЗаменить(СтрЗаменить(П107,"0",""),".","")<>"" Тогда
			
			Ошибка.ДобавитьЗначение("При неуказанном основании платежа не следует заполнять поле ""Период"".");
		КонецЕсли;
		Если СтрЗаменить(П108,"0","")<>"" Тогда
			
			Ошибка.ДобавитьЗначение("При неуказанном основании платежа не следует заполнять поле ""Номер"".");
		КонецЕсли;
		Если СтрЗаменить(П109,"0","")<>"" Тогда
			
			Ошибка.ДобавитьЗначение("При неуказанном основании платежа не следует заполнять поле ""Дата"".");
		КонецЕсли;
		Если (П110 <> "") и (П110 <> "НС") Тогда
			
			Ошибка.ДобавитьЗначение("При неуказанном основании платежа не следует заполнять поле ""Тип платежа"".");			
		КонецЕсли;
		
	ИначеЕсли П106 = "ЗД" Тогда
		Если СтрЗаменить(П108,"0","")<>"" Тогда
			
			Ошибка.ДобавитьЗначение("При основании платежа ""ЗД"" не следует заполнять поле ""Номер"".");
		КонецЕсли;
		Если СтрЗаменить(П109,"0","")<>"" Тогда
			
			Ошибка.ДобавитьЗначение("При основании платежа ""ЗД"" не следует заполнять поле ""Дата"".");
		КонецЕсли;
		
	ИначеЕсли П106 = "ТП" Тогда
		Если СтрЗаменить(П108,"0","")<>"" Тогда
			
			Ошибка.ДобавитьЗначение("При основании платежа ""ТП"" не следует заполнять поле ""Номер"".");
		КонецЕсли;
		
	ИначеЕсли (П106 = "АП") или (П106 = "АР") Тогда
		Если СтрЗаменить(СтрЗаменить(П107,"0",""),".","")<>"" Тогда
			
			Ошибка.ДобавитьЗначение("При основании платежа ""АП"" или ""АР"" не следует заполнять поле ""Период"".");
		КонецЕсли;
	КонецЕсли;
	
	Если Найти("01,02,03,04,05,08,09,10,11,12,13,14", П101) > 0 Тогда
		// Проверяем в зависимости от основания платежа
		Если СтрДлина(П106)<>2 Тогда
			Ошибка.ДобавитьЗначение("Возможно, неверно заполнено поле ""Основание платежа"".");
 
		ИначеЕсли Найти("ТП ,ЗД, БФ", П106) > 0 Тогда
			Если СтрЗаменить(СтрЗаменить(П107,"0",""),".","")<>"" Тогда
				ДД = Сред(СокрЛП(П107), 1, 2);                          
				ММ = Число(Сред(СокрЛП(П107), 4, 2));
				ГГ = Число(Сред(СокрЛП(П107), 7, 4));
				Если (Найти("МС", ДД) > 0) Тогда
					Если (ММ<1)или
						(ММ>12)или 
						(ГГ<2000)или
						(СтрДлина(П107)-СтрДлина(СтрЗаменить(П107,".",""))<>2) Тогда
						
						Ошибка.ДобавитьЗначение("Возможно, неверно указана дата в поле ""Период""."); 
					КонецЕсли;
				ИначеЕсли (Найти("КВ", ДД) > 0) Тогда	
					Если (ММ<1)или
						(ММ>4)или 
						(ГГ<2000)или
						(СтрДлина(П107)-СтрДлина(СтрЗаменить(П107,".",""))<>2) Тогда
							
							Ошибка.ДобавитьЗначение("Неверно указан показатель налогового периода в поле ""Период"".");
					КонецЕсли;
				ИначеЕсли (Найти("ПЛ", ДД) > 0) Тогда	
					Если (ММ<1)или
						(ММ>2)или 
						(ГГ<2000)или
						(СтрДлина(П107)-СтрДлина(СтрЗаменить(П107,".",""))<>2) Тогда
							
					    	Ошибка.ДобавитьЗначение("Неверно указан показатель налогового периода в поле ""Период"".");
					КонецЕсли;
				ИначеЕсли (Найти("ГД", ДД) > 0) Тогда	
					Если (ММ<>0)или
						(ГГ<2000)или
						(СтрДлина(П107)-СтрДлина(СтрЗаменить(П107,".",""))<>2) Тогда
							
							Ошибка.ДобавитьЗначение("Неверно указан показатель налогового периода в поле ""Период"".");
					КонецЕсли;
				Иначе
					Если ПустоеЗначение(Дата(П107))=1 Тогда
						
						Ошибка.ДобавитьЗначение("Возможно, неверно указана дата в поле ""Период""."); 
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если (СтрЗаменить(П108,"0","")<>"") и (П106 <> "БФ") Тогда
				Ошибка.ДобавитьЗначение("При основании платежа ""ТП"" или ""ЗД"" в поле ""Номер"" необходимо указывать ""0"".");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	//Выводим список найденых ошибок
	Для Ном=1 по Ошибка.РазмерСписка() Цикл
		Сообщить(Ошибка.ПолучитьЗначение(Ном),"!");
	КонецЦикла;
	
	Возврат Ошибка;   
	
КонецФункции    //Проверить()

//******************************************************************************
Процедура ПриИзмененииОснованияПлатежа()
	
	ВыбПериодичностьУплаты = ПериодичностьУплаты.ПолучитьЗначение(ПериодичностьУплаты.ТекущаяСтрока());
	ВыбОснованиеПлатежа = ОснованиеПлатежа.ПолучитьЗначение(ОснованиеПлатежа.ТекущаяСтрока());
	ВыбСтатусСоставителя = СтатусСоставителя.ПолучитьЗначение(СтатусСоставителя.ТекущаяСтрока());
	
	ВидНомераДокументаПлатежа.ПолучитьЗначение( ВидНомераДокументаПлатежа.НайтиЗначение( ОснованиеПлатежа.ТекущаяСтрока() ),НадписьНомерДокумента);
	ВидДатыДокументаПлатежа.ПолучитьЗначение( ВидДатыДокументаПлатежа.НайтиЗначение( ОснованиеПлатежа.ТекущаяСтрока() ),НадписьДатаДокумента);
	ВидДатыОснованияПлатежа.ПолучитьЗначение( ВидДатыОснованияПлатежа.НайтиЗначение( ОснованиеПлатежа.ТекущаяСтрока() ), НадписьДатаПлатежа);
	
	
	Если ВыбСтатусСоставителя = "15" Тогда
	    Форма.ИспользоватьСлой("Основной,Подвал", 2);
		Форма.ДатаДокумента.Видимость(0);
		Форма.НадписьОснованиеПлатежа.Видимость(0);
		Форма.ОснованиеПлатежа.Видимость(0);
		НадписьДатаДокумента = "";
		
	Иначе
	
		Если Найти("ТП", ВыбОснованиеПлатежа) > 0 Тогда
		 	
			Если Найти("-", ВыбПериодичностьУплаты) > 0 Тогда
				Форма.ИспользоватьСлой("Основной,ПоказательПериода,ПоказательДатой,Подвал", 2);
			Иначе
				Форма.ИспользоватьСлой("Основной,ПоказательПериода,СоставнойПоказатель,Подвал", 2);
			КонецЕсли;
		    Форма.ДатаДокумента.Видимость(1);
			
		ИначеЕсли Найти("БФ", ВыбОснованиеПлатежа) > 0 Тогда
		 	
			Если Найти("-", ВыбПериодичностьУплаты) > 0 Тогда
				Форма.ИспользоватьСлой("Основной,ПоказательПериода,ПоказательДатой,ДатаПериода,Подвал", 2);
			Иначе
				Форма.ИспользоватьСлой("Основной,ПоказательПериода,СоставнойПоказатель,ДатаПериода,Подвал", 2);
			КонецЕсли;
		    Форма.ДатаДокумента.Видимость(1);
			Форма.ДатаПлатежа.Видимость(0);
		
		ИначеЕсли Найти("ЗД", ВыбОснованиеПлатежа) > 0 Тогда
		 	
			Если Найти("-", ВыбПериодичностьУплаты) > 0 Тогда
				Форма.ИспользоватьСлой("Основной,ПоказательПериода,ПоказательДатой,Подвал", 2);
			Иначе
				Форма.ИспользоватьСлой("Основной,ПоказательПериода,СоставнойПоказатель,Подвал", 2);
			КонецЕсли;
			Форма.ДатаДокумента.Видимость(0);
			
		ИначеЕсли Найти("АП, АР", ВыбОснованиеПлатежа) > 0 Тогда
		 	
			Форма.ИспользоватьСлой("Основной,ДатаПериода,Подвал", 2);
			Форма.ДатаПлатежа.Видимость(0);
			Форма.ДатаДокумента.Видимость(1);
			Форма.НомерДокумента.Видимость(1);
			
		Иначе
					
			Форма.ИспользоватьСлой("Основной,ДатаПериода,Подвал", 2);
			Форма.ДатаПлатежа.Видимость(1);
			Форма.ДатаДокумента.Видимость(1);
			Форма.НомерДокумента.Видимость(1);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры // ПриИзмененииОснованияПлатежа()	

//******************************************************************************
Процедура ПриВыбореСтатусаСоставителя()
	
	ПриИзмененииОснованияПлатежа();
	
КонецПроцедуры  //ПриВыбореСтатусаСоставителя()

//******************************************************************************
Процедура ПриИзменениииПериодичностиУплаты()
	
	ВыбПериодичностьУплаты = ПериодичностьУплаты.ПолучитьЗначение(ПериодичностьУплаты.ТекущаяСтрока());
	Если Найти("МС", ВыбПериодичностьУплаты) > 0 Тогда
		ТекстПериодПлатежа = "Месяц:";
		Форма.НадписьПериодПлатежа.Доступность(1);
		Форма.ПериодПлатежа.Доступность(1);
	ИначеЕсли Найти("КВ", ВыбПериодичностьУплаты) > 0 Тогда
		ТекстПериодПлатежа = "Квартал:";
		Форма.НадписьПериодПлатежа.Доступность(1);
		Форма.ПериодПлатежа.Доступность(1);
	ИначеЕсли Найти("ПЛ", ВыбПериодичностьУплаты) > 0 Тогда
		ТекстПериодПлатежа = "Полугодие:";
		Форма.НадписьПериодПлатежа.Доступность(1);
		Форма.ПериодПлатежа.Доступность(1);
	Иначе
		Форма.НадписьПериодПлатежа.Доступность(0);
		Форма.ПериодПлатежа.Доступность(0);
		ПериодПлатежа = 00;
	КонецЕсли;
	ПриИзмененииОснованияПлатежа();
КонецПроцедуры // ПриИзменениииПериодичностиУплаты() 

//******************************************************************************
Процедура ПриВыбореНалога() 
	
	Если Налог.Выбран()=1 Тогда        
		
		Если СтатусСоставителя.НайтиЗначение(Налог.СтатусСоставителяПлДок)>0 Тогда
			СтатусСоставителя.ТекущаяСтрока(СтатусСоставителя.НайтиЗначение(Налог.СтатусСоставителяПлДок));
		КонецЕсли;
		
		Если Налог.Периодичность = Перечисление.ПериодичностьУплатыНалога.Месячная Тогда	
			ПериодичностьУплаты.ТекущаяСтрока(1);
		ИначеЕсли Налог.Периодичность = Перечисление.ПериодичностьУплатыНалога.Квартальная Тогда	
			ПериодичностьУплаты.ТекущаяСтрока(2);
		ИначеЕсли Налог.Периодичность = Перечисление.ПериодичностьУплатыНалога.Полугодовая Тогда	
			ПериодичностьУплаты.ТекущаяСтрока(3);
		ИначеЕсли Налог.Периодичность = Перечисление.ПериодичностьУплатыНалога.Годовая Тогда	
			ПериодичностьУплаты.ТекущаяСтрока(4);
		КонецЕсли;
		ПриИзменениииПериодичностиУплаты();
		
		КодБК = Налог.КодБК;
		КодОКАТО = Налог.КодОКАТО;
		
		Сформировать();
		
	КонецЕсли;
КонецПроцедуры  //ПриВыбореНалога()

//******************************************************************************
// ПриИзмененииТипаПлатежа()
Процедура ПриИзмененииТипаПлатежа()
	
	Если ПустаяСтрока(КодБК) = 1 Тогда
	    Возврат;
	КонецЕсли;
	
	ВыбТипаПлатежа = ТипПлатежа.ПолучитьЗначение(ТипПлатежа.ТекущаяСтрока());
	Символ14_1 = Сред(КодБК, 14, 1);
	
	Если (Найти("ПЕ", ВыбТипаПлатежа) > 0) или (Найти("ПЦ", ВыбТипаПлатежа) > 0) Тогда
		КодБК = Лев(КодБК, 13) + "2" + Прав(КодБК, 6);
		Символ14_2 = "2";
		
	ИначеЕсли (Найти("СА", ВыбТипаПлатежа) > 0) или (Найти("АШ", ВыбТипаПлатежа) > 0) или (Найти("ИШ", ВыбТипаПлатежа) > 0) Тогда
		КодБК = Лев(КодБК, 13) + "3" + Прав(КодБК, 6);
		Символ14_2 = "3";
		
	Иначе
		КодБК = Лев(КодБК, 13) + "1" + Прав(КодБК, 6);
		Символ14_2 = "1";
	КонецЕсли;
	
	Если Символ14_1 <> Символ14_2 Тогда
	    Сообщить("14-й знак кода БК изменен на """ + Символ14_2 + """.");
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииТипаПлатежа()

//******************************************************************************
Процедура ПриОткрытии()
	
	ОснованиеПлатежа.УдалитьВсе();
	ОснованиеПлатежа.ДобавитьЗначение("ТП", "ТП - платежи текущего года");
	ОснованиеПлатежа.ДобавитьЗначение("ЗД", "ЗД - добровольное погашение задолженности по истекшим налоговым периодам");
	ОснованиеПлатежа.ДобавитьЗначение("БФ", "БФ - текущие платежи физических лиц - клиентов банка (владельцев счета)");
	ОснованиеПлатежа.ДобавитьЗначение("ТР", "ТР - погашение задолженности по требованию об уплате налогов (сборов) от налогового органа");
	ОснованиеПлатежа.ДобавитьЗначение("РС", "РС - погашение рассроченной задолженности");
	ОснованиеПлатежа.ДобавитьЗначение("ОТ", "ОТ - погашение отсроченной задолженности");
	ОснованиеПлатежа.ДобавитьЗначение("РТ", "РТ - погашение реструктурируемой задолженности");
	ОснованиеПлатежа.ДобавитьЗначение("ВУ", "ВУ - погашение отсроченной задолженности в связи с введением внешнего управления");
	ОснованиеПлатежа.ДобавитьЗначение("ПР", "ПР - погашение задолженности, приостановленной к взысканию");
	ОснованиеПлатежа.ДобавитьЗначение("АП", "АП - погашение задолженности по акту проверки");
	ОснованиеПлатежа.ДобавитьЗначение("АР", "АР - погашение задолженности по исполнительному документу");
	ОснованиеПлатежа.ДобавитьЗначение("0", "0 - Конкретное значение указать невозможно");
	
	ТипПлатежа.УдалитьВсе();
	ТипПлатежа.ДобавитьЗначение("НС", "НС - уплата налога или сбора");
	ТипПлатежа.ДобавитьЗначение("ПЛ", "ПЛ - уплата платежа");
	ТипПлатежа.ДобавитьЗначение("ГП", "ГП - уплата пошлины");
	ТипПлатежа.ДобавитьЗначение("ВЗ", "ВЗ - уплата взноса");
	ТипПлатежа.ДобавитьЗначение("АВ", "АВ - уплата аванса или предоплата (в том числе декадные платежи)");
	ТипПлатежа.ДобавитьЗначение("ПЕ", "ПЕ - уплата пени");
	ТипПлатежа.ДобавитьЗначение("ПЦ", "ПЦ - уплата процентов");
	ТипПлатежа.ДобавитьЗначение("СА", "СА - налоговые санкции, установленные Налоговым кодексом РФ");
	ТипПлатежа.ДобавитьЗначение("АШ", "АШ - административные штрафы");
	ТипПлатежа.ДобавитьЗначение("ИШ", "ИШ - иные штрафы, установленные соответствующими нормативными актами");
	ТипПлатежа.ДобавитьЗначение("0", "0 - Конкретное значение указать невозможно");

	ПериодичностьУплаты.УдалитьВсе();
	ПериодичностьУплаты.ДобавитьЗначение("МС", "МС - месячный платеж");
	ПериодичностьУплаты.ДобавитьЗначение("КВ", "КВ - квартальный платеж");
	ПериодичностьУплаты.ДобавитьЗначение("ПЛ", "ПЛ - полугодовой платеж");
	ПериодичностьУплаты.ДобавитьЗначение("ГД", "ГД - годовой платеж");
	ПериодичностьУплаты.ДобавитьЗначение("-",  "Платеж по конкретной дате");
	
	СтатусСоставителя.УдалитьВсе();
    СтатусСоставителя.ДобавитьЗначение("01", "01 - налогоплательщик (плательщик сборов) - юридическое лицо");
	СтатусСоставителя.ДобавитьЗначение("02", "02 - налоговый агент");
	СтатусСоставителя.ДобавитьЗначение("03", "03 - сборщик налогов и сборов");
	СтатусСоставителя.ДобавитьЗначение("04", "04 - налоговый орган");
	СтатусСоставителя.ДобавитьЗначение("05", "05 - служба судебных приставов");
	СтатусСоставителя.ДобавитьЗначение("06", "06 - участник внешнеэкономической деятельности");
	СтатусСоставителя.ДобавитьЗначение("07", "07 - таможенный орган");
	СтатусСоставителя.ДобавитьЗначение("08", "08 - плательщик иных обязательных платежей"); 
	СтатусСоставителя.ДобавитьЗначение("09", "09 - налогоплательщик (плательщик сборов) - ИП");
	СтатусСоставителя.ДобавитьЗначение("10", "10 - налогоплательщик (плательщик сборов) - частный нотариус"); 
	СтатусСоставителя.ДобавитьЗначение("11", "11 - налогоплательщик (плательщик сборов) - адвокат"); 
	СтатусСоставителя.ДобавитьЗначение("12", "12 - налогоплательщик (плательщик сборов) - глава КФХ"); 
	СтатусСоставителя.ДобавитьЗначение("13", "13 - налогоплательщик (плательщик сборов) - иное физическое лицо"); 
	СтатусСоставителя.ДобавитьЗначение("14", "14 - налогоплательщик, производящий выплаты физическим лицам"); 
	СтатусСоставителя.ДобавитьЗначение("15", "15 - кредитная организация"); 
    	    
	ВидДатыДокументаПлатежа = СоздатьОбъект("СписокЗначений");
	
	// Значение - номер строки списка "Основание платежа", представление - 
	// текст, выдаваемый к качестве подсказки к полю для указания даты
	ВидДатыДокументаПлатежа.ДобавитьЗначение( 1, "Дата декларации (расчета), представленной в налоговый орган:");
	ВидДатыДокументаПлатежа.ДобавитьЗначение( 2, "");
	ВидДатыДокументаПлатежа.ДобавитьЗначение( 3, "Дата представления декларации, либо дата отправки почтового отправления:");
	ВидДатыДокументаПлатежа.ДобавитьЗначение( 4, "Дата требования об уплате налогов (сборов) от налогового органа:");
	ВидДатыДокументаПлатежа.ДобавитьЗначение( 5, "Дата решения о рассрочке:");
	ВидДатыДокументаПлатежа.ДобавитьЗначение( 6, "Дата решения об отсрочке:");
	ВидДатыДокументаПлатежа.ДобавитьЗначение( 7, "Дата решения о реструктуризации:");
	ВидДатыДокументаПлатежа.ДобавитьЗначение( 8, "Дата принятия арбитражным судом решения о введении внешнего управления:");
	ВидДатыДокументаПлатежа.ДобавитьЗначение( 9, "Дата решения о приостановлении взыскания:"); 
	ВидДатыДокументаПлатежа.ДобавитьЗначение( 10, "Дата акта проверки:");
	ВидДатыДокументаПлатежа.ДобавитьЗначение( 11,"Дата вынесения исполнительного документа:"); 
	ВидДатыДокументаПлатежа.ДобавитьЗначение( 12,"Показатель даты налогового документа:");
	
	ВидНомераДокументаПлатежа = СоздатьОбъект("СписокЗначений");
	
	// Значение - номер строки списка "Основание платежа", представление - 
	// текст, выдаваемый к качестве подсказки к полю для указания номера
	ВидНомераДокументаПлатежа.ДобавитьЗначение( 1, "");
	ВидНомераДокументаПлатежа.ДобавитьЗначение( 2, "");
	ВидНомераДокументаПлатежа.ДобавитьЗначение( 3, "Значение реквизита ""Индекс документа"" из извещения, заполненного налоговым органом:");
	ВидНомераДокументаПлатежа.ДобавитьЗначение( 4, "Номер требования об уплате налогов (сборов) от налогового органа:");
	ВидНомераДокументаПлатежа.ДобавитьЗначение( 5, "Номер решения о рассрочке:");
	ВидНомераДокументаПлатежа.ДобавитьЗначение( 6, "Номер решения об отсрочке:");
	ВидНомераДокументаПлатежа.ДобавитьЗначение( 7, "Номер решения о реструктуризации:");
	ВидНомераДокументаПлатежа.ДобавитьЗначение( 8, "Номер дела или материала, рассмотренного арбитражным судом:");
	ВидНомераДокументаПлатежа.ДобавитьЗначение( 9, "Номер решения о приостановлении взыскания:"); 
	ВидНомераДокументаПлатежа.ДобавитьЗначение( 10, "Номер акта проверки:");
	ВидНомераДокументаПлатежа.ДобавитьЗначение( 11,"Номер исполнительного документа:");
	ВидНомераДокументаПлатежа.ДобавитьЗначение( 12,"Показатель номера налогового документа:");
	
	ВидДатыОснованияПлатежа = СоздатьОбъект("СписокЗначений");
	
	// Значение - номер строки списка "Основание платежа", представление - 
	// текст, выдаваемый к качестве подсказки к полю для указания даты
	ВидДатыОснованияПлатежа.ДобавитьЗначение( 1,"");
	ВидДатыОснованияПлатежа.ДобавитьЗначение( 2,"");
	ВидДатыОснованияПлатежа.ДобавитьЗначение( 3,"");
	ВидДатыОснованияПлатежа.ДобавитьЗначение( 4, "Срок уплаты, установленный в требовании об уплате налогов (сборов):");
	ВидДатыОснованияПлатежа.ДобавитьЗначение( 5, "Дата уплаты части рассроченной суммы налога в соответствии с графиком:");
	ВидДатыОснованияПлатежа.ДобавитьЗначение( 6, "Дата завершения отсрочки:");
	ВидДатыОснованияПлатежа.ДобавитьЗначение( 7, "Дата уплаты части реструктурируемой задолженности в соответствии с графиком:");
	ВидДатыОснованияПлатежа.ДобавитьЗначение( 8, "Дата завершения внешнего управления:");
	ВидДатыОснованияПлатежа.ДобавитьЗначение( 9, "Дата завершения приостановления взыскания:"); 
	ВидДатыОснованияПлатежа.ДобавитьЗначение( 10, "");
	ВидДатыОснованияПлатежа.ДобавитьЗначение( 11,"");
	ВидДатыОснованияПлатежа.ДобавитьЗначение( 12,"Показатель налового периода");
	
	СписокПараметров = Форма.Параметр;
	
	Если ПустоеЗначение(СписокПараметров)=0 Тогда
					
		Конт = СписокПараметров.Получить("Контекст");
		П101 = СписокПараметров.Получить("П101");
		П104 = СписокПараметров.Получить("П104");
		П105 = СписокПараметров.Получить("П105");
		П106 = СписокПараметров.Получить("П106");
		П107 = СписокПараметров.Получить("П107");
		П108 = СписокПараметров.Получить("П108");
		П109 = Строка(СписокПараметров.Получить("П109"));
		П110 = СписокПараметров.Получить("П110");
		
		// Если обработка вызвана для контроля платежного документа перед печатью
		// Запустим функцию проверки и закроем форму
		
		Если СписокПараметров.Получить("Контроль") = 1 Тогда
			Форма.Параметр = Проверить(); 
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
		
		//Подставляем значения, полученные из платежного документа
		СпрНалоги = СоздатьОбъект("Справочник.НалогиИОтчисления");
		Найден = 0;
		Если ПустоеЗначение(СокрЛП(Конт.КодБК)) = 0 Тогда
			Найден = СпрНалоги.НайтиПоРеквизиту("КодБК", Конт.КодБК, 1);
		КонецЕсли;
		Если (Найден = 0) и (Конт.СчетКонтрагента.Выбран() = 1) Тогда
			Найден = СпрНалоги.НайтиПоРеквизиту("РасчетныйСчет", Конт.СчетКонтрагента, 1);
		КонецЕсли;
		Если Найден = 1 Тогда
			Налог = СпрНалоги.ТекущийЭлемент();
		КонецЕсли;
		
		Если СтатусСоставителя.НайтиЗначение(П101)>0 Тогда
			СтатусСоставителя.ТекущаяСтрока(СтатусСоставителя.НайтиЗначение(П101));
		Иначе
			СтатусСоставителя.ТекущаяСтрока(1);
		КонецЕсли; 
		
		КодБК 			= П104;
		КодОКАТО		= П105;
		
		Если ОснованиеПлатежа.НайтиЗначение(П106)>0 Тогда
			ОснованиеПлатежа.ТекущаяСтрока(ОснованиеПлатежа.НайтиЗначение(П106));
		Иначе
			ОснованиеПлатежа.ТекущаяСтрока(1);
		КонецЕсли;                                                               
		ПериодичностьУплаты.ТекущаяСтрока(1);
		ПриИзмененииОснованияПлатежа();
		
		ДД = Сред(СокрЛП(П107), 1, 2); 
		ММ = Сред(СокрЛП(П107), 4, 2);
		ГГ = Сред(СокрЛП(П107), 7, 4);
		Если ПериодичностьУплаты.НайтиЗначение(ДД)>0 Тогда
			ПериодичностьУплаты.ТекущаяСтрока(ПериодичностьУплаты.НайтиЗначение(ДД));
			ПериодПлатежа = Число(ММ); 
			ГодПлатежа    = Число(ГГ);
		ИначеЕсли  Найти("ТП, ЗД", П106)>0 Тогда
			ДатаПоказателя = Дата(Число(ГГ),Число(ММ),Число(ДД));
			ПериодичностьУплаты.ТекущаяСтрока(5);
		Иначе
			ДатаПлатежа = Дата(Число(ГГ),Число(ММ),Число(ДД));
			ПериодичностьУплаты.ТекущаяСтрока(1);
		КонецЕсли;                                                               
		ПриИзменениииПериодичностиУплаты();
		
		НомерДокумента = П108;
		ДД = Сред(СокрЛП(П109), 1, 2); 
		ММ = Сред(СокрЛП(П109), 4, 2);
		ГГ = Сред(СокрЛП(П109), 7, 4);
		ДатаДокумента = Дата(Число(ГГ),Число(ММ),Число(ДД));
		
		Если ТипПлатежа.НайтиЗначение(П110)>0 Тогда
			ТипПлатежа.ТекущаяСтрока(ТипПлатежа.НайтиЗначение(П110));
		Иначе
			ТипПлатежа.ТекущаяСтрока(1);
		КонецЕсли;
		
	Иначе
		ОснованиеПлатежа.ТекущаяСтрока(1);
		ПериодичностьУплаты.ТекущаяСтрока(1);
		ТипПлатежа.ТекущаяСтрока(1);
		СтатусСоставителя.ТекущаяСтрока(1);
		
		ПриИзменениииПериодичностиУплаты();
		ПриИзмененииОснованияПлатежа();
		Форма.ИспользоватьСлой("Основной,ПоказательПериода,СоставнойПоказатель,Подвал", 2);
	КонецЕсли;
КонецПроцедуры // ПриОткрытии

//*****************************************************************************
Процедура Перенести()
	
	// Установим значения реквизитов платежного документа
	Если Налог.Выбран()=1 Тогда
		Конт.Контрагент = Налог.Получатель;
		Конт.СчетКонтрагента = Налог.РасчетныйСчет;
		Если (ПустоеЗначение(Конт.Содержание) = 1) и (ПустоеЗначение(Конт.СчетКонтрагента.Назначение) = 0) Тогда 
			Конт.Содержание = Конт.СчетКонтрагента.Назначение;
		КонецЕсли;
		Конт.Очередность = Налог.Очередность;
		Если Налог.Счет.Выбран() = 1 Тогда
			БухИт = СоздатьОбъект("БухгалтерскиеИтоги");
			Для Индекс = 1 по Налог.Счет.КоличествоСубконто() Цикл
				Субк = Налог.ПолучитьАтрибут("Субконто" + Индекс);
				Если ПустоеЗначение(Субк) = 1 Тогда
					БухИт.ИспользоватьСубконто(Налог.Счет.ВидСубконто(Индекс));
				Иначе
					БухИт.ИспользоватьСубконто(Налог.Счет.ВидСубконто(Индекс), Субк, 2);
				КонецЕсли;
			КонецЦикла;
			
			БухИт.ВыполнитьЗапрос(,Конт.ДатаДок, Налог.Счет);
			Конт.Сумма = БухИт.СКК();
		КонецЕсли;
	КонецЕсли;
	
	Конт.СтатусСоставителяПлДок = П101; 
	Конт.КодБК 					= П104;
	Конт.КодОКАТО               = ?(П105="0","",П105);
	Конт.ПоказательОснования    = ?(П106="0","",П106);;
	Конт.ПоказательПериода      = ?((ПустоеЗначение(СокрЛП(СтрЗаменить(П107,"."," ")))=1) или (СокрЛП(СтрЗаменить(П107,"."," ")) = "0"),"",П107);
	Конт.ПоказательНомера       = ?(П108="0","",П108);;
	Конт.ПоказательДаты         = П109;
	Конт.ПоказательТипа         = ?(П110="0","",П110);; 
	
	Форма.Закрыть();
	
КонецПроцедуры; //Перенести()

