Перем ПоказПриЗапуске;
Перем ТекущаяСтраница;
Перем МенюОпрос,ОпросВнешний;
Перем СекцияЗаголовок,СекцияШапка,СекцияРазделитель,СекцияПолучить,СекцияСайт,СекцияОбновления;     
Перем Секция,Программа,Версия,Релиз,Номер,Компьютер,Память;
Перем Почта,Соединение,Инфо;
Перем ВходящиеП,АдресОтправителя,ЧислоПисем,ЗаголовокПисьма,ЕстьПисьма,СекцияТекст1,Письмо;  
Перем ОбновлятьЯщик,ДатаПросмотраНовостей,Опережение,ПроверкаКалендарь; 
Перем НДней,ЧерезНДней,Включение,НовыйНомерРелизаКонфигурации;  
Перем ДатаПоследнейНовости,ПроверятьНовости,ВалАвтоПолучать;  
Перем Новости,РОтчеты,Конфигурация;
Перем СтрокаИТС,ИмяФайла; 
Перем ПадежиДней[10], Дней;

//******************************************************************************
Процедура ОчиститьКаталог(Знач Каталог)
	Перем Размер,Атр,ВС,ВД,ВЗ,Расш;
	Перем СписокФайлов, СписокКаталогов;
	
	СписокФайлов	= СоздатьОбъект("СписокЗначений");
	СписокКаталогов	= СоздатьОбъект("СписокЗначений");
	
	// получим имя первого файла
	ФГ=СоздатьОбъект("ФС");
	Файл = ФГ.НайтиПервыйФайл(Каталог + "\*.*");
	Пока ПустаяСтрока(Файл) = 0 Цикл
		Если (Файл <> "..") И (Файл <> ".") Тогда
			ПолноеИмя	= Каталог + "\" + Файл;
			ФГ.АтрибутыФайла(ПолноеИмя, Размер, Атр, ВС, ВД, ВЗ, Расш);
			Если Сред(Атр, 4, 1) = "1" Тогда
				СписокКаталогов.ДобавитьЗначение(ПолноеИмя);
			Иначе
				СписокФайлов.ДобавитьЗначение(ПолноеИмя);
			КонецЕсли;
		КонецЕсли;
		Файл	= ФГ.НайтиСледующийФайл();
	КонецЦикла;
	ФГ = "";
	
	Для Индекс = 1 По СписокФайлов.РазмерСписка() Цикл
		ФС.УдалитьФайл(СписокФайлов.ПолучитьЗначение(Индекс));
	КонецЦикла;

	Для Индекс = 1 По СписокКаталогов.РазмерСписка() Цикл
		ОчиститьКаталог(СписокКаталогов.ПолучитьЗначение(Индекс));
	КонецЦикла;
КонецПроцедуры
//******************************************************************************
Функция ЗагрузитьКонфигурацию(КаталогКонф,ИмяФайлаВерсий)
	Перем АдресКлюча,Ответ;
	Перем Размер,Атр,ВС,ВД,ВЗ,Расш; 
	Стр = "";
	АдресAuth = "http://downloads.1c.ru/auth.jsp"; 
	АдресGet = "http://downloads.1c.ru/get.jsp"; 

	Делт = КодСимв("Z")-КодСимв("A")+1;
	СтрокаИТС = Инфо.ПроверитьИмяФайла("1");
	Если ПустоеЗначение(СтрокаИТС)=1 Тогда
		Сообщить("Вставьте диск ИТС в дисковод и повторите операцию");
		Возврат 0;
	КонецЕсли;
	ИТС = (КодСимв(Сред(СтрокаИТС,2,1))+Делт-КодСимв(Лев(СтрокаИТС,1)))%Делт;

	// определение каталога для установки обновления
	ТМП = КаталогИБ();
	Поз = Найти(ТМП,"\");
	Дл = Поз;
	Пока Дл<СтрДлина(КаталогИБ()) Цикл
		ТМП = Прав(ТМП,СтрДлина(ТМП)-Поз);
		Поз = Найти(ТМП,"\");
		Дл = Дл + Поз;
	КонецЦикла;
	Каталог = Лев(КаталогИБ(),Дл-Поз)+"UpDate";   // - где создаем каталог UpDate
	
	Попытка
		Если Вопрос("Начать получение обновления?",4,30)=6 Тогда
			Соединение.ПолучитьКакСтроку(АдресAuth,АдресКлюча);
			Если ТипЗначенияСтр(АдресКлюча)="Число"  Тогда
			    АдресКлюча = Строка(АдресКлюча); 
			КонецЕсли;
			Если СокрЛП(АдресКлюча)="" Тогда
				Сообщить("Доступ закрыт!");
				Форма.Закрыть();
				Возврат 0;
			КонецЕсли;

			Если Лев(КаталогИБ(), 2)="\\" Тогда
				ВремКаталог = КаталогВременныхФайлов()+"temp.grp";
			Иначе
				ВремКаталог = КаталогИБ()+"temp.grp";
			КонецЕсли;
 			Если ФС.СуществуетФайл(ВремКаталог)=0 Тогда 
				ФС.СоздатьКаталог(ВремКаталог);
			Иначе 
				ОчиститьКаталог(ВремКаталог);
			КонецЕсли;
			ВремКаталог = ВремКаталог+"\";

			Ключ = Инфо.ПроверитьИмяФайла(АдресКлюча);
			Соединение.ПолучитьКакФайл(АдресGet+"?addr="+АдресКлюча+"&d="+Ключ+"&its="+ИТС+"&dir="+Врег(КаталогКонф)+"&file="+Врег(ИмяФайлаВерсий),ВремКаталог+ИмяФайлаВерсий);
	
			Если ФС.СуществуетФайл(ВремКаталог+ИмяФайлаВерсий)=1 Тогда
				ФС.АтрибутыФайла(ВремКаталог+ИмяФайлаВерсий,Размер,Атр,ВС,ВД,ВЗ,Расш);
				Если Размер>10000 Тогда 
					Если ФС.СуществуетФайл(Каталог)=0 Тогда
						ФС.СоздатьКаталог(Каталог);
					Иначе
						ОчиститьКаталог(Каталог);
					КонецЕсли;  
					ТК = ФС.ТекКаталог();
					ФС.КопироватьФайл(ВремКаталог+ИмяФайлаВерсий,Каталог+"\"+ИмяФайлаВерсий,0); 
					ФС.УстТекКаталог(Каталог);
					КомандаСистемы(""""+ИмяФайлаВерсий+"""");
					ФС.УстТекКаталог(ТК);
					ФС.УдалитьФайл(ВремКаталог+ИмяФайлаВерсий);
					ФС.УдалитьФайл(Каталог+"\"+ИмяФайлаВерсий);
					ФС.УдалитьКаталог(ВремКаталог);
					ЗапуститьПриложение(Каталог+"\Update.txt");
				Иначе
					Сообщить("Ошибка получения обновления");
					Возврат 0;
				КонецЕсли;  
			КонецЕсли; 
		Иначе
			Возврат 0;
		КонецЕсли;  
	Исключение 
		Сообщить("Неудачная попытка соединения.");
		Возврат 0;
	КонецПопытки;
	СохранитьЗначение("ЛегальностьПолученияОбновления",1);
	Возврат 1;	
КонецФункции
//******************************************************************************
//	ОбновитьЯщик()
//
//	Описание:  В разделе "Ответы" обновляет список пришедших писем от отдела технической поддержки
Процедура ОбновитьЯщик()
	ЧислоПисем =0;
	Состояние("Подготовка к просмотру почты");
	Попытка
		Почта.Подключиться();
		Почта.ВыбратьСообщения();
		ВходящиеП.УдалитьСтроки();
		Пока Почта.ПолучитьСообщение() = 1 Цикл 
			Адрес=Почта.АдресОтправителя();
			Если Найти(Адрес,СокрЛП(АдресОтправителя))>0 Тогда 
				ЧислоПисем = ЧислоПисем + 1;
				Почта.ПрочитатьСообщение( 0 );
				ВходящиеП.НоваяСтрока();
				ДСтр = Почта.ДатаОтправки;
				ВходящиеП.ДатаСтр = Формат(Дата(Число(Лев(ДСтр,4)),Число(Сред(ДСтр,6,2)),Число(Сред(ДСтр,9,2))),"Д (0)ДДММГГГГ")+Прав(ДСтр,6);
				ВходящиеП.Тема = Почта.Заголовок;
				ВходящиеП.Адрес = Адрес;
				Заголовок=Почта.Заголовок;   
				Файл="";
				Каталог="";              
				Файлы="";
				Пока Почта.ПолучитьФайл( Файл, Каталог ) = 1 Цикл  
				   	Файлы=Каталог+Файл+";";
				КонецЦикла;                             
				Файл=Файлы;
				ВходящиеП.Текст = Почта.Текст;
			КонецЕсли;
		КонецЦикла;  
		Почта.Отключиться();
		Состояние("Просмотр почты завершен");
	Исключение  
		Сообщить("Подключение не произведено!");
	КонецПопытки;
КонецПроцедуры                                                  
//******************************************************************************
//	ПоказатьПисьмо(Ном)
//
//	Параметры:  Ном - номер строки в списке писем
//
//	Описание:  Показ текста письма
Процедура ПоказатьПисьмо(Ном)
	Т = СоздатьОбъект("Текст");
	ВходящиеП.ПолучитьСтрокуПоНомеру(Ном);
	Т.ДобавитьСтроку("Письмо от "+СокрЛП(ВходящиеП.ДатаСтр)+"  От адресата: "+СокрЛП(Прав(ВходящиеП.Адрес,СтрДлина(ВходящиеП.Адрес)-5))+"  Тема: "+СокрЛП(ВходящиеП.Тема)+РазделительСтрок);
	Т.ДобавитьСтроку(ВходящиеП.Текст+РазделительСтрок+РазделительСтрок);
	Т.Показать(" Посмотр письма от фирмы ""1С"" (письмо "+Ном+")"); 
КонецПроцедуры                                                  
//******************************************************************************
//	ПоказатьСтраницу(ИмяСтраницы) 
//
//	Параметры:  ИмяСтраницы - название таблицы
//
//	Описание:  Показ таблицы
Процедура ПоказатьСтраницу(ИмяСтраницы) 
	Таблица.Очистить();
	Таблица.ВывестиСекцию(СекцияЗаголовок);
	Если (ИмяСтраницы="Содержание")ИЛИ(ИмяСтраницы="Введение") Тогда
		Таблица.ВывестиСекцию(СекцияШапка);
	ИначеЕсли ИмяСтраницы="Обновления" Тогда
		Таблица.ВывестиСекцию(СекцияОбновления);
	ИначеЕсли ИмяСтраницы="Сайт" Тогда
		Таблица.ВывестиСекцию(СекцияСайт);
	ИначеЕсли ИмяСтраницы="Получить" Тогда
		Таблица.ВывестиСекцию(СекцияПолучить);
	КонецЕсли;
	Если ИмяСтраницы="Получить" Тогда
		Таблица.ИсходнаяТаблица(ИмяСтраницы);
		Таблица.ВывестиСекцию("Страница");
		Если ЧислоПисем=0 Тогда
			Таблица.ВывестиСекцию(ЕстьПисьма);
		Иначе    
			Таблица.ВывестиСекцию(ЗаголовокПисьма);
			Для Н=1 По ЧислоПисем Цикл
				ВходящиеП.ПолучитьСтрокуПоНомеру(Н);
				Письмо.Область("НПП").Текст = Строка(Н);
				Письмо.Область("ДатаПриема").Текст = СокрЛП(ВходящиеП.ДатаСтр);
				Письмо.Область("Тема").Текст = СокрЛП(ВходящиеП.Тема);
				Письмо.Область("Тема").Расшифровка("ТХТ_"+Формат(Н,"Ч3.0"),1);
				Таблица.ВывестиСекцию(Письмо);
			КонецЦикла;
			Таблица.ВывестиСекцию(СекцияТекст1);
			Таблица.ВывестиСекцию(ЕстьПисьма);
		КонецЕсли;
	ИначеЕсли ИмяСтраницы="Содержание" Тогда
		Таблица.ИсходнаяТаблица(ИмяСтраницы);
		// Вывести секцию про основные возможности
		Если Найти(",BUTK,BU42TK,BUBK,EPSBU,BMOB,BUOURCLK,BUOUSL,OUTK,OUMNPV,PBOUL,NLGPL,BASUOR,",","+Врег(глИдентификаторКонфигурации())+",")>0 Тогда 
			Таблица.ВывестиСекцию("Верх"); 
		Иначе
			Таблица.ВывестиСекцию("ВерхКратко"); 
		КонецЕсли;
		Таблица.ВывестиСекцию("Мнение");
		Таблица.ВывестиСекцию("Обращение");
		Таблица.ВывестиСекцию("Ответы");
		Таблица.ВывестиСекцию("Сведения");
		// Вывести секцию про опрос пользователей
		Если Найти(",BUTK,BU42TK,BUBK,EPSBU,BMOB,BUOURCLK,BUOUSL,BUPSB,BUPSB25,OUTK,OUMNPV,",","+Врег(глИдентификаторКонфигурации())+",")>0 Тогда 
			Если Врег(глИдентификаторКонфигурации())="OUTK" Тогда
				Таблица.ВывестиСекцию("ОпросТорг");
			ИначеЕсли Врег(глИдентификаторКонфигурации())="OUMNPV" Тогда
				Таблица.ВывестиСекцию("ОпросДеньги");
			ИначеЕсли Врег(глИдентификаторКонфигурации())="OUBV" Тогда
				Таблица.ВывестиСекцию("ОпросАспект");
			ИначеЕсли Лев(Врег(глИдентификаторКонфигурации()),5)="BUPSB" Тогда
				Таблица.ВывестиСекцию("ОпросПроизводство");
			Иначе 
				Таблица.ВывестиСекцию("Опрос");
			КонецЕсли;
		КонецЕсли;
		// Вывести секцию про обновления
		Если Найти(",BUTK,BU42TK,BUBK,EPSBU,BMOB,BUOURCLK,BUOUSL,BUPSB,BUPSB25,PBOUL,BASUOR,",","+Врег(глИдентификаторКонфигурации())+",")>0 Тогда
			Таблица.ВывестиСекцию("Обновления");
		Иначе 
			Таблица.ВывестиСекцию("ОбновленияКратко");
		КонецЕсли;
		Таблица.ВывестиСекцию("Подвал");
	ИначеЕсли ИмяСтраницы="Обновления" Тогда
		Таблица.ИсходнаяТаблица(ИмяСтраницы);
		// Вывести секцию про основные возможности
		Если Найти(",BUTK,BU42TK,BUBK,EPSBU,BMOB,BUOURCLK,BUOUSL,BUPSB,BUPSB25,OUTK,PBOUL,BASUOR,",","+Врег(глИдентификаторКонфигурации())+",")>0 Тогда
			Таблица.ВывестиСекцию("Верх");
		Иначе 
			Таблица.ВывестиСекцию("ВерхКратко");
		КонецЕсли;
		Таблица.ВывестиСекцию("Проверка");
		Таблица.ВывестиСекцию("Новости");
		Таблица.ВывестиСекцию("ОбновлениеКонфигурации");
		// Вывести секцию про Регламентированные отчеты
		Если Найти(",BUTK,BU42TK,BUBK,EPSBU,BMOB,NCPBK,NCBK,BUOURCLK,BUOUSL,BUPSB25,PBOUL,BASUOR,NLGPL,",","+Врег(глИдентификаторКонфигурации())+",")>0 Тогда 
			Таблица.ВывестиСекцию("РеглОтчеты");
		КонецЕсли;
		// Вывести секцию про курсы валют
		Если Найти(",BUTK,BU42TK,BUBK,EPSBU,BMOB,BUOURCLK,BUOUSL,BUPSB,BUPSB25,OUTK,OUMNPV,OUBV,PBOUL,BASUOR,",","+Врег(глИдентификаторКонфигурации())+",")>0 Тогда
			Таблица.ВывестиСекцию("Курсы");
		КонецЕсли;
		// Вывести секцию про БИК
		Если Найти(",BUTK,BU42TK,BUBK,EPSBU,BMOB,BUOURCLK,BUOUSL,BUPSB,BUPSB25,OUTK,PBOUL,BASUOR,",","+Врег(глИдентификаторКонфигурации())+",")>0 Тогда
			Таблица.ВывестиСекцию("БИК");
		КонецЕсли;
		// Вывести секцию про дополнительную информацию
		Если Найти(",BUTK,BU42TK,BUBK,EPSBU,BMOB,BUOURCLK,BUOUSL,BUPSB,BUPSB25,RCLKTK,R2CLKTK,PBOUL,NLGPL,BASUOR,",","+Врег(глИдентификаторКонфигурации())+",")>0 Тогда
			К = ?(((Опережение%10)=0) ИЛИ ((Опережение<20) И (Опережение>10)),10, Опережение%10);
			Дней = ПадежиДней[К];
			Таблица.ВывестиСекцию("Календарь");
		КонецЕсли;
		Таблица.ВывестиСекцию("Подвал");
	КонецЕсли;
	Если ПустаяСтрока(ИмяСтраницы)=0 Тогда
		Таблица.ИсходнаяТаблица(ИмяСтраницы);
		ТекущаяСтраница=ИмяСтраницы;		
	Иначе
		Таблица.ИсходнаяТаблица(ТекущаяСтраница);
	КонецЕсли;
	Если (ИмяСтраницы="Введение")ИЛИ(ИмяСтраницы="Сайт") Тогда 
		Таблица.ВывестиСекцию("Страница");
	КонецЕсли;
	Таблица.Опции(0,0,1,0,,"Старт1");
	Таблица.Защита(1);
	Таблица.ТолькоПросмотр();
	Таблица.Показать();
КонецПроцедуры
//******************************************************************************
//	ОбработкаЯчТаблицы(Зн)
//
//	Параметры:  Зн - значение расшифровки
//
//	Описание:  Обработка ячейки таблицы
Процедура ОбработкаЯчТаблицы(Зн)
	Перем ТМП,Ч,С,М;
	Перем ИсхКаталог;
	Перем Каталог;
	Перем ИмяФайла;
	Команда=Лев(Зн,3);
	Параметр=Сред(Зн,5,СтрДлина(Зн)-4);
	Если Команда="Таб" Тогда
		ПоказатьСтраницу(Параметр);
	ИначеЕсли Команда="ТКС" Тогда
		Предупреждение(Параметр);
	ИначеЕсли Команда="Обн" Тогда
		ОбновлятьЯщик=?(ОбновлятьЯщик=1,2,1);
		ПоказатьСтраницу("Получить");
	ИначеЕсли Команда="ВОТ" Тогда // Это обработки
		Если Параметр="ИППСистемнаяИнформация" Тогда
			ОткрытьФормуМодально("Обработка."+Параметр);
		ИначеЕсли Параметр="ИПППроверкаОбновлений" Тогда
			Парам = СоздатьОбъект("СписокЗначений");
			Парам.ДобавитьЗначение(1,"Режим");  
			ОткрытьФормуМодально("Обработка."+Параметр,Парам);
			ДатаПоследнейНовости=ВосстановитьЗначение("ДатаПоследнейНовости");
			Новости = Парам.Получить("Новости");
			Конфигурация = Парам.Получить("Конфигурация");
			РОтчеты = Парам.Получить("Отчеты");
			ПоказатьСтраницу("Обновления");
		ИначеЕсли Параметр="ПроверкаКалендаря" Тогда
			ОткрытьФорму("Отчет.ИППКалендарь#Проверка","Проверка");
		ИначеЕсли Параметр="РегламентированныеОтчеты" Тогда
			Парам = СоздатьОбъект("СписокЗначений");
			Парам.ДобавитьЗначение(1);  
			ОткрытьФорму("Обработка."+Параметр,Парам);
		Иначе	
			ОткрытьФорму("Обработка."+Параметр);
		КонецЕсли;
	ИначеЕсли Команда="ФВО" Тогда  // открыть Форму Внешней Обработки
		Имя = КаталогИБ()+"ExtForms\"+Параметр;
		Если ФС.СуществуетФайл(Имя)=1 Тогда
			ОткрытьФорму("Отчет",,Имя);
		КонецЕсли;
	ИначеЕсли Команда="КОМ" Тогда
		// Вызов команды
		ОбработкаЯчТаблицы("Таб_"+Параметр);
	ИначеЕсли Команда="ПРО" Тогда
		// Вызов процедуры
		Если Параметр="ОбновитьЯщик" Тогда
			ОбновитьЯщик();
		КонецЕсли;
		Состояние("Обновление списка писем");
		ПоказатьСтраницу("Получить");
	ИначеЕсли Команда="ТХТ" Тогда
		ПоказатьПисьмо(Число(Параметр));
	ИначеЕсли ВРег(Команда)="WWW" Тогда
		ЗапуститьПриложение(Зн);
	ИначеЕсли ВРег(Команда)="НОВ" Тогда
		ТекущееВремя(Ч,М,С);
		ДатаПросмотраНовостей = Формат(ТекущаяДата(),"ДГГГГММДД")+Формат(Ч,"Ч(0)2")+Формат(М,"Ч(0)2")+Формат(С,"Ч(0)2");
		Новости = "";
		ЗапуститьПриложение(Параметр);
		ПоказатьСтраницу(ТекущаяСтраница);
		СохранитьЗначение( "ДатаПросмотраНовостей",ДатаПросмотраНовостей);
	ИначеЕсли Команда="РЕГ" Тогда 
		ОбработкаЯчТаблицы("ВОТ_"+Параметр);
		РОтчеты = "";
		ПоказатьСтраницу("Обновления");
	ИначеЕсли Команда="КОН" Тогда 
		Если Параметр="Получить" Тогда     
			// Релиз конфигурации 
			Если ЗагрузитьКонфигурацию(глИдентификаторКонфигурации(),"update.exe")=1 Тогда // Константа.Идентификатор
				Конфигурация = "";
				ПоказатьСтраницу("Обновления");
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ВРег(Команда)="ПРН" Тогда
		Если Параметр="Проверять" Тогда
			ПроверятьНовости = ?(ПроверятьНовости=1,2,1);
			ПоказатьСтраницу("Обновления");
		ИначеЕсли (Параметр="Включение")И(ПроверятьНовости=2) Тогда
			Включение = 2;
			ЧерезНДней = 1;
			ПоказатьСтраницу("Обновления");
		ИначеЕсли (Параметр="ЧерезНДней")И(ПроверятьНовости=2) Тогда
			ЧерезНДней = 2;
			Включение = 1;
			ПоказатьСтраницу("Обновления");
		ИначеЕсли (Параметр="НДней")И(ПроверятьНовости=2)И(ЧерезНДней=2) Тогда
			Дн = НДней;
			Если ВвестиЧисло(Дн,"Введите число дней",2,0,80)=1 Тогда 
				НДней = Дн;
				ПоказатьСтраницу("Обновления");
			КонецЕсли;
		ИначеЕсли Параметр="Проверить" Тогда 
			Соединение.ПолучитьКакСтроку("http://www.1c.ru/buhplace/lastnewstime.asp", ДатаПоследнейНовости);
			ДатаПоследнейНовости = Лев(ДатаПоследнейНовости,СтрДлина(ДатаПоследнейНовости)-3);
			ПоказатьСтраницу("Обновления");
		КонецЕсли;
	ИначеЕсли ВРег(Команда)="ВАЛ" Тогда
		Если Параметр="ВалАвтоПолучать" Тогда 
			ВалАвтоПолучать=?(ВалАвтоПолучать=1,2,1);
			ПоказатьСтраницу("Обновления");
		КонецЕсли;
	ИначеЕсли ВРег(Команда)="БИК" Тогда 
		Если Вопрос("Начать получение справочника?",4,30)=6 Тогда
			КаталогБазы=КаталогИБ()+"ExtDb\";
			Параметры=СоздатьОбъект("СписокЗначений");
			Параметры.ДобавитьЗначение(КаталогБазы,"Каталог базы");
			ОткрытьФормуМодально("Обработка.ИППБИКсРБК", Параметры);
		КонецЕсли;
	ИначеЕсли ВРег(Команда)="ФВП" Тогда
		Опережение = ВосстановитьЗначение("КалендарьОпережение");
		Опережение = ?(ПустоеЗначение(Опережение)=1,7,Опережение);
		Значение = Опережение;
		Если ВвестиЧисло(Значение,"Введите число дней",2,0)=1 Тогда
			Если Опережение <> Значение Тогда
				Опережение = Значение;
				ПоказатьСтраницу("Обновления");
				СохранитьЗначение("КалендарьОпережение",Опережение);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ВРег(Команда)="ФПК" Тогда
		ПроверкаКалендарь = ВосстановитьЗначение("КалендарьПроверка");
		ПроверкаКалендарь = ?(ПроверкаКалендарь=1,2,1);
		СохранитьЗначение("КалендарьПроверка",ПроверкаКалендарь);
		ПоказатьСтраницу("Обновления");
	КонецЕсли;
КонецПроцедуры   // ОбработкаЯчТаблицы
//******************************************************************************
Процедура ОбработкаЯчейкиТаблицы(Зн,Фл)  // Предопределенная
	Перем СтрокаЗначения,ПунктМеню,Н;   
	СтрокаЗначения = "";
	Если Лев(Зн,3)="ВОП" Тогда
		Меню = СоздатьОбъект("СписокЗначений");     
		МенюОпрос.Выгрузить(Меню);
		Если Меню.РазмерСписка()=0 Тогда
			Предупреждение("Нет форм опроса",4);
		ИначеЕсли Меню.РазмерСписка()=1 Тогда
			Зн = ?(ОпросВнешний=1,"ФВО_","ВОТ_")+Меню.ПолучитьЗначение(1);
		ИначеЕсли Меню.ВыбратьЗначение(ПунктМеню,,Н,,1) = 1 Тогда 
			Зн = ?(ОпросВнешний=1,"ФВО_","ВОТ_")+Меню.ПолучитьЗначение(Н);
		КонецЕсли;
	КонецЕсли;
	ОбработкаЯчТаблицы(Зн);
	Фл=0;
КонецПроцедуры  // ОбработкаЯчейкиТаблицы
//******************************************************************************
//	ПриПовторномОткрытии()  // Предопределенная
//
//	Описание:  открывает обработку Интернет-поддержка пользователя с определенной страницы
Процедура ПриПовторномОткрытии()
	Перем Фл;
	Если ПустоеЗначение(Форма.Параметр) = 0 Тогда 
		Если  Форма.Параметр="Получить" Тогда
			ОбработкаЯчейкиТаблицы("Таб_Получить",Фл);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры   // ПриПовторномОткрытии
//******************************************************************************
Функция УстановитьКомпоненту()
	Если ЗагрузитьВнешнююКомпоненту(КаталогИБ()+"ExtForms\v7plus.dll")=0 Тогда
		Если ЗагрузитьВнешнююКомпоненту("v7plus.dll")=0 Тогда
			Сообщить("Не удалось обнаружить компоненту V7Plus.dll!"); 
			Возврат 0;
		КонецЕсли;
	КонецЕсли;
	Попытка
		Соединение = СоздатьОбъект("Addin.V7HttpReader");
		Почта = СоздатьОбъект("AddIn.V7Mail");
		Инфо = СоздатьОбъект("AddIn.V7SysInfo");
		Соединение.КоличествоПопытокАвторизации = 3;
	Исключение 
		Сообщить("Не удалось создать объекты из компоненты V7Plus.dll!"); 
		Возврат 0;
	КонецПопытки; 
	Возврат 1;
КонецФункции  // УстановитьКомпоненту
//******************************************************************************
Процедура ПриОткрытии()  // Предопределенная
	Перем Страница;    
	УстановитьКомпоненту();
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Содержание");
	Секц = Таб.ПолучитьСекцию("Страница");
	// В случае, когда опрос реализован внешними обработками, 
	// (переменная ОпросВнешний=1)
	// можно подключить несколько форм. 
	// Для этого необходимо, чтобы имена файлов удовлетворяли маске: "audit???.ert"
	// где ??? - порядковый номер опроса (с лидирующими нулями). 
	Если ОпросВнешний=1 Тогда
		// опрос реализован как внешеняя обработка
		ФГ=СоздатьОбъект("ФС");
		ИмяФ = ФГ.НайтиПервыйФайл(КаталогИБ()+"ExtForms\"+"audit*.ert");
		Н=0;
		Пока СокрЛП(ИмяФ)<>"" Цикл
			Н=Н+1;
			МенюОпрос.ДобавитьЗначение(ИмяФ,"Опрос "+Н);
			ИмяФ = ФГ.НайтиСледующийФайл();
		КонецЦикла;
	Иначе
		// опрос реализован как внутренняя обработка "ОпросПользователейБухгалтерии"
		МенюОпрос.ДобавитьЗначение("ОпросПользователейБухгалтерии","Опрос пользователей 1С:Бухгалтерии 7.7");
		// Вставьте аналогичные предыдущей строки для добавления других форм опроса
	КонецЕсли;
	
	СекцияЗаголовок=Таблица.ПолучитьСекцию("Заголовок");
	СекцияШапка=Таблица.ПолучитьСекцию("Шапка");
	СекцияПолучить=Таблица.ПолучитьСекцию("Получить");
	СекцияСайт=Таблица.ПолучитьСекцию("Сайт");
	СекцияОбновления=Таблица.ПолучитьСекцию("Обновления");
	СекцияРазделитель=Таблица.ПолучитьСекцию("Разделитель");
	ЗаголовокПисьма=Таблица.ПолучитьСекцию("ЗаголовокПисьма");
	Письмо=Таблица.ПолучитьСекцию("Письмо");
	ЕстьПисьма=Таблица.ПолучитьСекцию("ЕстьПисьма");
	СекцияТекст1=Таблица.ПолучитьСекцию("Текст1");

	ОбновлятьЯщик=ВосстановитьЗначение( "ОбновлятьЯщик" );
	Старт=ВосстановитьЗначение("Главная страница Письма");
	Старт = ?(ПустоеЗначение(Старт)=1,"Введение",Старт);
	ПоказПриЗапуске=ВосстановитьЗначение("ПоказПисьмаПриЗапуске");
	ДатаПросмотраНовостей=ВосстановитьЗначение("ДатаПросмотраНовостей");
	ДатаПоследнейНовости=ВосстановитьЗначение("ДатаПоследнейНовости");
	ВалАвтоПолучать=ВосстановитьЗначение("ВалАвтоПолучать");
	ВалАвтоПолучать=?(ПустоеЗначение(ВалАвтоПолучать)=1,1,ВалАвтоПолучать);
	ПроверятьНовости=ВосстановитьЗначение("НовостиПроверять");
	ПроверятьНовости=?(ПустоеЗначение(ПроверятьНовости)=1,1,ПроверятьНовости);
	Включение=ВосстановитьЗначение("НовостиВключение"); 
	Включение=?(ПустоеЗначение(Включение)=1,1,Включение);
	ЧерезНДней = ?(Включение=1,2,1);
	НДней=ВосстановитьЗначение("НовостиНДней");
	НДней=?(ПустоеЗначение(НДней)=1,7,НДней);
	Опережение = ВосстановитьЗначение("КалендарьОпережение");
	Опережение = ?(ПустоеЗначение(Опережение)=1,7,Опережение);
	ПроверкаКалендарь = ВосстановитьЗначение("КалендарьПроверка");
	
	Сп = Форма.Параметр;
	Если ПустоеЗначение(Сп)=0 Тогда
		Новости = Сп.Получить("Новости");
		Конфигурация = Сп.Получить("Конфигурация");
		РОтчеты = Сп.Получить("Отчеты");
		Если Сп.Получить("Закрывать")=1 Тогда
			Конт = Сп.Получить("Контекст");
			Конт.Форма.Закрыть();
		КонецЕсли;
		Старт = "Обновления";
	КонецЕсли;
	
	ВходящиеП = СоздатьОбъект("ТаблицаЗначений");
	ВходящиеП.НоваяКолонка("ДатаСтр","Строка",30,,"Дата",30,,1);
	ВходящиеП.НоваяКолонка("ДатаВр","Строка",30,,"Дата",15,,1);
	ВходящиеП.НоваяКолонка("Тема","Строка",100,0,"Тема",30,,1);
	ВходящиеП.НоваяКолонка("Текст","Текст",,0,"",0,,1);
	ВходящиеП.НоваяКолонка("Адрес","Строка",60,,"",0,,1);
	ЧислоПисем = 0;
	АдресОтправителя = "line@1c.ru"; 
	ОбновлятьЯщик = ?(ПустоеЗначение(ОбновлятьЯщик)=1,2,ОбновлятьЯщик);
	Если ОбновлятьЯщик=1 Тогда
		ОбновитьЯщик();
	КонецЕсли;
	Если Форма.Параметр="Получить" Тогда
	    Старт="Получить"; 
	КонецЕсли;
	ПадежиДней[1]="день";
	ПадежиДней[2]="дня";
	ПадежиДней[3]="дня";
	ПадежиДней[4]="дня";
	Для К=5 По 10 Цикл
		ПадежиДней[К]="дней";
	КонецЦикла; 
	
	Если ПустоеЗначение(Сп)=0 Тогда
		Если Сп.НайтиЗначение("КОН_Получить")>0 Тогда
			Сп.ДобавитьЗначение(ЗагрузитьКонфигурацию(глИдентификаторКонфигурации(),"update.exe"),"Результат");				
			СтатусВозврата(0);
			Возврат;		
		КонецЕсли;
	КонецЕсли;
	
	ПоказатьСтраницу(Старт);
	
КонецПроцедуры   // ПриОткрытии
//******************************************************************************
Процедура ПриЗакрытии()  // Предопределенная
	СохранитьЗначение( "ОбновлятьЯщик",ОбновлятьЯщик);
	СохранитьЗначение( "ПоказПисьмаПриЗапуске",ПоказПриЗапуске);
	СохранитьЗначение( "Главная страница Письма",ТекущаяСтраница);
	СохранитьЗначение( "ДатаПросмотраНовостей",ДатаПросмотраНовостей);
	СохранитьЗначение( "НовостиВключение",Включение);
	СохранитьЗначение( "НовостиПроверять",ПроверятьНовости);
	СохранитьЗначение( "ВалАвтоПолучать",ВалАвтоПолучать);
	СохранитьЗначение( "НовостиНДней",НДней);
	СохранитьЗначение( "ДатаПоследнейНовости",ДатаПоследнейНовости);
	Если (Включение=1)И(ПроверятьНовости=2) Тогда
		ДеньВключения=ТекущаяДата()+НДней;
		СохранитьЗначение( "НовостиДеньВключения",ДеньВключения);
	КонецЕсли;
	СохранитьЗначение( "НовыйНомерРелизаКонфигурации",НовыйНомерРелизаКонфигурации);
КонецПроцедуры  // ПриЗакрытии
//******************************************************************************
МенюОпрос = СоздатьОбъект("СписокЗначений");
ОпросВнешний=1;
