//*******************************************
Процедура Сформировать()
          
	Спр = СоздатьОбъект("Справочник.ОсновныеСредства"); 
	ПраваяЧисловаяЧастьКода = "";
	ВнКод = СокрЛП(Код);
	Если ПустоеЗначение(ВнКод) = 1 Тогда
	    Предупреждение("Укажите начальный инвентарный номер",60);
		Возврат;
	КонецЕсли;
	ЧисловаяЧастьКода = "";
	Максимум = "";
	Разрядов = 0;
	Символ = Прав(ВнКод,1);
	Пока Найти("0123456789", Символ) > 0 Цикл
	    ЧисловаяЧастьКода = Символ + ЧисловаяЧастьКода; 
		Максимум = Максимум + "9";
		Разрядов = Разрядов + 1;
		Если СтрДлина(ВнКод) > Разрядов Тогда
		    Символ = Сред(ВнКод,СтрДлина(ВнКод) - Разрядов ,1);
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	СтроковаяЧастьКода = Лев(ВнКод, СтрДлина(ВнКод) - Разрядов); 
	ЧисловаяЧастьКода = Число(ЧисловаяЧастьКода); 
	Максимум = Число(Максимум);
	Первый = "";
	Если (ЧисловаяЧастьКода + ЧислоЭл - 1) > Максимум Тогда
	    Предупреждение("Недостаточное количество числовых разрядов в начальном инвентарном номере",60);
		Возврат;
	КонецЕсли;
	НачатьТранзакцию(); 
	Для Н = 1 по ЧислоЭл Цикл
		Спр.Новый();
		Спр.Код = СтроковаяЧастьКода + Формат(ЧисловаяЧастьКода,"Ч(0)"+Строка(Разрядов));
		ЧисловаяЧастьКода = ЧисловаяЧастьКода + 1;
		Спр.Наименование = Наименование;
		Спр.Родитель = Группа;
		Спр.ИспользоватьДату('01.01.1980');
		Спр.НачислятьАмортизацию = 0;
		Спр.Собственное = 1;
		Спр.Группа = Перечисление.ГруппыОС.МашиныИОборудование;
		Спр.Подразделение = глЗначениеПоУмолчанию("ОсновноеПодразделение");
		Спр.ПодлежитАмортизации = 1;
		Спр.СпособНачисленияАмортизации = Перечисление.СпособыНачисленияАмортизацииОС.Линейный;
		Спр.МетодНачисленияАмортизации = Перечисление.МетодыНачисленияАмортизации.Линейный;
		Спр.СпециальныйКоэффициент = 1;
		Попытка
			Спр.Записать();
		Исключение
			ОтменитьТранзакцию();
			Предупреждение("Невозможно создать элемент с инвентарным номером "+Спр.Код,60);
			Возврат;
		КонецПопытки;         
		Если ПустоеЗначение(Первый) = 1 Тогда
		    Первый = Спр.ТекущийЭлемент();
		КонецЕсли;
	КонецЦикла;
	ЗафиксироватьТранзакцию();
	Форма.Параметр = Первый;
	Форма.Закрыть();
КонецПроцедуры

//*******************************************
Процедура ПриВыбореГруппы()
	Если Группа.ЭтоГруппа() = 0 Тогда
	    Группа = "";
	КонецЕсли;
КонецПроцедуры

//*******************************************
Процедура ПриначалеВыбораЗначения(ЭлементДиалога, Флаг);
	Если ЭлементДиалога = "Группа" Тогда
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(1,"Группы");
	    ОткрытьФорму("Справочник.ОсновныеСредства.ДляВыбора", Параметры);
		Если ТипЗначенияСтр(Параметры) = "Справочник" Тогда
			Если Параметры.Вид() = "ОсновныеСредства" Тогда
				Если Параметры.ЭтоГруппа() = 1 Тогда
					Группа = Параметры;
				Иначе
					Группа = "";
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Флаг = 0;
	КонецЕсли;
КонецПроцедуры

//*******************************************
Процедура ПриОткрытии()
	Спр = СоздатьОбъект("Справочник.ОсновныеСредства");
	Спр.Новый();
	Код = Спр.Код;
    ЧислоЭл = 1;
	Форма.Группа.ВыборГруппы(1);
	Если ПустоеЗначение(Форма.Параметр) = 0 Тогда
		Если Форма.Параметр.ЭтоГруппа() = 1 Тогда
		    Группа = Форма.Параметр;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
