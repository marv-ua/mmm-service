Перем Содержание, НеобходимостьОткрытия, НеобходимостьОткрытияПрежн, ТекущаяСтраница, СтартовыйНомерСтраницы;
Перем Версия, ДатаОбновления;
Перем ТабГородов, Индекс;

// Функция обрабатывает входные параметры
//*****************************************************************************
Функция Параметры()
	Рез = 1;
	Если ПустоеЗначение(Форма.Параметр)=0 Тогда
		Если ТипЗначенияСтр(Форма.Параметр)="СписокЗначений" Тогда
			Если НРег(СокрЛП(Форма.Параметр.Получить("Режим")))="опрос" Тогда
	    		Парам = Форма.Параметр;
				Парам.ДобавитьЗначение(Версия,"Версия");
				Парам.ДобавитьЗначение(ДатаОбновления,"ДатаОбновления");
				Рез = 0;
    		КонецЕсли;
		ИначеЕсли ТипЗначенияСтр(Форма.Параметр)="Число" Тогда
			СтартовыйНомерСтраницы = Форма.Параметр;
			Рез = 1;
		КонецЕсли;
	КонецЕсли;
	Возврат Рез;
КонецФункции  // Параметры 

//*****************************************************************************
Процедура ВыводСекции(Таб1,Таб2,ВД)
	Перем НомерСтроки;
	Если ВД="Москва" Тогда 
		Таб2.ВывестиСекцию(Таб1.ПолучитьСекцию("ШапкаМосква")); 
		
		Таб1.ИсходнаяТаблица("Москва");
		СекцияМосква = Таб1.ПолучитьСекцию("Москва");
		Для Н=1 По Таб1.ВысотаСекции("Москва") Цикл
			N = Н;
			Наименование = СекцияМосква.Область("R"+Н+"C1").Текст;
			Телефон = СекцияМосква.Область("R"+Н+"C2").Текст;
			Метро = СекцияМосква.Область("R"+Н+"C3").Текст;
			V8= ?(СекцияМосква.Область("R"+Н+"C4").Текст="0","",СекцияМосква.Область("R"+Н+"C4").Текст);
			Б = ?(СекцияМосква.Область("R"+Н+"C5").Текст="0","",СекцияМосква.Область("R"+Н+"C5").Текст);
			Т = ?(СекцияМосква.Область("R"+Н+"C6").Текст="0","",СекцияМосква.Область("R"+Н+"C6").Текст);
			Р = ?(СекцияМосква.Область("R"+Н+"C7").Текст="0","",СекцияМосква.Область("R"+Н+"C7").Текст);
			П = ?(СекцияМосква.Область("R"+Н+"C8").Текст="0","",СекцияМосква.Область("R"+Н+"C8").Текст);
			Таб1.ИсходнаяТаблица("ЦСОДетали");
			СекцияГород = Таб1.ПолучитьСекцию("СтрокаМосква");
			СекцияГород.Область("R1C2:R1C26").ЦветФона(?(N%2=1,16777215,15793151));
			Таб2.ВывестиСекцию(СекцияГород); 
			Таб1.ИсходнаяТаблица("Москва");
		КонецЦикла;
	Иначе 
		НомерСтроки = 0;
		Таб2.ВывестиСекцию(Таб1.ПолучитьСекцию("ШапкаГород")); 
		Если Индекс.НайтиЗначение(ВД,НомерСтроки,"Буква")=0 Тогда
		Иначе 
			Индекс.ПолучитьСтрокуПоНомеру(НомерСтроки);
			N=1;
			Для Н=Индекс.Индекс По Индекс.Индекс+Индекс.Количество-1 Цикл
				ТабГородов.ПолучитьСтрокуПоНомеру(Н);
				Город = ТабГородов.Город;
				Наименование = ТабГородов.Наименование;
				Телефон = ТабГородов.Телефон;
				V8= ?(ТабГородов.V8=0,"",ТабГородов.V8);
				Б = ?(ТабГородов.Б=0,"",ТабГородов.Б);
				Т = ?(ТабГородов.Т=0,"",ТабГородов.Т);
				Р = ?(ТабГородов.Р=0,"",ТабГородов.Р);
				П = ?(ТабГородов.П=0,"",ТабГородов.П);
				СекцияГород = Таб1.ПолучитьСекцию("СтрокаГород");
				СекцияГород.Область("R1C2:R1C26").ЦветФона(?(N%2=1,16777215,15793151));
				Таб2.ВывестиСекцию(СекцияГород);
				N = N+1;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;		
	Таб1.ИсходнаяТаблица("ЦСОДетали");
КонецПроцедуры  // ВыводСекции 

//*****************************************************************************
// ПодготовкаДанных(ИмяФайла="",ВыбДата="") 
//
// Описание: Сформировать таблицу событий
// Параметры:	ИмяФайла - имя исходного файла с данными, 
//				ВыбДата - дата, за которую необходимо вывести данные, 
//							если не указано число, то выводятся все данные за месяц
// 
Процедура ПодготовкаДанных(ВыбДата="")
	Таб1 = СоздатьОбъект("Таблица");
	Таб2 = СоздатьОбъект("Таблица");

	ДопТаблица=0;

	Таб1.ИсходнаяТаблица("ЦСОДетали");
	
	Если ПустоеЗначение(ВыбДата)=0 Тогда 
		Попытка 
			ВыводСекции(Таб1,Таб2,ВыбДата);
		Исключение
		КонецПопытки; 
	Иначе
		Таб2.ВывестиСекцию(Таб1.ПолучитьСекцию("ШапкаГород"));
	КонецЕсли; 
	Таб2.ВывестиСекцию(Таб1.ПолучитьСекцию("Подвал"));
	
	Таб2.Опции(0,0,4,0,,"Опции");  
	Таб2.ТолькоПросмотр(1);
	Таб2.Показать("ЦСО по городам ("+ВыбДата+")");
КонецПроцедуры  // ПодготовкаДанных   

//*****************************************************************************
Функция ПолучитьИндекс(Позиция)
	Перем Стр;
	Стр = 0;
	Содержание.НайтиЗначение(Позиция,Стр,"Позиция");
	Возврат Стр;
КонецФункции

//*****************************************************************************
Функция ВыборСтраницы(Содержание) 
	Перем  Час, Мн, Сек;
	
	ТекущееВремя(Час, Мн, Сек);
	
	Если Содержание.КоличествоСтрок()>1 Тогда
		ТМП = Число(Час)*60*60+Число(Мн)*60+Число(Сек);
		Индекс = ТМП - Цел(ТМП/Содержание.КоличествоСтрок())*Содержание.КоличествоСтрок()+1;
	Иначе
		Индекс = 0;
	КонецЕсли;
	Возврат Индекс;
КонецФункции 

//*****************************************************************************
Процедура ПрисоединитьТаблицу(ИндексСтраницы)
	Если ИндексСтраницы<>0 Тогда
		Содержание.ПолучитьСтрокуПоНомеру(ИндексСтраницы);
		Таб = СоздатьОбъект("Таблица");
		Таб.ИсходнаяТаблица(Содержание.ИмяТаблицы);
		Секц = Таб.ПолучитьСекцию("Страница");
		
		Таблица.ВывестиСекцию(Секц);
	КонецЕсли;
КонецПроцедуры

//*****************************************************************************
Процедура ВывестиСтраницу(ИндексСтраницы)
	Перем Индекс;
	Таблица.Очистить();
	Таблица.ИсходнаяТаблица("Шаблоны");
	
	Таб = СоздатьОбъект("Таблица");
	
	
	Таблица.ПрисоединитьСекцию("Заголовок|Главная");
	
	ЧислоПунктов = 0;
	Для К=1 По Содержание.КоличествоСтрок() Цикл
		Содержание.ПолучитьСтрокуПоНомеру(К);
		Если ПустаяСтрока(Содержание.ИмяТаблицы)=0 Тогда
			Наименование = Содержание.Название;
			Таблица.ПрисоединитьСекцию("Заголовок|Пункт");
			ЧислоПунктов = ЧислоПунктов + 1;
		КонецЕсли;
	КонецЦикла;
	Для К=ЧислоПунктов+1 По 4 Цикл
			Таблица.ПрисоединитьСекцию("Заголовок|Пустая"); 
		КонецЦикла;

	Если ТипЗначенияСтр(ИндексСтраницы)="Строка" Тогда
		Если ИндексСтраницы="Главная" Тогда
			Для Н=1 По Содержание.КоличествоСтрок() Цикл
				Содержание.ПолучитьСтрокуПоНомеру(Н);
				
				Попытка
					Таблица.ВывестиСекцию(Содержание.ИмяСекции+"|Баннер");
					Таблица.ПрисоединитьСекцию("Бегун" + Н + "|Рамблер");
				Исключение КонецПопытки;
				Индекс = "Главная";
			КонецЦикла;   
			Если Содержание.КоличествоСтрок() < 4 Тогда
			     Пока Н <= 4 Цикл
			     Таблица.ВывестиСекцию("Бегун" + Н + "|Баннер"); 
				 Таблица.ПрисоединитьСекцию("Бегун" + Н + "|Рамблер");
				 Н=Н+1;
			     КонецЦикла;
			КонецЕсли;			
		Иначе
			Если Содержание.НайтиЗначение(ИндексСтраницы,Индекс,"ИмяТаблицы")=0 Тогда
				Возврат;
			КонецЕсли;
			Таблица.ПрисоединитьСекцию("Заголовок|Проба"); 
			ПрисоединитьТаблицу(Индекс);
		КонецЕсли;
	Иначе
		Индекс = ИндексСтраницы;
		Таблица.ПрисоединитьСекцию("Заголовок|Проба"); 
		ПрисоединитьТаблицу(Индекс);
	КонецЕсли;
	
	

	Таблица.ВывестиСекцию("Подвал");
	
	Таблица.ВывестиСекцию("Настройка");
	
	Таблица.Опции(0,0,1,0,,"Опции");  
	Таблица.ТолькоПросмотр(1);
	Таблица.Показать();
    Форма.Заголовок("Информационный блок",0);
	ТекущаяСтраница = Индекс;
КонецПроцедуры

//*****************************************************************************
Процедура ПриОткрытии() 
	
	Если Параметры()=0 Тогда
		// это запрос параметров, выполнять не нужно
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	НомерСтраницы = СтартовыйНомерСтраницы;
	
	НеобходимостьОткрытия = ВосстановитьЗначение("НеобходимостьОткрытияИнформационныйБлок");
	НеобходимостьОткрытия = ?(ПустоеЗначение(НеобходимостьОткрытия)=1,2,НеобходимостьОткрытия);
	НеобходимостьОткрытияПрежн = НеобходимостьОткрытия;
	
	ДеньМесяца = ДатаЧисло(РабочаяДата());
	ДатаОткрытияИнформационныйБлок = ВосстановитьЗначение("ДатаОткрытияИнформационныйБлок");
	ДатаОткрытияИнформационныйБлок = ?(ПустоеЗначение(ДатаОткрытияИнформационныйБлок)=1,'01.01.1980', Дата(ДатаОткрытияИнформационныйБлок));
	
	
	Если НеобходимостьОткрытия <> 2 Тогда////
		Если (((ДеньМесяца=1) или (ДеньМесяца=15)) и (РабочаяДата() <> ДатаОткрытияИнформационныйБлок)) 
		или (((ДеньМесяца<15) и (ДатаОткрытияИнформационныйБлок <> НачМесяца(РабочаяДата()))) 
		или ((ДеньМесяца>=15) и (ДатаОткрытияИнформационныйБлок <> Дата(ДатаГод(РабочаяДата()),ДатаМесяц(РабочаяДата()),15)))) Тогда
			НеобходимостьОткрытия = 2;
		КонецЕсли;
	КонецЕсли;                                                
	
	
	Индекс = СоздатьОбъект("ТаблицаЗначений");
	Индекс.НоваяКолонка("Буква","Строка",1);
	Индекс.НоваяКолонка("Индекс","Число",3);
	Индекс.НоваяКолонка("Количество","Число",3);
	
	ТабГородов = СоздатьОбъект("ТаблицаЗначений");
	ТабГородов.НоваяКолонка("Город","Строка"); 	
	ТабГородов.НоваяКолонка("Наименование","Строка");	
	ТабГородов.НоваяКолонка("Телефон","Строка");	
	ТабГородов.НоваяКолонка("V8","Число",3,0);	
	ТабГородов.НоваяКолонка("Б","Число",3,0);	
	ТабГородов.НоваяКолонка("Т","Число",3,0);	
	ТабГородов.НоваяКолонка("Р","Число",3,0);
	ТабГородов.НоваяКолонка("П","Строка",3,0);
	
	ПерваяБуква = "А";  
	Индекс.НоваяСтрока();
	Индекс.Буква = ПерваяБуква;
	Индекс.Индекс = 1;
	Индекс.Количество = 0;
	
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Города");  
	СекцияГорода = Таб.ПолучитьСекцию("Города");
	Для Н=2 По Таб.ВысотаСекции("Города") Цикл
		ТабГородов.НоваяСтрока();
		ТабГородов.Город = СекцияГорода.Область("R"+Н+"C1").Текст; 
		ТабГородов.Наименование = СекцияГорода.Область("R"+Н+"C2").Текст;
		ТабГородов.Телефон = СекцияГорода.Область("R"+Н+"C3").Текст;
		ТабГородов.V8 = СекцияГорода.Область("R"+Н+"C4").Текст;
		ТабГородов.Б = СекцияГорода.Область("R"+Н+"C5").Текст;
		ТабГородов.Т = СекцияГорода.Область("R"+Н+"C6").Текст;
		ТабГородов.Р = СекцияГорода.Область("R"+Н+"C7").Текст;
		ТабГородов.П = СекцияГорода.Область("R"+Н+"C8").Текст;
		Буква = Врег(Лев( ТабГородов.Город,1));
		Если Буква <> ПерваяБуква Тогда
			Индекс.НоваяСтрока();
			Индекс.Буква = Врег(Лев( ТабГородов.Город,1));
			ПерваяБуква = Врег(Лев( ТабГородов.Город,1));
			Индекс.Индекс = Н-1; 
			Индекс.Количество = 1; 
		Иначе
			Индекс.Количество = Индекс.Количество + 1;
		КонецЕсли;
	КонецЦикла;
	
	
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Описатель");
	Секц = Таб.ПолучитьСекцию("ДанныеОписателя");
	Позиция = 0;
	Для Н=1 По Таб.ВысотаСекции("ДанныеОписателя") Цикл
		Содержание.НоваяСтрока();
		Содержание.Название = Секц.Область("R"+Н+"C2").Текст;
		Содержание.ИмяТаблицы = Секц.Область("R"+Н+"C3").Текст;
		Содержание.ИмяСекции = Секц.Область("R"+Н+"C4").Текст;
		ДатаН = Секц.Область("R"+Н+"C5").Текст;
		Если ПустаяСтрока(ДатаН)=1 Тогда
			ДатаНач = Дата(0);
		Иначе
			ДатаНач = Дата(Лев(ДатаН,4),Сред(ДатаН,5,2),Прав(ДатаН,2));
		КонецЕсли;
		
		Если ДатаНач > ТекущаяДата() Тогда
			Содержание.УдалитьСтроку();
		Иначе
			Содержание.ДатаНач = ДатаНач;
			ДатаК = Секц.Область("R"+Н+"C6").Текст;
			Если ПустаяСтрока(ДатаК)=1 Тогда
				ДатаК = Дата(2199,12,31);
			Иначе
				ДатаК = Дата(Лев(ДатаК,4),Сред(ДатаК,5,2),Прав(ДатаК,2));
			КонецЕсли;
			Если ДатаК < ТекущаяДата() Тогда
				Содержание.УдалитьСтроку();
			Иначе
				Содержание.ДатаКон = ДатаК;
			КонецЕсли;
		КонецЕсли;
		Если ПустаяСтрока(Содержание.ИмяТаблицы)=0 Тогда
			Позиция = Позиция + 1;
			Содержание.Позиция = Позиция;
		КонецЕсли;
	КонецЦикла;
	
	Если Содержание.КоличествоСтрок()=0 Тогда 
		СтатусВозврата(0);
	ИначеЕсли ПустоеЗначение(НомерСтраницы) = 1 Тогда
		ИмяСтраницы = "Главная";
		Если Содержание.КоличествоСтрок()=1 Тогда
			Содержание.ПолучитьСтрокуПоНомеру(1);
			ИмяСтраницы = Содержание.ИмяТаблицы;
		КонецЕсли;
		ВывестиСтраницу(ИмяСтраницы); 
	Иначе
		ВывестиСтраницу(ПолучитьИндекс(НомерСтраницы));
	КонецЕсли; 
	
КонецПроцедуры

//******************************************************************************
Процедура ОткрытьВложенную(ИмяСтраницы)
	Табл = СоздатьОбъект("Таблица");
	Табл.ИсходнаяТаблица(ИмяСтраницы);
	Табл.ВывестиСекцию("Страница");
	Табл.Опции(0,0,0,0,,"ДопОпции");  
	Табл.ТолькоПросмотр(1);
	Табл.Показать();
КонецПроцедуры 

//******************************************************************************
Процедура ОбработкаЯчейкиТаблицы(Зн,Фл, Табл, Адрес)  // Предопределенная
	Перем СтрокаЗначения,ПунктМеню,Н;   
	СтрокаЗначения = "";
	Если Зн="Главная" Тогда
		// показ основной страницы (формируемой из таблицы "Шаблоны")
		ВывестиСтраницу("Главная");
	ИначеЕсли Лев(Зн,3)="Ком" Тогда
		// показ выбранной страницы
		Если Адрес="R1C9" Тогда
			ВывестиСтраницу(ПолучитьИндекс(1));
		ИначеЕсли Адрес="R1C17" Тогда
			ВывестиСтраницу(ПолучитьИндекс(2));
		ИначеЕсли Адрес="R1C25" Тогда
			ВывестиСтраницу(ПолучитьИндекс(3));
		ИначеЕсли Адрес="R1C33" Тогда
			ВывестиСтраницу(ПолучитьИндекс(4));
		ИначеЕсли Адрес="R1C41" Тогда////12.02.08
			ВывестиСтраницу(ПолучитьИндекс(5));
		ИначеЕсли Адрес="R1C49" Тогда////12.02.08
			ВывестиСтраницу(ПолучитьИндекс(6));
		КонецЕсли;
	ИначеЕсли НРег(Лев(Зн,3))="стр" Тогда
		// показ локальной страницы
		ВывестиСтраницу(Сред(Зн,5));              
	ИначеЕсли (НРег(Лев(Зн,3))="www") ИЛИ (НРег(Лев(Зн,4))="http") ИЛИ (НРег(Лев(Зн,6))="mailto") Тогда////25.06.09
		// показ внешних страниц
		ЗапуститьПриложение(Зн);
	ИначеЕсли Лев(Зн,3)="Таб" Тогда
		// показ отдельной таблицы
		ОткрытьВложенную(Сред(Зн,5));
	ИначеЕсли Зн="НеобходимостьОткрытия_" Тогда
		НеобходимостьОткрытия = ?(НеобходимостьОткрытия=1,2,1);
		ВывестиСтраницу(ТекущаяСтраница);
		// Для показа ЦСО
	ИначеЕсли Лев(Зн,6)="Ссылка" Тогда 
		Значение = Сред(Зн,8);
		ПодготовкаДанных(Значение);
	КонецЕсли;
	Фл=0;
КонецПроцедуры  // ОбработкаЯчейкиТаблицы
//******************************************************************************

Процедура ПриЗакрытии()
	Если НеобходимостьОткрытияПрежн<>НеобходимостьОткрытия Тогда
		СохранитьЗначение("НеобходимостьОткрытияИнформационныйБлок",НеобходимостьОткрытия);
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
Версия = 27;//номер релиза (из "3" - BUH_r3.ERT)
ДатаОбновления = "30.01.2010";//приблизительная дата выпуска
СтартовыйНомерСтраницы = 0;

Содержание = СоздатьОбъект("ТаблицаЗначений");
Содержание.НоваяКолонка("Название","Строка");
Содержание.НоваяКолонка("ИмяТаблицы","Строка");
Содержание.НоваяКолонка("ИмяСекции","Строка");
Содержание.НоваяКолонка("ДатаНач","Дата");
Содержание.НоваяКолонка("ДатаКон","Дата");
Содержание.НоваяКолонка("Позиция","Число");

