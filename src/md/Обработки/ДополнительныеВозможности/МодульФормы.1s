//   Обработка позволяет:
//  - поставить в соответствие короткому английскому имени файла строку 
//    с длинным русским описанием файла;
//  - позволить неквалифицированным пользователям легко и свободно работать 
//    с внешними отчетами, обработками, текстовыми и табличными файлами.
// 
//   Основное назначение обработки - упростить "подключение" к конфигурации 
// внешних отчетов и обработок, взятых например, с диска ИТС.
//
//   Обработка работает с файлами отчетов (обработок), текстовых и табличных 
// документов (далее - "внешних файлов"), расположенных только в каталоге 
// ExtForms каталога информационной базы.
// 
//   Для внешнего файла, расположенного в этом каталоге, при помощи любого 
// текстового редактора может быть создан текстовый файл с именем таким же, 
// как и имя внешнего файла, и расширением EFD (External File Description). 
// Этот файл должен содержать строку такого вида:
//
//   Название=<текстовое название файла>
//
//   При открытии обработка осматривает каталог ExtForms и ищет все файлы 
// с расширением EFD.
//   Для найденных файлов проверяется наличие файла с таким же именем 
// и расширением: сначала ERT, если файла c таким расширением нет - тогда TXT, 
// если и такого файла нет - тогда MXL.
//   Если один из файлов с указанным расширением существует, файл EFD 
// просматривается на предмет извлечения параметра "Наименование". Извлеченные 
// наименования и соответствующие им имена внешних файлов помещаются в таблицу 
// значений, которая показывается в диалоге обработки.
//
// Под таблицей значений располагаются 3 кнопки:
//  - кнопка "Открыть" открывает выбранный внешний файл;
//  - кнопка "Закрыть" закрывает обработку;
//  - кнопка "Изменить" предназначена для работы со списком внешних файлов.
//
Перем ТекущийКаталог;
Перем Текст;

//******************************************************************************
Функция УстРасширение(ИмяФайла,Расширение);
	Возврат Лев(ИмяФайла,СтрДлина(ИмяФайла)-4)+"."+Расширение;
КонецФункции

//******************************************************************************
// Запоминает текущее положение курсора в списке файлов.
Процедура СохранитьТекОтчет(ФайлОписанияОтчета="")
	Если ФайлОписанияОтчета="" Тогда
		Если Отчеты.ТекущаяСтрока() = 0 Тогда
			Возврат;
		КонецЕсли;	
		ФайлОписанияОтчета=Отчеты.ПолучитьЗначение(Отчеты.ТекущаяСтрока(),"ФайлОписанияОтчета");
	КонецЕсли;
	СохранитьЗначение("ВнОтчОтчет",ФайлОписанияОтчета);
КонецПроцедуры // СохранитьТекОтчет

//******************************************************************************
// Восстанавливает текущее положение курсора в списке файлов.
Процедура ВосстановитьТекОтчет()
	Если Отчеты.КоличествоСтрок() <> 0 Тогда            
		Стр=0;
		Отчеты.НайтиЗначение(ВосстановитьЗначение("ВнОтчОтчет"),Стр,"ФайлОписанияОтчета");
		Отчеты.ТекущаяСтрока(Макс(1,Стр));
	КонецЕсли;
КонецПроцедуры // ВосстановитьТекОтчет

//******************************************************************************
Процедура ЗаписатьСтроку()
	Текст.Очистить();
	Текст.ДобавитьСтроку("Название="+Отчеты.Название);
	Если ПустаяСтрока(Отчеты.ФайлОписанияОтчета)=1 Тогда
		Отчеты.ФайлОписанияОтчета=Врег(УстРасширение(Отчеты.ФайлОтчета,"efd"));    
	КонецЕсли;
	Текст.Записать(ТекущийКаталог+Отчеты.ФайлОписанияОтчета);
КонецПроцедуры // ЗаписатьСтроку

//******************************************************************************
// Описание:
//   Заполняет таблицу значений информацией о внешних файлах, для которых 
// в каталоге ExtForms найдены файлы описаний.
// 
Процедура Заполнить()
	// Список расширений файлов, с которыми может работать обработка.
	Расширения=СоздатьОбъект("СписокЗначений");
	Расширения.ДобавитьЗначение("ert");
	Расширения.ДобавитьЗначение("txt");
	Расширения.ДобавитьЗначение("mxl");
	
	Отчеты.УдалитьСтроки();
	
	ФайлОписанияОтчета=ФС.НайтиПервыйФайл(ТекущийКаталог+"*.efd");
	Пока ПустаяСтрока(ФайлОписанияОтчета)=0 Цикл
	
		Текст.Открыть(ТекущийКаталог+ФайлОписанияОтчета);
	    
		// Проверяем существование файла с одним из расширений.
		Для СтрокаСписка=1 По Расширения.РазмерСписка() Цикл
			
			// Устанавливаем очередное расширение из списка
			ФайлОтчета=УстРасширение(ФайлОписанияОтчета,Расширения.ПолучитьЗначение(СтрокаСписка));
			
			Если ФС.СуществуетФайл(ТекущийКаталог+ФайлОтчета)=1 Тогда
				
				// Внешний файл, соответствующий файлу описания, найден.
				// Добавляем строку в список внешних файлов
				Отчеты.НоваяСтрока();
				Отчеты.ФайлОтчета=ФайлОтчета;
				Отчеты.ФайлОписанияОтчета=ВРег(ФайлОписанияОтчета);
				
				// Раскрываем описание отчета из файла описания
				Для Инд=1 По Текст.КоличествоСтрок() Цикл
					Стр=Текст.ПолучитьСтроку(1);
					Поз=Найти(Стр,"=");
					Если Поз>0 Тогда
						Имя=Лев(Стр,Поз-1);
						Стр=Сред(Стр,Поз+1);
						Если Имя="Название" Тогда
							Отчеты.Название=Стр;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				// Больше ничего делать не надо
				Прервать;
				
			КонецЕсли;
		КонецЦикла;

		ФайлОписанияОтчета=ФС.НайтиСледующийФайл();
	КонецЦикла;
	
	Отчеты.Сортировать("Название");
	ВосстановитьТекОтчет();
КонецПроцедуры // Заполнить

//******************************************************************************
// Вызывается из кнопки "Открыть".
//
// Открывает выбранный внешний файл.
// 
Процедура Выбрать()
	Если Отчеты.ТекущаяСтрока()=0 Тогда
		Возврат;
	КонецЕсли;
	Если ФС.СуществуетФайл(ТекущийКаталог+Отчеты.ФайлОтчета)=1 Тогда
		Расширение = ВРег(Прав(Отчеты.ФайлОтчета,3));
		Если Расширение = "ERT" Тогда
			ОткрытьФорму("Отчет",,ТекущийКаталог+Отчеты.ФайлОтчета);
		ИначеЕсли Расширение = "MXL" Тогда
			Таб = СоздатьОбъект("Таблица");
			Таб.Открыть(ТекущийКаталог+Отчеты.ФайлОтчета);
	     	Таб.ТолькоПросмотр(1);
	     	Таб.Опции(0, 0, 0, 0);
	     	Таб.Показать(Отчеты.Название);
		ИначеЕсли Расширение = "TXT" Тогда
			Текст = СоздатьОбъект("Текст");
			Текст.Открыть(ТекущийКаталог+Отчеты.ФайлОтчета);
	     	Текст.Показать(Отчеты.Название, "");
		КонецЕсли;
	Иначе
		Предупреждение("Выбранный отчет в каталоге внешних отчетов отсутствует!", 10 );
	КонецЕсли;
КонецПроцедуры // Выбрать

//******************************************************************************
Функция Редактировать()
	Стр=Отчеты.Название;
	Если ВвестиСтроку(Стр,"Введите название " + Отчеты.ФайлОтчета,100)=1 Тогда
		Отчеты.Название=Стр;					    
		ЗаписатьСтроку();
		Возврат 1;
	КонецЕсли;
	Возврат 0;
КонецФункции // Редактировать()

//******************************************************************************
// Вызывается из кнопки "Изменить".
// 
// Описание
//   При нажатии кнопки "Изменить" под ней "повисает" меню из 3-х пунктов: 
// "Изменить название", "Внести в список", "Удалить из списка".
//   Если выбран "Изменить название", выдается диалог для редактирования строки 
// с текстовым  названием внешнего файла. Отредактированное название 
// записывается обратно в EFD-файл.
//   Если выбран "Внести в список", под кнопкой открывается список для выбора, 
// содержащий имена ERT-файлов, еще отсутствующих в списке внешних файлов (это те файлы, 
// для которых еще нет файла EFD). В этот момент выдаются обычные имена файлов.
//   После выбора файла открывается диалог для ввода длинного названия файла. 
//   В итоге для выбранного внешнего файла формируется и записывается файл EFD, 
// содержащий строку в указанном выше формате.
//   Если выбран "Удалить", после дополнительного запроса из списка удаляется 
// выбранный внешний файл, а соответствующий ему EFD-файл удаляется еще и с диска.
//
Процедура Изменить()
	Меню=СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("Изменить","Изменить название");
	Меню.ДобавитьЗначение("Добавить","Внести в список");
	Меню.ДобавитьЗначение("Удалить","Удалить из списка");
	Выбор="";
	Меню.ВыбратьЗначение(Выбор,,,,1);
	Если Выбор="Добавить" Тогда
		СписокФайлов=СоздатьОбъект("СписокЗначений");
		ФайлОтчета=ФС.НайтиПервыйФайл(ТекущийКаталог+"*.ert");
		Пока ПустаяСтрока(ФайлОтчета)=0 Цикл
			Если Отчеты.НайтиЗначение(Врег(УстРасширение(ФайлОтчета,"efd")),,"ФайлОписанияОтчета")=0 Тогда
			    СписокФайлов.ДобавитьЗначение(ФайлОтчета," "+ФайлОтчета+"   ");
			КонецЕсли;
			ФайлОтчета=ФС.НайтиСледующийФайл();
		КонецЦикла;                                     
		
		Если СписокФайлов.РазмерСписка()=0 Тогда
			Предупреждение("В каталоге внешних отчетов нет файлов для добавления в список!");
		    Возврат;
		КонецЕсли;

		Если СписокФайлов.ВыбратьЗначение(ФайлОтчета,,,,2)=0 Тогда
		    Возврат;
		КонецЕсли;

		Отчеты.НоваяСтрока();
		Отчеты.ФайлОтчета=ФайлОтчета;
		Если Редактировать()=1 Тогда
			СохранитьТекОтчет(Отчеты.ФайлОписанияОтчета);
			Заполнить();
		Иначе		    
			Отчеты.УдалитьСтроку();
		КонецЕсли;
	ИначеЕсли Выбор="Удалить" Тогда  
		Если Отчеты.ТекущаяСтрока()=0 Тогда
		    Возврат;
		КонецЕсли;
		Если Вопрос("Удалить описание отчета (обработки)","Да+Нет")="Да" Тогда
			ФС.УдалитьФайл(ТекущийКаталог+Отчеты.ФайлОписанияОтчета);
			СохранитьТекОтчет();
			Заполнить();
		КонецЕсли;
	ИначеЕсли Выбор="Изменить" Тогда    
		Если Отчеты.ТекущаяСтрока()=0 Тогда
		    Возврат;
		КонецЕсли;   
		Если Редактировать()=1 Тогда
			СохранитьТекОтчет();
			Заполнить();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // Изменить

//******************************************************************************
// Вызывается из кнопки "Обновить".
// 
// Описание
//   При нажатии кнопки "Обновить" выполняется повторный анализ каталога ExtForms
// и заполненяется список внешних файлов.
//   Предназначена для обновления списка после установки новых отчетов (обработок),
// например, с диска ИТС.
//
Процедура Обновить()
	СохранитьТекОтчет();
	Заполнить();
КонецПроцедуры // Обновить

//******************************************************************************
Процедура ПриОткрытии() // Предопределенная процедура
	Отчеты.НоваяКолонка("Название","Строка",,,,30);
	Отчеты.НоваяКолонка("ФайлОтчета","Строка",,,"Файл",10);
	Отчеты.НоваяКолонка("ФайлОписанияОтчета","Строка");
	Отчеты.ВидимостьКолонки("ФайлОписанияОтчета",0);
	Заполнить();
КонецПроцедуры // ПриОткрытии

//******************************************************************************
Процедура ПриЗакрытии() // Предопределенная процедура
	СохранитьТекОтчет();
КонецПроцедуры // ПриЗакрытии

ТекущийКаталог=КаталогИБ()+"ExtForms\";
Текст=СоздатьОбъект("Текст");