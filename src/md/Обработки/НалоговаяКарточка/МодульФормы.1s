////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем Год; // год формирования карточки
Перем ИмяФайлаСохраненияОтчета;
Перем Резидент; // "1" - резидент, "2" - нерезидент
Перем ИменаИтоговКарточки;
Перем ТекСотрудник;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//******************************************************************************
Функция ЧислоВСтроку(Парам)
	
	Возврат СокрЛП(Формат(Парам, "Ч020.2."));
	
КонецФункции // ЧислоВСтроку()

//******************************************************************************
Процедура ПриВыбореСотрудника()
	
	// Очистка "желтых ячеек".
	НомПФР = "";
	Р2_2 = "";
	Р2_4 = "";
	Р2_41 = "";
	СтранаСотрудника = "";
	_АС_Индекс = "";
	_АС_Регион = "";
	_АС_Район = "";
	_АС_Город = "";
	_АС_НП = "";
	_АС_Улица = "";
	_АС_Дом = "";
	_АС_Корпус = "";
	_АС_Квартира = "";
	Р2_8 = 1;
	Р2_10 = "";
	П145 = "";
	П147 = "";
	П148 = "";
	П149 = "";
	П151 = "";
	П426 = "";
	П427 = "";
	П428 = "";
	П429 = "";
	П430 = "";
	П431 = "";
	П432 = "";
	П433 = "";
	
	// Если сотрудник указан, то известные данные заполняем.
	Если Сотрудник.Выбран() = 1 Тогда
		Р2_3 = Сотрудник.Наименование;
		Р2_1 = Сотрудник.ИНН;
		Р2_11 = Сотрудник.Должность;
		ДатаРождения = Сотрудник.ДатаРождения;
		ДокументСерияНомер = СокрП(Сотрудник.ДокументСерия) + " " + Сотрудник.ДокументНомер;
		Р2_11а = глДатаПриема(Сотрудник);
		Дети = Сотрудник.Дети.Получить("31.12." + Год);
		
	Иначе // если нет - очищаем все
		Р2_3 = "";
		Р2_1 = "";
		Р2_11 = "";
		ДатаРождения = "";
		ДокументСерияНомер = "";
		Р2_11а = "";
		Дети = "";
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореСотрудника()

//******************************************************************************
Функция УказатьСотрудника()
	
	Сотр = СоздатьОбъект("Справочник.Сотрудники");
	Если Сотр.Выбрать("Укажите сотрудника", "ДляВыбора") = 0 Тогда
		Возврат 0;
	КонецЕсли;
	Сотрудник = Сотр.ТекущийЭлемент();
	ПриВыбореСотрудника();
	Возврат 1;
	
КонецФункции // УказатьСотрудника()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПЕРЕСЧЕТА ПОКАЗАТЕЛЕЙ ПРИ ВНЕСЕНИИ ИЗМЕНЕНИЙ В КАРТОЧКУ
//

//******************************************************************************
Процедура ПриВводеЛьготныхДоходов()
	
    Таблица.Область("ИДохЛьгота").Значение = 0;
	
	Для а  = 1 по 12 Цикл
		Таблица.Область("ДохЛьгота" + Формат(а, "Ч(0)2")).Значение = 0;
	КонецЦикла;
	
	Для а = 1 по 5 Цикл
		Таблица.Область("ИДоход0" + а).Значение = 0;
		Для б = 1 по 12 Цикл
			Таблица.Область("ИДоход0" + а).Значение  =
				Таблица.Область("ИДоход0" + а).Значение +
				Таблица.Область("П"+(341+13*(а-1)+(б-1))).Значение;
			Таблица.Область("ДохЛьгота" + Формат(б, "Ч(0)2")).Значение = 	
				Таблица.Область("ДохЛьгота" + Формат(б, "Ч(0)2")).Значение +
				Таблица.Область("П"+(341+13*(а-1)+(б-1))).Значение;
		КонецЦикла;
		Таблица.Область("ИДохЛьгота").Значение = 
			Таблица.Область("ИДохЛьгота").Значение + 
			Таблица.Область("ИДоход0" + а).Значение;
	КонецЦикла;
		
    Таблица.Область("ИДоход0" + а).Значение = 0;
	а = 6;
	Для б = 1 по 12 Цикл
		Таблица.Область("ИДоход0" + а).Значение  =
			Таблица.Область("ИДоход0" + а).Значение +
			Таблица.Область("П"+(341+13*(а-1)+(б-1))).Значение;
		Таблица.Область("ДохЛьгота" + Формат(б, "Ч(0)2")).Значение = 	
			Таблица.Область("ДохЛьгота" + Формат(б, "Ч(0)2")).Значение -
			Таблица.Область("П"+(341+13*(а-1)+(б-1))).Значение;
	КонецЦикла;
	Таблица.Область("ИДохЛьгота").Значение = 
		Таблица.Область("ИДохЛьгота").Значение - 
		Таблица.Область("ИДоход0" + а).Значение;
	
КонецПроцедуры // ПриВводеПродаж()

//******************************************************************************
Процедура ПриВводеПродаж()
	
	Для а = 1 по 5 Цикл
		Таблица.Область("ИПродаж" + а).Значение = 0;
		Для б = 1 по 12 Цикл
			Таблица.Область("ИПродаж" + а).Значение  =
				Таблица.Область("ИПродаж" + а).Значение +
				Таблица.Область("П"+(276+13*(а-1)+(б-1))).Значение;
		КонецЦикла;
	КонецЦикла;
			
КонецПроцедуры // ПриВводеПродаж()

//******************************************************************************
Процедура ПриВводеСальдо(ВидСальдо)
	
	Если ВидСальдо = 1 Тогда
		Таблица.ДолгНП.Значение = Таблица.П001.Значение + Таблица.ИОбщСтДНП.Значение;
	Иначе
		Таблица.ДолгНА.Значение = Таблица.П002.Значение + Таблица.ИОбщСтДНА.Значение;
	КонецЕсли;
	
КонецПроцедуры // ПриВводеСальдо()

//******************************************************************************
Процедура ЗаполнитьРаздел6()
	
	Если Таблица.Р2_8.Значение = "2" Тогда
		Таблица.ОбщСт13НИ.Значение = 0;
		Таблица.ОбщСт30НИ.Значение = Таблица.ИНИ.Значение; 
		Таблица.П272.Значение = 0;
		Таблица.П273.Значение = Таблица.ИНУ.Значение;
		Таблица.ОбщСт13Взыск.Значение = 0;
		Таблица.ОбщСт30Взыск.Значение = Таблица.ИНалВзыск.Значение;
	Иначе
		Таблица.ОбщСт13НИ.Значение = Таблица.ИНИ.Значение;
		Таблица.ОбщСт30НИ.Значение = 0;
		Таблица.П272.Значение = Таблица.ИНУ.Значение;
		Таблица.П273.Значение = 0;
		Таблица.ОбщСт13Взыск.Значение = Таблица.ИНалВзыск.Значение;;
		Таблица.ОбщСт30Взыск.Значение = 0;
	КонецЕсли;
	
	Таблица.ОбщСт30НИ.Значение = 
		Таблица.ОбщСт30НИ.Значение + Таблица.ИНД30И.Значение;
	Таблица.П273.Значение = 
		Таблица.П273.Значение + Таблица.ИДивНУ.Значение;   
	Таблица.ОбщСт30Взыск.Значение = 
		Таблица.ОбщСт30Взыск.Значение + Таблица.ИДНалВзыск.Значение;
	
	Таблица.ОбщСт35НИ.Значение = Таблица.ИСТ35НИ.Значение;
	Таблица.П274.Значение = Таблица.ИСТ35_07.Значение;
	Таблица.ОбщСт35Взыск.Значение = Таблица.ИНал35Взыск.Значение;
	
	Таблица.ОбщНИ.Значение = Таблица.ОбщСт13НИ.Значение + 
		Таблица.ОбщСт30НИ.Значение + Таблица.ОбщСт35НИ.Значение;
	Таблица.ОбщНУ.Значение = Таблица.П272.Значение + 
		Таблица.П273.Значение + Таблица.П274.Значение;
	Таблица.ИОбщСтВзыск.Значение = Таблица.ОбщСт13Взыск.Значение + 
		Таблица.ОбщСт30Взыск.Значение + Таблица.ОбщСт35Взыск.Значение;
		
	Долг = Таблица.ОбщСт13НИ.Значение - Таблица.П272.Значение;
	Если Долг > 0 Тогда
		Таблица.ОбщСт13ДНП.Значение = Долг;
		Таблица.ОбщСт13ДНА.Значение = 0;
	Иначе
		Таблица.ОбщСт13ДНП.Значение = 0;
		Таблица.ОбщСт13ДНА.Значение = -Долг;
	КонецЕсли;
	
	Долг = Таблица.ОбщСт30НИ.Значение - Таблица.П273.Значение;
	Если Долг > 0 Тогда
		Таблица.ОбщСт30ДНП.Значение = Долг;
		Таблица.ОбщСт30ДНА.Значение = 0;
	Иначе
		Таблица.ОбщСт30ДНП.Значение = 0;
		Таблица.ОбщСт30ДНА.Значение = -Долг;
	КонецЕсли;
	
	Долг = Таблица.ОбщСт35НИ.Значение - Таблица.П274.Значение;
	Если Долг > 0 Тогда
		Таблица.ОбщСт35ДНП.Значение = Долг;
		Таблица.ОбщСт35ДНА.Значение = 0;
	Иначе
		Таблица.ОбщСт35ДНП.Значение = 0;
		Таблица.ОбщСт35ДНА.Значение = -Долг;
	КонецЕсли;
	
	Таблица.ИОбщСтДНП.Значение = Таблица.ОбщСт13ДНП.Значение + 
		Таблица.ОбщСт30ДНП.Значение + Таблица.ОбщСт35ДНП.Значение;
	Таблица.ИОбщСтДНА.Значение = Таблица.ОбщСт13ДНА.Значение + 
		Таблица.ОбщСт30ДНА.Значение + Таблица.ОбщСт35ДНА.Значение;
		
	ПриВводеСальдо(1);
	ПриВводеСальдо(2);
	
КонецПроцедуры // ЗаполнитьРаздел6()

//******************************************************************************
Процедура ПриВыбореОтветственного()
	
	Таблица.ДолжностьОтветственного.Текст = 
		Таблица.Ответственный.Значение.Должность;

КонецПроцедуры // ПриВыбореОтветственного()
	
//******************************************************************************
Процедура ПриВводеДивидендов()       
	
	б = 152; в = 164; г = 176;
	Таблица.Область("ИНД30И").Значение = 0;
	Таблица.Область("ИДив").Значение = 0;
	Таблица.Область("ИНДЗач").Значение = 0;
	Таблица.Область("ИДивНУ").Значение = 0;
	Таблица.Область("ИДНалВзыск").Значение = 0;
	Таблица.Область("ИДНалВозвр").Значение = 0;
	Долг = 0;
	Для а = 1 по 12 Цикл
		// Итоговая сумма дивидендов.
		Таблица.Область("ИДив").Значение =
			Таблица.Область("ИДив").Значение +
		 	Таблица.Область("П"+(а+в-1)).Значение;
		 	
		Ставка = ?(Таблица.Область("Р2_8").Значение <> "2" ,0.06 , 0.3);
		 	
		// Сумма к зачету (если организация уже платила налог.
		Таблица.Область("НД30И" + Формат(а, "Ч(0)2")).Значение =
			Окр(Ставка*Таблица.Область("П"+(а+в-1)).Значение, 0, 1);
			
		// Итоговая сумма налога
		Таблица.Область("ИНД30И").Значение =
			Таблица.Область("ИНД30И").Значение +
			Таблица.Область("НД30И" + Формат(а, "Ч(0)2")).Значение;
		 	
		// Сумма к зычету.
		Таблица.Область("НДЗач" + Формат(а, "Ч(0)2")).Значение =
			Таблица.Область("П"+(а+в-1)).Значение /100 *
			Таблица.Область("П"+(а+б-1)).Значение;
		 	
		// Итоговая сумма к зачету
		Таблица.Область("ИНДЗач").Значение =
			Таблица.Область("ИНДЗач").Значение +
			Таблица.Область("НДЗач" + Формат(а, "Ч(0)2")).Значение;
			
		// Итоговая сумма удержанного налога.
		Таблица.Область("ИДивНУ").Значение =
			Таблица.Область("ИДивНУ").Значение +
			Таблица.Область("П" + (а+г-1)).Значение;
			
		// Долги по налогу.
		НадоУдержатьНалог = МАКС(
			Таблица.Область("НД30И" + Формат(а, "Ч(0)2")).Значение -
			Таблица.Область("НДЗач" + Формат(а, "Ч(0)2")).Значение, 0);
		
		Если (Таблица.Область("П" + (а+в-1)).Значение <> 0) или
			 (Таблица.Область("П" + (а+г-1)).Значение <> 0) Тогда
			Долг = Долг + НадоУдержатьНалог - Таблица.Область("П" + (а+г-1)).Значение;
				
			Если Долг > 0 Тогда
				Таблица.Область("ДДолгНП" + Формат(а, "Ч(0)2")).Значение = Долг;
				Таблица.Область("ДДолгНА" + Формат(а, "Ч(0)2")).Значение = 0;
			Иначе
				Таблица.Область("ДДолгНП" + Формат(а, "Ч(0)2")).Значение = 0;
				Таблица.Область("ДДолгНА" + Формат(а, "Ч(0)2")).Значение = -Долг;
			КонецЕсли;
		Иначе
			Таблица.Область("ДДолгНП" + Формат(а, "Ч(0)2")).Значение = 0;
			Таблица.Область("ДДолгНА" + Формат(а, "Ч(0)2")).Значение = 0;
		КонецЕсли;
		
		// Суммы переданные на взыскание и возвращенные
		Таблица.Область("ИДНалВзыск").Значение = 
			Таблица.Область("ИДНалВзыск").Значение +
			Таблица.Область("ДНалВзыск" + Формат(а, "Ч(0)2")).Значение;
			
		Таблица.Область("ИДНалВозвр").Значение = 
			Таблица.Область("ИДНалВозвр").Значение +
			Таблица.Область("ДНалВозвр" + Формат(а, "Ч(0)2")).Значение;
		
	КонецЦикла;
		       
	// Итоговые суммы долгов.
	Если Долг > 0 Тогда
		Таблица.Область("ИДДолгНП").Значение = Долг;
		Таблица.Область("ИДДолгНА").Значение = 0;
	Иначе
		Таблица.Область("ИДДолгНП").Значение = 0;
		Таблица.Область("ИДДолгНА").Значение = -Долг;
	КонецЕсли;
	
	ЗаполнитьРаздел6();
	
КонецПроцедуры // ПриВводеДивидендов() 

//******************************************************************************
Процедура ПриВводеМатВыгод()       
	
	Таблица.Область("ИСТ35_01").Значение = 0;
	Таблица.Область("ИСТ35_02").Значение = 0;
	Таблица.Область("ИСТ35_03").Значение = 0;
	Таблица.Область("ИСТ35_04").Значение = 0;
	Таблица.Область("ИСТ35_05").Значение = 0;
	Таблица.Область("ИСТ35_06").Значение = 0;
	Таблица.Область("ИДох35НБ").Значение = 0;
	Для а = 1 по 12 Цикл
		Таблица.Область("Дох35НБ" + Формат(а, "Ч(0)2")).Значение = 0;
	КонецЦикла;
	Таблица.Область("ИСТ35НИ").Значение = 0;
	Таблица.Область("ИСТ35_07").Значение = 0;
	Таблица.Область("ИДолг35НП").Значение = 0;
	Таблица.Область("ИДолг35НА").Значение = 0;
	Таблица.Область("ИНал35Взыск").Значение = 0;
	Таблица.Область("ИНал35Возвр").Значение = 0;
	Долг = 0;
	Для а = 1 по 12 Цикл
		// Налоговая база.
		Для б = 1 по 5 Цикл
			Таблица.Область("Дох35НБ" + Формат(а, "Ч(0)2")).Значение  =
				Таблица.Область("Дох35НБ" + Формат(а, "Ч(0)2")).Значение +
				Таблица.Область("П"+(188+((б-1)*12)+(а-1))).Значение;
				
			Таблица.Область("ИСТ35_" + Формат(б, "Ч(0)2")).Значение = 
				Таблица.Область("ИСТ35_" + Формат(б, "Ч(0)2")).Значение +
				Таблица.Область("П"+(188+((б-1)*12)+(а-1))).Значение;
		КонецЦикла;
			
		б = 6;
		Таблица.Область("Дох35НБ" + Формат(а, "Ч(0)2")).Значение  =
			Таблица.Область("Дох35НБ" + Формат(а, "Ч(0)2")).Значение -
			Таблица.Область("П"+(188+((б-1)*12)+(а-1))).Значение;
			
		Таблица.Область("ИСТ35_" + Формат(б, "Ч(0)2")).Значение = 
			Таблица.Область("ИСТ35_" + Формат(б, "Ч(0)2")).Значение +
			Таблица.Область("П"+(188+((б-1)*12)+(а-1))).Значение;
			
		// Исчисленный налог.
		Таблица.Область("СТ35НИ" + Формат(а, "Ч(0)2")).Значение = 
			Окр(0.35*Таблица.Область("Дох35НБ" + Формат(а, "Ч(0)2")).Значение, 0, 1);
		Таблица.Область("ИСТ35НИ").Значение = 
			Таблица.Область("ИСТ35НИ").Значение +
			Таблица.Область("СТ35НИ" + Формат(а, "Ч(0)2")).Значение;
			
		// УдержанныйНалог
		Таблица.Область("ИСТ35_07").Значение = 
			Таблица.Область("ИСТ35_07").Значение +
			Таблица.Область("П"+(а+260-1)).Значение;		 	
					
		Если (Таблица.Область("Дох35НБ" + Формат(а, "Ч(0)2")).Значение <> 0) или
			 (Таблица.Область("П" + (а+260-1)).Значение <> 0) Тогда
			Долг = Долг + 
				   Таблица.Область("СТ35НИ" + Формат(а, "Ч(0)2")).Значение -
				   Таблица.Область("П" + (а+260-1)).Значение;
				
			Если Долг > 0 Тогда
				Таблица.Область("Долг35НП" + Формат(а, "Ч(0)2")).Значение = Долг;
				Таблица.Область("Долг35НА" + Формат(а, "Ч(0)2")).Значение = 0;
			Иначе
				Таблица.Область("Долг35НП" + Формат(а, "Ч(0)2")).Значение = 0;
				Таблица.Область("Долг35НА" + Формат(а, "Ч(0)2")).Значение = -Долг;
			КонецЕсли;
		Иначе
			Таблица.Область("Долг35НП" + Формат(а, "Ч(0)2")).Значение = 0;
			Таблица.Область("Долг35НА" + Формат(а, "Ч(0)2")).Значение = 0;
		КонецЕсли;
		
		// Суммы переданные на взыскание и возвращенные.
		Таблица.Область("ИНал35Взыск").Значение = 
			Таблица.Область("ИНал35Взыск").Значение +
			Таблица.Область("Нал35Взыск" + Формат(а, "Ч(0)2")).Значение;
			
		Таблица.Область("ИНал35Возвр").Значение = 
			Таблица.Область("ИНал35Возвр").Значение +
			Таблица.Область("Нал35Возвр" + Формат(а, "Ч(0)2")).Значение;
		
	КонецЦикла;
		       
	// Итоговые суммы долгов.
	Если Долг > 0 Тогда
		Таблица.Область("ИДолг35НП").Значение = Долг;
		Таблица.Область("ИДолг35НА").Значение = 0;
	Иначе
		Таблица.Область("ИДолг35НП").Значение = 0;
		Таблица.Область("ИДолг35НА").Значение = -Долг;
	КонецЕсли;
	
	ЗаполнитьРаздел6();
	
КонецПроцедуры // ПриВводеМатВыгод()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПЕРЕСЧЕТА ПОКАЗАТЕЛЕЙ РАЗДЕЛА 3 ПРИ ВНЕСЕНИИ ИЗМЕНЕНИЙ В КАРТОЧКУ
//

//******************************************************************************
Процедура ПересчитатьИтоговыеПоказатели3()
	
	// Налоговая база и исчисленный налог.
	Если Таблица.Область("Р2_8").Значение <> "2" Тогда // резидент
		ДоходНарастающимИтогом = 0;
		ВычетыНарастающимИтогом = 0;
		Для а = 1 по 12 Цикл
			ИзменениеНалоговойБазы = 0;
			Если Таблица.Область("ГДБезВ"+Формат(а, "Ч(0)2")).Значение <> 0 Тогда
				ДоходНарастающимИтогом = 
					Таблица.Область("ГДБезВ"+Формат(а, "Ч(0)2")).Значение;
				ИзменениеНалоговойБазы = 1;
			КонецЕсли;
			
			Если Таблица.Область("ОбщСВ"+Формат(а, "Ч(0)2")).Значение <> 0 Тогда
				ВычетыНарастающимИтогом =
					Таблица.Область("ОбщСВ"+Формат(а, "Ч(0)2")).Значение;
				ИзменениеНалоговойБазы = 1;
			КонецЕсли;
				
			Если ИзменениеНалоговойБазы = 1 Тогда
				Таблица.Область("НБаза"+Формат(а, "Ч(0)2")).Значение =
					Макс(ДоходНарастающимИтогом - ВычетыНарастающимИтогом, 0);
					
				Таблица.Область("НИ"+Формат(а, "Ч(0)2")).Значение =
					Окр(0.13*Таблица.Область("НБаза"+Формат(а, "Ч(0)2")).Значение, 0, 1);
			Иначе
				Таблица.Область("НБаза"+Формат(а, "Ч(0)2")).Значение = 0;
				Таблица.Область("НИ"+Формат(а, "Ч(0)2")).Значение = 0;
			КонецЕсли;
		КонецЦикла;
		Таблица.Область("ИНБаза").Значение = 
			Макс(ДоходНарастающимИтогом - ВычетыНарастающимИтогом,0);
		Таблица.Область("ИНИ").Значение = 
			Окр(0.13*Таблица.Область("ИНБаза").Значение, 0, 1);
	Иначе // не резидент
		ИтоговаяСуммаНалога = 0;
		ИтоговаяНалоговаяБаза = 0;
		Для а = 1 по 12 Цикл
			Таблица.Область("НБаза"+Формат(а, "Ч(0)2")).Значение =
				Таблица.Область("МДБезВ"+Формат(а, "Ч(0)2")).Значение;
			
			ИтоговаяНалоговаяБаза = ИтоговаяНалоговаяБаза +
				Таблица.Область("НБаза"+Формат(а, "Ч(0)2")).Значение;
					
			Таблица.Область("НИ"+Формат(а, "Ч(0)2")).Значение =
				Окр(0.3*Таблица.Область("НБаза"+Формат(а, "Ч(0)2")).Значение, 0, 1);
				
			ИтоговаяСуммаНалога = ИтоговаяСуммаНалога + 
				Таблица.Область("НИ"+Формат(а, "Ч(0)2")).Значение;
		КонецЦикла;
		Таблица.Область("ИНБаза").Значение = ИтоговаяНалоговаяБаза;
		Таблица.Область("ИНИ").Значение = ИтоговаяСуммаНалога;
	КонецЕсли;
	
	// Долги за налогоплательщиком и налоговым агентом
	Долг = 0;
	Для а = 1 по 12 Цикл 
		Если (Таблица.Область("НБаза"+Формат(а, "Ч(0)2")).Значение <> 0) или
			 (Таблица.Область("П" + (а+133-1)).Значение <> 0) Тогда
			Если Таблица.Область("Р2_8").Значение <> "2" Тогда
				Долг = 0;
			КонецЕсли;
			Долг = Долг + 
				   Таблица.Область("НИ"+Формат(а, "Ч(0)2")).Значение -
				   Таблица.Область("П" + (а+133-1)).Значение;
				
			Если Долг > 0 Тогда
				Таблица.Область("МДолгНП"+Формат(а, "Ч(0)2")).Значение = Долг;
				Таблица.Область("МДолгНА"+Формат(а, "Ч(0)2")).Значение = 0;
			Иначе
				Таблица.Область("МДолгНП"+Формат(а, "Ч(0)2")).Значение = 0;
				Таблица.Область("МДолгНА"+Формат(а, "Ч(0)2")).Значение = -Долг;
			КонецЕсли;
		Иначе
			Таблица.Область("МДолгНП"+Формат(а, "Ч(0)2")).Значение = 0;
			Таблица.Область("МДолгНА"+Формат(а, "Ч(0)2")).Значение = 0;
		КонецЕсли;
	КонецЦикла;
	
	// Итоговые суммы долгов.
	Если Долг > 0 Тогда
		Таблица.Область("ИМДолгНП").Значение = Долг;
		Таблица.Область("ИМДолгНА").Значение = 0;
	Иначе
		Таблица.Область("ИМДолгНП").Значение = 0;
		Таблица.Область("ИМДолгНА").Значение = -Долг;
	КонецЕсли;
		
	ЗаполнитьРаздел6();
	
КонецПроцедуры // ПересчитатьИтоговыеПоказатели3()

//******************************************************************************
Процедура ПриВводеДоходов()
	
	// Итоговые суммы дохода за год по видам дохода.
	в = 4;
	Для а = 1 по 10 Цикл
		Таблица.Область("ИД"+Формат(а, "Ч(0)2")).Значение = 0;
		Для б = в по (в + 11) Цикл
			Таблица.Область("ИД"+Формат(а, "Ч(0)2")).Значение = 
				Таблица.Область("ИД"+Формат(а, "Ч(0)2")).Значение + 
				Таблица.Область("П"+Формат(б, "Ч(0)3")).Значение;
		КонецЦикла;
		в = в + 13;
	КонецЦикла;
	
	// Итоговые суммы дохода за год с учетом нестандартных вычетов.
	Для а = 1 по 12 Цикл     
		в = 4;
		Таблица.Область("МДБезВ"+Формат(а, "Ч(0)2")).Значение = 0;
		Для б = 1 по 5 Цикл
			Таблица.Область("МДБезВ"+Формат(а, "Ч(0)2")).Значение = 
				Таблица.Область("МДБезВ"+Формат(а, "Ч(0)2")).Значение + 
				Таблица.Область("П"+Формат(в+а-1, "Ч(0)3")).Значение;
			в = в + 13;
		КонецЦикла;
		
		Для б = 1 по 5 Цикл
			Таблица.Область("МДБезВ"+Формат(а, "Ч(0)2")).Значение = 
				Таблица.Область("МДБезВ"+Формат(а, "Ч(0)2")).Значение -
				Таблица.Область("П"+Формат(в+а-1, "Ч(0)3")).Значение;
			в = в + 13;
		КонецЦикла;
	КонецЦикла;
	
	// Годовой доход нарастающим итогом.	
	ДоходНарастающимИтогом = 0;
	Для а = 1 по 12 Цикл
		Если (Таблица.Область("МДБезВ"+Формат(а, "Ч(0)2")).Значение <> 0) и
			 (Таблица.Область("Р2_8").Значение <> "2") Тогда
				ДоходНарастающимИтогом  =  ДоходНарастающимИтогом + 
					Таблица.Область("МДБезВ"+Формат(а, "Ч(0)2")).Значение;
			
			Таблица.Область("ГДБезВ"+Формат(а, "Ч(0)2")).Значение = 
				ДоходНарастающимИтогом;
		Иначе
			Таблица.Область("ГДБезВ"+Формат(а, "Ч(0)2")).Значение = 0;
		КонецЕсли;
	КонецЦикла;
	Таблица.Область("ИГДБезВ").Значение = ДоходНарастающимИтогом;
	
	ПересчитатьИтоговыеПоказатели3();
	
КонецПроцедуры // ПриВводеДоходов()

//******************************************************************************
Процедура ПриВыбореТипаДохода()
	
	// В конфигурации "Бухгалтерский учет" не используется
	
КонецПроцедуры // ПриВыбореТипаДохода()

//******************************************************************************
Процедура ПриВводеСтандартныхВычетов()
	
	// В конфигурации "Бухгалтерский учет" не используется
	
КонецПроцедуры // ПриВводеСтандартныхВычетов()

//******************************************************************************
Процедура ПриВводеВычетов()
	
	// Итоговые суммы стандартных вычетов по видам вычетов.
	Для а = 1 по 3 Цикл
		Таблица.Область("ИСВМес"+а).Значение = 0;
		Для б = 1 по 12 Цикл
			Таблица.Область("ИСВМес"+а).Значение = 
				Таблица.Область("ИСВМес"+а).Значение +
				Таблица.Область("СВМес"+а+Формат(б, "Ч(0)2")).Значение;
		КонецЦикла;
	КонецЦикла;
	
	// Стандартные вычеты нарастающим итогом.
	ВычетНарастающимИтогом = 0;
	Для а = 1 по 12 Цикл
		ВычетЗаМесяц = Таблица.Область("СВМес1"+Формат(а, "Ч(0)2")).Значение + 
					   Таблица.Область("СВМес2"+Формат(а, "Ч(0)2")).Значение + 
					   Таблица.Область("СВМес3"+Формат(а, "Ч(0)2")).Значение;
		Если (ВычетЗаМесяц <> 0) и (Таблица.Область("Р2_8").Значение <> "2") Тогда
			ВычетНарастающимИтогом = ВычетНарастающимИтогом + ВычетЗаМесяц;
			Таблица.Область("ОбщСВ"+Формат(а, "Ч(0)2")).Значение = 
				ВычетНарастающимИтогом ;
		Иначе
			Таблица.Область("ОбщСВ"+Формат(а, "Ч(0)2")).Значение = 0;
		КонецЕсли;
	КонецЦикла;
	Таблица.Область("ИОбщСВ").Значение = ВычетНарастающимИтогом;
	
	Таблица.Область("ЕстьВычеты").Значение = ?(ВычетНарастающимИтогом = 0, "2", "1");
	ПересчитатьИтоговыеПоказатели3();
	
КонецПроцедуры // ПриВводеВычетов()

//******************************************************************************
Процедура ПриВводеУдержанныхНалогов()
	
	Таблица.Область("ИНУ").Значение = 0;
	Для а = 133 по 144 Цикл
		Таблица.Область("ИНУ").Значение = Макс(Таблица.Область("ИНУ").Значение, 
											   Таблица.Область("П"+а).Значение);
	КонецЦикла;
	ПересчитатьИтоговыеПоказатели3();
	
КонецПроцедуры // ПриВводеУдержанныхНалогов()

//******************************************************************************
Процедура ПриВводеНалоговНаВзыскание()
	
	Таблица.Область("ИНалВзыск").Значение = 0;
	Для а = 1 по 12 Цикл
		Таблица.Область("ИНалВзыск").Значение =
			Таблица.Область("ИНалВзыск").Значение +
			Таблица.Область("НалВзыск"+Формат(а, "Ч(0)2")).Значение;
	КонецЦикла;
	
КонецПроцедуры // ПриВводеНалоговНаВзыскание()

//******************************************************************************
Процедура ПриВводеНалоговКВозврату()
	
	Таблица.Область("ИНалВозр").Значение = 0;
	Для а = 1 по 12 Цикл
		Таблица.Область("ИНалВозвр").Значение =
			Таблица.Область("ИНалВозвр").Значение +
			Таблица.Область("НалВозвр"+Формат(а, "Ч(0)2")).Значение;
	КонецЦикла;
	// ПересчитатьИтоговыеПоказатели3();
	
КонецПроцедуры // ПриВводеНалоговКВозврату()

////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ И ПРОЦЕДУРЫ, ВЫЗЫВАЕМЫЕ ИЗ ФОРМУЛ ЭЛЕМЕНТОВ ДИАЛОГА
// 

//******************************************************************************
Процедура Сохранить(Спрашивать)
	
	Перем СписокСохранения;
	Перем СписокСотрудников;
	
	Если ПустаяСтрока(ИмяФайлаСохраненияОтчета)=1 Тогда
		Возврат;
	КонецЕсли;
	Если Спрашивать=1 Тогда
		Если Вопрос("Сохранить данные отчета?","Да+Нет",60)="Нет" Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Сотрудник.Выбран() = 0 Тогда
		Если УказатьСотрудника() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Запись реквизитов справочника "Сотрудники".
	Если (ДатаГод(Сотрудник.ДатаПриема) = Год) и
		 (ДатаМесяц(Сотрудник.ДатаПриема) > 1) Тогда
		Сотр = СоздатьОбъект("Справочник.Сотрудники");
		Сотр.НайтиЭлемент(Сотрудник);
		НалоговаяБаза = 0;
		НалогИсчисленный = 0;
		Для а = 1 по (ДатаМесяц(Сотрудник.ДатаПриема) - 1) Цикл
			НалоговаяБазаТекущегоМесяца = Таблица.Область("НБаза"+Формат(а, "Ч(0)2")).Значение;
			Если НалоговаяБазаТекущегоМесяца <> НалоговаяБаза Тогда
				НалоговаяБаза = НалоговаяБазаТекущегоМесяца;
				НалогИсчисленный = Таблица.Область("НИ"+Формат(а, "Ч(0)2")).Значение;
			КонецЕсли;
		КонецЦикла;
		
		Если Сотр.ДоходОблагаемыйНДФЛ_СНачалаГода <> НалоговаяБаза Тогда
		    Сотр.ДоходОблагаемыйНДФЛ_СНачалаГода = НалоговаяБаза;
			Сотр.НачисленныйНДФЛ_СНачалаГода = НалогИсчисленный;
			Попытка
				Сотр.Записать();
				Сообщить("В справочник ""Сотрудники"" записаны начальные данные расчета НДФЛ сотрудника "+Сотрудник);
			Исключение
				Предупреждение("Неудачная попытка записать начальные данные 
								|расчета НДФЛ в справочник ""Сотрудники"".
								|Запись """+Сотрудник+""" заблокирована.");
				Возврат;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	// Запись налоговой карточки в файл.
	Таблица.Выгрузить(СписокСохранения);
	
	// Добавим в список итоговые значения.
	Для а = 1 по ИменаИтоговКарточки.РазмерСписка() Цикл
		ТекИтог = ИменаИтоговКарточки.ПолучитьЗначение(а);
		СписокСохранения.Установить(ТекИтог, Таблица.Область(ТекИтог).Значение);
	КонецЦикла;
	
	
	СписокСотрудников = СоздатьОбъект("СписокЗначений");
	Если ФС.СуществуетФайл(ИмяФайлаСохраненияОтчета) = 1 Тогда
		ЗначениеИзФайла(ИмяФайлаСохраненияОтчета, СписокСотрудников);
	КонецЕсли;
	СписокСотрудников.Установить(Сотрудник.Код, СписокСохранения);		
	
	ЗначениеВФайл(ИмяФайлаСохраненияОтчета, СписокСотрудников);

КонецПроцедуры // Сохранить()

//******************************************************************************
Процедура Восстановить(Спрашивать)

	Если ПустаяСтрока(ИмяФайлаСохраненияОтчета)=1 Тогда
		Возврат;
	КонецЕсли;
	Если Спрашивать=1 Тогда
		Если Вопрос("Текущие данные будут очищены! Восстановить?","Да+Нет")="Нет" Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Если Сотрудник.Выбран() = 0 Тогда
		Если УказатьСотрудника() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	СписокСохранения=СоздатьОбъект( "СписокЗначений" );
	СписокСотрудников=СоздатьОбъект( "СписокЗначений" );
	ЗначениеИзФайла(ИмяФайлаСохраненияОтчета, СписокСотрудников);
	Если СписокСотрудников.РазмерСписка() > 0 Тогда
		СписокСохранения = СписокСотрудников.Получить(Сотрудник.Код);
		Таблица.Загрузить(СписокСохранения);
	КонецЕсли;
	
КонецПроцедуры // Восстановить()

//******************************************************************************
Процедура Очистить()
	
	П001 = "";
	П002 = "";
	ДолгНП = "";
	ДолгНА = "";
	СВКод1 = "";
	СВКод2 = "";
	СВКод3 = "";
	
	Для а = 1 по 11 Цикл
		Таблица.Область("ПолеЧисел" + а).Значение = "";	
	КонецЦикла
	
КонецПроцедуры // Очистить()

//******************************************************************************
Процедура ПриПовторномВыбореСотрудника()
	
	Если Сотрудник = ТекСотрудник Тогда
		Возврат;
		
	ИначеЕсли ПустоеЗначение(ТекСотрудник) = 0 Тогда
		ПриВыбореСотрудника();
		Очистить();
		Восстановить(0);
		
	Иначе
		ПриВыбореСотрудника();
		Очистить();
	КонецЕсли;
	
	Таблица.Название.Текст = "Налоговая карточка по учету доходов и налога "+
							 "на доходы физических лиц за "+Год+" год № "+
						     глПреобразоватьНомерДок(Сотрудник.Код, 0, 0);
	
КонецПроцедуры // ПриПовторномВыбореСотрудника()

//******************************************************************************
Процедура Заполнить()
	 	
	Если Сотрудник.Выбран() = 0 Тогда
		Если УказатьСотрудника() = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НачДата = Дата("01.01."+Год);
	КонДата = Макс(НачДата, Мин(Дата("31.12."+Год), КонецРассчитанногоПериодаБИ()));
	Если КонДата > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		               "Расчет итогов выполняется в режиме"+РазделительСтрок+
					   """Операции - Управление бухгалтерскими итогами"".");
		Возврат;
	КонецЕсли;

	Если ДатаГод(Сотрудник.ДатаПриема)=Год Тогда
		ВаловойДоход = Сотрудник.ДоходОблагаемыйНДФЛ_СНачалаГода;
		НачисленныйНДФЛ_СНачалаГода = Сотрудник.НачисленныйНДФЛ_СНачалаГода;
	
	Иначе
		ВаловойДоход = 0;
		НачисленныйНДФЛ_СНачалаГода = 0;
	КонецЕсли;
	
	Если ДатаГод(глДатаПриема(Сотрудник))=Год Тогда
		ВаловойДоход = Сотрудник.ВаловойДоход_СНачалаГода;
	КонецЕсли;
	
    БухИт = СоздатьОбъект("БухгалтерскиеИтоги");
	БухИт.Опции(1,1);
	БухИт.ВключатьСубсчета(1,1);
	БухИт.ИспользоватьСубконто();
	БухИт.ИспользоватьКорСубконто();
	БухИт.ВыполнитьЗапрос(НачДата, КонДата, "70,КН", , , 2, "Месяц");
	
	НачисленныйНалогНаДоходы = 0;
	Таблица.ЕстьВычеты.Текст = "2";
	ЕстьИтоги = БухИт.ПолучитьСубконто(,,Сотрудник);
	// Найдем месяц последнего начисления в этом году.
	МесяцПоследнегоНачисления = 0;
	Если ЕстьИтоги = 1 Тогда
	    БухИт.ВыбратьПериоды();
		Пока БухИт.ПолучитьПериод() = 1 Цикл
		    МесяцПоследнегоНачисления = ДатаМесяц(БухИт.КонДата);
		КонецЦикла;
	КонецЕсли;
	
	БухИт.ВыбратьПериоды(1);
	Пока БухИт.ПолучитьПериод() = 1 Цикл
		Месяц = ДатаМесяц(БухИт.КонДата);
		Если глСотрудникЧислитсяРаботающим(Сотрудник, БухИт.КонДата) = 0 Тогда
			Продолжить;
		КонецЕсли;

		// Сумма начислений.
		НачисленоЗаМесяц = 0;
		Если ЕстьИтоги = 1 Тогда
			НачисленоЗаМесяц = БухИт.КО();
			Если БухИт.ПолучитьКорСчет(, СчетПоКоду("00")) = 1 Тогда
				// Учтем начальный остаток.
				НачисленоЗаМесяц = НачисленоЗаМесяц - БухИт.КорКО();
			КонецЕсли;
			Если БухИт.ПолучитьКорСчет(, СчетПоКоду("68")) = 1 Тогда
				Если БухИт.ПолучитьКорСубконто(1,, Перечисление.ВидыПлатежейВБюджет.Налог) = 1 Тогда
					НачисленныйНалогНаДоходы = НачисленныйНалогНаДоходы + БухИт.КорДО();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Таблица.Область("П0"+Формат(3+Месяц,"Ч(0)2")).Значение = НачисленоЗаМесяц;
		
		// Определим суммы стандартных вычетов за месяц.
		// Очистим предыдущие значения.
		Таблица.Область("СВМес1" + Формат(Месяц, "Ч(0)2")).Значение = 0;
		Таблица.Область("СВМес2" + Формат(Месяц, "Ч(0)2")).Значение = 0;
		Таблица.Область("СВМес3" + Формат(Месяц, "Ч(0)2")).Значение = 0;
		
		ВаловойДоход = ВаловойДоход + НачисленоЗаМесяц;
		
		// Вычеты НДФЛ
		Если глСотрудникЧислитсяРаботающим(Сотрудник, БухИт.НачДата) = 0 Тогда
			Продолжить;
			
		ИначеЕсли Месяц > МесяцПоследнегоНачисления Тогда
			Продолжить;
		КонецЕсли;
		
		Вычет = 0;
		ВычетНаРебенка = 0;
		ПределВычетов = Константа.ПределВычетов.Получить(БухИт.КонДата);
		ТипВычета = Сотрудник.ТипВычета.Получить(БухИт.КонДата);
		Если ТипВычета = Перечисление.ТипыВычетов.Обычный Тогда
			Вычет = 400*?(ВаловойДоход<=ПределВычетов,1,0);
		ИначеЕсли ТипВычета = Перечисление.ТипыВычетов.Льготный1 Тогда
			Вычет = 500;
		ИначеЕсли ТипВычета = Перечисление.ТипыВычетов.Льготный2 Тогда
			Вычет = 3000;
		КонецЕсли;
		
		Если ВаловойДоход<=ПределВычетов Тогда
		    ВычетНаРебенка = 300*Сотрудник.Дети.Получить(БухИт.КонДата);
		КонецЕсли;
				
		Если Вычет > 0 Тогда
			Таблица.Область("СВМес1"+Формат(Месяц, "Ч(0)2")).Значение = Вычет;
			Таблица.ЕстьВычеты.Текст = "1";
			Таблица.Р2_8.Значение = "1";
		КонецЕсли;
			
		Если ВычетНаРебенка > 0 Тогда
			Таблица.Область("СВМес2"+Формат(Месяц, "Ч(0)2")).Значение = ВычетНаРебенка;
			Таблица.ЕстьВычеты.Текст = "1";
			Таблица.Р2_8.Значение = "1";
		КонецЕсли;
	КонецЦикла;
	
	ПриВводеДоходов();
	ПриВводеВычетов();
	ПриВводеДивидендов();
	ПриВводеМатВыгод();
	ПриВводеПродаж();
	ПриВводеЛьготныхДоходов();
	
	Если Таблица.ОбщНИ.Значение <> (НачисленныйналогНаДоходы + НачисленныйНДФЛ_СНачалаГода) Тогда
		Сообщить("Сумма исчисленного налога в строке ""Итого"" раздела 6 формы"+
				 " не совпадает с данными бухгалтерского учета");
	КонецЕсли;
	
КонецПроцедуры // Заполнить()

//******************************************************************************
Процедура Печать()
	
	ПунктМеню="";
	
	Меню = СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("ПечататьСразу","Печатать сразу");
	Меню.ДобавитьЗначение("ПоказатьБланк","Показать бланк");
	Меню.ДобавитьЗначение("ПоказатьПоследний","Открыть последний бланк");
	
	Если Меню.ВыбратьЗначение(ПунктМеню,,,,1) = 1 Тогда
		ИмяФайлаПечатнойФормы = КаталогИБ() + "tcard-" + Прав(Год, 2) + ".mxl";
		ПечатнаяФорма=СоздатьОбъект("Таблица");
		
		Если ПунктМеню="ПоказатьПоследний" Тогда
			Если ФС.СуществуетФайл(ИмяФайлаПечатнойФормы)=0 Тогда
				Сообщить("Файл печатной формы "+ИмяФайлаПечатнойФормы+" не найден","i");
				Возврат;
			КонецЕсли;
		Иначе
			Таблица.Записать(ИмяФайлаПечатнойФормы);
		КонецЕсли;
		
		ПечатнаяФорма.Открыть(ИмяФайлаПечатнойФормы);
		
		ПечатнаяФорма.Область().ЦветТекста(-1);
		ПечатнаяФорма.Область().ЦветФона(-1);
		ПечатнаяФорма.Область().ЦветРамки(-1);
		ПечатнаяФорма.Опции(0,0,0,0,"Налоговая карточка ("+Сотрудник+")",1);
		ПечатнаяФорма.ПараметрыСтраницы(,,,,,,,,,1,1,);
		
		Если ПунктМеню="ПечататьСразу" Тогда
			ПечатнаяФорма.ТолькоПросмотр(1);
			ПечатнаяФорма.Напечатать(0);
		Иначе
			ПечатнаяФорма.Показать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // Печать()

//******************************************************************************
Процедура ПриВыбореРезидента()
	
	Если Таблица.Р2_8.Значение = "2" Тогда
		Для а = 1 по 3 Цикл
			Для б = 1 по 12 Цикл
				Таблица.Область("СВМес"+а+Формат(б, "Ч(0)2")).Значение = 0;
			КонецЦикла;
		КонецЦикла;
		Для а = 1 по 12 Цикл
			Таблица.Область("П"+(152 + а - 1)).Значение = 0;
		КонецЦикла;
	КонецЕсли;
	
	ПриВводеДоходов(); 
	ПриВводеВычетов();
	ПриВводеДивидендов();
			
КонецПроцедуры // ПриВыбореРезидента()

//******************************************************************************
Процедура ПриВыбореГода(РазрешитьСменуГода = 0)
	
	Год = СпГод.ПолучитьЗначение(СпГод.ТекущаяСтрока());
	Таблица.Название.Текст = "Налоговая карточка по учету доходов и налога "+
							 "на доходы физических лиц за "+Год+" год № "+
						     глПреобразоватьНомерДок(Сотрудник.Код, 0, 0);
	ИмяФайлаСохраненияОтчета = КаталогИБ() + "tcard-" + Прав(Год, 2) + ".rvs";
	
КонецПроцедуры // ПриВыбореГода()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//******************************************************************************
Процедура ПриОткрытии(Флаг)
	
	Если Константа.УчетЗарплатыВоВнешнейПрограмме = Да Тогда
		Предупреждение("Налоговая карточка не используется в режиме
		               |расчета зарплаты во внешней программе");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	Стр = "";
	Стр = Стр + """ДолгНП"",""ДолгНА"",";
	Стр = Стр + """ИД01"",""ИД02"",""ИД03"",""ИД04"",""ИД05"",""ИД06"",""ИД06"",""ИД08"",""ИД09"",""ИД10"",";
	Стр = Стр + """МДБезВ01"",""МДБезВ02"",""МДБезВ03"",""МДБезВ04"",""МДБезВ05"",""МДБезВ06"",""МДБезВ07"",""МДБезВ08"",""МДБезВ09"",""МДБезВ10"",""МДБезВ11"",""МДБезВ12"",";
	Стр = Стр + """ГДБезВ01"",""ГДБезВ02"",""ГДБезВ03"",""ГДБезВ04"",""ГДБезВ05"",""ГДБезВ06"",""ГДБезВ07"",""ГДБезВ08"",""ГДБезВ09"",""ГДБезВ10"",""ГДБезВ11"",""ГДБезВ12"",""ИГДБезВ"",";
	Стр = Стр + """ИСВМес1"",""ИСВМес2"",""ИСВМес3"",";
	Стр = Стр + """ОбщСВ01"",""ОбщСВ02"",""ОбщСВ03"",""ОбщСВ04"",""ОбщСВ05"",""ОбщСВ06"",""ОбщСВ07"",""ОбщСВ08"",""ОбщСВ09"",""ОбщСВ10"",""ОбщСВ11"",""ОбщСВ11"",""ИОбщСВ"",";
	Стр = Стр + """НБаза01"",""НБаза02"",""НБаза03"",""НБаза04"",""НБаза05"",""НБаза06"",""НБаза07"",""НБаза08"",""НБаза09"",""НБаза10"",""НБаза11"",""НБаза12"",""ИНБаза"",";
	Стр = Стр + """НИ01"",""НИ02"",""НИ03"",""НИ04"",""НИ05"",""НИ06"",""НИ07"",""НИ08"",""НИ09"",""НИ10"",""НИ11"",""НИ12"",""ИНИ"",""ИНУ"",";
	Стр = Стр + """МДолгНП01"",""МДолгНП02"",""МДолгНП03"",""МДолгНП04"",""МДолгНП05"",""МДолгНП06"",""МДолгНП07"",""МДолгНП08"",""МДолгНП09"",""МДолгНП10"",""МДолгНП11"",""МДолгНП12"",""ИМДолгНП"",";
	Стр = Стр + """МДолгНА01"",""МДолгНА02"",""МДолгНА03"",""МДолгНА04"",""МДолгНА05"",""МДолгНА06"",""МДолгНА07"",""МДолгНА08"",""МДолгНА09"",""МДолгНА10"",""МДолгНА11"",""МДолгНА12"",""ИМДолгНА"",";
	Стр = Стр + """ИНалВзыск"",""ИНалВозвр"",""П150"",""ИНПД"",""ИДив"",";
	Стр = Стр + """НД30И01"",""НД30И02"",""НД30И03"",""НД30И04"",""НД30И05"",""НД30И06"",""НД30И07"",""НД30И08"",""НД30И09"",""НД30И10"",""НД30И11"",""НД30И12"",""ИНД30И"",";
	Стр = Стр + """НДЗач01"",""НДЗач02"",""НДЗач03"",""НДЗач04"",""НДЗач05"",""НДЗач06"",""НДЗач07"",""НДЗач08"",""НДЗач09"",""НДЗач10"",""НДЗач11"",""НДЗач12"",""ИНДЗач"",";
	Стр = Стр + """ИДивНУ"",""ИДНалВзыск"",""ИДНалВозвр"",";
	Стр = Стр + """ДДолгНП01"",""ДДолгНП02"",""ДДолгНП03"",""ДДолгНП04"",""ДДолгНП05"",""ДДолгНП06"",""ДДолгНП07"",""ДДолгНП08"",""ДДолгНП09"",""ДДолгНП10"",""ДДолгНП11"",""ДДолгНП12"",""ИДДолгНП"",";
	Стр = Стр + """ДДолгНА01"",""ДДолгНА02"",""ДДолгНА03"",""ДДолгНА04"",""ДДолгНА05"",""ДДолгНА06"",""ДДолгНА07"",""ДДолгНА08"",""ДДолгНА09"",""ДДолгНА10"",""ДДолгНА11"",""ДДолгНА12"",""ИДДолгНА"",";
	Стр = Стр + """ИСТ35_01"",""ИСТ35_02"",""ИСТ35_03"",""ИСТ35_04"",""ИСТ35_05"",""ИСТ35_06"",""ИСТ35_07"",""ИНал35Взыск"",""ИНал35Возвр"",";
	Стр = Стр + """Дох35НБ01"",""Дох35НБ02"",""Дох35НБ03"",""Дох35НБ04"",""Дох35НБ05"",""Дох35НБ06"",""Дох35НБ07"",""Дох35НБ08"",""Дох35НБ09"",""Дох35НБ10"",""Дох35НБ11"",""Дох35НБ12"",""ИДох35НБ"",";
	Стр = Стр + """СТ35НИ01"",""СТ35НИ02"",""СТ35НИ03"",""СТ35НИ04"",""СТ35НИ05"",""СТ35НИ06"",""СТ35НИ07"",""СТ35НИ08"",""СТ35НИ09"",""СТ35НИ10"",""СТ35НИ11"",""СТ35НИ12"",""ИСТ35НИ"",";
	Стр = Стр + """Долг35НП01"",""Долг35НП02"",""Долг35НП03"",""Долг35НП04"",""Долг35НП05"",""Долг35НП06"",""Долг35НП07"",""Долг35НП08"",""Долг35НП09"",""Долг35НП10"",""Долг35НП11"",""Долг35НП12"",""ИДолг35НП"",";
	Стр = Стр + """ОбщСт13НИ"",""П272"",""ОбщСт13ДНП"",""ОбщСт13ДНА"",""ОбщСт13Взыск"",";
	Стр = Стр + """ОбщСт30НИ"",""П273"",""ОбщСт30ДНП"",""ОбщСт30ДНА"",""ОбщСт30Взыск"",";
	Стр = Стр + """ОбщСт35НИ"",""П274"",""ОбщСт35ДНП"",""ОбщСт35ДНА"",""ОбщСт35Взыск"",";
	Стр = Стр + """ОбщНИ"",""ОбщНУ"",""ИОбщСтДНП"",""ИОбщСтДНА"",""ИОбщСтВзыск"",";
	Стр = Стр + """ИПродаж1"",""ИПродаж2"",""ИПродаж3"",""ИПродаж4"",""ИПродаж5"",";
	Стр = Стр + """ИДоход01"",""ИДоход02"",""ИДоход03"",""ИДоход04"",""ИДоход05"",""ИДоход06"",";
	Стр = Стр + """ДохЛьгота01"",""ДохЛьгота02"",""ДохЛьгота03"",""ДохЛьгота04"",""ДохЛьгота05"",""ДохЛьгота06"",""ДохЛьгота07"",""ДохЛьгота08"",""ДохЛьгота09"",""ДохЛьгота10"",""ДохЛьгота11"",""ДохЛьгота12"",""ИДохЛьгота""";
	ИменаИтоговКарточки = СоздатьОбъект("СписокЗначений");
	ИменаИтоговКарточки.ИзСтрокиСРазделителями(Стр);
	
	Если ПустоеЗначение(Форма.Параметр) = 1 Тогда
		Год = ДатаГод(РабочаяДата());
		Если Год < 2001 Тогда
			Год = 2001;
		ИначеЕсли Год > 2003 Тогда
			Год = 2003;
		КонецЕсли;

	Иначе
		Сотрудник = Форма.Параметр.Получить("Сотрудник");
		Таблица.Р2_3.Значение = Сотрудник;
		Год = Форма.Параметр.Получить("Год");
		ПриВыбореСотрудника();
		ИмяФайлаСохраненияОтчета = КаталогИБ() + "tcard-" + Прав(Год, 2) + ".rvs";
		Восстановить(0);
	КонецЕсли;
	
	Таблица.ИНН.Текст = Константа.ИННОрганизации;
	Таблица.ИМНС.Текст = Константа.КодИМНС;
	Таблица.РегистрационныйНомерПФР.Текст = глПолучитьНалог("ПФР_страх").РегНомер;
	Таблица.НазваниеОрганизации.Текст = Константа.ОфициальноеНазваниеОрганизации;
	
	СпГод.ДобавитьЗначение(2001,"2001");
	СпГод.ТекущаяСтрока(1);
	ПриВыбореГода();
	
	Резидент = СоздатьОбъект("СписокЗначений");
	Резидент.ДобавитьЗначение("1","1 - резидент");
	Резидент.ДобавитьЗначение("2","2 - нерезидент");
	
	Форма.Сотрудник.ВыполнятьФормулуТолькоПриИзменении(1);
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
Процедура ПриНачалеВыбораЗначения(Адрес, Флаг)               
	
	Если Адрес = "Р2_8" Тогда
		Флаг = 0;
		а = "";
		Резидент.ВыбратьЗначение(а,,,,2);
		Таблица.Область("Р2_8").Значение = а;
		
	ИначеЕсли Адрес = "Сотрудник" Тогда
		ТекСотрудник = Сотрудник;
		Сохранить(1);
	КонецЕсли;                             
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//******************************************************************************
//
// Описание:
//  При закрытии запоминает вариант формы, который редактируется, чтобы
// при последующем открытии данный вариант можно было восстановить.
//
Процедура ПриЗакрытии()
	
	Если ПустоеЗначение(Сотрудник) = 0 Тогда
		Сохранить(1);
	КонецЕсли;
	
КонецПроцедуры // ПриЗакрытии()
