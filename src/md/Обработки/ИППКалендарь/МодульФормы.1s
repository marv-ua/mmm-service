// Версия обработки 2.0 
// Отличия текущей версии от предыдущей:
//		Добавлены виды ссылок:
//			Календарь_ИмяТаблицы[#ИмяСекции]
//				открывает новый календарь с активизацией нужной секции (если указано)
//			КТаблица_ИмяТаблицы[#ИмяСекции[!]] 
//				открывает в отдельном окне содержимое секции (если не указана секция, то всей таблицы)
//				если не указано ИмяСекции, то выводится только секция "Главная"; 
//				"!" - признак необходимости вывода секции "Главная" указанной таблицы 
//			Приложение_ИмяФайла
//				запускает ассоциированное приложение
//
Перем ТД,СекцияПодзаголовок,СекцияЗаголовок; 
Перем КаталогКалендарей,СправкаКонтент;  
Перем ДопТаблица, ИсходныйФайл, ТекКалендарь; 
Перем Квартал, Год, Соединение, Действие;
Перем Опережение,ПроверкаКалендарь,Открытие,Парам,Контент;
Перем СекцияЗаголовокОтчета,СекцияЗаголовокДоп,СекцияРазделитель,Опережение1;
Перем ТипДанных,НазваниеДанных,ПериодГод,ПериодТип,ПериодЗначение,НазваниеРаздела,Версия,ПроверятьПриЗапуске;
Перем ПадежиДней[10], Дней;
Перем Норм_Комп;

//*****************************************************************************
// ПоказатьСправку() 
//
// Описание: Формирует справочную информацию по использованию Календарей
//
Процедура ПоказатьСправку(Режим="1") 
	Таблица.ИсходнаяТаблица("Справка");
	Таблица.Очистить();
	Таблица.ВывестиСекцию("Работа");
	Если Режим="1" Тогда
		Таблица.ВывестиСекцию("Календари");
	Иначе
		Таблица.ВывестиСекцию("Тексты");
	КонецЕсли;
	Таблица.Опции(0,0,1,0,,"ОпцииСправка");
	Таблица.ТолькоПросмотр(1);
	Таблица.Показать();
КонецПроцедуры //ПоказатьСправку

//*****************************************************************************
Процедура ПоказатьСекциюТаблицы(ИсходныйФайл,ИмяСекции,Главная=0) 
	Если ФС.СуществуетФайл(КаталогКалендарей+ИсходныйФайл)=1 Тогда 
		Табл = СоздатьОбъект("Таблица");
        Табл.ИсходнаяТаблица(КаталогКалендарей+ИсходныйФайл);
		//Табл.Очистить();
		Если ИмяСекции<>"" Тогда 
			Если Главная > 0 Тогда
				Попытка
					Табл.ВывестиСекцию("Главная");
				Исключение
				КонецПопытки;
			КонецЕсли;  
			Попытка
				Табл.ВывестиСекцию(ИмяСекции);
			Исключение
			КонецПопытки;
		Иначе
			Табл.Вывести();
		КонецЕсли;
		Табл.Опции(0,0,0,0,,?(ПустоеЗначение(ИмяСекции)=1,"",ИмяСекции));
		Табл.ТолькоПросмотр(1);
		Табл.Показать();
	КонецЕсли;
КонецПроцедуры //ПоказатьСправку
//*****************************************************************************
// __ПолучитьСправку() 
//
// Описание: Получить правовую информацию
//
Процедура __ПолучитьСправку(СтрКод) 
	Если ТипЗначения(Норм_Комп) = 0 тогда
		Предупреждение("Не установлены интегрируемые правовые системы:
						|""1С:Гарант. Правовая поддержка.""
						|По  вопросам  их  подключения  и приобретения
						|обращайтесь в фирму ""1С"" или к ее партнерам.
						|");
		Возврат;
	КонецЕсли;
	Если ФС.СуществуетФайл(Норм_Комп.ПутьБД+"1CIts")=1 Тогда
		Норм_Комп.ИскатьВБД("*"+СтрКод);
	Иначе
		Сообщить("В дисководе не установлен диск ИТС");
	КонецЕсли;
КонецПроцедуры  // __ПолучитьСправку

//*****************************************************************************
// Вспомогательная(ДН,Н,Таб,ТабД,ТаблицаДат[]) 
//
// Описание: Формирует элемент массива данных по конкретной дате
// Параметры: 	ДН - день недели; Н - день месяца; 
//				Таб - исходная таблица с информацией, расположенной в секциях по датам
//				ТабД - исходная таблица с дополнительной информацией, расположенной в секциях по датам;
//				ТаблицаДат[] - результирующий массив данных
//
Процедура Вспомогательная(ДН,Н,Таб,ТабД,ТаблицаДат[])
	Попытка
		ВыбДата = СтрЗаменить(Формат(Н,"ДДДММГГ"),".","_");
		СекцияДата = Таб.ПолучитьСекцию(ВыбДата+"|Основная");
		Мес = ДатаМесяц(Н);
		Тхт = СекцияДата.Область("R1C1:R1C1").Текст;  // для проверки
		ТаблицаДат[42*(Мес-1)+ДатаЧисло(Н)+ДН-1]= ДатаЧисло(Н);
	Исключение
		ТаблицаДат[42*(Мес-1)+ДатаЧисло(Н)+ДН-1]= -ДатаЧисло(Н);
	КонецПопытки;
	Если ДопТаблица=1 Тогда
		Попытка
			ВыбДата = СтрЗаменить(Формат(Н,"ДДДММГГ"),".","_");
			СекцияДата = ТабД.ПолучитьСекцию(ВыбДата);
			Тхт = СекцияДата.Область("R1C1:R1C1").Текст;  // для проверки 
			Мес = ДатаМесяц(Н);
			ТаблицаДат[42*(Мес-1)+ДатаЧисло(Н)+ДН-1]= ДатаЧисло(Н);
		Исключение
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры // Вспомогательная

//*****************************************************************************
// ПодготовитьТаблицуДат(ИмяФайла,Мес,ГодД,ТаблицаДат[],СекцияКалендарь) 
//
// Описание: Формирует массив данных календаря
// Параметры: 	ИмяФайла - имя файла таблицы исходных данных;
//				Мес - значение месяца; ГодД - значеие года; 
//				ТаблицаДат[] - результирующий массив данных
//				СекцияКалендарь - результирующая секция таблицы календаря
//
Процедура ПодготовитьТаблицуДат(ИмяФайла,Мес,ГодД,ТаблицаДат[],СекцияКалендарь)
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица(КаталогКалендарей+ИмяФайла);  
	ТабДоп = СоздатьОбъект("Таблица");
	ИмяФайлаД = Лев(ИмяФайла,7)+"R.mxl";
	ДопТаблица=0;
	Если ФС.СуществуетФайл(КаталогКалендарей+ИмяФайлаД)=1 Тогда
		ДопТаблица=1;
		ТабДоп.ИсходнаяТаблица(КаталогКалендарей+ИмяФайлаД);  
	КонецЕсли; 

	Для Кол=1 По 6 Цикл
		Для ДН=1 По 7 Цикл
			Н=(Мес-1)*42+(Кол-1)*7+ДН;
			Если ТаблицаДат[Н]>0 Тогда
				СекцияКалендарь.Область("Ч"+ДН+Кол).Текст = ""+ТаблицаДат[Н];
				СекцияКалендарь.Область("Ч"+ДН+Кол).Подчеркнутый(1);   
				СекцияКалендарь.Область("Ч"+ДН+Кол).ЦветТекста(0,0,255);  
				СекцияКалендарь.Область("Ч"+ДН+Кол).Расшифровка("Отработка_"+Формат(ТаблицаДат[Н],"Ч(0)2")+"_"+Формат(Мес,"Ч(0)2")+"_"+ГодД);
			ИначеЕсли ТаблицаДат[Н]<0 Тогда
				СекцияКалендарь.Область("Ч"+ДН+Кол).ЦветТекста(127,127,127);  
				СекцияКалендарь.Область("Ч"+ДН+Кол).Текст = Строка(-ТаблицаДат[Н]);
				СекцияКалендарь.Область("Ч"+ДН+Кол).Подчеркнутый(0);   
				СекцияКалендарь.Область("Ч"+ДН+Кол).Расшифровка("",2);
			Иначе
				СекцияКалендарь.Область("Ч"+ДН+Кол).Текст = "";
				СекцияКалендарь.Область("Ч"+ДН+Кол).Подчеркнутый(0);   
				СекцияКалендарь.Область("Ч"+ДН+Кол).Расшифровка("",2);
			КонецЕсли;
		КонецЦикла;          
	КонецЦикла;          
КонецПроцедуры  // ПодготовитьТаблицуДат

//*****************************************************************************
// ОпределитьПериод(Год,Квартал,Файл) 
//
// Описание: Определение существования регламентированного отчета 
// Параметры: 	Год,Квартал - год и квартал, комплекта регламентированных отчетов;
//				Файл - имя файла регламентированного отчета
//
Функция ОпределитьПериод(Год,Квартал,Файл)
	Год1 = Год;
	Квартал1 = Квартал;
	Если ФС.СуществуетФайл(КаталогИБ()+"ExtForms\Rp"+Прав(Формат(Год,"Ч(0)4"),2)+"q"+Квартал+".grp\"+Файл)=0 Тогда
		Квартал1 = ?(Квартал=1,4,Квартал-1);
		Год1 = ?(Квартал1=4,Год-1,Год);
	КонецЕсли;
	Возврат КаталогИБ()+"ExtForms\Rp"+Прав(Формат(Год1,"Ч(0)4"),2)+"q"+Квартал1+".grp\"+Файл;
КонецФункции  // ОпределитьПериод

//*****************************************************************************
// СформироватьОтчет() 
//
// Описание: Сформировать таблицу ближайших событий
//
Процедура СформироватьОтчет()
	ПроверкаКалендарь = ВосстановитьЗначение("КалендарьПроверка");
	Опережение = ВосстановитьЗначение("КалендарьОпережение");
	Опережение = ?(ПустоеЗначение(Опережение)=1,7,Опережение);
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Календарь");
	Опережение1 = Опережение;
	К = ?(((Опережение%10)=0) ИЛИ ((Опережение<20) И (Опережение>10)),10, Опережение%10);
	Дней = ПадежиДней[К];
	СекцияЗаголовокОтчета = Таб.ПолучитьСекцию("ЗаголовокОтчета");
	СекцияДатаОтчета = Таб.ПолучитьСекцию("ДатаОтчета"); 
	СекцияРазделитель = Таб.ПолучитьСекцию("Разделитель"); 
	СекцияИсточник = Таб.ПолучитьСекцию("ОтчетИсточник"); 
	Таблица.Очистить();
	Таблица.ВывестиСекцию(СекцияЗаголовокОтчета);
	Таб = СоздатьОбъект("Таблица");
	ТД = ТекущаяДата();
	КонДата = ТекущаяДата()+Опережение-1;
	Год = ДатаГод(ТД);
	Квартал = Цел((ДатаМесяц(ТД)-1)/3)+1;
	ТекКаталог = ФС.ТекКаталог();
	ФС.УстТекКаталог(КаталогКалендарей); 
	Файл = ФС.НайтиПервыйФайл(КаталогКалендарей+"*.mxl"); // вместо CLDB надо использовать маску
	Проверка = 0;
	Пока ПустаяСтрока(Файл) = 0 Цикл              
		Если (Файл <> "..") И (Файл <> ".") И (Сред(Файл,8,1)<>"R") Тогда
			Таб.ИсходнаяТаблица(КаталогКалендарей+Файл); 
			Попытка   
				СекцияСлужебная = Таб.ПолучитьСекцию("Служебная");
				Проверять = СекцияСлужебная.Область("R17C5").Текст;
				Если Проверять="1" Тогда
					Есть=0;
					Для Н=ТД По КонДата Цикл 
						Попытка
							ВыбДата = СтрЗаменить(Формат(Н,"ДДДММГГ"),".","_");
							СекцияДата = Таб.ПолучитьСекцию(ВыбДата+"|Основная");
							Тхт = СекцияДата.Область("R1C1:R1C1").Текст;
							Если Есть=0 Тогда
								Источник = СекцияСлужебная.Область("R8C5").Текст+" "+СекцияСлужебная.Область("R9C5").Текст;
								СекцияИсточник = Таблица.ПолучитьСекцию("ОтчетИсточник");
								СекцияИсточник.Область().Расшифровка("Календарь_"+Файл);
								Таблица.ВывестиСекцию(СекцияИсточник);
								Есть=1;
							КонецЕсли;
							ДатаОтчетов = Н;
							Таблица.ВывестиСекцию("ДатаОтчетов");
							Для К=1 По Таб.ВысотаСекции(ВыбДата+"|Основная") Цикл
								СекцияДата = Таб.ПолучитьСекцию(ВыбДата+"|Основная");
								СекцияДата.Область("R"+К+"C25").Расшифровка(""+Н,2);
							КонецЦикла;
							СекцияДата = Таб.ПолучитьСекцию(ВыбДата+"|Основная");
							Таблица.ВывестиСекцию(СекцияДата);
							СекцияРазделитель = Таб.ПолучитьСекцию("Разделитель"); 
							Таблица.ВывестиСекцию(СекцияРазделитель);
							Проверка = 1;
						Исключение
						КонецПопытки;
					КонецЦикла;
				КонецЕсли;
			Исключение
			КонецПопытки;
		КонецЕсли;
		Файл = ФС.НайтиСледующийФайл();
	КонецЦикла; 

	Таблица.Опции(0,0,1,0,"Календарь3","КалендарьБухгалтера");
	Таблица.ТолькоПросмотр(1);
	Таблица.Показать();
КонецПроцедуры  // СформироватьОтчет

//*****************************************************************************
// ПодготовкаДанных(ИмяФайла="",ВыбДата="") 
//
// Описание: Сформировать таблицу событий
// Параметры:	ИмяФайла - имя исходного файла с данными, 
//				ВыбДата - дата, за которую необходимо вывести данные, 
//							если не указано число, то выводятся все данные за месяц
// 
Процедура ПодготовкаДанных(ИмяФайла="",ВыбДата="")
	Перем ТаблицаДат[504]; 
	СекцияДатаОтчета = Таблица.ПолучитьСекцию("ДатаОтчета");
	ВД = ВыбДата;
	Таб = СоздатьОбъект("Таблица");
	ТабДоп = СоздатьОбъект("Таблица");

	Таблица.Очистить();
	СекцияЗаголовок = Таблица.ПолучитьСекцию("Заголовок");
	СекцияЗаголовок.Название = " "+НазваниеРаздела; 
	СекцияЗаголовок.ПериодНазвание.Текст = НазваниеДанных;
	Таблица.ВывестиСекцию(СекцияЗаголовок); 

	ДопТаблица=0;
	ИмяФайлаД = Лев(ИмяФайла,7)+"R.mxl";
	Если ФС.СуществуетФайл(КаталогКалендарей+ИмяФайлаД)=1 Тогда
		ДопТаблица=1;
		ТабДоп.ИсходнаяТаблица(КаталогКалендарей+ИмяФайлаД);  
	КонецЕсли;
	
	ИмяФайлаД = Лев(ИмяФайла,7)+"R.mxl";
	ДопТаблица=0;
	Если ФС.СуществуетФайл(КаталогКалендарей+ИмяФайлаД)=1 Тогда
		ДопТаблица=1;
		ТабДоп.ИсходнаяТаблица(КаталогКалендарей+ИмяФайлаД);  
	КонецЕсли; 

	Если ТипДанных="0" Тогда
		// не выводить календарь 
		Таб.ИсходнаяТаблица(КаталогКалендарей+ИмяФайла);
		Попытка
			Таблица.ВывестиСекцию(Таб.ПолучитьСекцию("Источник|Основная"));
		Исключение
		КонецПопытки;
		Попытка
			Таблица.ВывестиСекцию(Таб.ПолучитьСекцию("Главная|Основная"));
		Исключение
		КонецПопытки; 
		Если ПустоеЗначение(ВД)=0 Тогда 
			Попытка
				Таблица.ВывестиСекцию(Таб.ПолучитьСекцию(ВД+"|Основная"));
			Исключение
			КонецПопытки; 
		КонецЕсли; 
	Иначе // с календарем
		Если ПроверятьПриЗапуске="1" Тогда
			Таблица.ВывестиСекцию("ПроверкаКалендарей");
		КонецЕсли; 
		ГодД=Прав(ПериодГод,2);
		Если ПериодТип="0" Тогда // год
			Месяц=1; 
			ЧислоМесяцев = 12;
		ИначеЕсли ПериодТип="1" Тогда  // полугодие
			Месяц=(Число(ПериодЗначение)-1)*6+1; 
			ЧислоМесяцев = 6;
		ИначеЕсли ПериодТип="2" Тогда  // квартал
			Месяц=(Число(ПериодЗначение)-1)*3+1; 
			ЧислоМесяцев = 3;
		ИначеЕсли ПериодТип="3" Тогда  // месяц
			Месяц=Число(ПериодЗначение); 
			ЧислоМесяцев = 1;
		КонецЕсли; 
		
		Для Н=1 По 504 Цикл 
			ТаблицаДат[Н]=0;
		КонецЦикла; 
		Таб.ИсходнаяТаблица(КаталогКалендарей+ИмяФайла);
		Для Н=Месяц По Месяц+ЧислоМесяцев-1 Цикл
			ДатаНач = Дата("01."+Формат(Н,"Ч(0)2.0")+"."+ГодД);
			ДН = НомерДняНедели(ДатаНач);
			Для НН = ДатаНач По КонМесяца(ДатаНач) Цикл 
				Вспомогательная(ДН,НН,Таб,ТабДоп,ТаблицаДат);
			КонецЦикла;
			СекцияКалендарь = Таблица.ПолучитьСекцию("КалендарьДни|Месяц"); 
			ПодготовитьТаблицуДат(ИмяФайла,Н,ГодД,ТаблицаДат,СекцияКалендарь);
			СекцияКалендарь.Месяц1.Текст = Формат(ДатаНач,"ДММММ"); 
			СекцияКалендарь.Месяц1.Расшифровка("Месяц_"+Формат(Н,"Ч(0)2.0")+"_"+ГодД);
			Если (Н-1)=Цел((Н-1)/3)*3 Тогда
				Таблица.ВывестиСекцию(СекцияКалендарь);
			Иначе
				Таблица.ПрисоединитьСекцию(СекцияКалендарь);
			КонецЕсли;
		КонецЦикла;
		Попытка
			Таблица.ВывестиСекцию(Таб.ПолучитьСекцию("Источник|Основная"));
		Исключение
		КонецПопытки; 
	КонецЕсли;
	
	// Вывод сведений по указанной дате
	Если ПустоеЗначение(ВД)=0 Тогда
		Таб.ИсходнаяТаблица(КаталогКалендарей+ИмяФайла);
		Если (СтрДлина(ВД)=8) И ((Сред(ВД,3,1)="_") И (Сред(ВД,6,1)="_")) Тогда  // по конкретной дате
			СекцияПодзаголовок.Период = Нрег(Формат(Дата(СтрЗаменить(ВД,"_",".")),"ДДДММММГГГГ"));
			Таблица.ВывестиСекцию(СекцияПодзаголовок);
			Попытка
				Таблица.ВывестиСекцию(Таб.ПолучитьСекцию(ВД+"|Основная"));
			Исключение
			КонецПопытки;
			Если ДопТаблица=1 Тогда
				Попытка
					Таблица.ВывестиСекцию(Таб.ПолучитьСекцию(ВД+"|Основная"));
				Исключение
				КонецПопытки;
			КонецЕсли;
		ИначеЕсли (СтрДлина(ВД)=5) И (Сред(ВД,3,1)="_") Тогда  // все за месяц
			НачДата = Дата("01."+СтрЗаменить(ВД,"_","."));
			СекцияПодзаголовок.Период.Текст = "События за "+Нрег(Формат(НачДата,"ДММММГГГГ"));
			Таблица.ВывестиСекцию(СекцияПодзаголовок);
			Для Н=НачДата По КонМесяца(НачДата) Цикл
				Попытка
					СекцияДата = Таб.ПолучитьСекцию(СтрЗаменить(Формат(Н,"ДДДММГГ"),".","_")+"|Основная");
					СекцияДата.Область("R1C1").Текст="";
					СекцияДатаОтчета = Таблица.ПолучитьСекцию("ДатаОтчета");
					СекцияДатаОтчета.ДатаОтчета = Формат(Н,"ДДДММГГГГ");
					Таблица.ВывестиСекцию(СекцияДатаОтчета);
					Таблица.ВывестиСекцию(СекцияДата);
				Исключение
				КонецПопытки;
			КонецЦикла;
		КонецЕсли; 
	КонецЕсли; 
	Таблица.Опции(0,0,1,0,,"Опции"+ИмяФайла);
	Таблица.ТолькоПросмотр(1);
	Таблица.Показать();
КонецПроцедуры  // ПодготовкаДанных 

//*****************************************************************************
// УстановитьКомпоненту() 
//
// Описание: Инициализирует объект V7HttpReader
//
Функция УстановитьКомпоненту()
	Если ЗагрузитьВнешнююКомпоненту(КаталогИБ()+"ExtForms\v7plus.dll")=0 Тогда
		Если ЗагрузитьВнешнююКомпоненту("v7plus.dll")=0 Тогда
			Сообщить("Не удалось обнаружить компоненту v7plus.dll!"); 
			Возврат 0;
		КонецЕсли;
	КонецЕсли;
	Попытка
		Соединение = СоздатьОбъект("Addin.V7HttpReader");
		Соединение.КоличествоПопытокАвторизации = 3;
	Исключение 
		Сообщить("Не удалось создать объект Addin.V7HttpReader!");
		Возврат 0;
	КонецПопытки; 
	Возврат 1;
КонецФункции  //  УстановитьКомпоненту

//*****************************************************************************
// ПриОткр()
//
// Описание: Общая часть кода, необходимого для предопределенных процедур ПриОткрытии() и ПриПовторномОткрытии()
// 
Процедура ПриОткр()
	УстановитьКомпоненту();
	ПроверкаКалендарь = ВосстановитьЗначение("КалендарьПроверка");
	ПроверкаКалендарь = ?(ПустоеЗначение(ПроверкаКалендарь)=1,1,ПроверкаКалендарь);
	СохранитьЗначение("КалендарьПроверка",ПроверкаКалендарь);
	Опережение = ВосстановитьЗначение("КалендарьОпережение");
	Опережение = ?(Опережение=0,7,Опережение);
	Если (ПустоеЗначение(Форма.Параметр)=0)И(Открытие=0) Тогда 
		Если ТипЗначенияСтр(Форма.Параметр)="СписокЗначений" Тогда 
		ИначеЕсли ТипЗначенияСтр(Форма.Параметр)="Строка" Тогда
			Если Лев(Форма.Параметр,7)="Справка" Тогда
				// Открыть справку по работе с календарем
				ИсходныйФайл = "Справка";
				Открытие = 0;  
				НазваниеРаздела="Справка";
				ПоказатьСправку(Прав(Форма.Параметр,1));
				Форма.Заголовок(НазваниеРаздела+" - "+?(Прав(Форма.Параметр,1)="1","Календарь","Просмотр дополнительной информации"),0);
			ИначеЕсли Форма.Параметр="Проверка" Тогда
				// режим проверки наступления событий
				СформироватьОтчет();
				Форма.Заголовок("Проверка сроков событий",0);
			Иначе				
				// показ календаря, имя кот. находится в переданном параметре
				ИсходныйФайл = Форма.Параметр;
				ТД = НачГода(ТекущаяДата())-4;
				ТабТМП=СоздатьОбъект("Таблица");
				Если ФС.СуществуетФайл(КаталогКалендарей+ИсходныйФайл)=1 Тогда
					ТабТМП.ИсходнаяТаблица(КаталогКалендарей+ИсходныйФайл);
					Попытка
						Секц=ТабТМП.ПолучитьСекцию("Служебная|Основная");
						ТипДанных=Секц.Область("R7C5").Текст;
						НазваниеРаздела=Секц.Область("R8C5").Текст;
						НазваниеДанных=Секц.Область("R9C5").Текст;
						Версия=Секц.Область("R10C5").Текст;
						ПериодГод=Секц.Область("R14C5").Текст;
						ПериодТип=Секц.Область("R15C5").Текст;
						ПериодЗначение=Секц.Область("R16C5").Текст;
						ПроверятьПриЗапуске=Секц.Область("R17C5").Текст;
						Квартал = Число(ПериодЗначение);
						Год = Число(ПериодГод);
					Исключение 
						Если ФС.СуществуетФайл(КаталогКалендарей+ИсходныйФайл)=0 Тогда
							Сообщить("Таблица "+ИсходныйФайл+" не найдена!");
						Иначе
							Инфо = СоздатьОбъект("Таблица");
							Инфо.Открыть(КаталогКалендарей+ИсходныйФайл);
							Инфо.Показать(); 
						КонецЕсли;
						СтатусВозврата(0);
						Возврат;
					КонецПопытки;
					
					Таб = СоздатьОбъект("Таблица");
					Таб.ИсходнаяТаблица("Календарь");
					СекцияЗаголовок = Таб.ПолучитьСекцию("Заголовок");
					СекцияПодзаголовок = Таб.ПолучитьСекцию("Подзаголовок");
					ПодготовкаДанных(Форма.Параметр);
					Форма.Заголовок(НазваниеРаздела+"  "+НазваниеДанных,0);
				Иначе
					Сообщить("Файл "+КаталогКалендарей+Форма.Параметр+" не найден");
					СтатусВозврата(0);
					Возврат;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Форма.Параметр="Проверка" Тогда
			СформироватьОтчет();
			Форма.Заголовок("Проверка сроков событий",0);
		КонецЕсли;
	ИначеЕсли Форма.Параметр="Проверка" Тогда
		СформироватьОтчет();
		Форма.Заголовок("Проверка сроков событий",0);
	КонецЕсли;
КонецПроцедуры  // ПриОткр

//*****************************************************************************
// Предопределенные процедуры
Процедура ОбработкаЯчейкиТаблицы(Зн,Фл,ТБ,Адрес)
	Перем ТМП,Поз;  
	
	// Для Проверки календарей
	Если ВРег(Лев(Зн,3)) = "ВПР" Тогда
		Опережение = ВосстановитьЗначение("КалендарьОпережение");
		Опережение = ?(ПустоеЗначение(Опережение)=1,7,Опережение);
		Значение = Опережение;
		Если ВвестиЧисло(Значение,"Введите число дней",2,0)=1 Тогда
			Если Опережение <> Значение Тогда
				Опережение = Значение;
				СохранитьЗначение("КалендарьОпережение",Опережение);
				СформироватьОтчет();
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Зн="Обновить" Тогда 
		СформироватьОтчет();
	ИначеЕсли ВРег(Лев(Зн,3))="ПРК" Тогда
		ПроверкаКалендарь = ВосстановитьЗначение("КалендарьПроверка");
		ПроверкаКалендарь = ?(ПустоеЗначение(ПроверкаКалендарь)=1,2,ПроверкаКалендарь);
		ПроверкаКалендарь = ?(ПроверкаКалендарь=1,2,1);
		СохранитьЗначение("КалендарьПроверка",ПроверкаКалендарь);
		СформироватьОтчет();
		
	// Для Календаря
	ИначеЕсли ВРег(Лев(Зн,3)) = "ФВП" Тогда
		Опережение = ВосстановитьЗначение("КалендарьОпережение");
		Опережение = ?(ПустоеЗначение(Опережение)=1,7,Опережение);
		Значение = Опережение;
		Если ВвестиЧисло(Значение,"Введите число дней",2,0)=1 Тогда
			Если Опережение <> Значение Тогда
				Опережение = Значение;
				СохранитьЗначение("КалендарьОпережение",Опережение);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ВРег(Лев(Зн,3))="ФПК" Тогда
		ПроверкаКалендарь = ?(ПроверкаКалендарь=1,2,1);
	ИначеЕсли Лев(Зн,9) = "Отработка" Тогда   // вывести события на указанную дату
		Значение = Сред(Зн,11);
		Таб = СоздатьОбъект("Таблица");
		Таб.ИсходнаяТаблица("Календарь");
		СекцияПодзаголовок = Таб.ПолучитьСекцию("Подзаголовок"); 
		ПодготовкаДанных(ИсходныйФайл,Значение);
	ИначеЕсли Лев(Зн,5)="Месяц" Тогда  // вывести события на указанный месяц
		Значение = Сред(Зн,7);
		Таб = СоздатьОбъект("Таблица");
		Таб.ИсходнаяТаблица("Календарь");
		СекцияПодзаголовок = Таб.ПолучитьСекцию("Подзаголовок"); 
		ПодготовкаДанных(ИсходныйФайл,Значение);
	ИначеЕсли Лев(Зн,3)="Отч" Тогда  // Вызов отчета
		Значение = Сред(Зн,5);
		Если Лев(Значение,1)="/" Тогда  // если есть символ "/", то это указан период, 
										// по кот. ищется отчет (например "/004_Frm5.ert")
			Гд = Число(Сред(Значение,2,2));
			Кв = Число(Сред(Значение,4,1));  
			Значение = Сред(Значение,6);
			Путь = КаталогИБ()+"ExtForms\Rp"+Прав(Формат(Гд,"Ч(0)4"),2)+"q"+Кв+".grp\";
			Если ФС.СуществуетФайл(Путь+Значение)=1 Тогда
				ОткрытьФорму("Отчет",,Путь+Значение);
			Иначе
				Сообщить("В комплекте регламентированных отчетов нет формы "+Значение+" "+Путь);
			КонецЕсли; 
		Иначе 
			Путь = ОпределитьПериод(Год,Квартал,Значение);
			Если ФС.СуществуетФайл(Путь)=1 Тогда
				ОткрытьФорму("Отчет",,Путь);
			Иначе
				Сообщить("В комплекте регламентированных отчетов нет формы "+Путь);
			КонецЕсли; 
		КонецЕсли; 
	ИначеЕсли Лев(Зн,3)="Док" Тогда  // Вызов ПЛАТЕЖНОГО ПОРУЧЕНИЯ
		Если Метаданные.Документ("ПлатежноеПоручение").Выбран() = 1 Тогда
			ОткрытьФорму("Документ.ПлатежноеПоручение");
		Иначе
			Сообщить("В конфигурации нет документа ""Платежное поручение""");
		КонецЕсли; 
	ИначеЕсли ВРег(Лев(Зн,3))="ТАБ" Тогда  // Вызов таблицы
		Значение = Сред(Зн,5);
		Инфо = СоздатьОбъект("Таблица");
		Если Лев(Значение,1)="/" Тогда  // если есть символ "/", то это указан период, 
										// по кот. ищется отчет (например "/004_Frm5.ert")
			Гд = Число(Сред(Значение,2,2));
			Кв = Число(Сред(Значение,4,1));  
			Значение = Сред(Значение,6);
			Путь = КаталогИБ()+"ExtForms\Rp"+Прав(Формат(Гд,"Ч(0)4"),2)+"q"+Кв+".grp\";
			Если ФС.СуществуетФайл(Путь)=1 Тогда
				Инфо.Открыть(Путь+Сред(Зн,5));
				Инфо.Показать(ТБ.Область(Адрес).Текст,""); 
			Иначе
				Сообщить("Нет таблицы "+Путь);
			КонецЕсли; 
		Иначе
			Путь = ОпределитьПериод(Год,Квартал,Значение);
			Если ФС.СуществуетФайл(Путь)=1 Тогда
				Инфо.Открыть(Путь);
				Инфо.Показать(ТБ.Область(Адрес).Текст,""); 
			Иначе
				Сообщить("Нет таблицы "+Путь);
			КонецЕсли; 
		КонецЕсли; 

	ИначеЕсли Лев(Зн,6)="Ссылка" Тогда 
		Значение = Сред(Зн,8);
		ПодготовкаДанных(ИсходныйФайл,Значение);
	ИначеЕсли Лев(Зн,8)="КТаблица" Тогда 
		Значение = Сред(Зн,10);
		Поз = Найти(Значение,"#");
		ИсходныйФайл = ?(Поз=0,Значение,Лев(Значение,Поз-1));
		Значение = ?(Поз=0,"",Сред(Значение,Поз+1));
		Поз = Найти(Значение,"!");
		Значение = ?(Поз>0,Лев(Значение,Поз-1),Значение);
		ПоказатьСекциюТаблицы(ИсходныйФайл,Значение,Поз); 
	ИначеЕсли Лев(Зн,10)="Приложение" Тогда
		Значение = Сред(Зн,12);
		ЗапуститьПриложение(КаталогКалендарей+"\"+Значение);
	ИначеЕсли (ВРег(Лев(Зн,3))="WWW") ИЛИ (ВРег(Лев(Зн,4))="HTTP") Тогда 
		ЗапуститьПриложение(Зн);
	ИначеЕсли ВРег(Лев(Зн,6))="ГАРАНТ" Тогда 
		Значение = Сред(Зн,8);
		__ПолучитьСправку(Значение);
	ИначеЕсли Зн="Справка" Тогда 
		ОткрытьФорму("Отчет.ИППКалендарь#Справка"+ТипДанных,Зн+ТипДанных); 
	ИначеЕсли Лев(Зн,9)="Календарь" Тогда 
		Если Врег(СокрЛП(Сред(Зн,11)))=Врег(СокрЛП(ИсходныйФайл)) Тогда
			ПодготовкаДанных(ИсходныйФайл,Сред(Зн,11));
		Иначе	
			ОткрытьФорму("Отчет.ИППКалендарь#"+Сред(Зн,11),Сред(Зн,11)); 
		КонецЕсли;
	ИначеЕсли ВРег(Лев(Зн,3))="ТЕК" Тогда 
		ТекКалендарь = ?(ПустоеЗначение(ТекКалендарь)=1,ИсходныйФайл,"");
		СохранитьЗначение("ТекущийКалендарь",ТекКалендарь);
		ПодготовкаДанных(ИсходныйФайл);
	ИначеЕсли ВРег(Лев(Зн,3))="ТКС" Тогда 
		Предупреждение(Сред(Зн,5));
	ИначеЕсли ВРег(Лев(Зн,3))="ФРМ" Тогда // запустить обработку 
		Если Сред(Зн,5)="Отчет.ПроверкаКалендаря" Тогда
			ОткрытьФорму("Отчет.ИППКалендарь#Проверка","Проверка");
		Иначе
			ОткрытьФорму(Сред(Зн,5));
		КонецЕсли;
	КонецЕсли;
	Фл=0;
КонецПроцедуры  // ОбработкаЯчейкиТаблицы()

//*****************************************************************************
Процедура ПриОткрытии()
	ПадежиДней[1]="день";
	ПадежиДней[2]="дня";
	ПадежиДней[3]="дня";
	ПадежиДней[4]="дня";
	Для К=5 По 10 Цикл
		ПадежиДней[К]="дней";
	КонецЦикла;
	Если ПустоеЗначение(Форма.Параметр)=1 Тогда
		// открыть текущий календарь
		Пар = ВосстановитьЗначение("ТекущийКалендарь");
		Если ФС.СуществуетФайл(КаталогКалендарей+"\"+Пар)=1 Тогда 
			ОткрытьФорму("Отчет.ИППКалендарь#"+Пар,Пар);
			СтатусВозврата(0);
		Иначе
			ОткрытьФорму("Обработка.ИППДополнительнаяИнформация");
			Сообщить("Не указаны данные, вызываемые по этой команде!"+ РазделительСтрок+
			"В открывшейся форме откройте нужный календарь и установите флажок ""Сделать этот календарь главным""."+РазделительСтрок+
			"Если таблица пустая, получите информацию с сайта фирмы 1С или с диска ИТС,"+РазделительСтрок+
			"для этого перейдите на закладку ""Получение"" и нажмите кнопку ""Получить"".");
			СтатусВозврата(0);
		КонецЕсли;
	ИначеЕсли Форма.Параметр=1 Тогда
		// Запуск обработки ИППКалендарь в режиме проверки наступления событий
		ОткрытьФорму("Отчет.ИППКалендарь#Проверка","Проверка");
		СтатусВозврата(0);
	Иначе
		ТекКалендарь = ВосстановитьЗначение("ТекущийКалендарь");
		ПриОткр();
	КонецЕсли;
КонецПроцедуры  // ПриОткрытии

//*****************************************************************************
Процедура ПриПовторномОткрытии()
	Если (ПустоеЗначение(Форма.Параметр)=0)И(Открытие=0) Тогда 
		Если ТипЗначенияСтр(Форма.Параметр)="Строка" Тогда
			Если Форма.Параметр<>"Справка" Тогда
				ИсходныйФайл = Форма.Параметр;
				ПриОткр();
			КонецЕсли;
		КонецЕсли;
	Иначе
		Пар = ВосстановитьЗначение("ТекущийКалендарь");
		ОткрытьФорму("Отчет.ИППКалендарь#"+Пар,Пар);
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры  // ПриПовторномОткрытии

//*****************************************************************************
Процедура ПриЗакрытии() 
	Если ПустоеЗначение(СправкаКонтент)=0 Тогда
		СправкаКонтент.Форма.Закрыть(); 
	КонецЕсли;
	СохранитьЗначение("КалендарьОпережение",Опережение);
КонецПроцедуры

//*****************************************************************************
СекцияПодзаголовок = Таблица.ПолучитьСекцию("Подзаголовок");
СекцияЗаголовок = Таблица.ПолучитьСекцию("Заголовок");
СекцияЗаголовокДоп = Таблица.ПолучитьСекцию("ЗаголовокДоп");
СекцияРазделитель = Таблица.ПолучитьСекцию("СекцияРазделитель");

Открытие=0; 
КаталогКалендарей = КаталогИБ()+"ExtForms\Calendar";
Если ФС.СуществуетФайл(КаталогИБ()+"ExtForms")=0 Тогда 
	ФС.СоздатьКаталог(КаталогИБ()+"ExtForms");
КонецЕсли;
Если ФС.СуществуетФайл(КаталогКалендарей)=0 Тогда 
	ФС.СоздатьКаталог(КаталогКалендарей);
КонецЕсли;
КаталогКалендарей=КаталогКалендарей+"\";
Парам = СоздатьОбъект("СписокЗначений");

// Для правовой справки:
Если ФС.СуществуетФайл( КаталогПрограммы()+"1CRtInf.dll" )=1 Тогда
	ЗагрузитьВнешнююКомпоненту("1CRtInf.dll");
	Норм_Комп = СоздатьОбъект("AddIn.DBExtension");
КонецЕсли;

