Перем Почта,АдресПолучателя,АдресУмолчание;
Перем ШаблонПисьма,Прогр,Сообщение,СообщениеСимвол;
Перем Программа,РелизП,РелизК,Конфигурация,Номер,Компьютер,Быстродействие,Память,ОС,Сеть,Организация,ФИО,Телефон,Факс;
Перем ПутьФайлаОбработки,ИмяФайлаОбработки;
//*******************************************
Функция Предупредить(Стр)
	Предупреждение(Стр);  
	Возврат "";
КонецФункции // Предупредить
//******************************************************************************
//	ПроверитьСведения()
//
//	Описание:  Осуществляет проверку введенных значений по обязательным реквизитам 
Функция ПроверитьСведения()
    Возврат СтрДлина(СокрЛП(Программа))*СтрДлина(СокрЛП(РелизП))*СтрДлина(СокрЛП(Конфигурация))*
			СтрДлина(СокрЛП(РелизК))*СтрДлина(СокрЛП(Номер))*
			СтрДлина(СокрЛП(Организация))*СтрДлина(СокрЛП(ФИО));
КонецФункции  // ПроверитьСведения
//******************************************************************************
//	ИсхДанные()
//
//	Описание:  Осуществляет загрузку данных, "собранных" в обработках 
//			   "СистемнаяИнформация" и "ДополнительнаяСистемнаяИнформация"
Процедура ИсхДанные()
	Программа = ВосстановитьЗначение("ПрограммаПисьмо");
	РелизП = ВосстановитьЗначение("РелизППисьмо");
	РелизК = ВосстановитьЗначение("РелизКПисьмо");
	Конфигурация = ВосстановитьЗначение("КонфигурацияПисьмо");
	Номер = ВосстановитьЗначение("РегНомерПисьмо");
	Компьютер = ВосстановитьЗначение("КомпьютерПисьмо");
	Быстродействие = ВосстановитьЗначение("БыстродействиеПисьмо");
	Память = ВосстановитьЗначение("ПамятьПисьмо");
	ОС = ВосстановитьЗначение("ОСПисьмо");
	Сеть = ВосстановитьЗначение("СетьПисьмо");
	Организация = ВосстановитьЗначение("ОрганизацияПисьмо");
	ФИО = ВосстановитьЗначение("ОтветственныйПисьмо");
	Телефон = ВосстановитьЗначение("ТелефонПисьмо");
	Факс = ВосстановитьЗначение("ФаксПисьмо");
КонецПроцедуры // ИсхДанные
//******************************************************************************
Функция Рефреш()
	ИсхДанные();
	Сообщение = ?(ПроверитьСведения()=0,"Отправка невозможна! Не достаточно сведений о программе и пользователе!"+РазделительСтрок+"Для ввода нажмите кнопку ""Сведения""",""); //обязательные учетные 
	СообщениеСимвол = ?(ПроверитьСведения()=0,"G",""); 
	Возврат "";
КонецФункции  // Рефреш
//******************************************************************************
//	ПослатьПисьмо(Адрес,Текст,Заголовок="",Файл="")
//
//	Параметры: Адрес - почтовый адрес, Текст - текст письма, Заголовок - заголовок письма, 
//			   Файл - полное имя для передачи с письмом
//
//	Описание:  Осуществляет посылку письма по указанному адресу
Процедура ПослатьПисьмо(Адрес,Текст,Заголовок="",Файл="")
	Предупреждение("Подготовка к отправке обращения",1);
	Попытка  
		Адрес=?(Лев(Адрес,5)="SMTP:",Адрес,"SMTP:"+Адрес);
		Почта.Подключиться();
		Почта.НовоеСообщение();
		Почта.ДобавитьАдрес(Адрес);
		Если ПустаяСтрока(Файл)=0 Тогда
			Почта.ДобавитьФайл(Файл);
		КонецЕсли;
		Почта.Заголовок=Заголовок;
		Почта.Текст=Текст;
		Почта.Послать(1);
		Почта.Отключиться();
		ТекстЗамечания = "";
		Форма.Закрыть();
		Предупреждение("         Спасибо!         "+РазделительСтрок+РазделительСтрок+"Обращение отправлено.             ",2); 
	Исключение
		Сообщить("Неудачная попытка!  Отправка письма не произведена!");
	КонецПопытки;
КонецПроцедуры  //  ПослатьПисьмо
//******************************************************************************
//	ПереносВСтроке(СтрИсх,Дл)
//
//	Параметры:  СтрИсх - исходный текст, Дл - примерная длина строки
//
//	Описание:  Осуществляет разбивку "длинных" (более 60 символов) строк на подстроки
//			   Возвращает строку, состоящую из подстрок.
Функция ПереносВСтроке(СтрИсх,Дл)
	Перем СтрЧ,Стр;
	Если СтрДлина(СтрИсх)<=Дл Тогда
		Возврат СтрИсх;
	КонецЕсли;
	Стр = СтрИсх;
	Поз = Найти(Стр," ");
	СтрЧ = ""; 
	НачПоз = 1;
	СтрД=СтрДлина(Стр);
	Пока СтрДлина(СтрЧ)<=СтрД Цикл
		Пока (НачПоз<Дл) И (СтрДлина(Стр)>0) И (Поз>0) Цикл
			СтрЧ = СтрЧ + Сред(Стр,1,Поз);
			Стр = Прав(Стр,СтрДлина(Стр)-Поз);
			НачПоз = НачПоз+Поз;
			Поз = Найти(Стр," ");
		КонецЦикла;
		НачПоз = 1;
		СтрЧ = СтрЧ +?(Поз=0,Стр,"")+ РазделительСтрок;
	КонецЦикла;
	Возврат СтрЧ;
КонецФункции  // ПереносВСтроке
//******************************************************************************
//	Послать()
//
//	Описание:  Осуществляет формирование и посылку письма
Процедура Послать()
	Стр = "------- Сведения о программе -------"+РазделительСтрок;
	Стр = Стр+"Регистрационный номер: "+СокрЛП(Номер)+РазделительСтрок;
	Стр = Стр+"Программа: "+СокрЛП(Программа)+РазделительСтрок;
	Стр = Стр+"Релиз программы: "+СокрЛП(РелизП)+РазделительСтрок;
	Стр = Стр+"Конфигурация: "+СокрЛП(Конфигурация)+РазделительСтрок;
	Стр = Стр+"Релиз конфигурации: "+СокрЛП(РелизК)+РазделительСтрок+РазделительСтрок;
	Стр = Стр+"------- Сведения о пользователе ------- "+РазделительСтрок;
	Стр = Стр+"Организация: "+СокрЛП(Организация)+РазделительСтрок;
	Стр = Стр+"Ответственный: "+СокрЛП(ФИО)+РазделительСтрок;
	Стр = Стр+"Телефон: "+СокрЛП(Телефон)+РазделительСтрок;
	Стр = Стр+"Факс: "+СокрЛП(Факс)+РазделительСтрок+РазделительСтрок;
	Стр = Стр+"------- Сведения о компьютере ------- "+РазделительСтрок;
	Стр = Стр+"Компьютер: "+СокрЛП(Компьютер)+"  "+СокрЛП(Быстродействие)+РазделительСтрок;
	Стр = Стр+"Память: "+Память+" Мб"+РазделительСтрок;
	Стр = Стр+"Опер. система: "+СокрЛП(ОС)+РазделительСтрок+РазделительСтрок;
	Стр = Стр+"Сеть: "+СокрЛП(Сеть)+РазделительСтрок+РазделительСтрок;
	Заголовок = "Вопросы/Ошибки/Замечания";
	Стр = Стр+"------- Текст письма ------- "+РазделительСтрок;
	АдресG = "SMTP:"+АдресПолучателя; 
	ПослатьПисьмо(АдресG,Стр+ПереносВСтроке(ТекстЗамечания,60),Заголовок);
КонецПроцедуры   //  Послать
//******************************************************************************
//	ПоказатьВопрос()
//
//	Описание:  Осуществляет показ пояснений по заполнению формы
Процедура ПоказатьВопрос()
	Т = СоздатьОбъект("Текст");
	Т.ДобавитьСтроку("     Описание действий при заполнении обращения"+РазделительСтрок);
	Т.ДобавитьСтроку("1. Указание сведений о программе и пользователе.");
	Т.ДобавитьСтроку("   Для правильной обработки Вашего обращения необходимо знать различные сведения");
	Т.ДобавитьСтроку("   (об используемой программе, пользователе и компьютере). Для ввода сведений дважды щелкните ");
	Т.ДобавитьСтроку("   мышью ячейку ""Сведения"", расположенную в верхней строке формы.");
	Т.ДобавитьСтроку("   Многие параметры программа определит самостоятельно, но Вы можете их исправить или");
	Т.ДобавитьСтроку("   стереть, если не хотите их сообщать. Обязательными к заполнению являются параметры,");
	Т.ДобавитьСтроку("   отмеченные символом ""*""."+РазделительСтрок);
	Т.ДобавитьСтроку("2. Ввод многострочного текста.");
	Т.ДобавитьСтроку("   Для создания нового абзаца при написании обращения используйие сочетание клавиш CTRL+ENTER.");
	Т.ДобавитьСтроку("   Нажатие клавиши ENTER завершает ввод текста мнения.");
	Т.ДобавитьСтроку("   Чтобы продолжить ввод текста нажмите клавишу ENTER. При этом весь текст выделяется.");
	Т.ДобавитьСтроку("   Для снятия выделения нажмите любую клавишу управления курсором");
	Т.ДобавитьСтроку("   или щелкнуть мышью в выделеном тексте. Далее можете продолжить ввод текста."+РазделительСтрок);
	Т.ДобавитьСтроку("3. Сохранение введенной таблицы.");
	Т.ДобавитьСтроку("   Если требуется сохранить всю таблицу, выберите пункт ""Сохранить как"" раздела меню ""Файл"".");
	Т.ДобавитьСтроку("   Укажите имя файла и тип (""Таблицы (*.mxl)"")."+РазделительСтрок);
	Т.ДобавитьСтроку("4. Печать таблицы.");
	Т.ДобавитьСтроку("   Для печати таблицы выберите пункт ""Печать"" раздела меню ""Файл"".");
	Т.ДобавитьСтроку("   Укажите принтер и количество экземпляров при печати.");
	Т.ДобавитьСтроку("   Для настройки отступов выберите пункт ""Параметры страницы"".");
	Т.ДобавитьСтроку("   Если не используется цветной принтер, рекомендуем указать режим ""Черно-белая печать"".");
	Т.Показать(" Основные приемы при заполнении"); 
КонецПроцедуры   //  ПоказатьВопрос                                             
//******************************************************************************
//	УстановитьКомпоненту()
//
//	Описание:  Осуществляет загрузку внешней компоненты V7Plus.dll
Функция УстановитьКомпоненту()
	РасположениеФайла(ПутьФайлаОбработки,ИмяФайлаОбработки);
	Если ЗагрузитьВнешнююКомпоненту(ПутьФайлаОбработки+"v7plus.dll")=0 Тогда
		Если ЗагрузитьВнешнююКомпоненту(КаталогИБ()+"ExtForms\v7plus.dll")=0 Тогда
			Если ЗагрузитьВнешнююКомпоненту("v7plus.dll")=0 Тогда
				Сообщить("Не удалось обнаружить компоненту v7plus.dll!"); 
				Возврат 0;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Попытка
		Почта = СоздатьОбъект("AddIn.V7Mail");
	Исключение 
		Сообщить("Не удалось создать объект Addin.V7Mail!");
		Возврат 0;
	КонецПопытки; 
	Возврат 1;
КонецФункции   // УстановитьКомпоненту
//******************************************************************************
Процедура ПриОткрытии()  // Предопределенная
	Перем Страница;  	
	Если УстановитьКомпоненту()=0 Тогда
	    СтатусВозврата(0);
		Возврат;
	КонецЕсли;;
	Главная=ВосстановитьЗначение( "Главная страница" );
	Старт = Главная;
	Таблица.Опции(0,0,1,0,,"Письмо");
	Рефреш();
	Форма.Заголовок("Обращение в отдел технической поддержки фирмы ""1С""");
	АдресПолучателя = ?(ПустоеЗначение(АдресПолучателя)=1,АдресУмолчание,АдресПолучателя);
КонецПроцедуры  // ПриОткрытии
//******************************************************************************
Процедура ПриЗакрытии()   // Предопределенная
	Если ПустоеЗначение(ТекстЗамечания)=0 Тогда
		Если ПроверитьСведения()>0 Тогда
			Отв = Вопрос("Отправить обращение?",3);
		    Если Отв=6 Тогда // "Да" 
				Послать();
			ИначеЕсли Отв = 2 Тогда // Отказ
				СтатусВозврата(0);
			Иначе
				ТекстЗамечания="";
				Форма.Закрыть();
			КонецЕсли;
		Иначе 
			Форма.Закрыть();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры  //  ПриЗакрытии
//******************************************************************************
Процедура ПриВыбореЯчейкиТаблицы(Имя,Значение)  // Предопределенная
	Перем Стр;
	Стр = "";
	Если (Лев(Имя,9)="Отправить")ИЛИ(Имя="Пикт") Тогда 
		Если ПроверитьСведения()>0 Тогда
			Если ПустоеЗначение(ТекстЗамечания)=1 Тогда
			    Предупреждение("Введите текст обращения!");
			Иначе
				Если Вопрос("Отправить обращение?",4)=6 Тогда
					Послать();
				КонецЕсли;
			КонецЕсли;
		Иначе
		    Предупреждение("Недостаточно сведений о программе и пользователе!"+РазделительСтрок+
							"С помощью ячейки ""Сведения"" в верхней строке формы"+РазделительСтрок+ 
							"откройте диалог и введите недостающую информацию!");
		КонецЕсли;
	ИначеЕсли Имя="Сведения" Тогда
		ОткрытьФормуМодально("Обработка.ИППСистемнаяИнформация"); 
		Рефреш();
	ИначеЕсли Лев(Имя,6)="Ответы" Тогда 
		ОткрытьФорму("Обработка.ИППГлавная","Получить"); 
	ИначеЕсли Имя="Закрыть" Тогда
		Если ПустоеЗначение(ТекстЗамечания)=1 Тогда
			Форма.Закрыть();
		Иначе
			ПриЗакрытии();
		КонецЕсли;
	ИначеЕсли Имя="Вопрос" Тогда
		ПоказатьВопрос();
	КонецЕсли;
	ККК = Имя;
КонецПроцедуры  //  ПриВыбореЯчейкиТаблицы
//******************************************************************************
АдресУмолчание = "hline@1c.ru";
