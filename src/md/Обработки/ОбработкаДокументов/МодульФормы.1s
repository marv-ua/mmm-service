//_____________________________________________________________________________
Перем СписокНеПроводимыхДокументов; 
Перем Обновить;
Перем Расшифровка;
Перем Таб;
//_____________________________________________________________________________
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции
//_____________________________________________________________________________  
Процедура ПереносГруппы(Источник, Приемник)
	Для i=1 По Источник.РазмерСписка() Цикл
		Представление = "";
		Значение = Источник.ПолучитьЗначение(i, Представление);
		Приемник.ДобавитьЗначение(Значение, Представление);
	КонецЦикла;
	Источник.УдалитьВсе();
	Приемник.СортироватьПоПредставлению();
КонецПроцедуры //ПереносГруппы
//_____________________________________________________________________________
Процедура ПереносЭлемента(Источник,Приемник)
	Если Источник.ТекущаяСтрока() > 0 Тогда
		Представление = "";
		Значение = Источник.ПолучитьЗначение(Источник.ТекущаяСтрока(), Представление);
		Приемник.ДобавитьЗначение(Значение, Представление);
		ИсточникПозиция = Источник.ТекущаяСтрока();
		Если ИсточникПозиция = Источник.РазмерСписка() Тогда
			ИсточникПозиция = ИсточникПозиция - 1;
		КонецЕсли;
		Источник.УдалитьЗначение(Источник.ТекущаяСтрока());
		Источник.ТекущаяСтрока(ИсточникПозиция);
		Приемник.СортироватьПоПредставлению();
	КонецЕсли;
КонецПроцедуры //ПереносЭлемента
//_____________________________________________________________________________
Процедура ПриВыбореОбработки()
	ВидОбработки = СписокВидовОбработки.ПолучитьЗначение(СписокВидовОбработки.ТекущаяСтрока());
	СохранитьЗначение("ВидОбработки", ВидОбработки);
КонецПроцедуры
//_____________________________________________________________________________
Процедура Выполнить(Обработка)
	Перем БИ;
    
	Если ВыбранныеДокументы.РазмерСписка() = 0 Тогда
		Возврат;
	КонецЕсли;

	Док = СоздатьОбъект("Документ");

	Если ВыбрКонтрагент.Выбран() = 1 Тогда
		Док.ВыбратьПоЗначению(Дата1,Дата2,"Контрагент",ВыбрКонтрагент);
	Иначе
		Док.ВыбратьДокументы(Дата1,Дата2);
	КонецЕсли;

	Если (Обработка = "Печать") или (Обработка = "ПечатьСКомментарием") Тогда
		Док.ИспользоватьЖурнал("Общий",0);
		Если Обновить = 2 Тогда
		    СтрокаДействийФормы = "#Закрыть";
		КонецЕсли;
		Если (ТипЗначенияСтр(Таб) <> "Таблица") ИЛИ (Обновить = 0) Тогда
		   	Таб = СоздатьОбъект("Таблица");
		Иначе
		 	Таб.Очистить();
		КонецЕсли;
		Таб.ИсходнаяТаблица( "Таблица" );
		
		Расшифровка = СоздатьОбъект("СписокЗначений");
	 	Расшифровка.Установить("Отчет","ОбработкаДокументов");
	 	Расшифровка.Установить("Дата1", Дата1);
		Расшифровка.Установить("Дата2", Дата2);
		Расшифровка.Установить("ВыбранныеДокументы", ВыбранныеДокументы);
        Расшифровка.Установить("ВсеДокументы", ВсеДокументы);
		Расшифровка.Установить("ВыбрКонтрагент", ВыбрКонтрагент);
		Расшифровка.Установить("ВыбрСтрока", ВыбрСтрока);
		Расшифровка.Установить("ВыбрСтрокаКом", ВыбрСтрокаКом);
		Расшифровка.Установить("Признак", СписокПризнаков.ПолучитьЗначение(СписокПризнаков.ТекущаяСтрока()));
		Расшифровка.Установить("ВидОбработки", СписокВидовОбработки.ПолучитьЗначение(СписокВидовОбработки.ТекущаяСтрока()));

		Заголовок = "";
		Если ВыбрКонтрагент.Выбран() = 1 Тогда
			Заголовок = "по контрагенту " + СокрП(ВыбрКонтрагент.Наименование);
		КонецЕсли;
		Если ПустаяСтрока(ВыбрСтрока) = 0 Тогда
		    Если ПустаяСтрока(Заголовок) = 0 Тогда
		        Заголовок = Заголовок + "; ";
		    КонецЕсли;
		    Заголовок = Заголовок + "строка: """ + СокрП(ВыбрСтрока) + """";
		КонецЕсли;
		Если ПустаяСтрока(ВыбрСтрокаКом) = 0 Тогда
		    Если ПустаяСтрока(Заголовок) = 0 Тогда
		        Заголовок = Заголовок + "; ";
		    КонецЕсли;
		    Заголовок = Заголовок + "комментарий: """ + СокрП(ВыбрСтрокаКом) + """";
		КонецЕсли;
		Если СписокПризнаков.ТекущаяСтрока() > 1 Тогда
		    Если ПустаяСтрока(Заголовок) = 0 Тогда
		        Заголовок = Заголовок + "; ";
		    КонецЕсли;

			Стр = "";
			СписокПризнаков.ПолучитьЗначение(СписокПризнаков.ТекущаяСтрока(),Стр);
		    Заголовок = Заголовок + "только " + Нрег(Стр);
		КонецЕсли;
        
		Если Обработка = "Печать" Тогда
			Таб.ВывестиСекцию("Отчет|Основная");
		Иначе
			Таб.ВывестиСекцию("Отчет");
		КонецЕсли;
	Иначе
		
		Если Дата1 <= Константа.ДатаЗапретаРедактирования Тогда
			Предупреждение("Нельзя обрабатывать документы с датой, более ранней, чем дата запрета редактирования документов!");
			Возврат;
		КонецЕсли;
		
		Если (Обработка = "Провести") И (МонопольныйРежим() = 1) Тогда
			БИ = СоздатьОбъект("БухгалтерскиеИтоги");
			БИ.Актуальность(1);
		Иначе
			НачатьТранзакцию();
		КонецЕсли;
	КонецЕсли;

	НПП = 0;
	СуммаВсего = 0;
	Коммент    = "Выполнено обработкой ""Обработка документов""";
	ТипСобытия = "Документ";
	Пока Док.ПолучитьДокумент() = 1 Цикл
        Событие    = "";
		Объект     = "";
		Категория  = 0;
		
		//Проверка вида документа
		Если ВыбранныеДокументы.НайтиЗначение(Док.Вид()) = 0 Тогда
		    Продолжить;
		КонецЕсли;

		//Проверка строки операции
		Если ПустаяСтрока(ВыбрСтрока) = 1 Тогда
		ИначеЕсли Док.СуществуетОперация() = 0 Тогда
			Продолжить;
		ИначеЕсли Найти(ВРег(Док.Операция.Содержание),ВРег(ВыбрСтрока)) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		//Проверка строки комментария
		Если ПустаяСтрока(ВыбрСтрокаКом) = 1 Тогда
		ИначеЕсли Найти(ВРег(Док.Комментарий),ВРег(ВыбрСтрокаКом)) = 0 Тогда
			Продолжить;
		КонецЕсли;

		//Проверка признака документа
		Признак = СписокПризнаков.ПолучитьЗначение(СписокПризнаков.ТекущаяСтрока());

		Если Признак = "Проведенные" Тогда
		    Если Док.Проведен() = 0 Тогда
			    Продолжить;
		    КонецЕсли;
		ИначеЕсли Признак = "Непроведенные" Тогда
		    Если (Док.Проведен() = 1) или (Док.Вид() = "Операция") Тогда
			    Продолжить;
		    КонецЕсли;
		ИначеЕсли Признак = "Помеченные" Тогда
		    Если Док.ПометкаУдаления() = 0 Тогда
		        Продолжить;
		    КонецЕсли;
		ИначеЕсли Признак = "НеПомеченные" Тогда
		    Если Док.ПометкаУдаления() = 1 Тогда
		        Продолжить;
		    КонецЕсли;
		КонецЕсли;

		Состояние("Обработка "+Док.ДатаДок);

		НПП=НПП+1;
		Если Обработка = "Выкл" Тогда
		    Если Док.ПометкаУдаления() = 1 Тогда
		    ИначеЕсли Док.СуществуетОперация() = 0 Тогда
		    ИначеЕсли Док.Операция.ВключитьПроводки() = 0 Тогда
		    Иначе
				Сообщить("Выключение проводок "+Док);
				Док.Операция.ВключитьПроводки(0);
				Событие    = "ПроводкиВыключены";
				Объект     = Док.ТекущийДокумент();
				Категория  = 2;
		    КонецЕсли;

		ИначеЕсли Обработка = "Вкл" Тогда
		    Если Док.ПометкаУдаления() = 1 Тогда
		    ИначеЕсли Док.СуществуетОперация() = 0 Тогда
		    ИначеЕсли Док.Операция.ВключитьПроводки() = 1 Тогда
		    Иначе
		    	Сообщить("Включение проводок "+Док);
		    	Док.Операция.ВключитьПроводки(1);
				Событие    = "ПроводкиВключены";
				Объект     = Док.ТекущийДокумент();
				Категория  = 2;
		    КонецЕсли;

		ИначеЕсли Обработка = "Пометить" Тогда
			Если Док.ПометкаУдаления() = 0 Тогда
				Сообщить("Пометка на удаление "+Док);
				Док.Удалить(0); 
				Событие    = "ПомеченНаУдаление";
				Объект     = Док.ТекущийДокумент();
				Категория  = 2;
			КонецЕсли;

		ИначеЕсли Обработка = "ОтменитьПометку" Тогда
		    Если Док.ПометкаУдаления() = 1 Тогда
		    	Сообщить("Отмена пометки на удаление "+Док);
		    	Док.СнятьПометкуУдаления();
				Событие    = "СнятаПометкаНаУдаление";
				Объект     = Док.ТекущийДокумент();
				Категория  = 2;
		    КонецЕсли;

		ИначеЕсли Обработка = "ОтменаПроведения" Тогда
			Если Док.Проведен() = 1 Тогда
				Сообщить("Отмена проведения "+Док);
				Док.СделатьНеПроведенным();
				Событие    = "СделанНеПроведенным";
				Объект     = Док.ТекущийДокумент();
				Категория  = 2;
			КонецЕсли;

		ИначеЕсли Обработка = "Провести" Тогда
			Если Док.ПометкаУдаления() = 1 Тогда
			ИначеЕсли СписокНеПроводимыхДокументов.НайтиЗначение(Док.Вид()) = 0 Тогда
				Сообщить("Проведение "+Док);
				Если МонопольныйРежим() = 1 Тогда
					БИ.Рассчитать(, Док.ТекущийДокумент());
				КонецЕсли;
				Если Док.Провести() = 0 Тогда
					Возврат;
				КонецЕсли;
				Событие    = "Проведен";
				Объект     = Док.ТекущийДокумент();
				Категория  = 2;
			КонецЕсли;

		ИначеЕсли Обработка = "Печать" Тогда
			Таб.ВывестиСекцию("Строка|Основная");
            СуммаВсего = СуммаВсего + Док.Графа("Сумма");
			
		ИначеЕсли Обработка = "ПечатьСКомментарием" Тогда
			Таб.ВывестиСекцию("Строка");
			СуммаВсего = СуммаВсего + Док.Графа("Сумма");
		КонецЕсли; 
		Если    (Обработка = "Печать")
			или (Обработка = "ПечатьСКомментарием") Тогда   
		Иначе		
			ЗаписьЖурналаРегистрации(Коммент,ТипСобытия,Событие,Объект,Категория);
		КонецЕсли;
	КонецЦикла;

	Если (Обработка = "Печать") или (Обработка = "ПечатьСКомментарием") Тогда
		Если Обработка = "Печать" Тогда
			Таб.ВывестиСекцию("КонецОтчета|Основная");
			Таб.Опции(0, 0, 5, 0, "ОпцииПечатиРеестраКраткаяФорма", "ОкноРеестраКраткаяФорма");
			Таб.ТолькоПросмотр(1);
			Таб.Показать("Реестр документов");
			Таб.ПараметрыСтраницы(1);      
			Таб.ОбластьПечати(2,2,6+НПП,7);
		Иначе
			Таб.ВывестиСекцию("КонецОтчета");
			Таб.Опции(0, 0, 5, 0, "ОпцииПечатиРеестраСКомментарием", "ОкноРеестраСКомментарием");
			Таб.ТолькоПросмотр(1);
			Таб.Показать("Реестр документов");
			Таб.ПараметрыСтраницы(2);  
			Таб.ОбластьПечати(2,2,6+НПП,8);
		КонецЕсли;
	Иначе
		Если (Обработка = "Провести") И (МонопольныйРежим() = 1) Тогда
		Иначе
			ЗафиксироватьТранзакцию();
		КонецЕсли;

		Предупреждение("Обработка закончена!");
	КонецЕсли;
КонецПроцедуры
//_____________________________________________________________________________
Процедура ПриОткрытии()

	СписокВидовОбработки.УдалитьВсе();
	СписокВидовОбработки.ДобавитьЗначение("Печать", "Печать реестра (краткая форма)");
	СписокВидовОбработки.ДобавитьЗначение("ПечатьСКомментарием", "Печать реестра (с комментарием)");
    СписокВидовОбработки.ДобавитьЗначение("Выкл", "Выключить проводки");
    СписокВидовОбработки.ДобавитьЗначение("Вкл", "Включить проводки");
    СписокВидовОбработки.ДобавитьЗначение("Пометить", "Пометить на удаление");
    СписокВидовОбработки.ДобавитьЗначение("ОтменитьПометку", "Отменить пометки на удаление");
    СписокВидовОбработки.ДобавитьЗначение("ОтменаПроведения", "Сделать не проведенными");
    СписокВидовОбработки.ДобавитьЗначение("Провести", "Провести");

    СписокПризнаков.УдалитьВсе();
	СписокПризнаков.ДобавитьЗначение("Все","Все, удовлетворяющие заданным условиям");
	СписокПризнаков.ДобавитьЗначение("Проведенные","Проведенные");
	СписокПризнаков.ДобавитьЗначение("Непроведенные","Непроведенные");
	СписокПризнаков.ДобавитьЗначение("Помеченные","Помеченные на удаление");
	СписокПризнаков.ДобавитьЗначение("НеПомеченные","Не помеченные на удаление");  
	
	СписокНеПроводимыхДокументов = СоздатьОбъект("СписокЗначений");
	ВсеДокументы.УдалитьВсе();
	Для Индекс=1 По Метаданные.Документ() Цикл
		Значение = Метаданные.Документ(Индекс).Идентификатор;
		Представление = Метаданные.Документ(Индекс).Представление();
		Комментарий = Метаданные.Документ(Индекс).Комментарий;
		Если ПустаяСтрока(Комментарий) = 0 Тогда
			Представление = Представление + " (" + Комментарий +")";
		КонецЕсли;
		ВсеДокументы.ДобавитьЗначение(Значение, Представление);

		Если Метаданные.Документ(Индекс).РазрешитьПроведение = 0 Тогда
			СписокНеПроводимыхДокументов.ДобавитьЗначение(Значение, Представление);
		КонецЕсли;
	КонецЦикла;
	
    Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ(); 
	
	ВсеДокументы.СортироватьПоПредставлению();
	ВидОбработки = ВосстановитьЗначение("ВидОбработки");
	СписокВидовОбработки.ТекущаяСтрока(СписокВидовОбработки.НайтиЗначение(ВидОбработки));
	
	ВыбрКонтрагент.ВыборГруппы(0);
	Форма.Кн_Видеокурс.Видимость(?(ТипЗначения(Видео_Компонента) = 0, 0, 1));

	Если глФлагРасшифровки = 0 Тогда
		Обновить = 0;
	Иначе
		Обновить = глОбновить;
		
		Расшифровка = глРасшифровка;
		Дата1 = Расшифровка.Получить("Дата1");
		Дата2 = Расшифровка.Получить("Дата2");
		Спс = Расшифровка.Получить("ВыбранныеДокументы");
		ВыбранныеДокументы.УдалитьВсе();
		Спс.Выгрузить(ВыбранныеДокументы);
        Спс = Расшифровка.Получить("ВсеДокументы");
		ВсеДокументы.УдалитьВсе();
		Спс.Выгрузить(ВсеДокументы);
		ВыбрКонтрагент = Расшифровка.Получить("ВыбрКонтрагент");
		ВыбрСтрока = Расшифровка.Получить("ВыбрСтрока");
		ВыбрСтрокаКом = Расшифровка.Получить("ВыбрСтрокаКом");
		Признак = Расшифровка.Получить("Признак");
		СписокПризнаков.ТекущаяСтрока(СписокПризнаков.НайтиЗначение(Признак));
		ВидОбработки = Расшифровка.Получить("ВидОбработки");
		СписокВидовОбработки.ТекущаяСтрока(СписокВидовОбработки.НайтиЗначение(ВидОбработки));
		
		Если Обновить <> 0 Тогда
			Таб = глТаблица;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Выполнить(ВидОбработки);
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
//_____________________________________________________________________________