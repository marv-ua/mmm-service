Перем Трассировка;            // список значений-трассировка
Перем СписокРаскрытыхЗначений;
 
//*****************************************************************************
// Скопировать()
//
// Параметры
//		нет
//
// Вызывается из формул элементов диалога
//		Скопировать
//
// Возвращаемое значение
//		нет
//
// Описание
//		Копирует результат трассировки в отдельную таблицу    
//

Процедура Скопировать()
	
	ИмяФайла=КаталогПользователя()+"Komm_temp";
	Таблица.Записать(ИмяФайла);
	Таблица1 = СоздатьОбъект("Таблица"); 
	Таблица1.Открыть(ИмяФайла);
	Таблица1.ТолькоПросмотр(1);
	Таблица1.Опции(0,0,0,0,"Трассировка", "Трассировка");
	Таблица1.Показать("Комментарий выполняемых действий")
	
КонецПроцедуры //Скопировать

//*****************************************************************************
// Очистить()
//
// Параметры
//		нет
//
// Вызывается из формул элементов диалога
//		Очистить
//
// Возвращаемое значение
//		нет
//
// Описание
//		  	 
//
Процедура Очистить()
	
	Если Таблица.ВысотаТаблицы() > 0 тогда
		Если Вопрос("Таблица будет очищена."
			"Продолжить?","Да+Нет", 60)="Нет" Тогда
			Возврат 
		Иначе
			Таблица.Очистить();
			ЗаголовокОкна = "";         
			Таблица.ВывестиСекцию("Описатель");
			Таблица.Показать()
		КонецЕсли
	КонецЕсли	
	
КонецПроцедуры	// Очистить	

//******************************************************************************
//	ОтобразитьТаблицу()
//
//	Параметры: 
//		нет
//
//	Возвращаемое значение:
// 		нет
//
//	Описание:
// 		Отображает таблицу обработки - вызывается при открытии и повторном открытии
//
Процедура ОтобразитьТаблицу()
	
	Если Таблица.ВысотаТаблицы() > 0 Тогда  // пропустим пустую строку
		Символ=""; СтрокаСообщения ="";
		Таблица.ВывестиСекцию("Строка");
	КонецЕсли;
	СписокСообщений = Трассировка.ПолучитьЗначение(2); // таблица сообщений трассировки    
	Если СписокСообщений.КоличествоСтрок()>0 тогда
		ЗаголовокОкна = Трассировка.ПолучитьЗначение(3,СтрокаСообщения);         
		Таблица.ВывестиСекцию("Описатель");
		Таблица.Область(Таблица.ВысотаТаблицы(),1,Таблица.ВысотаТаблицы(),1).ЦветТекста(255,0,0);
		Для Сч=1 По СписокСообщений.КоличествоСтрок() Цикл 
			СписокСообщений.ПолучитьСтрокуПоНомеру(Сч);
			Значение = СписокСообщений.ТипСообщения;  // тип сообщения - определяет его цвет 
			СтрокаСообщения = СписокСообщений.ТекстСообщения; // текст сообщения - выводится в таблице
			Значение1 = СписокСообщений.Форма;
			Значение2 = СписокСообщений.Объект;
			Значение3 = СписокСообщений.Контрол;
			Символ="";
			Если ПустоеЗначение(Значение1) = 0 тогда
				Символ = "V";
			КонецЕСли;	
			Таблица.ВывестиСекцию("Строка");
			Если ПустоеЗначение(Значение1) = 0 тогда 
				Таблица.Область(Таблица.ВысотаТаблицы(),1,Таблица.ВысотаТаблицы(),1).Расшифровка(Значение1,1) 
			КонецЕСли;	
			Область = Таблица.Область(Таблица.ВысотаТаблицы(),2,Таблица.ВысотаТаблицы(),2);
			Если Значение = 1 Тогда // "красное" сообщение (фатальная ошибка)
				Область.ЦветТекста(255,0,0);
			ИначеЕсли Значение = -1 Тогда  // предупреждение
				Область.ЦветТекста(0,0,255);
			КонецЕсли;
		КонецЦикла;  
	КонецЕсли;
	Таблица.Опции(0,0,0,0,,"Трассировка");
	Таблица.Показать();
КонецПроцедуры	// ОтобразитьТаблицу

//******************************************************************************
//	Предопределенная процедура
//
Процедура ПриОткрытии()
	Попытка
		Форма.Заголовок("Комментарий выполняемых действий",0);
		Трассировка = Форма.Параметр;
		Если Трассировка.ПолучитьЗначение(2).КоличествоСтрок() > 0 Тогда
			ОтобразитьТаблицу(); 
		Иначе
			СтатусВозврата(0); // не открываем обработку
		КонецЕсли;
	Исключение
		СтатусВозврата(0); // не открываем обработку
	КонецПопытки
КонецПроцедуры	// ПриОткрытии

//******************************************************************************
//	Предопределенная процедура
//
Процедура ПриПовторномОткрытии()
	Попытка
		Если ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений" Тогда
			Трассировка = Форма.Параметр;
			Если Трассировка.ПолучитьЗначение(2).КоличествоСтрок() > 0 Тогда
				ОтобразитьТаблицу(); 
			Иначе
				СтатусВозврата(0); // не открываем обработку
			КонецЕсли;
		КонецЕсли;
	Исключение
		СтатусВозврата(0); // не открываем обработку
	КонецПопытки
КонецПроцедуры	// ПриПовторномОткрытии 

//******************************************************************************
//	Предопределенная процедура
//

Процедура ОбработкаЯчейкиТаблицы(Значение,Флаг)
	ТипОбъекта = ТипЗначения(Значение);
	Флаг = 1;   
	Если (ТипОбъекта <= 2) Тогда
		Флаг = 0;   
		ОткрытьФорму(Значение)
	КонецЕсли;
КонецПроцедуры  // ОбработкаЯчейкиТаблицы
