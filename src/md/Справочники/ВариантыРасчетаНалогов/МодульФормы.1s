//******************************************************************************
// СформироватьНаименование()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога: 
//  Флажок "Сумма включает НДС",
//  Флажок "Сумма включает НП".
//
// Описание:
//  Наименование формируется по следующим правилам:
//  1. Наименование состоит из сведений об отношении налогов (НДС, НП) 
//     к цене и сумме, указанной в первичном документе.
//	2. Сначала в наименовании отражаются сведения об НДС, затем - об НП.
//     Исключение составляют случаи, когда выбранная налоговая ставка равна 
//     нулю. Тогда сведения о налоге опускаются. Если для обоих налогов
//     указаны нулевые ставки, формируется наименование "Без налогов".
//	3. Сведения о налоге начинаются с названия налога ("НДС" или "НП" 
//     соответственно).
//	4. Если налог входит в цену и сумму первичного документа, то к названию 
//     налога добавляется "в сумме", если не входит, то - "сверху".
//  5. Затем указывается ставка налога. Исключение составляют те случаи,
//     когда для варианта расчета налогов указано: "Использовать ставку 
//     из справочника" - тогда налоговая ставка в наименовании не отражается.
//
Процедура СформироватьНаименование()
	
	Стр = "";
	Если ((ПустоеЗначение(СтавкаНДС) = 0) и (СтавкаНДС.Ставка <> 0)) 
		или (ПустоеЗначение(СтавкаНДС) = 1) Тогда
		Стр = Стр + "НДС ";
		Если СуммаВключаетНДС = 1 Тогда
			Стр = Стр + "в сумме";
		Иначе
			Стр = Стр + "сверху";
		КонецЕсли;
		
		Если СтавкаНДС.Ставка <> 0 Тогда
			Стр = Стр + " " + глПредставлениеСтавки(СтавкаНДС);
		КонецЕсли;
	КонецЕсли;

	Если ((ПустоеЗначение(СтавкаНП) = 0) и (СтавкаНП.Ставка <> 0)) 
		или (ПустоеЗначение(СтавкаНП) = 1) Тогда
		Если ПустаяСтрока(Стр) = 0 Тогда
		    Стр = Стр + "; ";
		КонецЕсли;
		
		Стр = Стр + "НП ";
		Если СуммаВключаетНП = 1 Тогда
			Стр = Стр + "в сумме";
		Иначе
			Стр = Стр + "сверху";
		КонецЕсли;
		
		Если СтавкаНП.Ставка <> 0 Тогда
			Стр = Стр + " " + глПредставлениеСтавки(СтавкаНП);
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(Стр) = 1 Тогда
	    Стр = "Без налогов";
	КонецЕсли;

	Если Наименование <> Стр Тогда
	    Наименование = Стр;
		Форма.Обновить(1);
	КонецЕсли;
	
КонецПроцедуры // СформироватьНаименование()

//******************************************************************************
// ПриВыбореСтавки(Налог)
//
// Параметры:
//  Налог - Строка - налог, ставка которого изменилась.
//			"НДС" - при изменении ставки НДС.
//			"НП" - при изменении ставки НП.
//
// Вызывается из формул элементов диалога:
//  Реквизит СтавкаНДС,
//  Реквизит СтавкаНП.
//
// Описание:
//  Если выбрана нулевая налоговая ставка сбрасываются и становятся недоступными 
// флаги "Сумма включает НДС" и "Сумма включает НП" соответственно. Во всех 
// случаях при изменении налоговой ставки изменяется наименование варианта 
// расчета налогов.
//
Процедура ПриВыбореСтавки(Налог)
	
	Если Налог = "НДС" Тогда
		Если (СтавкаНДС.Выбран() = 1) и (СтавкаНДС.Ставка = 0) Тогда
			Если СуммаВключаетНДС = 1 Тогда
				СуммаВключаетНДС = 0;
			КонецЕсли;
			Форма.СуммаВключаетНДС.Доступность(0);
		Иначе
			Форма.СуммаВключаетНДС.Доступность(1);
		КонецЕсли;

	Иначе
		Если (СтавкаНП.Выбран() = 1) и (СтавкаНП.Ставка = 0) Тогда
			Если СуммаВключаетНП = 1 Тогда
				СуммаВключаетНП = 0;
			КонецЕсли;
			Форма.СуммаВключаетНП.Доступность(0);
		Иначе
			Форма.СуммаВключаетНП.Доступность(1);
		КонецЕсли;
	КонецЕсли;
	СформироватьНаименование();
	
КонецПроцедуры // ПриВыбореСтавки()

//******************************************************************************
// ПриВыбореВариантаИспользованияСтавки(Налог)
//
// Параметры:
//  Налог - Строка - налог, ставка которого изменилась.
//			"НДС" - при изменении ставки НДС.
//			"НП" - при изменении ставки НП.
//
// Вызывается из формул элементов диалога:
//  Переключатель "НДС: Использовать ставку...",
//  Переключатель "НП: Использовать ставку...".
//
// Описание:
//  В зависимости от положения переключателя налоговая ставка попадает в 
// наименование (положение "Использовать ставку...") или исключается из него
// ("Использовать ставку из справочника").
//
Процедура ПриВыбореВариантаИспользованияСтавки(Налог)

	Если Налог = "НДС" Тогда
		Если ИспользоватьСтавкуНДС = 1 Тогда //Использовать ставку из справочника
			Если ПустоеЗначение(СтавкаНДС) = 0 Тогда
				СтавкаНДС = "";
			КонецЕсли;
			Форма.СтавкаНДС.Доступность(0);
			
		Иначе //Использовать указанную ставку
			Если ПустоеЗначение(СтавкаНДС) = 1 Тогда
			    СтавкаНДС = Константа.ОсновнаяСтавкаНДС;
			КонецЕсли;
			Форма.СтавкаНДС.Доступность(1);
		КонецЕсли;
		ПриВыбореСтавки("НДС");
		
	Иначе
		Если ИспользоватьСтавкуНП = 1 Тогда //Использовать ставку из справочника
			Если ПустоеЗначение(СтавкаНП) = 0 Тогда
			    СтавкаНП = "";
			КонецЕсли;
			Форма.СтавкаНП.Доступность(0);
			
		Иначе //Использовать указанную ставку
			Если ПустоеЗначение(СтавкаНП) = 1 Тогда
			    СтавкаНП = Константа.ОсновнаяСтавкаНП;
			КонецЕсли;
			Форма.СтавкаНП.Доступность(1);
		КонецЕсли;
		ПриВыбореСтавки("НП");
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореВариантаИспользованияСтавки()

//****************************************************************************** 
// Предопределенная процедура
//
Процедура ВводНового(Копирование)
	
	Если Копирование = 1 Тогда
		Возврат;
	КонецЕсли;
	
	СтавкаНДС = глЗначениеПоУмолчанию("ОсновнаяСтавкаНДС");
	СтавкаНП = глЗначениеПоУмолчанию("ОсновнаяСтавкаНП");
	СформироватьНаименование();

КонецПроцедуры // ВводНового()

//******************************************************************************
// Предопределенная процедура
// 
Процедура ПриОткрытии() 

	Если ПустоеЗначение(СтавкаНДС) = 1 Тогда
		ИспользоватьСтавкуНДС = 1; //Использовать ставку из справочника
	Иначе
		ИспользоватьСтавкуНДС = 2; //Использовать указанную ставку
	КонецЕсли;
	Если ПустоеЗначение(СтавкаНП) = 1 Тогда
		ИспользоватьСтавкуНП = 1; //Использовать ставку из справочника
	Иначе
		ИспользоватьСтавкуНП = 2; //Использовать указанную ставку
	КонецЕсли;
	ПриВыбореВариантаИспользованияСтавки("НДС");
	ПриВыбореВариантаИспользованияСтавки("НП");

КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи()
	
	Если (ИспользоватьСтавкуНДС = 2) и (ПустоеЗначение(СтавкаНДС) = 1) Тогда
		Предупреждение("Введите ставку НДС или поставьте переключатель ""Использовать ставку НДС""
						|в положение ""Использовать ставку из справочника""");
		СтатусВозврата(0);
		Возврат;
		
	ИначеЕсли (ИспользоватьСтавкуНП = 2) и (ПустоеЗначение(СтавкаНП) = 1) Тогда
		Предупреждение("Введите ставку НП или поставьте переключатель ""Использовать ставку НП""
						|в положение ""Использовать ставку из справочника""");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()