//******************************************************************************
// ПоКнопкеВыбораДаты()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка выбора даты.
//
// Описание:
//  Выбирается дата просмотра и редактирования значений периодических реквизитов 
// выбирается по кнопке, чтобы не менялся признак модифицированности формы.
//
Процедура ПоКнопкеВыбораДаты()
	
	глВвестиДатуПериодическихРеквизитов(Контекст, 1);
	
КонецПроцедуры // ПоКнопкеВыбораДаты()

//******************************************************************************
// ПолучитьЕдИзмНормы()
// 
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	единица измерения элемента.
//
// Вызывается из формул элементов диалога:
//  - текст рядом с полем "Норма".
//
// Описание:
//	
Функция ПолучитьЕдИзмНормы()

	Стр = "";
	
	Если (Элемент.Выбран() = 1) Тогда
	
		Если (Метаданные.Справочник(Элемент.Вид()).Реквизит("ЕдиницаИзмерения").Выбран() = 1) Тогда
		
			Стр = Элемент.ЕдиницаИзмерения;
		
		КонецЕсли;
	
	КонецЕсли;

	Возврат Стр;

КонецФункции // ПолучитьЕдИзмНормы()

//******************************************************************************
// ПриИзмененииВидаЭлемента()
//
// Параметры:
//	Нет.
//
// Вызывается из формул элементов диалога:
//  - поле "ВидыДляВыбора".
//
// Описание:
//	Обработка изменения значения поля.
//
Процедура ПриИзмененииВидаЭлемента()
    
	ВидЭлемента = ВидыДляВыбора.ТекущаяСтрока();
	
	Если ВидЭлемента = 1 Тогда // материал
		НазначитьВид(Элемент, "Материалы");
		
	Иначе  // продукция (полуфабрикат)
		НазначитьВид(Элемент, "Номенклатура");
	КонецЕсли;

КонецПроцедуры // ПриИзмененииВидаЭлемента()

//******************************************************************************
//	ПриИзмененииЭлемента()
//
// Параметры:
//	Нет.
//
// Вызывается из формул элементов диалога:
//  - поле "Элемент".
//
// Описание:
//	Обработка изменения значения поля.
//
Процедура ПриИзмененииЭлемента()

	Если ПустаяСтрока(Наименование) = 1 Тогда
		Вид = "";
		ВидыДляВыбора.ПолучитьЗначение(ВидыДляВыбора.ТекущаяСтрока(), Вид);
		Наименование = "" + Вид + ", " + Элемент;
	КонецЕсли;   

КонецПроцедуры // ПриИзмененииЭлемента()

//*****************************************************************************
// ПроверкаЗацикливания(ТекЭлемент,ЭлементПоиска)
//
// Параметры:
//	ТекЭлемент - текущий элемент справочника
//	ЭлементПоиска - элемент для сравнения.
//
// Возвращаемое значение:
//	0 - есть зацикливание, 1 - нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//	Проверка на использование элемента типа "продукция" 
//	в вышестоящих нормах расходов.
//
Функция ПроверкаЗацикливания(ТекЭлемент,ЭлементПоиска)

	Если ТекЭлемент.Вид() <> "Номенклатура" Тогда
		Возврат(1);
	КонецЕсли;

	Если ТекЭлемент = ЭлементПоиска Тогда
		Вид = "";
		ВидыДляВыбора.ПолучитьЗначение(ВидыДляВыбора.ТекущаяСтрока(), Вид);
		Предупреждение(""+Вид + " "+ СокрЛП(ТекЭлемент.Наименование) + "
		               |не может быть использован в качестве элемента затрат
		               |для " + СокрЛП(ЭлементПоиска.Наименование) + ".
		               |Использование замкнутых циклов нормативов расходов
		               |приведет к неправильной работе программы!");   
		Возврат (0);

	КонецЕсли;

	НормыРасходов = СоздатьОбъект("Справочник.НормыРасходов");
	НормыРасходов.ИспользоватьВладельца(ТекЭлемент);

	НормыРасходов.ВыбратьЭлементы();
	Пока НормыРасходов.ПолучитьЭлемент() = 1 Цикл  

		Если НормыРасходов.ВидЭлемента <> 1 Тогда // не материал
			Продолжить;
		КонецЕсли;

		Если НормыРасходов.ЭтоГруппа() = 1 Тогда
			Продолжить;
		КонецЕсли;     

		Если НормыРасходов.ПометкаУдаления() = 1 Тогда
			Продолжить;
		КонецЕсли;  

		Если НормыРасходов.Элемент = ЭлементПоиска Тогда

			Предупреждение("Обнаружено зацикливание по элементу:
			               |"+ СокрЛП(ЭлементПоиска.Наименование) + "
			               |используется как элемент затрат для 
			               |"+ СокрЛП(ТекЭлемент.Наименование) + ".
			               |Использование замкнутых циклов нормативов расходов
			               |приведет к неправильной работе программы!");   
			Возврат (0);

		Иначе 

			Если ПроверкаЗацикливания(НормыРасходов.Элемент, ЭлементПоиска) = 0 Тогда
				Возврат(0);
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

	Возврат(1);	

КонецФункции // ПроверкаЗацикливания()

//******************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(ВведенКопированием)

	Если ВведенКопированием = 1 Тогда
		Возврат;
	КонецЕсли;

	КолПрод = 1;
	ВидЭлемента = 1;
	НазначитьВид(Элемент, "Материалы");
	
КонецПроцедуры // ВводНового()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()

	// Форме может быть передана дата просмотра и редактирования значений
	// периодических реквизитов, например, когда форма открыта из обработки
	// "ЗначенияПериодическихРеквизитов".
	глПолучитьДатуПериодическихРеквизитов(Контекст);

	// Системно не надо контролировать изменение периодических реквизитов.
	СохранениеПериодическихРеквизитов(0);

	Форма.Элемент.НеИзменятьВид(1);
	
	ВидыДляВыбора.ДобавитьЗначение(1, "Материал");
	ВидыДляВыбора.ДобавитьЗначение(2, "Полуфабрикат");
	ВидыДляВыбора.ДобавитьЗначение(3, "Продукция");
	ВидыДляВыбора.ТекущаяСтрока(ВидЭлемента);

КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриПовторномОткрытии()
	
	// Форме может быть передана дата просмотра и редактирования значений
	// периодических реквизитов, например, когда форма открыта из обработки
	// "ЗначенияПериодическихРеквизитов".
	глПолучитьДатуПериодическихРеквизитов(Контекст);
	
КонецПроцедуры // ПриПовторномОткрытии()

//******************************************************************************
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлементаДиалога, ФлагСтандартнойОбработки) // Предопределенная процедура

	Если ИдентЭлементаДиалога = "Элемент" Тогда

		Если ВидЭлемента = 2 Тогда //Полуфабрикат

			ФлагСтандартнойОбработки = 0;
			ОткрытьФорму("Справочник.Номенклатура.ГотоваяПродукция", Перечисление.ТипыНоменклатуры.Полуфабрикат);

		ИначеЕсли ВидЭлемента = 3 Тогда //Продукция

			ФлагСтандартнойОбработки = 0;
			ОткрытьФорму("Справочник.Номенклатура.ГотоваяПродукция", Перечисление.ТипыНоменклатуры.Продукция);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ПриНачалеВыбораЗначения

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи()

	Если ПустоеЗначение(ВидыДляВыбора) = 1 Тогда
		Предупреждение("Укажите вид элемента затрат.");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	Если (ВидЭлемента = 2) // полуфабрикат
	 или (ВидЭлемента = 3) Тогда // продукция
	 	Если ПроверкаЗацикливания(Элемент,Владелец) = 0 Тогда
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли; 

	Если (Элемент.Выбран() = 1) Тогда

		Если Найти(Врег(Наименование), СокрЛП(Врег(Элемент.Наименование))) = 0 Тогда
	        Вид = "";
			ВидыДляВыбора.ПолучитьЗначение(ВидыДляВыбора.ТекущаяСтрока(), Вид);
			Если Вопрос("Наименование элемента норм расходов не включает
			            |наименование элемента затрат. Изменить на 
			            |""" + "" + Вид + ", " + Элемент +""" ? ", "Да+Нет") = "Да" Тогда
	
				Наименование = "" + Вид + ", " + Элемент; 
				Форма.Обновить();
				Активизировать("Наименование");
				СтатусВозврата(0);
				Возврат;
	
			КонецЕсли;
	
		КонецЕсли;

	КонецЕсли;
	
	// Обработке ЗаписьПериодическихРеквизитов необходимо передать
	// значения периодических реквизитов, введенные в форме диалога.
	Список = СоздатьОбъект("СписокЗначений");
	Список.Установить("Норма", Норма);
	Список.Установить("КолПрод", КолПрод);
	
	// При записи вызывается обработка ЗаписьПериодическихРеквизитов 
	// для управления записью значений периодических реквизитов.
	Если глЗаписьПериодическихРеквизитов(Контекст, Список) = 0 Тогда
		СтатусВозврата(0);
	КонецЕсли;

КонецПроцедуры // ПриЗаписи()
