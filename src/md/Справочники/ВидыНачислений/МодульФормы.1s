//******************************************************************************
//	Установить(Режим="") 
//
//	Параметры: Режим - строка, режим работы: "Все" - отметить все строки
//					   "Сброс" - снять отметку всех строк
//					   "" - инвертировать отметку всех строк
//
//	Описание: Устанавливает, снимает или инвертирует отметки выбора строк 
//
Процедура Установить(Режим="") 
	
	Для Н = 1 По СписокНалогов.РазмерСписка() Цикл
		Если Режим="Все" Тогда
			СписокНалогов.Пометка(Н, 1);
		ИначеЕсли Режим="Сброс" Тогда
			СписокНалогов.Пометка(Н, 0); 
		Иначе
			СписокНалогов.Пометка(Н, ?(СписокНалогов.Пометка(Н) = 1, 0, 1)); 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры  // Установить()

//****************************************************************************** 
// Предопределенная процедура
//
Процедура ВводНового(Скопирован)
	
	Если Скопирован = 1 Тогда
		Возврат;
	КонецЕсли;
		
	ЕСН_ФСС = 1;
	ЕСН_ФОМС = 1;
	ПФР = 1;
	ФСС_НС = 1;
	
	ВидыДоходов = СоздатьОбъект("Справочник.ВидыДоходов");
	Если ВидыДоходов.НайтиПоКоду("2000") = 1 Тогда
		ВидДохода = ВидыДоходов.ТекущийЭлемент();
		Код = "2000";
	КонецЕсли;

КонецПроцедуры // ВводНового()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.ВыбратьВсе.Доступность(0);
		Форма.СброситьВсе.Доступность(0);
		Форма.Инвертировать.Доступность(0);
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаЗаписать.Доступность(0);
	КонецЕсли; 
	
	СписокНалогов.ДобавитьЗначение("ЕСН_ФБ", "ЕСН в части, зачисляемой в федеральный бюджет (до 2010 г.)");
	СписокНалогов.ДобавитьЗначение("ЕСН_ФСС", "Взносы в части, зачисляемой в ФСС");
	СписокНалогов.ДобавитьЗначение("ЕСН_ФОМС", "Взносы в части, зачисляемой в ФОМС");
	СписокНалогов.ДобавитьЗначение("ПФР", "Страховые взносы в ПФР");
	СписокНалогов.ДобавитьЗначение("ФСС_НС", "Взносы на страхование от несчастных случаев и профессиональных заболеваний");
	СписокНалогов.ДобавитьЗначение("СО", "Сбор на образование");
	
	Для СчетчикЦикла = 1 По СписокНалогов.РазмерСписка() Цикл
		Реквизит = СписокНалогов.ПолучитьЗначение(СчетчикЦикла);
		СписокНалогов.Пометка(СчетчикЦикла, ПолучитьАтрибут(Реквизит));
	КонецЦикла;
	
	СписокНалогов.ТекущаяСтрока(1);
	
КонецПроцедуры // ПриОткрытии()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи()
	
	Для СчетчикЦикла = 1 По СписокНалогов.РазмерСписка() Цикл
		Реквизит = СписокНалогов.ПолучитьЗначение(СчетчикЦикла);
		УстановитьАтрибут(Реквизит, СписокНалогов.Пометка(СчетчикЦикла));
	КонецЦикла;
	
	Если ВидДохода.Выбран() = 1 Тогда
	    Код = ВидДохода.Код;
		
	Иначе
		Код = "";
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

//*****************************************************************************
