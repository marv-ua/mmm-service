////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//*****************************************************************************

// ПроверитьВидРасхода()
//
// Возвращаемое 
// 0 - если вид расхода нельзя выбирать в правило, 1 - можно
//
// Вызывается при редактировании в форме списка реквизита Вид расхода и Статья затрат
//
// Описание:
// 
Функция ПроверитьВидРасхода(ВидРасхода) 
	
	Если ВидРасхода = Перечисление.ГруппыВидыРасходов.Амортизация Тогда  
		
		Сообщить("Вид расхода ""Амортизация"" не может быть выбран: порядок отнесения расходов по начисленной амортизации задается в справочнике ""Основные средства"" или в справочнике ""Нематериальные активы""");
		Возврат 0;
	КонецЕсли;  
	
	
	Если (ВидРасхода = Перечисление.ГруппыВидыРасходов.НИОКР) Или (ВидРасхода = Перечисление.ВидыРасходов.ОсвоениеПриродныхРесурсов) Тогда  
		
		Сообщить("Вид расхода ""НИОКР"" не может быть выбран: порядок отнесения расходов задается в справочнике ""Расходы будущих периодов""");
		Возврат 0;
	КонецЕсли;  
	
	Если (ВидРасхода = Перечисление.ГруппыВидыРасходов.ГарантийныйРемонтИОбслуживание) Или 
	     (ВидРасхода = Перечисление.ГруппыВидыРасходов.ПредставительскиеРасходы) Или  
	     (ВидРасхода = Перечисление.ГруппыВидыРасходов.РасходыНаРекламу) Или   
	     (ВидРасхода = Перечисление.ГруппыВидыРасходов.ДобровольноеИОбязательноеСтрахование) Тогда  
		
		Сообщить("Вид расхода " + ВидРасхода +" может быть только косвенным");
		Возврат 0;
	КонецЕсли;  
	
		Возврат 1; 
		
КонецФункции // ПроверитьВидРасхода()

// ПриИзменииСчетаКредита()
//
// Возвращаемое нет
//
// Вызывается при редактировании в форме списка реквизита Счет кредита
//
// Описание:
// 
			
Процедура ПриИзменииСчетаКредита() 
	
	Если СравнитьСчета(СчетПоКоду("10.7"), СчетКредита) = 1 Тогда  
		
		Сообщить("Счет 10.7 не может быть выбран: для материалов, перерабатываемых сторонними организациями, используются правила учета для счетов, заданных в справочнике ""Материалы""");
		СчетКредита = "";
		Возврат;
	КонецЕсли;
	           
	Если СравнитьСчета(СчетПоКоду("02"), СчетКредита) = 1 Тогда  
		
		Сообщить("Счет 02 не может быть выбран: порядок отнесения расходов по начисленной амортизации задается в справочнике ""Основные средства""");
		СчетКредита = "";
		Возврат;
	КонецЕсли;  
	
	Если СравнитьСчета(СчетПоКоду("04"), СчетКредита) = 1 Тогда  
		
		Сообщить("Счет 04 не может быть выбран: порядок отнесения расходов по начисленной амортизации задается в справочнике ""Нематериальные активы""");
		СчетКредита = "";
		Возврат;
	КонецЕсли;  
	
	Если СравнитьСчета(СчетПоКоду("05"), СчетКредита) = 1 Тогда  
		
		Сообщить("Счет 05 не может быть выбран: порядок отнесения расходов по начисленной амортизации задается в справочнике ""Нематериальные активы""");
		СчетКредита = "";
		Возврат;
	КонецЕсли;  
	
	Если СравнитьСчета(СчетПоКоду("97"), СчетКредита) = 1 Тогда  
		
		Сообщить("Счет 97 не может быть выбран: порядок отнесения расходов будущих периодов задается в справочнике ""Расходы будущих периодов""");
		СчетКредита = "";
		Возврат;
	КонецЕсли;
		
	Если СравнитьСчета(СчетПоКоду("69"), СчетКредита) = 1 Тогда  
		
		Сообщить("Счет 69 не может быть выбран: порядок отнесения расходов по начисленным налогам задается в справочнике ""Налоги и отчисления""");
		СчетКредита = "";
		Возврат;
	КонецЕсли;  

КонецПроцедуры   

//*****************************************************************************
// ПриИзмененииВидаРасходов()
//
// Возвращаемое нет
//
// Вызывается при редактировании в форме списка реквизита Вид расхода
//
// Описание: неоходима для контроля за вводом только допустимых значений
// для контроля за согласованностью ввода вида расхода и статьи затрат
// 
			
Процедура ПриИзмененииВидаРасходов() 
	
	Если ВидРасходовДляНалоговогоУчета.Выбран() = 0 Тогда  
		
		Возврат;   
	Иначе
		Если ПроверитьВидРасхода(ВидРасходовДляНалоговогоУчета) = 0 Тогда
			ВидРасходовДляНалоговогоУчета = ""; 
			Возврат;
		КонецЕсли;
	КонецЕсли; 
	
	Если СодержитсяВСпискеИсключенийПрямыхЗатрат(ВидРасходовДляНалоговогоУчета) = 1 Тогда
		Сообщить ("Вид расходов " + СтатьяЗатрат.ВидРасходовДляНалоговогоУчета + " не доступен для выбора");
		ВидРасходовДляНалоговогоУчета = "";
		Возврат;
	КонецЕсли;
	
	Если СтатьяЗатрат.Выбран() = 1 Тогда 
		
		Если СтатьяЗатрат.ВидРасходовДляНалоговогоУчета <> ВидРасходовДляНалоговогоУчета Тогда
			СтатьяЗатрат = "";
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры 

//*****************************************************************************
// ПриИзмененииВидаРасходов()
//
// Возвращаемое нет
//
// Вызывается при редактировании в форме списка реквизита Статья затрат
//
// Описание: неоходима для контроля за вводом только допустимых значений
// для контроля за согласованностью ввода вида расхода и статьи затрат
// 
			

Процедура ПриИзмененииСтатьиЗатрат() 
	
	Если СтатьяЗатрат.Выбран() = 0 Тогда  
		
		Возврат; 
	Иначе
		 Если ПроверитьВидРасхода(СтатьяЗатрат.ВидРасходовДляНалоговогоУчета) = 0 Тогда  
		 	
			СтатьяЗатрат = "";
			Возврат;
			
		 КонецЕсли;
	КонецЕсли;
	
	Если СодержитсяВСпискеИсключенийПрямыхЗатрат(СтатьяЗатрат.ВидРасходовДляНалоговогоУчета) = 1 Тогда
	     Сообщить ("Статья с видом затрат " + СтатьяЗатрат.ВидРасходовДляНалоговогоУчета + " не доступна для выбора");
		 СтатьяЗатрат = "";
		 Возврат;
	КонецЕсли;
	
	
	Если ВидРасходовДляНалоговогоУчета.Выбран() = 1 Тогда  
		
		Если СтатьяЗатрат.ВидРасходовДляНалоговогоУчета <> ВидРасходовДляНалоговогоУчета Тогда
			
			Сообщить("Укажите статью затрат с видом расходов (НУ): " + ВидРасходовДляНалоговогоУчета);
			СтатьяЗатрат = "";
			Возврат;
			
		КонецЕсли; 
	Иначе
		ВидРасходовДляНалоговогоУчета = СтатьяЗатрат.ВидРасходовДляНалоговогоУчета;	
	КонецЕсли; 
	
КонецПроцедуры    

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, Флаг)
	
	Если ЭлементДиалога = "СтатьяЗатрат" Тогда
		Если ВидРасходовДляНалоговогоУчета.Выбран() = 1 Тогда
			ОткрытьФорму("Справочник.СтатьиЗатратНаПроизводство", ВидРасходовДляНалоговогоУчета);
			Флаг = 0;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//********************************************************************************  

Процедура ПриОткрытии()     
	
	   Сортировка("Код", 1);  
	   
КонецПроцедуры // ПриОткрытии


СчетКредита.ВыборГруппы(1);
	