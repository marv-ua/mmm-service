//******************************************************************************
// ПриВыбореСчета()
// 
// Вызывается из формул элементов диалога:
//  Счет.
//
// Описание:
//  Устанавливает тип объектов аналитического учета по счету затрат.
//
Процедура ПриВыбореСчета()
	
	Для а = 1 По 3 Цикл
		НазначитьТип("Субконто"+а,Счет.ВидСубконто(а));
	КонецЦикла;

КонецПроцедуры // ПриВыбореСчета()

//******************************************************************************
// ПриВыбореВидаДвижения()
// 
// Вызывается из формул элементов диалога:
//  ВидДвижения.
//
// Описание:
//  Устанавливает тип объектов аналитического учета по счету затрат.
//
Процедура ПриВыбореВидаДвижения()
	
	Если Счет.Выбран() = 0 Тогда
		Если ВидДвижения = Перечисление.ВидыДвиженийДенежныхСредств.ВыручкаПоОбычнымВидамДеятельности Тогда
			НазначитьТип("Субконто1", "Справочник.Контрагенты");
			НазначитьТип("Субконто2", "Справочник.Договоры");
			
		ИначеЕсли ВидДвижения = Перечисление.ВидыДвиженийДенежныхСредств.ПриобретениеТоваровПродукцииРаботИУслуг Тогда
			НазначитьТип("Субконто1", "Справочник.Контрагенты");
			НазначитьТип("Субконто2", "Справочник.Договоры");
			НазначитьТип("Субконто3", "Неопределенный");
			
		Иначе
			НазначитьТип("Субконто1", "Неопределенный");
			НазначитьТип("Субконто2", "Неопределенный");
			НазначитьТип("Субконто3", "Неопределенный");
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ПриВыбореВидаДвижения()

//******************************************************************************
// ИнформацияОВидеДвижения()
//
// Возвращаемое значение:
//  Строка - информационная строка.
// 
// Вызывается из формул элементов диалога:
//  Текст "ИнформацияОВидеДвижения".
//
// Описание:
//  Формирует информационную строку, если для вида движения допустимо 
// автоматическое распределения оплаты на оплату и аванс.
//
Функция ИнформацияОВидеДвижения()
	
	Если ВидДвижения = Перечисление.ВидыДвиженийДенежныхСредств.ВыручкаПоОбычнымВидамДеятельности Тогда
		Возврат "Для выбранного вида движения допустимо автоматическое распределение поступающих " +
				"денежных средств на оплату и аванс";
		
	ИначеЕсли ВидДвижения = Перечисление.ВидыДвиженийДенежныхСредств.ПриобретениеТоваровПродукцииРаботИУслуг Тогда
		Возврат "Для выбранного вида движения допустимо автоматическое распределение списываемых " +
				"денежных средств на оплату и аванс";
		
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции // ИнформацияОВидеДвижения()

//******************************************************************************
// ПредставлениеСубконто(НомерСубконто)
//
// Параменты:
//  НмерСубконто - Число.
//
// Возвращаемое значение:
//  Строка - представление вида субконто.
// 
// Вызывается из формул элементов диалога:
//  Текст "ПредставлениеСубконто(1)",
//  Текст "ПредставлениеСубконто(2)",
//  Текст "ПредставлениеСубконто(3)".
//
// Описание:
//  Формирует представление субконто.
//
Функция ПредставлениеСубконто(НомерСубконто)
	
	Если ТипЗначения(ПолучитьАтрибут("Субконто" + НомерСубконто)) = 0 Тогда
		Возврат "";
		
	Иначе
		Возврат ПолучитьАтрибут("Субконто" + НомерСубконто).ПредставлениеВида() + ":";
	КонецЕсли;

КонецФункции // ПредставлениеСубконто()