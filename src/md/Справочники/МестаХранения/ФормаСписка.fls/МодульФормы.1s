////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем СтарыйТипЦен, СтарыйТипСклада, ОбязательныйОтбор;
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
Процедура ПриОткрытии()
	ОбязательныйОтбор = 0;
	Форма.ТипСклада.Видимость(1);
	Если ПустоеЗначение(Форма.Параметр)=0 Тогда
		Если ТипЗначенияСтр(Форма.Параметр) = "Перечисление" Тогда
			ОтборСклада = Форма.Параметр;
			УстановитьОтбор("ТипСклада",ОтборСклада);                 
		ИначеЕсли ТипЗначенияСтр(Форма.Параметр) = "Строка" Тогда
			Если СокрЛП(Форма.Параметр) = "Нет" Тогда
				Форма.ТипСклада.Видимость(0);
			КонецЕсли;
		ИначеЕсли ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений" Тогда
			Параметры = Форма.Параметр;
			ОтборСклада = Параметры.Получить("ОтборСклада");
			ПоказыватьТипСклада = Параметры.Получить("ПоказыватьТипСклада");
			ПоказыватьТипЦен = Параметры.Получить("ПоказыватьТипЦен"); 
			Если ПустоеЗначение(ОтборСклада)=0 Тогда
				УстановитьОтбор("ТипСклада",ОтборСклада);
				ОбязательныйОтбор = 1;
				ВидыОтбора("");
			КонецЕсли;
			Форма.ТипСклада.Видимость(ПоказыватьТипСклада);
			Форма.ТипЦен.Видимость(ПоказыватьТипЦен);
		КонецЕсли;   
	КонецЕсли; 
	Если Константа.ИспользоватьТипыЦен = Нет Тогда
	    Форма.ТипЦен.Видимость(0);
	КонецЕсли;
КонецПроцедуры 
//******************************************************************************
Процедура ПриВводеСтроки()
	Если ОбязательныйОтбор = 1 Тогда
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры
//******************************************************************************
Процедура ПриНачалеРедактированияСтроки()
	СтарыйТипЦен = ТипЦен;
	СтарыйТипСклада = ТипСклада;
КонецПроцедуры

//******************************************************************************
Процедура ПриЗаписи()
	Если СтарыйТипСклада = Перечисление.ТипыСкладов.Розничный Тогда
		Сообщение = "";
		Предупреждать = 0;
		Если ТипСклада<>СтарыйТипСклада Тогда 
			Предупреждать = 1;
			Сообщение = Сообщение + "Перед снятием признака ""Розничный""";
		КонецЕсли;
		Если (ПустоеЗначение(СтарыйТипЦен) = 0) и (СтарыйТипЦен <> ТипЦен) Тогда
			Сообщение = Сообщение + ?(Предупреждать = 1," и изменением типа цен розничного склада ","Перед изменением типа цен розничного склада ");
			Предупреждать = 1;
		КонецЕсли;
		Если Предупреждать = 1 Тогда
			Сообщение = Сообщение + РазделительСтрок+ "необходимо провести инвентаризацию."+ РазделительСтрок+"Принять изменения?";
			Если Вопрос(Сообщение,"Да+Нет",60) <> "Да" Тогда
				СтатусВозврата(0);
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры