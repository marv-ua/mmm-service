//******************************************************************************
// ПоКнопкеВыбораДаты()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка выбора даты.
//
// Описание:
//  Выбирается дата просмотра и редактирования значений периодических реквизитов 
// выбирается по кнопке, чтобы не менялся признак модифицированности формы.
//
Процедура ПоКнопкеВыбораДаты()
	
	глВвестиДатуПериодическихРеквизитов(Контекст, 1);
	
КонецПроцедуры // ПоКнопкеВыбораДаты()

//******************************************************************************
// ПриВыбореСпособаПогашенияСтоимости()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  СпособПогашенияСтоимости - Поле ввода.
//
// Описание:
//  Осуществяется управление видимостью реквизитов,
//  неоходимых для расчета суммы ежемесячного погашения стоимости.
//
Процедура ПриВыбореСпособаПогашенияСтоимости()
	
	Если ПустоеЗначение(СпособПогашенияСтоимости) = 1 Тогда
	    СпособПогашенияСтоимости = Перечисление.СпособПогашенияСтоимости.Линейный;
	КонецЕсли;
	
	Если СпособПогашенияСтоимости = Перечисление.СпособПогашенияСтоимости.Линейный Тогда
		Форма.СтатикСрокПолезногоИспользования.Заголовок("Срок полезного использования:");
		Форма.СтатикФактическийОбъем.Видимость(0);
	    Форма.ФактическийОбъемПродукцииРаботЗаМесяц.Видимость(0);
		Форма.КнопкаИстория.Видимость(0);
		
	ИначеЕсли СпособПогашенияСтоимости = Перечисление.СпособПогашенияСтоимости.ПропорциональноОбъемуПродукцииРабот Тогда
		Форма.СтатикСрокПолезногоИспользования.Заголовок("Предполагаемый объем выпуска продукции:");
		Форма.СтатикФактическийОбъем.Видимость(1);
	    Форма.ФактическийОбъемПродукцииРаботЗаМесяц.Видимость(1);
		Форма.КнопкаИстория.Видимость(1);
		
	ИначеЕсли СпособПогашенияСтоимости = Перечисление.СпособПогашенияСтоимости.ПогашатьСтоимостьВМоментПередачиВПроизводство Тогда
		Форма.СтатикСрокПолезногоИспользования.Заголовок("Срок полезного использования:");
		Форма.СтатикФактическийОбъем.Видимость(0);
	    Форма.ФактическийОбъемПродукцииРаботЗаМесяц.Видимость(0);
		Форма.КнопкаИстория.Видимость(0);
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореСпособаПогашенияСтоимости()

//_____________________________________________________________________________
Процедура ПриВыбореСчета()
	Для А = 1 По 3 Цикл
		НазначитьТип("Субконто"+А,СчетЗатрат.ВидСубконто(А));
	КонецЦикла; 
	Если (Константа.ИспользоватьСписокКорректныхПроводок = Да) Тогда
		глПроверкаКорректныхПроводок(СчетЗатрат,СчетПоКоду("10.10"));
	КонецЕсли;
КонецПроцедуры //ПриВыбореСчета

//******************************************************************************
// ПоКнопкеИстория(ВидимыеРеквизиты)
//
// Параметры:
//  ВидимыеРеквизиты - "строка" - идентификаторы периодических реквизитов,
//                     значения которых надо показать, разделенные запятыми;
//                     если не передана - показываются все реквизиты.
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "История".
//
// Описание:
//  Вызывается обработка "ЗначенияПериодическихРеквизитов" для просмотра истории 
// значений периодических реквизитов.
//
Процедура ПоКнопкеИстория(ВидимыеРеквизиты="")
	                                     
	глЗначенияПериодическихРеквизитов(Контекст, , , ВидимыеРеквизиты);
	
КонецПроцедуры // ПоКнопкеИстория()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, ФлагСтандОбр)
	
	Если (ИдентЭлемДиалога = "СчетЗатрат") и (Константа.ИспользоватьСписокКорректныхПроводок = Да) Тогда
		СписокКорректныхПроводок = СоздатьОбъект("СписокЗначений");
	    СписокКорректныхПроводок.Установить("Счет", СчетПоКоду("10.10"));
		глЗначениеОтбора = СписокКорректныхПроводок;
	КонецЕсли;
	
КонецПроцедуры

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Если ДокументПередачаВПроизводство.Выбран() = 1 Тогда
		Форма.Код.Доступность(0);
		Форма.Наименование.Доступность(0);
		Форма.ДатаВводаВЭксплуатацию.Доступность(0);
		Форма.СпособПогашенияСтоимости.Доступность(0);
		Форма.СрокПолезногоИспользования.Доступность(0);
		Форма.СчетЗатрат.Доступность(0);
		Форма.Субконто1.Доступность(0);
		Форма.Субконто2.Доступность(0);
		Форма.Субконто3.Доступность(0);
	КонецЕсли;
	
	ПриВыбореСпособаПогашенияСтоимости();
	
	// Форме может быть передана дата просмотра и редактирования значений
	// периодических реквизитов, например, когда форма открыта из обработки
	// "ЗначенияПериодическихРеквизитов".
	глПолучитьДатуПериодическихРеквизитов(Контекст);
	
	// Системно не надо контролировать изменение периодических реквизитов.
	СохранениеПериодическихРеквизитов(2, "*");
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриПовторномОткрытии()
	
	// Форме может быть передана дата просмотра и редактирования значений
	// периодических реквизитов, например, когда форма открыта из обработки
	// "ЗначенияПериодическихРеквизитов".
	глПолучитьДатуПериодическихРеквизитов(Контекст);
	
КонецПроцедуры // ПриПовторномОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи()
	
	СтатусВозврата(глБухЗаписьПериодическихРеквизитов(Контекст));

КонецПроцедуры // ПриЗаписи()