////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;
Перем СписокНедоступныхКолонок;
Перем ИдентификаторФормы;
Перем ПолныйСписокКолонок;

//********************************************************************************
Процедура ОткрытьПутеводитель()
	ОткрытьФорму("Обработка.Путеводитель", "РасчетыСКонтрагентами");
КонецПроцедуры //ОткрытьПутеводитель 

//********************************************************************************
Процедура ОткрытьДоговоры()
	Если ТекущийЭлемент().Выбран() = 0 Тогда
	    Предупреждение("Нельзя открыть список договоров, не указав Контрагента.
						|Введите Контрагента.");
	ИначеЕсли ТекущийЭлемент().ЭтоГруппа() = 1 Тогда
	    Предупреждение("Нельзя открыть список договоров для группы Контрагентов.
						|Выберите конкретного Контрагента.");
	Иначе
		ОткрытьФорму("Справочник.Договоры");
	КонецЕсли;
КонецПроцедуры //ОткрытьДоговоры

//********************************************************************************
Процедура ОткрытьРасчетныеСчета()
	Если ТекущийЭлемент().Выбран() = 0 Тогда
	    Предупреждение("Нельзя открыть список расчетных счетов, не указав Контрагента.
						|Введите Контрагента.");
	ИначеЕсли ТекущийЭлемент().ЭтоГруппа() = 1 Тогда
	    Предупреждение("Нельзя открыть список расчетных счетов для группы Контрагентов.
						|Выберите конкретного Контрагента.");
	Иначе
		ОткрытьФорму("Справочник.РасчетныеСчета");
	КонецЕсли;
КонецПроцедуры //ОткрытьРасчетныеСчета 

//********************************************************************************
Процедура ПечатьСписка()                                              
	глРасшифровка = СоздатьОбъект("СписокЗначений");
	глРасшифровка.Установить("ВыбРодитель",ИспользоватьРодителя());
	глРасшифровка.Установить("ТекФильтр", 2);
	глРасшифровка.Установить("ФильтроватьПо","");	    
	глРасшифровка.Установить("ПоказатьРС", 0);	    
	глРасшифровка.Установить("ПоказатьДоговора", 0);	    
	Если (Сортировка()="Код") Тогда
		глРасшифровка.Установить("Сортировать", 1);            
	ИначеЕсли (Сортировка()="Наименование") Тогда
		глРасшифровка.Установить("Сортировать", 2);            
	Иначе
		глРасшифровка.Установить("Сортировать", 2);
	КонецЕсли;			
	глФлагРасшифровки = 1;
	ОткрытьФорму("Отчет.СписокКонтрагентов");
	глФлагРасшифровки = 0;
КонецПроцедуры

//******************************************************************************
// ОСВпоСчету()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "ОСВ по счету".
//
// Описание:
//  Вызов оборотно-сальдовой ведомости по счету.
//
Процедура ОСВпоСчету()
	
	Меню = СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("60", "счет 60");
	Меню.ДобавитьЗначение("62", "счет 62");
	Меню.ДобавитьЗначение("76.5", "счет 76.5");

	КодСчета = "";
	Если Меню.ВыбратьЗначение(КодСчета,,,,1) = 1 Тогда
		Расшифровка = СоздатьОбъект("СписокЗначений");
		Расшифровка.Установить("Отчет", "ОборотноСальдоваяВедомостьПоСчету");
		Расшифровка.Установить("Счет", СчетПоКоду(КодСчета));
		Расшифровка.Установить("Дата1", НачМесяца(РабочаяДата()));
		Расшифровка.Установить("Дата2", КонМесяца(РабочаяДата()));
		Расшифровка.Установить("ДанныеПоСубсчетам", 1);
		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.Контрагенты);
		Если ТекущийЭлемент().Выбран() = 1 Тогда
			Расшифровка.Установить("Субконто1", ТекущийЭлемент());
			Если ТекущийЭлемент().ЭтоГруппа() = 1 Тогда
				Расшифровка.Установить("ПоГруппам1", 1);
				Расшифровка.Установить("ОтборСубконто1", 1);
			Иначе
				Расшифровка.Установить("ПоГруппам1", 0);
				Расшифровка.Установить("ОтборСубконто1", 2);
			КонецЕсли;
		КонецЕсли;
		Расшифровка.Установить("ВидСубконто2", ВидыСубконто.Договоры);
		Расшифровка.Установить("ОтборСубконто2", 1);
		Расшифровка.Установить("ПоГруппам2", 1);

		глРасшифровка = Расшифровка;
		глОбновить = 2;
		глТаблица = "";
		глФлагРасшифровки = 1;
		ОткрытьФорму("Отчет.ОборотноСальдоваяВедомостьПоСчету");
		глФлагРасшифровки = 0;
		глОбновить = 0;
	КонецЕсли;
	
КонецПроцедуры // ОСВпоСчету()
                               
//******************************************************************************
Процедура ИнвентаризацияРасчетов()
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "ИнвентаризацияРасчетов");
	Расшифровка.Установить("Дата", РабочаяДата());
	Расшифровка.Установить("ТипЗадолженности", 1);
	Расшифровка.Установить("Контрагент", ТекущийЭлемент());
		
	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	глОбновить = 2;
	ОткрытьФорму("Отчет.ИнвентаризацияРасчетов");
	глФлагРасшифровки = 0;
	глОбновить = 0;
	
КонецПроцедуры //ИнвентаризацияРасчетов()

//******************************************************************************
// ПоКнопкеПечать()
// 
// Параметры: 
//	Нет
//
// Описание:
// 	Вызывается по кнопке "Печать"  
// 	
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		ПечатьСписка();
		
	ИначеЕсли НомерТекущейФормы = 2  Тогда	
		ИнвентаризацияРасчетов()
		
	ИначеЕсли НомерТекущейФормы = 3  Тогда
		ОСВпоСчету();
		
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(Контекст, "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Параметры:
//  нет.    
//
// Возвращаемое значение:
//  нет.
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Справочник." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()

//******************************************************************************
// УправлениеДиалогом()
//
// Параметры:
//  Нет
//
// Описание:
//  Управляет доступностью колонок
//
Процедура УправлениеДиалогом()
	
	Для Сч = 1 По СписокНедоступныхКолонок.РазмерСписка() Цикл
		Форма.ПолучитьАтрибут(СписокНедоступныхКолонок.ПолучитьЗначение(Сч)).Доступность(0);
	КонецЦикла;
	
КонецПроцедуры // УправлениеДиалогом()

//********************************************************************************
Процедура ПриОткрытии() // предопределенная
	Если Форма.РежимВыбора() = 1 Тогда
	    Форма.кнПутеводитель.Видимость(0);
	КонецЕсли;
	
	РедактироватьВДиалоге(1,1);
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Справочник." + Вид(),ТаблицаПечФорм);
	
	// Управление видимостью колонок	
	глУстановитьВидимостьКолонок(Контекст, ИдентификаторФормы, ПолныйСписокКолонок, 1);
	
КонецПроцедуры //ПриОткрытии

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриРедактированииНовойСтроки()
	
	УправлениеДиалогом();
	
	Копирование = ?(ПустоеЗначение(Наименование) = 1, 0, 1);
	Если Копирование = 1 Тогда	//копирование элемента
		ОсновнойДоговор = "";
		ОсновнойСчет = "";
	    Возврат;
	КонецЕсли;
	
	ВидКонтрагента = Перечисление.ВидыКонтрагентов.Организация;
	
КонецПроцедуры // ПриРедактированииНовойСтроки()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеРедактированияСтроки()
	
	УправлениеДиалогом();
	
КонецПроцедуры // ПриНачалеРедактированияСтроки()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//
ИдентификаторФормы	=	"Справочник_Контрагенты_ФормаСписка_Основная";

ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");
	
// добавим информацию о встроенной форме
ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Список контрагентов";
ТаблицаПечФорм.Кнопка       = "Список";

ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Инвентаризация расчетов с контрагентом";
ТаблицаПечФорм.Кнопка       = "Инв.расч.";


ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "ОСВ по счету";
ТаблицаПечФорм.Кнопка       = "ОСВ";

// Список колонок видимостью которых будем управлять
ПолныйСписокКолонок = СоздатьОбъект("СписокЗначений");
ПолныйСписокКолонок.ДобавитьЗначение("Код", "Код");
ПолныйСписокКолонок.ДобавитьЗначение("Наименование", "Наименование");
ПолныйСписокКолонок.ДобавитьЗначение("ПолнНаименование", "Полное наименование");
ПолныйСписокКолонок.ДобавитьЗначение("ИНН", "ИНН");
ПолныйСписокКолонок.ДобавитьЗначение("ВидКонтрагента", "Вид контрагента");
ПолныйСписокКолонок.ДобавитьЗначение("ЮридическийАдрес", "Юридический адрес");
ПолныйСписокКолонок.ДобавитьЗначение("ПочтовыйАдрес", "Почтовый адрес");
ПолныйСписокКолонок.ДобавитьЗначение("Телефоны", "Телефоны");
ПолныйСписокКолонок.ДобавитьЗначение("ОсновнойДоговор", "Основной договор (редактируется в форме элемента)");
ПолныйСписокКолонок.ДобавитьЗначение("ОсновнойСчет", "Основной счет"); //NDline
//	При первом запуске, пока пользователь не настроил видимость колонок,
//  будут видимы только колонки "Код","Наименование"
ПолныйСписокКолонок.Пометка(1,	1);
ПолныйСписокКолонок.Пометка(2, 	1);

// Список недоступных для редактирования колонок
СписокНедоступныхКолонок = СоздатьОбъект("СписокЗначений");
СписокНедоступныхКолонок.ДобавитьЗначение("ОсновнойДоговор");
