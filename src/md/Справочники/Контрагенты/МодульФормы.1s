Перем НовыйКонтрагент;
//******************************************************************************
//
Процедура ПриИзмененииВидаКонтрагента()
	Если Форма.ВидКонтрагента.Доступность() = 1 Тогда
		Если ВидКонтрагента = Перечисление.ВидыКонтрагентов.ЧастноеЛицо Тогда
			// Добавляем закладку с паспортными данными
			Если Форма.Закладки.НайтиЗначение("Документ") = 0 Тогда
				Форма.Закладки.ДобавитьЗначение("Документ", "Паспортные данные");
			КонецЕсли;
			Форма.ИспользоватьСлой("Общий, ОсновныеСведения, ЧастноеЛицо", 2);
			ОКПО = "";
		Иначе
			// Удаляем закладку с паспортными данными
			Поз = Форма.Закладки.НайтиЗначение("Документ");
			Если Поз > 0 Тогда
				Форма.Закладки.УдалитьЗначение(Поз);
			КонецЕсли;
			Форма.ИспользоватьСлой("Общий, ОсновныеСведения, Организация", 2);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПриИзмененииВидаКонтрагента

//******************************************************************************
//
Процедура ОткрытьДоговоры()
	Если Выбран() = 0 Тогда
		Если Вопрос("Список договоров можно редактировать
		            |только после записи контрагента.
		            |Записать контрагента?", "Да+Нет") = "Да" Тогда
			Записать();
		КонецЕсли;
	КонецЕсли;
	Если Выбран() = 0 Тогда
		Возврат;
	КонецЕсли;

	КонтекстФормы = "";
	ОткрытьФорму("Справочник.Договоры", КонтекстФормы);
	КонтекстФормы.ИспользоватьВладельца(ТекущийЭлемент());
КонецПроцедуры // ОткрытьДоговоры

//******************************************************************************
//
Процедура ОбновитьСписокСчетов()
	СписокСчетов.УдалитьВсе();
	Спр = СоздатьОбъект("Справочник.РасчетныеСчета");
	Спр.ИспользоватьВладельца(ТекущийЭлемент());
	Спр.ПорядокНаименований();
	Спр.ВыбратьЭлементы();
	Пока Спр.ПолучитьЭлемент() = 1  Цикл
		Если Спр.ПометкаУдаления() = 1 Тогда
			Продолжить;
		КонецЕсли;
		СписокСчетов.ДобавитьЗначение(Спр.ТекущийЭлемент(), Спр.Наименование + ": р/с №" + СокрЛП(Спр.Номер) + " в " + СокрЛП(Спр.БанкОрганизации.Наименование));
	КонецЦикла;
КонецПроцедуры //ОбновитьСписокСчетов

//******************************************************************************
//
Процедура ДобавитьРС()
	Если Выбран() = 0 Тогда
		Если Вопрос("Добавить новый расчетный счет можно
		            |только после записи контрагента.
		            |Записать контрагента?", "Да+Нет") = "Да" Тогда
			Записать();
		КонецЕсли;
	КонецЕсли;
	Если Выбран() = 1 Тогда
		ОткрытьФормуМодально("Элемент.РасчетныеСчета",,,, ТекущийЭлемент());
		НомерТекущейСтроки = СписокСчетов.ТекущаяСтрока();
		СтарыйСписокСчетов = СоздатьОбъект("СписокЗначений");
		СписокСчетов.Выгрузить(СтарыйСписокСчетов);
		ОбновитьСписокСчетов();
		НовыйСчетВведен = 0;
		А = 1;
		Пока (А <= СписокСчетов.РазмерСписка()) и (НовыйСчетВведен = 0) Цикл
			Если СтарыйСписокСчетов.НайтиЗначение(СписокСчетов.ПолучитьЗначение(А)) = 0 Тогда
				НовыйСчетВведен = 1;
			КонецЕсли;
			А = А + 1;
		КонецЦикла;
		Если НовыйСчетВведен = 1 Тогда
			СписокСчетов.ТекущаяСтрока(А-1);
		Иначе
			СписокСчетов.ТекущаяСтрока(НомерТекущейСтроки);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // Добавить

//******************************************************************************
//
Процедура ИзменитьРС()
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Возврат;
	КонецЕсли;
	Если СписокСчетов.РазмерСписка() = 0 Тогда
		ДобавитьРС();
	ИначеЕсли СписокСчетов.ТекущаяСтрока() > 0 Тогда
		РасчетныйСчет = СписокСчетов.ПолучитьЗначение(СписокСчетов.ТекущаяСтрока());
		ОткрытьФормуМодально(РасчетныйСчет);
		ОбновитьСписокСчетов();
		СписокСчетов.ТекущаяСтрока(СписокСчетов.НайтиЗначение(РасчетныйСчет));
	КонецЕсли;
КонецПроцедуры // Изменить

//******************************************************************************
//
Процедура УдалитьРС()
	НомерТекущейСтроки = СписокСчетов.ТекущаяСтрока();
	Если НомерТекущейСтроки = 0 Тогда
		Возврат;
	КонецЕсли;
	Если Вопрос("Вы действительно хотите удалить счет?
	            |(Cчет будет помечен на удаление.)", "Да+Нет") = "Нет" Тогда
		Возврат;
	КонецЕсли;

	РасчетныйСчет = СписокСчетов.ПолучитьЗначение(НомерТекущейСтроки);
	Спр = СоздатьОбъект("Справочник.РасчетныеСчета");
	Спр.ИспользоватьВладельца(ТекущийЭлемент());
	Если Спр.НайтиЭлемент(РасчетныйСчет) = 1 Тогда
		Спр.Удалить(0);
		СписокСчетов.УдалитьЗначение(НомерТекущейСтроки);
		Если НомерТекущейСтроки > СписокСчетов.РазмерСписка() Тогда
			СписокСчетов.ТекущаяСтрока(СписокСчетов.РазмерСписка());
		Иначе
			СписокСчетов.ТекущаяСтрока(НомерТекущейСтроки);
		КонецЕсли;
		Форма.Обновить();
	КонецЕсли;
КонецПроцедуры // УдалитьРС

//******************************************************************************
//
Процедура ВводНового(ВведенКопированием) // Предопределенная процедура
	
	НовыйКонтрагент = 1;
	
	Если ВведенКопированием = 1 Тогда
		ОсновнойДоговор = "";
		ОсновнойСчет = "";
	    Возврат;
	КонецЕсли;

	Наименование = "Новый";
	ВидКонтрагента = Перечисление.ВидыКонтрагентов.Организация;
	
КонецПроцедуры // ВводНового

//******************************************************************************
//
Процедура ПриОткрытии() // Предопределенная процедура
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Общие","Общие");
	Форма.Закладки.ДобавитьЗначение("РасчетныеСчета","Расчетные счета");
	Если ВидКонтрагента = Перечисление.ВидыКонтрагентов.ЧастноеЛицо Тогда
		Форма.Закладки.ДобавитьЗначение("Документ","Паспортные данные");
		Форма.ИспользоватьСлой("Общий, ОсновныеСведения, ЧастноеЛицо", 2);
	Иначе
		Форма.ИспользоватьСлой("Общий, ОсновныеСведения, Организация", 2);
	КонецЕсли;

	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаИзменить.Доступность(0);
		Форма.КнопкаДобавить.Доступность(0);
		Форма.КнопкаУдалить.Доступность(0);
	КонецЕсли;
КонецПроцедуры // ПриОткрытии

//******************************************************************************
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлементаДиалога, ФлагСтандартнойОбработки) // Предопределенная процедура
	Если ИдентЭлементаДиалога = "ОсновнойДоговор" Тогда
		ФлагСтандартнойОбработки = 0;
		Если Выбран() = 0 Тогда
			Если Вопрос("Список договоров можно открыть
			            |только после записи контрагента.
			            |Записать контрагента?", "Да+Нет") = "Да" Тогда
				Записать();
			КонецЕсли;
		КонецЕсли;
		Если Выбран() = 0 Тогда
			Возврат;
		КонецЕсли;

		ОткрытьФорму("Справочник.Договоры", ТекущийЭлемент());
	ИначеЕсли ИдентЭлементаДиалога = "ОсновнойСчет" Тогда
		ФлагСтандартнойОбработки = 0;
		Если Выбран() = 0 Тогда
			Если Вопрос("Список счетов можно открыть
			            |только после записи контрагента.
			            |Записать контрагента?", "Да+Нет") = "Да" Тогда
				Записать();
			КонецЕсли;
		КонецЕсли;
		Если Выбран() = 0 Тогда
			Возврат;
		КонецЕсли;
		ОткрытьФорму("Справочник.РасчетныеСчета", ТекущийЭлемент());
	КонецЕсли;
КонецПроцедуры // ПриНачалеВыбораЗначения

//******************************************************************************
//
Процедура ОбработкаВыбораЗначения(Выбор, ИдентЭлементаДиалога, ФлагСтандартнойОбработки) // Предопределенная процедура
	Если ИдентЭлементаДиалога = "ОсновнойДоговор" Тогда
		Если (Выбор.Наименование = глИмяДоговораДляПлатежейБезДоговора)
			или (Выбор.Наименование = глИмяДоговораДляПлатежейБезДоговораТорг) Тогда
			Предупреждение("Здесь нельзя выбирать этот служебный договор!");
			СтатусВозврата(0);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ОбработкаВыбораЗначения

//******************************************************************************
//
Процедура ПриВыбореЗакладки(НомерЗакладки, Значение) // Предопределенная процедура
	Если Значение = "Общие" Тогда
		Если ВидКонтрагента = Перечисление.ВидыКонтрагентов.ЧастноеЛицо Тогда
			Форма.ИспользоватьСлой("Общий, ОсновныеСведения, ЧастноеЛицо", 2);
		Иначе
			Форма.ИспользоватьСлой("Общий, ОсновныеСведения, Организация", 2);
		КонецЕсли;
		Форма.ВидКонтрагента.Доступность(1);

	ИначеЕсли Значение = "РасчетныеСчета" Тогда
		Форма.ИспользоватьСлой("Общий, РасчСчета", 2);
		Форма.ВидКонтрагента.Доступность(0);

		Если Выбран() = 0 Тогда
			Если Вопрос("Расчетные счета можно редактировать
			            |только после записи контрагента.
			            |Записать контрагента?", "Да+Нет") = "Да" Тогда
				Записать();
			КонецЕсли;
		КонецЕсли;
		Если Выбран() = 1 Тогда
			ОбновитьСписокСчетов();
		КонецЕсли;

	ИначеЕсли Значение = "Документ" Тогда
		Форма.ИспользоватьСлой("Общий, Документ", 2);
		Форма.ВидКонтрагента.Доступность(0);
	КонецЕсли;
КонецПроцедуры // ПриВыбореЗакладки
//_____________________________________________________________________________
Процедура ПриЗакрытии() //предопределенная
	Если (НовыйКонтрагент = 1) и (Выбран() = 1) Тогда
	    СпрДог = СоздатьОбъект("Справочник.Договоры");
		СпрДог.ИспользоватьВладельца(ТекущийЭлемент());
		Если СпрДог.ВыбратьЭлементы() = 0 Тогда
		    СпрДог.Новый();
		    СпрДог.Наименование = "Основной договор";
			СпрДог.ВидДоговора  = глЗначениеПоУмолчанию("ОсновнойВидДоговоров");
			СпрДог.ОплатаДоговора = 1;
			СпрДог.АвтоОбработкаНДС = ?(Константа.АвтоКнигаПокупокПродаж = Да, 1, 0);
		    СпрДог.Записать();
			
			ОсновнойДоговор = СпрДог.ТекущийЭлемент();
			Записать();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры //ПриЗакрытии
//_____________________________________________________________________________
НовыйКонтрагент = 0;