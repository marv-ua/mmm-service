//*****************************************************************************
// ПоказатьСтавки()
//
Процедура ПоказатьСтавки()
	
	Если Найти(глФондыЕСН, СокрЛ(Код)) > 0 Тогда
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.Установить("Владелец", ТекущийЭлемент());
		Параметры.Установить("Дата", ИспользоватьДату());
		ОткрытьФормуМодально("Справочник.СтавкиНалогов", Параметры);
		Форма.Обновить();
		
	Иначе
		Предупреждение("Ставки регрессивной шкалы существуют только для
					   |единого социального налога (ЕСН в ФБ, ФСС, ФФОМС, ТФОМС)
					   |и взносов в Пенсионный фонд РФ");
	КонецЕсли;
	
КонецПроцедуры // ПоказатьСтавки()

//*****************************************************************************
// ПолучитьСтавку()
//
// Возвращаемое значение:
//  Строка - представление ставки налога.
//
// Вызывается из формул элементов диалога:
//  Колонка "Ставка".
//
// Описание:
//  Возвращает значение ставки налога. Для частей ЕСН возвращает значение 
// ставки у первого предела регрессивной шкалы.
//
Функция ПолучитьСтавку()
	
	Если (Код = "ПФР_страх") или (Код = "ПФР_нак") Тогда
		Возврат "-";
	КонецЕсли;
	
	Если Найти(глФондыЕСН, СокрЛ(Код)) > 0 Тогда
		Ставки = СоздатьОбъект("Справочник.СтавкиНалогов");
		Ставки.ИспользоватьВладельца(ТекущийЭлемент());
		Ставки.ПорядокКодов();
		Ставки.ВыбратьЭлементы();
		Если Ставки.ПолучитьЭлемент() = 1 Тогда
			Возврат СокрЛП(Формат(Ставки.Процент.Получить(ИспользоватьДату()),"Ч15.2.,"));
		Иначе
			Возврат "";
		КонецЕсли;
		
	Иначе
		Возврат СокрЛП(Формат(Ставка ,"Ч15.2.,"));
	КонецЕсли;
	
КонецФункции // ПолучитьСтавку()

//******************************************************************************
// УправлениеВидимостьюКонстант()
//
Процедура УправлениеВидимостьюКонстант()
	
    Если ДатаГод(ИспользоватьДату()) < 2005 Тогда
        Форма.ИспользоватьРегрессивнуюШкалу.Видимость(1);
		Форма.ИспользоватьРегрессивнуюШкалуПФР.Видимость(1);
		
	Иначе
		Форма.ИспользоватьРегрессивнуюШкалу.Видимость(0);
		Форма.ИспользоватьРегрессивнуюШкалуПФР.Видимость(0);
    КонецЕсли;
	
КонецПроцедуры // УправлениеВидимостьюВычетов()

//******************************************************************************
// ПоКнопкеВыбораДаты()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка выбора даты.
//
// Описание:
//  Выбирается дата просмотра и редактирования значений периодических реквизитов 
// выбирается по кнопке, чтобы не менялся признак модифицированности формы.
//
Процедура ПоКнопкеВыбораДаты()
	
	глВвестиДатуПериодическихРеквизитов(Контекст, 0);
	Если Константа.ИспользоватьРегрессивнуюШкалу.Получить(КонМесяца(ИспользоватьДату())) = Да Тогда
		ИспользоватьРегрессивнуюШкалу = 1;
	
	Иначе
		ИспользоватьРегрессивнуюШкалу = 0;
	КонецЕсли;
	
	Если Константа.ИспользоватьРегрессивнуюШкалуПФР.Получить(КонМесяца(ИспользоватьДату())) = Да Тогда
		ИспользоватьРегрессивнуюШкалуПФР = 1;
	
	Иначе
		ИспользоватьРегрессивнуюШкалуПФР = 0;
	КонецЕсли;
	
	УправлениеВидимостьюКонстант();
	
КонецПроцедуры // ПоКнопкеВыбораДаты()

//_____________________________________________________________________________
Процедура ПриОткрытии() //предопределенная
	Если Константа.ИспользоватьРегрессивнуюШкалу.Получить(КонМесяца(ИспользоватьДату())) = Да Тогда
		ИспользоватьРегрессивнуюШкалу = 1;
	КонецЕсли;	
	
	Если Константа.ИспользоватьРегрессивнуюШкалуПФР.Получить(КонМесяца(ИспользоватьДату())) = Да Тогда
		ИспользоватьРегрессивнуюШкалуПФР = 1;
	КонецЕсли;
	
	УправлениеВидимостьюКонстант();
	
КонецПроцедуры //ПриОткрытии
//_____________________________________________________________________________
Процедура УстановитьЗначениеКонстанты()
	Если ИспользоватьРегрессивнуюШкалу = 1 Тогда
		Если Вопрос("Начать использовать регрессивную шкалу ставок ЕСН,
					|начиная с "+Строка(НачМесяца(ИспользоватьДату()))+"?","Да+Нет") = "Да" Тогда
	    	Константа.ИспользоватьРегрессивнуюШкалу.Установить(КонМесяца(ИспользоватьДату()), Да);
		Иначе
			ИспользоватьРегрессивнуюШкалу = 0;
		КонецЕсли;
	Иначе
		Если Вопрос("Прекратить использование регрессивной шкалы ставок ЕСН,
					|начиная с "+Строка(НачМесяца(ИспользоватьДату()))+"?","Да+Нет") = "Да" Тогда
	    	Константа.ИспользоватьРегрессивнуюШкалу.Установить(КонМесяца(ИспользоватьДату()), Нет);
		Иначе
			ИспользоватьРегрессивнуюШкалу = 1;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
//_____________________________________________________________________________
Процедура УстановитьЗначениеКонстантыПФР()
	Если ИспользоватьРегрессивнуюШкалуПФР = 1 Тогда
		Если Вопрос("Начать использовать регрессивную шкалу тарифов взносов в ПФР,
					|начиная с "+Строка(НачМесяца(ИспользоватьДату()))+"?","Да+Нет") = "Да" Тогда
	    	Константа.ИспользоватьРегрессивнуюШкалуПФР.Установить(КонМесяца(ИспользоватьДату()), Да);
		Иначе
			ИспользоватьРегрессивнуюШкалуПФР = 0;
		КонецЕсли;
	Иначе
		Если Вопрос("Прекратить использование регрессивной шкалы тарифов взносов в ПФР,
					|начиная с "+Строка(НачМесяца(ИспользоватьДату()))+"?","Да+Нет") = "Да" Тогда
	    	Константа.ИспользоватьРегрессивнуюШкалуПФР.Установить(КонМесяца(ИспользоватьДату()), Нет);
		Иначе
			ИспользоватьРегрессивнуюШкалуПФР = 1;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
//_____________________________________________________________________________