//******************************************************************************
// Печать справок о счетах

Процедура ПечатьСправкиОСчетах( ВидСправки )
	ЮридическийАдрес=глПредставлениеАдреса(Константа.АдресОрганизации);
	ОфициальноеНаименование=Константа.ОфициальноеНазваниеОрганизации;
	Телефоны=Константа.ТелефоныОрганизации;
	ИНН=Константа.ИННОрганизации;
	ГНИ=Константа.ГНИОрганизации;
	
	ДатаНачалаКвартала=НачКвартала( РабочаяДата() );
	ДатаНачалаСледПериода=КонКвартала( РабочаяДата() )+1;

	Справка=СоздатьОбъект("Таблица");
	Если ВидСправки = 1 Тогда
	    Справка.ИсходнаяТаблица("Рублевые");
	Иначе
		Справка.ИсходнаяТаблица("Валютные");
	КонецЕсли;

	Справка.ВывестиСекцию("Шапка");

	Спр=СоздатьОбъект("Справочник.БанковскиеСчета");
	Спр.ПорядокКодов();
	Спр.ВыбратьЭлементы();
	СчетчикСтрок=0;
	Пока Спр.ПолучитьЭлемент()=1  Цикл
		Если Спр.ПометкаУдаления()=1 Тогда
		    Продолжить;
		КонецЕсли;
		Если ВидСправки = 1 Тогда
			Если Спр.ТипСчета=Перечисление.ТипыБанковскихСчетов.Валютный Тогда
	        	Продолжить;
			КонецЕсли;
	    ИначеЕсли ВидСправки = 2 Тогда
			Если Спр.ТипСчета=Перечисление.ТипыБанковскихСчетов.Рублевый Тогда
	        	Продолжить;
			КонецЕсли;
	    КонецЕсли;

		// В отчет должны попадать счета, у которых дата закрытия счета пустая или
		// лежит в текущем квартале
		Если Число( Спр.ДатаЗакрытияСчета ) > 0 Тогда
			Если (Спр.ДатаЗакрытияСчета < ДатаНачалаКвартала ) Тогда
			    Продолжить;
			КонецЕсли;
		КонецЕсли;

		СчетчикСтрок=СчетчикСтрок+1;
		Справка.ВывестиСекцию( "Строка" );

	КонецЦикла;

	Справка.ВывестиСекцию( "Подвал" );
	Справка.ТолькоПросмотр(1);
	Справка.Опции(0,0,0,0);
	Если ВидСправки = 1 Тогда
		Справка.Показать("Справка о рублевых счетах");
	ИначеЕсли ВидСправки = 2 Тогда
		Справка.Показать("Справка о валютных счетах");
	КонецЕсли;

КонецПроцедуры
