////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;
Перем БухИт;

//_____________________________________________________________________________
Функция ИнформационнаяСтрока()
	Перем Стр;

	Стр = "";
	Если ТекущийЭлемент().Выбран() = 0 Тогда
	ИначеЕсли ТекущийЭлемент().ЭтоГруппа() = 1 Тогда
	ИначеЕсли ТекущийЭлемент().СубСчет10.Выбран() = 0 Тогда
	ИначеЕсли КонМесяца(РабочаяДата()) > КонецРассчитанногоПериодаБИ() Тогда
		Стр = "На " + Формат(РабочаяДата(), "Д (0)ДДММГГГ") + " бухгалтерские итоги не рассчитаны!"+РазделительСтрок+
				"Расчет итогов выполняется в режиме"+РазделительСтрок+
				"""Операции - Управление бухгалтерскими итогами"".";

	Иначе
		Стр = "Остаток материала """ + ТекущийЭлемент() + """: " + 
				БухИт.СКД(ТекущийЭлемент().СубСчет10,"К",,ТекущийЭлемент()) +
				" " +ТекущийЭлемент().ЕдиницаИзмерения + РазделительСтрок +
				"Вид материала: """+ТекущийЭлемент().СубСчет10.ПолнНаименование+""" (счет "+ТекущийЭлемент().СубСчет10+")"+РазделительСтрок+
				"Данные актуальны на конец дня " + Формат(КонМесяца(РабочаяДата()), "Д (0)ДДММГГГ");
	КонецЕсли;

	Возврат Стр;
КонецФункции //ИнформационнаяСтрока
//_____________________________________________________________________________
Процедура Отчет()
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "ОборотноСальдоваяВедомостьПоСчету");
	Расшифровка.Установить("РазделительУчета", БухИтоги.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Счет", СчетПоКоду("10"));
	Расшифровка.Установить("ДанныеПоСубсчетам", 1);
	Расшифровка.Установить("Дата1", НачМесяца(РабочаяДата()));
	Расшифровка.Установить("Дата2", КонМесяца(РабочаяДата()));
	Расшифровка.Установить("ВидСубконто1", ВидыСубконто.Материалы);
	Если ТекущийЭлемент().Выбран() = 1 Тогда
		Расшифровка.Установить("Субконто1", ТекущийЭлемент());
		Если ТекущийЭлемент().ЭтоГруппа() = 1 Тогда
			Расшифровка.Установить("ПоГруппам1", 1);
			Расшифровка.Установить("ОтборСубконто1", 1);
		Иначе
			Расшифровка.Установить("ПоГруппам1", 0);
			Расшифровка.Установить("ОтборСубконто1", 2);
		КонецЕсли;
	КонецЕсли;
	Расшифровка.Установить("ВидСубконто2", ВидыСубконто.МестаХранения);
	Расшифровка.Установить("ОтборСубконто2", 1);

	глРасшифровка = Расшифровка;
	глОбновить = 2;
	глТаблица = "";
	глФлагРасшифровки = 1;
	ОткрытьФорму("Отчет.ОборотноСальдоваяВедомостьПоСчету");
	глФлагРасшифровки = 0;
	глОбновить = 0;
КонецПроцедуры //Отчет

//******************************************************************************
// ПоКнопкеПечать()
// 
// Параметры: 
//	Нет
//
// Описание:
// 	Вызывается по кнопке "Печать"  
// 	
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		Отчет();
		
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(Контекст, "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Параметры:
//  нет.    
//
// Возвращаемое значение:
//  нет.
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Справочник." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()

//******************************************************************************
//
Процедура ОткрытьНазначенияИспользования()
    
	
	Если ТекущийЭлемент().Выбран() = 0 Тогда
	    Предупреждение("Нельзя открыть список назначений использования, не указав материал.
						|Введите материаал.");
						
	ИначеЕсли ТекущийЭлемент().ЭтоГруппа() = 1 Тогда
	    Предупреждение("Нельзя открыть список назначений использования для группы материалов.
						|Выберите конкретный материал.");
						
	ИначеЕсли ТекущийЭлемент().СубСчет10 <> СчетПоКоду("10.10") Тогда
		Предупреждение("Нельзя открыть список назначений использования для материала не являющегося спецодеждой (спецоснасткой).
						|Выберите спецодежду или спецоснастку.");
						
	Иначе
		ОткрытьФорму("Справочник.НазначенияИспользования");
	КонецЕсли;
	
КонецПроцедуры // ОткрытьНормативы()

//_____________________________________________________________________________
Процедура ПриОткрытии() //предопределенная
	БухИт = СоздатьОбъект("БухгалтерскиеИтоги");
	БухИт.ПериодМ(РабочаяДата());
	
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Справочник." + Вид(),ТаблицаПечФорм);
	
КонецПроцедуры //ПриОткрытии
                                        
ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");
	
// добавим информацию о встроенной форме
ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Оборотно-сальдовая ведомость по счету 10";
ТаблицаПечФорм.Кнопка       = "ОСВ-10";