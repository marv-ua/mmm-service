Перем КонтекстФормыДокумента;
Перем БухИт, СчГТД;

//******************************************************************************
// Функция ОстатокНаСкладе()
// 
// Вызывается из текстовой строки или колонки, в зависимости от того, где
// показываются остатки.
//
Функция ОстатокНаСкладе(РасчетИзКолонки=1)
    Остаток = 0;
	Если (РасчетИзКолонки = 0) или ((РасчетИзКолонки = 1) и (ФлагОстаткиВКолонке = 1)) Тогда
		Если (КонМесяца(КонтекстФормыДокумента.ДатаДок) <= КонецРассчитанногоПериодаБИ()) 
			и (ТекущийЭлемент().Выбран() = 1) Тогда
	 		Если Товар.Выбран() = 1 Тогда
 				Остаток = БухИт.СКД(СчГТД, "К",, Товар, ТекущийЭлемент());
	 		КонецЕсли;
		КонецЕсли;
	КонецЕсли;
    
	Возврат СокрЛ(Формат(Остаток, "Ч15.3."));
КонецФункции // ОстатокНаСкладе

Функция ИнформационнаяСтрока()
	Перем Стр;

	Стр = "";
	Если КонМесяца(КонтекстФормыДокумента.ДатаДок) > КонецРассчитанногоПериодаБИ() Тогда
		Стр = "На " + Формат(КонтекстФормыДокумента.ДатаДок, "Д (0)ДДММГГГГ") + " бухгалтерские итоги не рассчитаны!"+РазделительСтрок+
				"Расчет итогов выполняется в режиме" + РазделительСтрок +
				"""Операции - Управление бухгалтерскими итогами"".";

	Иначе
		Если Товар.Выбран() = 1 Тогда
			Если Товар.ЭтоГруппа() = 0 Тогда
				Стр = "Остаток по счету ГТД: " +
						ОстатокНаСкладе(0) + " " + Товар.ЕдиницаИзмерения +
						РазделительСтрок + "Тип номенклатуры товара: " + Товар.ТипНоменклатуры;
				Если Товар.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Товар Тогда
					//Стр = Стр + " (" + Товар.ТипТовара + "), " + "страна происхождения: ";
					//Если ПустоеЗначение(Товар.СтранаПроисхождения) = 1 Тогда
					//    Стр = Стр + "Россия";
					//Иначе
					//	Стр = Стр + СокрЛП(Товар.СтранаПроисхождения);
					//КонецЕсли;
				КонецЕсли;
				Стр = Стр + РазделительСтрок + "Данные актуальны на конец дня " + Формат(КонМесяца(КонтекстФормыДокумента.ДатаДок), "Д (0)ДДММГГГГ");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат Стр;
КонецФункции //ИнформационнаяСтрока
//_____________________________________________________________________________
Процедура Отчет()
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "ОборотноСальдоваяВедомостьПоСчету");
	Расшифровка.Установить("РазделительУчета", БухИтоги.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Счет", СчГТД);
	Расшифровка.Установить("ДанныеПоСубсчетам", 0);
	Расшифровка.Установить("Дата1", НачМесяца(КонтекстФормыДокумента.ДатаДок));
	Расшифровка.Установить("Дата2", КонМесяца(КонтекстФормыДокумента.ДатаДок));
	Расшифровка.Установить("ВидСубконто1", ВидыСубконто.Номенклатура);
	Если Товар.Выбран() = 1 Тогда
		Расшифровка.Установить("Субконто1", Товар);
		Если Товар.ЭтоГруппа() = 1 Тогда
			Расшифровка.Установить("ПоГруппам1", 1);
			Расшифровка.Установить("ОтборСубконто1", 1);
		Иначе
			Расшифровка.Установить("ПоГруппам1", 0);
			Расшифровка.Установить("ОтборСубконто1", 2);
		КонецЕсли;
	КонецЕсли;
	Расшифровка.Установить("ВидСубконто2", ВидыСубконто.ГТД);
	Если ТекущийЭлемент().Выбран() = 1 Тогда
		Расшифровка.Установить("Субконто2", ТекущийЭлемент());
		Расшифровка.Установить("ПоГруппам2", 0);
		Расшифровка.Установить("ОтборСубконто2", 2);
	КонецЕсли;

	глРасшифровка = Расшифровка;
	глОбновить = 2;
	глТаблица = "";
	глФлагРасшифровки = 1;
	ОткрытьФорму("Отчет.ОборотноСальдоваяВедомостьПоСчету");
	глФлагРасшифровки = 0;
	глОбновить = 0;
КонецПроцедуры //Отчет

//******************************************************************************
// Процедура ПриВыбореПоказаОстатковВКолонке()
// 
Процедура ПриВыбореПоказаОстатковВКолонке()
	Если ФлагОстаткиВКолонке = 0 Тогда
		Форма.КолонкаОстатка.Видимость(0);
	Иначе
		Форма.КолонкаОстатка.Видимость(1);
	КонецЕсли;
КонецПроцедуры // ПриВыбореПоказаОстатковВКолонке

//******************************************************************************
// 
Процедура ЗаполнениеФормы(Параметр)
	Если ТипЗначенияСтр(Параметр) = "СписокЗначений" Тогда
		КонтекстФормыДокумента = Параметр.Получить("Контекст");
		Товар = Параметр.Получить("Товар");
		БухИт.ПериодМ(КонтекстФормыДокумента.ДатаДок);
	КонецЕсли;
КонецПроцедуры // ЗаполнениеФормы

//******************************************************************************
// 
Процедура ПриОткрытии() // Предопределенная процедура
	БухИт = СоздатьОбъект("БухгалтерскиеИтоги");
	СчГТД = СчетПоКоду("ГТД");

	ЗаполнениеФормы(Форма.Параметр);
	ФлагОстаткиВКолонке = ВосстановитьЗначение("ФормаПодбораГТД_ФлагОстаткиВКолонке");
	ПриВыбореПоказаОстатковВКолонке();
	Форма.ИспользоватьСлой("Основной", 2);
КонецПроцедуры // ПриОткрытии

//******************************************************************************
// 
Процедура ПриПовторномОткрытии() // Предопределенная процедура
	ЗаполнениеФормы(Форма.Параметр);
КонецПроцедуры // ПриПовторномОткрытии

//******************************************************************************
// 
Процедура ПриЗакрытии() // Предопределенная процедура
	СохранитьЗначение("ФормаПодбораГТД_ФлагОстаткиВКолонке", ФлагОстаткиВКолонке);
КонецПроцедуры // ПриЗакрытии
