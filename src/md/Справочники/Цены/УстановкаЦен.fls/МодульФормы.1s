Перем СписокЭлементов;
Перем СтарыйТипЦены;
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
// Валюта()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Строка - возвращается наименование валюты цен в договоре или "руб.", 
// 			 если валюта не выбрана.
//
// Вызывается из формул элементов диалога:
//  Графа списка "Валюта".
//
Функция Валюта()
	Если ТекущийЭлемент().Выбран() = 1 Тогда
		Если ПустоеЗначение(Валюта) = 1 Тогда
			Стр = "руб.";
		Иначе
			Стр = Валюта.Наименование;
		КонецЕсли;
		Возврат Стр;
	КонецЕсли;
	Возврат "";
КонецФункции // Валюта()

//******************************************************************************
Процедура Заполнить()
    Перем Запрос, ТекстЗапроса, Таб;
	
	Если ВыбТипЦен.Выбран()=0 Тогда
		Предупреждение("Не выбран тип цен");
		Возврат;
	КонецЕсли;
	
	Если ПустоеЗначение(ВыбДата)=1 Тогда
		Предупреждение("Не указана дата установки реквизитов");
		Возврат;
	КонецЕсли;
	
	Предупреждать = 0;
	Сообщение = "Тип цен """+ ВыбТипЦен + """ установлен для розничных торговых точек:"+РазделительСтрок; 
	МХ = СоздатьОбъект("Справочник.МестаХранения");
	МХ.ВыбратьЭлементыПоРеквизиту("ТипЦен",ВыбТипЦен,0,0);
	Пока МХ.ПолучитьЭлемент() = 1 Цикл
		Если МХ.ТипСклада = Перечисление.ТипыСкладов.Розничный Тогда
			Предупреждать = 1;
			Сообщение = Сообщение + МХ.Наименование + РазделительСтрок;
		КонецЕсли;
	КонецЦикла;
	Если Предупреждать = 1 Тогда
		Сообщение = Сообщение + "Перед редактированием розничных цен по перечисленным"+ РазделительСтрок +
		"торговым точкам должна быть проведена инвентаризация."+РазделительСтрок + "Продолжить заполнение?"; 
		Если Вопрос(Сообщение,"Да+Нет",60) <> "Да" Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
		
		
	Если (ИспользоватьМХ=1) Тогда
		БухИт = СоздатьОбъект("БухгалтерскиеИтоги");
		КолРазрезов = 1;
		Если (ВыбМестоХранения.Выбран()=1) Тогда
			БухИт.ИспользоватьСубконто(ВидыСубконто.МестаХранения,ВыбМестоХранения,2);
			КолРазрезов = 2;
		КонецЕсли;
		БухИт.ИспользоватьСубконто(ВидыСубконто.Номенклатура,,1);
		БухИт.Опции(1,0);
		БухИт.ВыполнитьЗапрос(,ВыбДата,"41, 43, КТР",,,,,"К"); 
	КонецЕсли;
	Запрос = СоздатьОбъект("Запрос");
	СписокНоменклатуры = СоздатьОбъект("СписокЗначений");
	ТекстЗапроса = 
	"//{{ЗАПРОС(ЗапросПоНоменклатуре)
	|Период с ВыбДата по ВыбДата;
	|Номенклатура = Справочник.Номенклатура.ТекущийЭлемент;
	|ТипНоменклатуры = Справочник.Номенклатура.ТипНоменклатуры;
	|Группировка Номенклатура упорядочить по Номенклатура.Наименование без групп;";
	Если (ВыбТипНоменклатуры.Выбран()=1) Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|Условие(ТипНоменклатуры = ВыбТипНоменклатуры);";
	КонецЕсли;
	Если (ВыбГруппаНоменклатуры.Выбран()=1) Тогда
		Если ВыбГруппаНоменклатуры.ЭтоГруппа() = 1 Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|Условие(Номенклатура.ПринадлежитГруппе(ВыбГруппаНоменклатуры)=1);"
		Иначе
			ТекстЗапроса = ТекстЗапроса + "
			|Условие(Номенклатура = ВыбГруппаНоменклатуры);"
		КонецЕсли;
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|"//}}ЗАПРОС
	;
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;
	Пока Запрос.Группировка(1) =1 Цикл         
		Если (ИспользоватьМХ=1) Тогда
			Если БухИт.ПолучитьСубконто(КолРазрезов,,Запрос.Номенклатура) = 1 Тогда
				Если БухИт.СКД("К")>0 Тогда
					СписокНоменклатуры.ДобавитьЗначение(Запрос.Номенклатура);
				КонецЕсли;
			КонецЕсли;
		Иначе
		    СписокНоменклатуры.ДобавитьЗначение(Запрос.Номенклатура);
		КонецЕсли;
	КонецЦикла;
	СпрЦены = СоздатьОбъект("Справочник.Цены");
	СписокЭлементов.УдалитьВсе();
	Для а = 1 по СписокНоменклатуры.РазмерСписка() Цикл
		Ном = СписокНоменклатуры.ПолучитьЗначение(а,);
		СпрЦены.ИспользоватьВладельца(Ном);
		Если СпрЦены.НайтиПоРеквизиту("ТипЦен",ВыбТипЦен,0) = 1 Тогда
			СписокЭлементов.ДобавитьЗначение(СпрЦены.ТекущийЭлемент());
		ИначеЕсли (Создавать = 1) Тогда
			СпрЦены.Новый();
			СпрЦены.ТипЦен =  ВыбТипЦен.ТекущийЭлемент();
			СпрЦены.Валюта = ВыбТипЦен.Валюта;
			СпрЦены.Владелец = Ном;
			СпрЦены.Записать();
			СписокЭлементов.ДобавитьЗначение(СпрЦены.ТекущийЭлемент());
		КонецЕсли;
	КонецЦикла;
	СписокЭлементов.Сортировать(,);
	ИспользоватьСписокЭлементов(СписокЭлементов);
КонецПроцедуры

//******************************************************************************
// ПоКнопкеИстория()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "История".
//
// Описание:
//  Вызывается обработка "ЗначенияПериодическихРеквизитов" для просмотра истории 
// значений периодических реквизитов.
//
Процедура ПоКнопкеИстория()
	
	глЗначенияПериодическихРеквизитов(Контекст);
	
КонецПроцедуры // ПоКнопкеИстория()

//******************************************************************************
Процедура ПриВыбореТипаЦен()
    Если СписокЭлементов.РазмерСписка() > 0 Тогда
		Если СписокЭлементов.ПолучитьЗначение(1).ТипЦен <> ВыбТипЦен Тогда
		    Если Вопрос("Изменен тип цены. Очистить список цен?","Да+Нет",60) = "Да" Тогда
				СписокЭлементов.УдалитьВсе();
				ИспользоватьСписокЭлементов(СписокЭлементов);
			Иначе
				ВыбТипЦен = СписокЭлементов.ПолучитьЗначение(1).ТипЦен;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры	
	
//******************************************************************************
Процедура ПриВыбореДаты();
	ИспользоватьДату(ВыбДата);
КонецПроцедуры
////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	СписокЭлементов = СоздатьОбъект("СписокЗначений");
	ВыбДата = РабочаяДата();
	ИспользоватьВладельца();
	ВыбТипЦен = ?(ПустоеЗначение(глЗначениеПоУмолчанию("ОсновнойСклад"))=1, "", глЗначениеПоУмолчанию("ОсновнойСклад").ТипЦен);
	ИспользоватьДату(ВыбДата);
	ИерархическийСписок(0, 0);
	СохранениеПериодическихРеквизитов(0);
	ИспользоватьСписокЭлементов(СписокЭлементов);
	Форма.ВыбГруппаНоменклатуры.ВыборГруппы(1);
	Форма.Заголовок("Установка цен",0);
КонецПроцедуры  
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗаписи()
    Если Выбран() = 1 Тогда
		ИспользоватьДату(ВыбДата);
		ИспользоватьВладельца(Владелец);
		СпрЦены = СоздатьОбъект("Справочник.Цены");
		Если СпрЦены.НайтиЭлемент(ТекущийЭлемент()) = 1 Тогда
			Если СпрЦены.Цена.Получить(ВыбДата)<>Цена Тогда
				СпрЦены.Цена.Установить(ВыбДата,Цена);    
			КонецЕсли;
		КонецЕсли;
		ТекСтрока = СписокЭлементов.НайтиЗначение(СпрЦены.ТекущийЭлемент());
		Если ТекСтрока < СписокЭлементов.РазмерСписка() Тогда
			ЭлементНовойСтроки = СписокЭлементов.ПолучитьЗначение(ТекСтрока + 1);
			ИспользоватьВладельца(ЭлементНовойСтроки.Владелец);
			АктивизироватьОбъект(ЭлементНовойСтроки);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
