Перем СтараяВалюта;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
// 
//******************************************************************************
// Валюта()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Строка - возвращается наименование валюты цен в договоре или "руб.", 
// 			 если валюта не выбрана.
//
// Вызывается из формул элементов диалога:
//  Графа списка "Валюта".
//
Функция Валюта()
	Если ТекущийЭлемент().Выбран() = 1 Тогда
		Если ПустоеЗначение(Валюта) = 1 Тогда
			Стр = "руб.";
		Иначе
			Стр = Валюта.Наименование;
		КонецЕсли;
		Возврат Стр;
	КонецЕсли;
	Возврат "";
КонецФункции // Валюта()

//******************************************************************************
// ЕстьДубльЦены()
//
// Параметры:    
//  Нет
//
// Возвращаемое значение:
//  Найденный дубль, если нет дубля цены, то возвращается пустая строка
//
// Описание:
//  Функция проверяет есть ли у данного товара цена данного типа
//
Функция ЕстьДубльЦены() 
	
	ВыборкаЦен = СоздатьОбъект("Справочник.Цены");
	ВыборкаЦен.ИспользоватьВладельца(Номенклатура);
	ВыборкаЦен.ВыбратьЭлементы();
	
	Пока ВыборкаЦен.ПолучитьЭлемент() = 1 Цикл
		
		Если ВыборкаЦен.ТипЦен <> ТипЦен Тогда
			Продолжить;
		КонецЕсли;
		
		Если Выбран() = 0 Тогда
			
			// Новый элемент
			Возврат ВыборкаЦен.ТекущийЭлемент();
		Иначе           
			
			// Для сохраненного элемента проверить несовпадение типов цен
			// недостаточно, т.к. из выборки можно получить редактируемый элемент
			Если ТекущийЭлемент() <> ВыборкаЦен.ТекущийЭлемент() Тогда
				Возврат ВыборкаЦен.ТекущийЭлемент();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции // ЕстьДубльЦены()

//******************************************************************************
// ПриИзмененииТипаЦены()
//
// Параметры: 
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//  Устанавливает валюту и наценку, если они не заданы, из типа цены.
//
Процедура ПриИзмененииТипаЦены()
	
	Если ПустоеЗначение(ТипЦен) = 0 Тогда
	    Если ПустоеЗначение(Валюта) = 1 Тогда
	        Валюта	= ТипЦен.Валюта;
			СтараяВалюта = Валюта;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииТипаЦены()

//*****************************************************************************
// ПриИзмененииВалюты()
//
// Параметры: 
//	Нет
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Колонка "Валюта"
//
// Описание:
//  Вызывается при выборе валюты. Пересчитывает цену из "старой" в 
// "новую" валюту по курсу на рабочую дату.
//
Процедура ПриИзмененииВалюты()
	
	Если (ПустоеЗначение(СтараяВалюта) = 1) или (ПустоеЗначение(Валюта) = 1) или (Цена = 0) Тогда
		
		СтараяВалюта = Валюта;
		Возврат;
	КонецЕсли;
	
	Если СтараяВалюта <> Валюта Тогда // изменили валюту
		                    
		Если Вопрос("Изменилась валюта задания цены. Пересчитать цену?", "Да+Нет", 60) = "Да" Тогда
			
			Цена = глПересчет(Цена, СтараяВалюта, РабочаяДата(), Валюта, РабочаяДата());
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтараяВалюта = Валюта;
	
КонецПроцедуры // ПриИзмененииВалюты()

//******************************************************************************
// ПоКнопкеВыбораДаты()
//
// Параметры:
//  Нет
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога:
//  Кнопка выбора даты.
//
// Описание:
//  Выбирается дата просмотра значений периодических реквизитов.
//
Процедура ПоКнопкеВыбораДаты()
	
	глВвестиДатуПериодическихРеквизитов(Контекст, 0);
	
КонецПроцедуры // ПоКнопкеВыбораДаты()

//******************************************************************************
// ПоКнопкеИстория()
//
// Параметры:
//
// Возвращаемое значение:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "История".
//
// Описание:
//  Вызывается обработка "ЗначенияПериодическихРеквизитов" для просмотра истории 
// значений периодических реквизитов.
//
Процедура ПоКнопкеИстория()
	
	глЗначенияПериодическихРеквизитов(Контекст);
	
КонецПроцедуры // ПоКнопкеИстория()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
// 
//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	
	ИерархическийСписок(1, 1);
	Если ТипЗначенияСтр(Форма.Параметр) = "Справочник" Тогда
		Если Форма.Параметр.Вид() = "Номенклатура" Тогда
			Если Форма.Параметр.ЭтоГруппа() = 0 Тогда
				ИспользоватьВладельца(Форма.Параметр);
			КонецЕсли;
			ИерархическийСписок(1, 0);
			Форма.Номенклатура.Видимость(0);
		КонецЕсли;
	КонецЕсли;
	
	// Системно не надо контролировать изменение периодических реквизитов.
	СохранениеПериодическихРеквизитов(0);
	
КонецПроцедуры // ПриОткрытии()                                                 

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриПовторномОткрытии()
	
	Если ТипЗначенияСтр(Форма.Параметр) = "Справочник" Тогда
		Если Форма.Параметр.Вид() = "Номенклатура" Тогда
			Если Форма.Параметр.ЭтоГруппа() = 0 Тогда
				ИспользоватьВладельца(Форма.Параметр);
			КонецЕсли;
			ИерархическийСписок(1, 0);
			Форма.Номенклатура.Видимость(0);
		КонецЕсли;
	КонецЕсли;
	                    
КонецПроцедуры // ПриПовторномОткрытии()

//******************************************************************************
// Предопределенная процедура.
Процедура ПриРедактированииНовойСтроки()
	
	СтараяВалюта  = Валюта;
	
КонецПроцедуры // ПриРедактированииНовойСтроки()

//******************************************************************************
// Предопределенная процедура.
Процедура ПриНачалеРедактированияСтроки()
	
	СтараяВалюта  = Валюта;
	
КонецПроцедуры // ПриРедактированииНовойСтроки()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗаписи()
	
	// ПРОВЕРКА ЗАПОЛНЕНИЯ ОБЯЗАТЕЛЬНЫХ ПОЛЕЙ
	Если ПустоеЗначение(ТипЦен) = 1 Тогда
		Предупреждение("Не заполнен ""Тип Цен""",60);
		Активизировать("ТипЦен");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	// все обязательные поля заполнены
	// ПРОВЕРКА ДУБЛИРОВАНИЯ ЦЕНЫ 
	Дубль = ЕстьДубльЦены();
	Если  ПустоеЗначение(Дубль) = 0 Тогда
		Предупреждение("У номенклатурной позиции уже есть цена типа """ + ТипЦен + """" +
		?(Дубль.ПометкаУдаления() = 1," (помечена на удаление).",""), 60);
		Активизировать("ТипЦен");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	//Проверка на использование цены на розничных складах
	Если (ТипЦен.Выбран()=1) и (цена<>0) Тогда
		Предупреждать = 0;
		Сообщение = "Тип цен """+ ТипЦен + """ установлен для розничных торговых точек:"+РазделительСтрок; 
		МХ = СоздатьОбъект("Справочник.МестаХранения");
		МХ.ВыбратьЭлементыПоРеквизиту("ТипЦен",ТипЦен,0,0);
		Пока МХ.ПолучитьЭлемент() = 1 Цикл
			Если МХ.ТипСклада = Перечисление.ТипыСкладов.Розничный Тогда
				Предупреждать = 1;
				Сообщение = Сообщение + МХ.Наименование + РазделительСтрок;
			КонецЕсли;
		КонецЦикла;
		Если Предупреждать = 1 Тогда
			Сообщение = Сообщение + "Перед изменением розничной цены по перечисленным"+ РазделительСтрок +
			"торговым точкам должна быть проведена инвентаризация."+РазделительСтрок + "Изменить цену?"; 
			Если Вопрос(Сообщение,"Да+Нет",60) <> "Да" Тогда
				СтатусВозврата(0);
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Записью периодических реквизитов невозможно управлять при вводе 
	// новой строки в форме списка справочника. Система автоматически 
	// запишет значения периодических реквизитов на текущую дату.
	Если Выбран() = 1 Тогда
		// Обработке ЗаписьПериодическихРеквизитов необходимо передать
		// значения периодических реквизитов, введенные в форме диалога.
		Список = СоздатьОбъект("СписокЗначений");
		Список.Установить("Цена" , Цена);
		
		// При записи вызывается обработка ЗаписьПериодическихРеквизитов 
		// для управления записью значений периодических реквизитов.
		Если глЗаписьПериодическихРеквизитов(Контекст, Список) = 0 Тогда
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры // ПриЗаписи()