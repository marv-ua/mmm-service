////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//******************************************************************************
// ВыборИзКлассификатора()
//
// Вызывается из формул элементов диалога:
//  Кнопка "Добавить...".
//
// Описание:
//  Открывает внешний классификатор для выбора значения.
//
Процедура ВыборИзКлассификатора()
	
	Параметр=СоздатьОбъект("СписокЗначений");
	Параметр.ДобавитьЗначение("Валюты", "Вид справочника");
	Параметр.ДобавитьЗначение(1, "Способ выбора");
	
	ОткрытьФорму("Обработка.ВыборИзКлассификатора#Валюты", Параметр);
	
КонецПроцедуры // ВыборИзКлассификатора()

//******************************************************************************
// ПостроитьОтчет()
//
Процедура ПостроитьОтчет()
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "КурсыВалют");
	Расшифровка.Установить("ДатаНач", НачалоПериодаБИ());
	Расшифровка.Установить("ДатаКон", КонецПериодаБИ());
	Расшифровка.Установить("Валюта", ТекущийЭлемент());

	глРасшифровка = Расшифровка;
	глФлагРасшифровки = 1;
	глОбновить = 0;
	ОткрытьФорму("Отчет.КурсыВалют");
	глФлагРасшифровки = 0;
	
КонецПроцедуры // ПостроитьОтчет()

//******************************************************************************
// ВыбратьПравовуюСправку()
//
Процедура ВыбратьПравовуюСправку()
	
	Меню = СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("KURS_TV", "Курсы твердых валют");
	Меню.ДобавитьЗначение("KURS_MG", "Курсы мягких валют");
	Ссылка = "";
	Если Меню.ВыбратьЗначение(Ссылка,,,,1) = 1 Тогда
	    Норм_ПолучитьСправку(Ссылка);
	КонецЕсли;
	
КонецПроцедуры // ВыбратьПравовуюСправку()

//******************************************************************************
// ПоКнопкеВыбораДаты()
//
// Вызывается из формул элементов диалога:
//  Кнопка выбора даты.
//
// Описание:
//  Выбирается дата просмотра значений периодических реквизитов.
//
Процедура ПоКнопкеВыбораДаты()
	
	глВвестиДатуПериодическихРеквизитов(Контекст, 0);
	
КонецПроцедуры // ПоКнопкеВыбораДаты()

//******************************************************************************
// ПоКнопкеИстория()
//
// Вызывается из формул элементов диалога:
//  Кнопка "История".
//
// Описание:
//  Вызывается обработка "ЗначенияПериодическихРеквизитов" для просмотра истории 
// значений периодических реквизитов.
//
Процедура ПоКнопкеИстория()
	
	глЗначенияПериодическихРеквизитов(Контекст);
	
КонецПроцедуры // ПоКнопкеИстория()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриОткрытии()
	
	Форма.Кн_Справка.Видимость(?(ТипЗначения(Норм_Компонента) = 0, 0, 1));
	
	// Системно не надо контролировать изменение периодических реквизитов.
	СохранениеПериодическихРеквизитов(2, "*");
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриРедактированииНовойСтроки()

	Кратность = 1;
	Курс      = 1;
	
КонецПроцедуры // ПриРедактированииНовойСтроки()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ПриЗаписи()
	
	Если ПустоеЗначение(Наименование) = 1 Тогда
		Предупреждение("Не заполнено поле ""Наименование""!", 60);
		Активизировать("Наименование");
		СтатусВозврата(0);
		Возврат;
	
	КонецЕсли;

	СтатусВозврата(глБухЗаписьПериодическихРеквизитов(Контекст));
	
КонецПроцедуры // ПриЗаписи()
