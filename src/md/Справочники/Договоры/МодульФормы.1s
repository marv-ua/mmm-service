////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем СписокВременныхЕдиницРасчета;

//******************************************************************************
// УправлениеДиалогом()
//
Процедура УправлениеДиалогом()
	
	Если ЦеныВДоговоре = 1  Тогда
		Если ПустоеЗначение(ВалютаДоговора) = 0 Тогда
			ВалютаДоговора = "";
		КонецЕсли;
		Форма.ВалютаДоговора.Доступность(0);
		Если ОплатаДоговора <> 1 Тогда
		    ОплатаДоговора = 1;
		КонецЕсли;
		Форма.ОплатаДоговораВал.Доступность(0);
		
	Иначе
		Если ПустоеЗначение(ВалютаДоговора) = 1 Тогда
			ВалютаДоговора = Константа.ОсновнаяВалюта;
		КонецЕсли;
		Форма.ВалютаДоговора.Доступность(1);
		Форма.ОплатаДоговораВал.Доступность(1)
	КонецЕсли;
	
	Если ОплатаДоговора = 2 Тогда
	    Если АвтоОбработкаНДС = 1 Тогда
	        АвтоОбработкаНДС = 0;
		КонецЕсли;               
		Форма.АвтоОбработкаНДС.Доступность(0);
	Иначе
		Форма.АвтоОбработкаНДС.Доступность(1);
	КонецЕсли;
	
	Если (ЦеныВДоговоре = 2) и (ОплатаДоговора = 1) Тогда
		Форма.ВестиУчетРасчетовУЕ.Доступность(1);
		
	Иначе
		Форма.ВестиУчетРасчетовУЕ.Доступность(0);
		
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			ВестиУчетРасчетовУЕ = 0;
		КонецЕсли;
	КонецЕсли;
	
	Форма.ТекстОплатаПроизводитсяВРублях.Видимость(ВестиУчетРасчетовУЕ);
	Форма.ОтражатьСуммовуюРазницуПриЗачетеАванса.Видимость(ВестиУчетРасчетовУЕ);
	Форма.ПроцентКорректировкиКурсаУЕ.Видимость(ВестиУчетРасчетовУЕ);
	Форма.ТекстПроцентов.Видимость(ВестиУчетРасчетовУЕ);
	
КонецПроцедуры // УправлениеДиалогом()

//******************************************************************************
// ПриВыбореВалютыОплаты()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//	Переключатель "Оплата договора...".
// 
// Описание:
//  Если цены в договоре в валюте, то становится невозможным установить 
//  режим автоматической обработки НДС.
//
Процедура ПриВыбореВалютыОплаты()
	
	Если (ОплатаДоговора = 1) и (ЦеныВДоговоре = 2) Тогда
		ВестиУчетРасчетовУЕПоУмолчанию = глЗначениеПоУмолчанию("ВестиУчетРасчетовУЕ");
		ВестиУчетРасчетовУЕПоУмолчанию = ?(ВестиУчетРасчетовУЕПоУмолчанию = Да, 1, 0);
		Если ВестиУчетРасчетовУЕ <> ВестиУчетРасчетовУЕПоУмолчанию Тогда
			ВестиУчетРасчетовУЕ = ВестиУчетРасчетовУЕПоУмолчанию;
		КонецЕсли;
		
		ОтражатьСуммовуюРазницуПриЗачетеАвансаПоУмолчанию = глЗначениеПоУмолчанию("ОтражатьСуммовуюРазницуПриЗачетеАванса");
		ОтражатьСуммовуюРазницуПриЗачетеАвансаПоУмолчанию = ?(ОтражатьСуммовуюРазницуПриЗачетеАвансаПоУмолчанию = Да, 1, 0);
		Если ОтражатьСуммовуюРазницуПриЗачетеАванса <> ОтражатьСуммовуюРазницуПриЗачетеАвансаПоУмолчанию Тогда
			ОтражатьСуммовуюРазницуПриЗачетеАванса = ОтражатьСуммовуюРазницуПриЗачетеАвансаПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
	УправлениеДиалогом();
	
КонецПроцедуры // ПриВыбореВалютыОплаты()

//******************************************************************************
// ПриВыбореТипаЦенВДоговоре()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//	Переключатель "Цены в договоре...".
// 
// Описание:
//  Если цены в договоре в рублях, то становится невозможным указать оплату 
// договора в валюте.
//
Процедура ПриВыбореТипаЦенВДоговоре()
	
	Если ЦеныВДоговоре = 2  Тогда
		Если ПустоеЗначение(ВалютаДоговора) = 0 Тогда
			ВалютаДоговора = "";
		КонецЕсли;
		
		Если ОплатаДоговора <> 1 Тогда
		    ОплатаДоговора = 1;
		КонецЕсли;
		
	Иначе
		Если ПустоеЗначение(ВалютаДоговора) = 1 Тогда
			ВалютаДоговора = Константа.ОсновнаяВалюта;
		КонецЕсли;
	КонецЕсли;
	
	ПриВыбореВалютыОплаты();
	
КонецПроцедуры // ПриВыбореТипаЦенВДоговоре() 

//******************************************************************************
// ПриИзмененииРеквизитаДоговора()  
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//	Переключатель "Использовать документы по договору...".
// 
// Описание:
//  проверяет наличие проведенных документов по договору 
//
Процедура ПриИзмененииРеквизитаДоговора()
	
	Если Выбран() = 1 Тогда
		Объект = СоздатьОбъект("Документ");
		Объект.ВыбратьПоЗначению(,,"Контрагент", ТекущийЭлемент().Владелец);
		
		Пока Объект.ПолучитьДокумент() = 1 Цикл
			Найден = 0;
			Если Метаданные.Документ(Объект.Вид()).РеквизитШапки("Договор").Выбран() = 1 Тогда
				Если Объект.Договор = ТекущийЭлемент() Тогда
					Найден = 1;
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если Найден = 1 Тогда
			Если Вопрос("Договор """+Наименование+""" используется в документах!
			            |Изменять режим использования документов по договору
				 	    |для автоматического формирования книги покупок и книги
						|продаж нельзя! Продолжить?", "Да+Нет",60) = "Нет" Тогда 							   	
				
			АвтоОбработкаНДС = ?(АвтоОбработкаНДС = 1, 0, 1);											
				
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;

КонецПроцедуры

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Форма.ИспользоватьСлой("Основной", 2);
	Если Наименование = глИмяДоговораДляПлатежейБезДоговора Тогда
		Форма.ДатаВозникновенияОбязательства.Видимость(0);
		Форма.ПодписьДатаВозникновенияОбязательства.Видимость(0);
		Форма.ДатаПогашенияОбязательства.Видимость(0);
		Форма.ПодписьДатаПогашенияОбязательства.Видимость(0);
		
	Иначе
		Форма.ИспользоватьСлой("ШтрафныеСанкции", 1);
	КонецЕсли;
		
	Если (Константа.ВалютныйУчет = Да) или (ПустоеЗначение(ВалютаДоговора) = 0) Тогда
		Если Наименование <> глИмяДоговораДляПлатежейБезДоговора Тогда
			Форма.ИспользоватьСлой("ВалютныйУчет", 1);
		КонецЕсли;
		Если ПустоеЗначение(ВалютаДоговора) = 0 Тогда
		    ЦеныВДоговоре = 2;
		Иначе
			ЦеныВДоговоре = 1
		КонецЕсли;
		
		УправлениеДиалогом();
	КонецЕсли;	

	СписокВременныхЕдиницРасчета = СоздатьОбъект("СписокЗначений");
	СписокВременныхЕдиницРасчета.ДобавитьЗначение("День");
	СписокВременныхЕдиницРасчета.ДобавитьЗначение("Рабочий день");
	СписокВременныхЕдиницРасчета.ДобавитьЗначение("Неделя");
	СписокВременныхЕдиницРасчета.ДобавитьЗначение("Месяц"); 
	
КонецПроцедуры // ПриОткрытии()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(Скопирован)
	
	Если Скопирован = 0 Тогда
	    АвтоОбработкаНДС=?(Константа.АвтоКнигаПокупокПродаж = Да, 1, 0);
	КонецЕсли;
	
	ВидДоговора = глЗначениеПоУмолчанию("ОсновнойВидДоговоров");
	
КонецПроцедуры

//*****************************************************************************
// Предопределенная процедура
//

Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, Флаг)
	
	Если ЭлементДиалога = "ВременнаяЕдиницаРасчетаШтрафныхСанкций" Тогда
		Флаг = 0;
		ВыбраннаяВременнаяЕдиницаРасчета = СокрП(ВременнаяЕдиницаРасчетаШтрафныхСанкций);
		Если СписокВременныхЕдиницРасчета.ВыбратьЗначение(ВыбраннаяВременнаяЕдиницаРасчета,,,, 2) = 1 Тогда
		    ВременнаяЕдиницаРасчетаШтрафныхСанкций = ВыбраннаяВременнаяЕдиницаРасчета;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры //ПриНачалеВыбораЗначения()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи()
	
	Если (ЦеныВДоговоре = 2) и (ПустоеЗначение(ВалютаДоговора) = 1) Тогда
		Предупреждение("Введите валюту договора или поставьте переключатель 
						|""Цены в договоре"" в положение ""в рублях""");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()