Перем ПрежнееНаименование;

//******************************************************************************
// Валюта()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Строка - возвращается наименование валюты цен в договоре или "руб.", 
// 			 если валюта не выбрана.
//
// Вызывается из формул элементов диалога:
//  Графа списка "Валюта".
//
Функция Валюта()
	        
	Стр = "";
	
	     
	Если (ТекущийЭлемент().Выбран() = 0) или (ТекущийЭлемент().ЭтоГруппа() = 1) Тогда
	ИначеЕсли ПустоеЗначение(ВалютаДоговора) = 1 Тогда
	    Если Наименование <> глИмяДоговораДляПлатежейБезДоговора Тогда
			Стр = "руб.";
		КонецЕсли;
	Иначе
	    Стр = ВалютаДоговора.Наименование;
	КонецЕсли;
	
	Возврат Стр;
	
КонецФункции // Валюта()

//******************************************************************************
// Оплата()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Строка - возвращается наименование валюты оплаты договора. Предполагается, 
//			 что валюта оплаты договора совпадает с валютой цен в договоре, если 
//			 договор оплачивается в валюте. Если договор оплачивается в рублях, 
//			 возвращается строка "руб.".
//
// Вызывается из формул элементов диалога:
//  Графа списка "Оплата".
//
Функция Оплата()
	
	Если ОплатаДоговора = 1 Тогда
	    Если Наименование = глИмяДоговораДляПлатежейБезДоговора Тогда
			Стр = "";
		Иначе
			Стр = "руб.";
		КонецЕсли;
	ИначеЕсли ПустоеЗначение(ВалютаДоговора) = 0 Тогда
	    Стр = ВалютаДоговора.Наименование;
	Иначе
		Стр = "";
	КонецЕсли;
	Возврат Стр;
	
КонецФункции // Оплата()

//******************************************************************************
// ОткрытьПутеводитель()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "Путеводитель".
//
Процедура ОткрытьПутеводитель()
	
	ОткрытьФорму("Обработка.Путеводитель", "РасчетыСКонтрагентами");
	
КонецПроцедуры // ОткрытьПутеводитель()

//******************************************************************************
// ОСВпоСчету()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "ОСВ по счету".
//
// Описание:
//  Вызов оборотно-сальдовой ведомости по счету.
//
Процедура ОСВпоСчету()
	
	Меню = СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("60", "счет 60");
	Меню.ДобавитьЗначение("62", "счет 62");
	Меню.ДобавитьЗначение("76.5", "счет 76.5");

	КодСчета = "";
	Если Меню.ВыбратьЗначение(КодСчета,,,,1) = 1 Тогда
		Расшифровка = СоздатьОбъект("СписокЗначений");
		Расшифровка.Установить("Отчет", "ОборотноСальдоваяВедомостьПоСчету");
		Расшифровка.Установить("Счет", СчетПоКоду(КодСчета));
		Расшифровка.Установить("Дата1", НачМесяца(РабочаяДата()));
		Расшифровка.Установить("Дата2", КонМесяца(РабочаяДата()));
		Расшифровка.Установить("ДанныеПоСубсчетам", 1);
		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.Контрагенты);
		Расшифровка.Установить("Субконто1", ТекущийЭлемент().Владелец);
		Расшифровка.Установить("ОтборСубконто1", 2);
		Расшифровка.Установить("ПоГруппам1", 0);
		Расшифровка.Установить("ВидСубконто2", ВидыСубконто.Договоры);
		Если ТекущийЭлемент().Выбран() = 1 Тогда
			Расшифровка.Установить("Субконто2", ТекущийЭлемент());
			Если ТекущийЭлемент().ЭтоГруппа() = 1 Тогда
				Расшифровка.Установить("ПоГруппам2", 1);
				Расшифровка.Установить("ОтборСубконто2", 1);
			Иначе
				Расшифровка.Установить("ПоГруппам2", 0);
				Расшифровка.Установить("ОтборСубконто2", 2);
			КонецЕсли;
		КонецЕсли;

		глРасшифровка = Расшифровка;
		глОбновить = 2;
		глТаблица = "";
		глФлагРасшифровки = 1;
		ОткрытьФорму("Отчет.ОборотноСальдоваяВедомостьПоСчету");
		глФлагРасшифровки = 0;
		глОбновить = 0;
	КонецЕсли;
	
КонецПроцедуры // ОСВпоСчету()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Если Константа.ВалютныйУчет = Да Тогда
	    Форма.Валюта.Видимость(1);
	    Форма.Оплата.Видимость(1);
		
	Иначе
	    Форма.Валюта.Видимость(0);
	    Форма.Оплата.Видимость(0);
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриПереносеЭлементаВДругуюГруппу(Элемент, Группа)

	Если (Элемент.Наименование = глИмяДоговораДляПлатежейБезДоговора)
		или (Элемент.Наименование = глИмяДоговораДляПлатежейБезДоговораТорг) Тогда
		Предупреждение("Служебный договор нельзя перенести в другую группу.");
		СтатусВозврата(0);
	КонецЕсли;
	
КонецПроцедуры // ПриПереносеЭлементаВДругуюГруппу()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВводеСтроки()

	ПрежнееНаименование = "";
	
КонецПроцедуры // ПриВводеСтроки()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеРедактированияСтроки()

	ПрежнееНаименование = Наименование;

КонецПроцедуры // ПриНачалеРедактированияСтроки()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи()
	
	Если (ПрежнееНаименование = глИмяДоговораДляПлатежейБезДоговора)
		или (ПрежнееНаименование = глИмяДоговораДляПлатежейБезДоговораТорг) Тогда
		Если Наименование <> ПрежнееНаименование Тогда
			Предупреждение("Нельзя изменять наименование служебного договора.");
			Активизировать("Наименование");
			СтатусВозврата(0);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	Если (Наименование = глИмяДоговораДляПлатежейБезДоговора)
		или (Наименование = глИмяДоговораДляПлатежейБезДоговораТорг) Тогда
		Предупреждение("Указанное наименование договора соответствует
						|наименованию служебного элемента справочника.
						|Измените наименование договора.");
		Активизировать("Наименование");
		СтатусВозврата(0);
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()