Перем ТекстЗагружаемыеДанные;		// Текст - файл загрузки 
Перем ФайлЗагрузки;
Перем СписокЗагрузки;
Перем Справочник; 
Перем Реквизиты;
Перем ЗначенияРеквизитов;
Перем РежимЗагрузки;
Перем НомКод,ДлинаКода; 
Перем ТаблУдалСписок;
Перем СписокПодчиненных;   
Перем РезультатЗагрузки;

//******************************************************************************
//
Процедура Сформировать()
	Таб1 = СоздатьОбъект("Таблица");   
	Спр=СоздатьОбъект("Справочник.ОбщероссийскийКлассификаторОсновныхФондов");  
	Таб1.ПовторятьПриПечатиСтроки(Таб1.ВысотаСекции("Заголовок")+1,Таб1.ВысотаСекции("Заголовок")+Таб1.ВысотаСекции("Шапка"));
	Таб1.ВывестиСекцию("Заголовок");
  	Таб1.ВывестиСекцию("Шапка");  
    Спр.ВыбратьЭлементы();
	Пока Спр.ПолучитьЭлемент()> 0 Цикл  
		ПКодс=Спр.Код;
		ПНаим=Спр.Наименование;
		Если Спр.Уровень()= 1 Тогда
			Таб1.ВывестиСекцию("Группа");
		ИначеЕсли Спр.Уровень()= 2 Тогда 
			Если Спр.ЭтоГруппа()= 1 Тогда
				Таб1.ВывестиСекцию("Группа1");
			Иначе    
				Таб1.ВывестиСекцию("Элемент1");
			КонецЕсли;	
		ИначеЕсли Спр.Уровень()= 3 Тогда    
			Если Спр.ЭтоГруппа()= 1 Тогда
				Таб1.ВывестиСекцию("Группа2");
			Иначе
				Таб1.ВывестиСекцию("Элемент2");
			КонецЕсли;  
		Иначе
			Таб1.ВывестиСекцию("Элемент3");
		КонецЕсли;	
	КонецЦикла;	 
	Таб1.ТолькоПросмотр(1);
	Таб1.Опции(0,0,0,0);
	Таб1.Показать("Классификатор основных фондов");

КонецПроцедуры   

//*****************************************************************************
// 
Функция ПодчиненныеСправочники(СпрВладелец, СпСправ) Экспорт 
	ФлагВозврата=0;
	СпСправ=СоздатьОбъект("СписокЗначений");
	Для А=1 По Метаданные.Справочник() Цикл   
		ТекСправ=Метаданные.Справочник(А);
		Если ТекСправ.Владелец.Выбран() = 1 Тогда
			Если ТекСправ.Владелец.Идентификатор=СпрВладелец.Вид() Тогда 
				//СпСправ.ДобавитьЗначение(ТекСправ.Владелец.Идентификатор);
				СпСправ.ДобавитьЗначение(ТекСправ.Идентификатор);
				ФлагВозврата=1;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;  
	Возврат ФлагВозврата;
КонецФункции

//*****************************************************************************
// 
Процедура ОчиститьСправочник(Справочник) 
	
	СпрУдал   =СоздатьОбъект("Справочник."+Справочник);

	ТаблУдалСписок=СоздатьОбъект("ТаблицаЗначений"); 
	ТаблУдалСписок.НоваяКолонка("ВидСправочника");
	ТаблУдалСписок.НоваяКолонка("ЭлементСправочника");
	СписокПодчиненных="";
	Флаг=ПодчиненныеСправочники(СпрУдал, СписокПодчиненных); 
	Если Флаг=1 Тогда 
		Для А=1 По СписокПодчиненных.РазмерСписка() Цикл 
			ВидСправочника=СписокПодчиненных.ПолучитьЗначение(А);
			ПодчСправ=СоздатьОбъект("Справочник."+ВидСправочника);
			ПодчСправ.ВыбратьЭлементы(0);
			Пока ПодчСправ.ПолучитьЭлемент() =1 Цикл 
				Если ПодчСправ.ПометкаУдаления()=1 Тогда
					ТаблУдалСписок.НоваяСтрока();
					ТаблУдалСписок.ВидСправочника =ВидСправочника;
					ТаблУдалСписок.ЭлементСправочника=ПодчСправ.ТекущийЭлемент();
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	СпрУдал.ВыбратьЭлементы();
	Пока СпрУдал.ПолучитьЭлемент() =1 Цикл
		СпрУдал.Удалить(0);
	КонецЦикла;
КонецПроцедуры
//*****************************************************************************
// 
Процедура ПроверитьССылки(Справочник)
	СпрУдал   =СоздатьОбъект("Справочник."+Справочник);
	Флаг=ПодчиненныеСправочники(СпрУдал, СписокПодчиненных); 
	ТаблицаССылок = СоздатьОбъект("ТаблицаЗначений"); 
	СпрУдал.ОбратныйПорядок();
	СпрУдал.ВыбратьЭлементы();
	Пока СпрУдал.ПолучитьЭлемент() =1 Цикл
		Если СпрУдал.ПометкаУдаления()=1 Тогда  
			ТаблицаССылок.УдалитьСтроки();
			НайтиСсылки(СпрУдал.ТекущийЭлемент(),ТаблицаССылок);
			Если ТаблицаССылок.КоличествоСтрок()>0 Тогда 
				СнятьПометку=0;
				ТаблицаССылок.ВыбратьСтроки();
				НомСтр=0;
				Пока ТаблицаССылок.ПолучитьСтроку()=1 Цикл
					НомСтр=НомСтр+1;
					Ссылка=ТаблицаССылок.Ссылка;
					Если ТипЗначенияСтр(Ссылка) ="" Тогда
						Продолжить;
					КонецЕсли;
					Если Ссылка.ПометкаУдаления()=0 Тогда
						СнятьПометку=1;
					КонецЕсли;
				КонецЦикла;
				Если СнятьПометку=1 Тогда
					СпрУдал.СнятьПометкуУдаления(); 
					СпрУдал.Записать(); 
					Если Флаг=1 Тогда  
						Для А=1 По СписокПодчиненных.РазмерСписка() Цикл 
							ВидСправочника=СписокПодчиненных.ПолучитьЗначение(А);
							ПодчСправ=СоздатьОбъект("Справочник."+ВидСправочника);
							ПодчСправ.ИспользоватьВладельца(СпрУдал.ТекущийЭлемент());
							ПодчСправ.ВыбратьЭлементы();
							Пока ПодчСправ.ПолучитьЭлемент()=1 Цикл 
								Если ПодчСправ.ПометкаУдаления()=1 Тогда
									стр=0;
									Если ТаблУдалСписок.НайтиЗначение(ПодчСправ.ТекущийЭлемент(),стр,"ЭлементСправочника")=1 Тогда
										Продолжить;
									КонецЕсли;
									ПодчСправ.СнятьПометкуУдаления();
									ПодчСправ.Записать();
								КонецЕсли;
							КонецЦикла;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			Иначе
				Сообщить("Элемент справочника ОКОФ "+СокрП(СпрУдал.ТекущийЭлемент())+""" помечен на удаление","!!");
			КонецЕсли;
		Иначе
			Если Флаг=1 Тогда  
				Для А=1 По СписокПодчиненных.РазмерСписка() Цикл 
					ВидСправочника=СписокПодчиненных.ПолучитьЗначение(А);
					ПодчСправ=СоздатьОбъект("Справочник."+ВидСправочника);
					ПодчСправ.ИспользоватьВладельца(СпрУдал.ТекущийЭлемент());
					ПодчСправ.ВыбратьЭлементы();
					Пока ПодчСправ.ПолучитьЭлемент()=1 Цикл 
						Если ПодчСправ.ПометкаУдаления()=1 Тогда
							стр=0;
							Если ТаблУдалСписок.НайтиЗначение(ПодчСправ.ТекущийЭлемент(),стр,"ЭлементСправочника")=1 Тогда
								Продолжить;
							КонецЕсли;
							ПодчСправ.СнятьПометкуУдаления();
							ПодчСправ.Записать();
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
//*****************************************************************************
// 
Процедура ЗагрузитьРеквизиты(ЭлСправочника);
	МетаСправочник=Метаданные.Справочник(Справочник);
	Для А=1 По Метаданные.Справочник(Справочник).Реквизит() Цикл 
		Реквизит = МетаСправочник.Реквизит(А);
		Если (ЭлСправочника.ЭтоГруппа()=1) и (Реквизит.Использование="ДляЭлемента") Тогда
			Продолжить;
		КонецЕсли;
		Если (ЭлСправочника.ЭтоГруппа()=0) и (Реквизит.Использование="ДляГруппы") Тогда
			Продолжить;
		КонецЕсли;
		Для Б=1 По Реквизиты.РазмерСписка() Цикл
			Если (Реквизит.Идентификатор=Реквизиты.ПолучитьЗначение(Б)) или ((Реквизит.Идентификатор = "КонтрольноеЧисло") и (Реквизиты.ПолучитьЗначение(Б) = "КЧ")) Тогда 
				Если Реквизит.Периодический = 0 Тогда 
					Если Реквизит.Тип="Справочник" Тогда
						СправочникСсылок=СоздатьОбъект("Справочник."+Реквизит.Вид); 
						Если Метаданные.Справочник(Справочник).ОсновноеПредставление = "ВВидеНаименования" Тогда
							Нашли=СправочникССылок.НайтиПоНаименованию(ЗначенияРеквизитов.ПолучитьЗначение(Б),0);
						Иначе
							Нашли=СправочникССылок.НайтиПоКоду(ЗначенияРеквизитов.ПолучитьЗначение(Б)); 
						КонецЕсли;
						Если Нашли=1 Тогда
							ЭлСправочника.УстановитьАтрибут(Реквизит,СправочникССылок.ТекущийЭлемент());
						КонецЕсли;
					ИначеЕсли Реквизит.Тип="Перечисление" Тогда
						ВидПеречисления = Реквизит.Вид;
					    ЗначениеПеречисления=Перечисление.ПолучитьАтрибут(ВидПеречисления).ЗначениеПоИдентификатору(ЗначенияРеквизитов.ПолучитьЗначение(Б));
						ЭлСправочника.УстановитьАтрибут(Реквизит,ЗначениеПеречисления);
					Иначе
						ЭлСправочника.УстановитьАтрибут(Реквизит,ЗначенияРеквизитов.ПолучитьЗначение(Б));
					КонецЕсли;
				КонецЕсли;
				Прервать;
			КонецЕсли; 
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры
//*****************************************************************************
// 
Функция ОпределитьИмяСправочника()
	Реквизиты.УдалитьВсе();
    ИмяСправочника=СписокЗагрузки.ПолучитьЗначение(1);
	
	Если Лев(ИмяСправочника,2)<>"//" Тогда
		Сообщить("Не определена служебная строка", "!!!");
		Возврат(0);
	КонецЕсли;       
	
	ИмяСправочника=Сред(ИмяСправочника,3); 
	Если ПустаяСтрока(ИмяСправочника)=1 Тогда
		Возврат(0);
	КонецЕсли;  
	Если Лев(ИмяСправочника,11)="Справочник." Тогда
		Справочник=Сред(ИмяСправочника,12);
	Иначе
		Сообщить("Не задан вид справочника", "!!!");
		Возврат(0);
		Справочник="";
	КонецЕсли;  
	Если  СписокЗагрузки.РазмерСписка()>1 Тогда 
		Для А=2 по СписокЗагрузки.РазмерСписка() Цикл 
			Реквизиты.ДобавитьЗначение(СписокЗагрузки.ПолучитьЗначение(А));
		КонецЦикла;
	КонецЕсли;  
	Возврат(1);
КонецФункции // ОпределитьИмяСправочника	

//******************************************************************************
//	ПрочитатьСлужебнуюСтроку()
//
//	Возвращаемое значение:
//	1 - строка разобрана успешно, 0 - иначе
//
//	Описание:
//	Разбирает первую служебную строку файла загрузки.
Функция ПрочитатьСлужебнуюСтроку()    

	СписокЗагрузки	= СоздатьОбъект("СписокЗначений");
	
	ТекстЗагружаемыеДанные.ОткрытьФайл(ФайлЗагрузки, 0);

	ТекстЗагружаемыеДанные.ВыбратьСтроки();
	                                            
	СтрокаЗагрузки = "";
	Если ТекстЗагружаемыеДанные.ПрочитатьСтроку(СтрокаЗагрузки) = 0 Тогда
		Сообщить("Файл загрузки пустой!", "!!!");
		Возврат(0);		
	КонецЕсли;

	СписокЗагрузки.ИзСтрокиСРазделителями(СтрокаЗагрузки);

	Если СписокЗагрузки.РазмерСписка() < 1 Тогда
		Сообщить("Служебная строка файла загрузки пустая!", "!!!");
		Возврат(0);
	КонецЕсли; 
	КодВозврата=ОпределитьИмяСправочника();
	
	Возврат(КодВозврата);
КонецФункции // ПрочитатьСлужебнуюСтроку()	
//*****************************************************************************
// 
Процедура НовыйКод(Элем)	
	Элем.УстановитьНовыйКод("Б");
КонецПроцедуры	

//******************************************************************************
// ЗагрузитьСправочник();
//
// Параметры: 
//  Справочник- строка, идентификатор загружаемого
//                                справочника
//  ИмяФайла - строка, имя файла  с данными для загрузки
//
//  Возвращаемое значение: Нет.    
//
Процедура ЗагрузитьСправочник(); 
	Справочник = "ОбщероссийскийКлассификаторОсновныхФондов";
	Спр   =СоздатьОбъект("Справочник."+Справочник);
	СпрГруппа=СоздатьОбъект("Справочник."+Справочник);
	СпрГруппа1=СоздатьОбъект("Справочник."+Справочник);
	СпрГруппа2=СоздатьОбъект("Справочник."+Справочник);
	СпрТекущийРодитель=СоздатьОбъект("Справочник."+Справочник);
	Если Спр.ВыбратьЭлементы()= 0 Тогда
		ПолныйКодДляПоиска=1;
	Иначе
		Если Метаданные.Справочник(Справочник).СерииКодов="ВПределахПодчинения" Тогда
			ПолныйКодДляПоиска=1;
		Иначе
			ПолныйКодДляПоиска=0;
		КонецЕсли;
	КонецЕсли;
		
	ОчиститьСправочник(Справочник);
	СтрокаЗагрузки = "";                     
	НомКод=0;                 
	ДлинаКода=Метаданные.Справочник(Справочник).ДлинаКода;
	
	Пока ТекстЗагружаемыеДанные.ПрочитатьСтроку(СтрокаЗагрузки) = 1  Цикл
		СтрокаЗагрузки = СокрЛ(СтрокаЗагрузки);
		
		Если (ПустаяСтрока(СтрокаЗагрузки) = 1)  Тогда 
			Продолжить 
		КонецЕсли;

		СписокЗагрузки.ИзСтрокиСРазделителями(СтрокаЗагрузки);  
		Если """//" = Лев(СтрокаЗагрузки, 3) Тогда 
			ПроверитьССылки(Справочник);
			КодВозврата=ОпределитьИмяСправочника();
			Если КодВозврата=0 Тогда
				Возврат;
			Иначе  
				Спр   =СоздатьОбъект("Справочник."+Справочник);
				СпрГруппа=СоздатьОбъект("Справочник."+Справочник);
				СпрГруппа1=СоздатьОбъект("Справочник."+Справочник);
				СпрГруппа2=СоздатьОбъект("Справочник."+Справочник); 
				СпрТекущийРодитель=СоздатьОбъект("Справочник."+Справочник); 
				ОчиститьСправочник(Справочник);
			КонецЕсли;
			Продолжить; 
		КонецЕсли;
		
		КоличествоРеквизитов=Реквизиты.РазмерСписка();
		Если СписокЗагрузки.РазмерСписка()<КоличествоРеквизитов+5 Тогда
			Продолжить;
		КонецЕсли;
		
		Код1=СписокЗагрузки.ПолучитьЗначение(1);
		Код2=СписокЗагрузки.ПолучитьЗначение(2);
		Код3=СписокЗагрузки.ПолучитьЗначение(3);
		Код4=СписокЗагрузки.ПолучитьЗначение(4);
		Наименование=СписокЗагрузки.ПолучитьЗначение(5); 
		
		Если КоличествоРеквизитов>0 Тогда
			ЗначенияРеквизитов.УдалитьВсе();
			Для А=1 По КоличествоРеквизитов Цикл
				ЗначенияРеквизитов.ДобавитьЗначение(СписокЗагрузки.ПолучитьЗначение(А+5));
			КонецЦикла;
		КонецЕсли;
		
		
		ПолныйКод=""; 
		УровеньЭлемента=0;
		Если ПустаяСтрока(Код1)=0 Тогда 
			ПолныйКод=""+СокрЛП(Код1);  
			УровеньЭлемента=УровеньЭлемента+1;
			Если ПустаяСтрока(Код2)=0 Тогда 
				ПолныйКод=ПолныйКод+"/"+СокрЛП(Код2);
				УровеньЭлемента=УровеньЭлемента+1;
				Если ПустаяСтрока(Код3)=0 Тогда 
					УровеньЭлемента=УровеньЭлемента+1;
					ПолныйКод=ПолныйКод+"/"+СокрЛП(Код3);
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;	
		
		ГруппаРодитель="";
		Если УровеньЭлемента>0 Тогда     
		// Ищем группу   
			Если ПолныйКодДляПоиска=1 Тогда  
				КодДляПоиска=Код1; 
				Признак=2;
			Иначе
				КодДляПоиска=Код1;
				Признак=0;
			КонецЕсли;                    
		
			Если СпрГруппа.НайтиПоКоду(Код1,0)= 1 Тогда     
				НайденныйПолныйКод=СпрГруппа.ПолныйКод();
				Если НайденныйПолныйКод<>Код1 Тогда
					НовыйКод(СпрГруппа);
					СпрГруппа.Записать();
					СпрГруппа.ИспользоватьРодителя();
					СпрГруппа.НоваяГруппа();
					СпрГруппа.Код=Код1; 
				Иначе
					Если СпрГруппа.ЭтоГруппа()=1 Тогда          
						СпрГруппа.СнятьПометкуУдаления();
					Иначе 
						НовыйКод(СпрГруппа);
						СпрГруппа.Записать();
						СпрГруппа.ИспользоватьРодителя();
						СпрГруппа.НоваяГруппа();
						СпрГруппа.Код=Код1; 
					КонецЕсли;                                    
				КонецЕсли;                                    
			Иначе                                          
				СпрГруппа.ИспользоватьРодителя();
				СпрГруппа.НоваяГруппа();
				СпрГруппа.Код=Код1;  
			КонецЕсли;                                      
			Если (УровеньЭлемента=1) и (ПустоеЗначение(Код4)=1) Тогда          
				СпрГруппа.Наименование = Наименование; 
				ЗагрузитьРеквизиты(СпрГруппа);
			КонецЕсли;                                  
			СпрГруппа.Записать(); 
			ГруппаРодитель=СпрГруппа;
			Если УровеньЭлемента>1 Тогда                  
				Если ПолныйКодДляПоиска=1 Тогда
					Если УровеньЭлемента=2 Тогда
						КодДляПоиска=ПолныйКод;
					Иначе
						КодДляПоиска=СокрЛП(Код1)+"/"+СокрЛП(Код2); 
					КонецЕсли;
					Признак=2;
				Иначе
					КодДляПоиска=Код2;
					Признак=0;
				КонецЕсли;                    
				Если СпрГруппа1.НайтиПоКоду(КодДляПоиска,Признак)=1 Тогда 
					НайденныйПолныйКод=СпрГруппа1.ПолныйКод();
					Если НайденныйПолныйКод<>СокрЛП(Код1)+"/"+СокрЛП(Код2) Тогда
						НовыйКод(СпрГруппа1);
						СпрГруппа1.Записать();
						СпрГруппа1.ИспользоватьРодителя(СпрГруппа);
						СпрГруппа1.НоваяГруппа();
						СпрГруппа1.Код=Код2;  
					Иначе
						Если СпрГруппа1.ЭтоГруппа()=1 Тогда      
							СпрГруппа1.СнятьПометкуУдаления();
						Иначе  
							НовыйКод(СпрГруппа1);
							СпрГруппа1.Записать();
							СпрГруппа1.ИспользоватьРодителя(СпрГруппа);
							СпрГруппа1.НоваяГруппа();
							СпрГруппа1.Код=Код2;  
						КонецЕсли;
					КонецЕсли; 
				Иначе
					СпрГруппа1.ИспользоватьРодителя(СпрГруппа);
					СпрГруппа1.НоваяГруппа();
					СпрГруппа1.Код=Код2;  
				КонецЕсли;
				Если (УровеньЭлемента=2) и (ПустоеЗначение(Код4)=1) Тогда
					СпрГруппа1.Наименование= Наименование;
					ЗагрузитьРеквизиты(СпрГруппа1);
				КонецЕсли;
				СпрГруппа1.Записать();
				ГруппаРодитель=СпрГруппа1;
				Если УровеньЭлемента=3 Тогда
					Если ПолныйКодДляПоиска=1 Тогда
						КодДляПоиска=ПолныйКод;
						Признак=2;
					Иначе
						КодДляПоиска=Код3;
						Признак=0;
					КонецЕсли;                    
					Если СпрГруппа2.НайтиПоКоду(КодДляПоиска,Признак)=1 Тогда 
						НайденныйПолныйКод=СпрГруппа2.ПолныйКод();
						Если НайденныйПолныйКод<>ПолныйКод Тогда 
							НовыйКод(СпрГруппа2);
							СпрГруппа2.Записать();
							СпрГруппа2.ИспользоватьРодителя(СпрГруппа1);
							СпрГруппа2.НоваяГруппа();
							СпрГруппа2.Код=Код3;
						Иначе
							Если СпрГруппа2.ЭтоГруппа()=1 Тогда
								СпрГруппа2.СнятьПометкуУдаления();
							Иначе 
								НовыйКод(СпрГруппа2);
								СпрГруппа2.Записать();
								СпрГруппа2.ИспользоватьРодителя(СпрГруппа1);
								СпрГруппа2.НоваяГруппа();
								СпрГруппа2.Код=Код3; 
								СпрГруппа2.Записать();
							КонецЕсли;
						КонецЕсли; 
					Иначе
						СпрГруппа2.ИспользоватьРодителя(СпрГруппа1);
						СпрГруппа2.НоваяГруппа();
						СпрГруппа2.Код=Код3; 
						СпрГруппа2.Записать();
					КонецЕсли; 
					Если ПустоеЗначение(Код4)=1 Тогда
						СпрГруппа2.Наименование= Наименование;
						ЗагрузитьРеквизиты(СпрГруппа2);
					КонецЕсли;
					СпрГруппа2.Записать();
					ГруппаРодитель=СпрГруппа2;
				КонецЕсли;
			КонецЕсли; 
			Спр.ИспользоватьРодителя(ГруппаРодитель);
		КонецЕсли;
		Если ПустаяСтрока(Код4)=0 Тогда  
			СпрТекущийРодитель.НайтиПоКоду(ПолныйКод,2);
			Если ПолныйКодДляПоиска=1 Тогда
				Если ПустаяСтрока(ПолныйКод)=0 Тогда
					ПолныйКод=ПолныйКод+"/";
				КонецЕсли;
				ПолныйКод=ПолныйКод+СокрЛП(Код4); 
				КодДляПоиска=ПолныйКод;
				Признак=2;
			Иначе
				КодДляПоиска=Код4;
				Признак=0;
			КонецЕсли;
			
			Если Спр.НайтиПоКоду(КодДляПоиска,Признак)= 0 Тогда
				Спр.Новый();
				Спр.Код=Код4;
			Иначе
				ОК=1;
				Если Признак=0 Тогда
					НайденныйПолныйКод=Спр.ПолныйКод();
					Если НайденныйПолныйКод<>ПолныйКод Тогда 
						ОК=0; 
						Спр.Родитель=СпрТекущийРодитель; 
						Спр.Записать();
					КонецЕсли;
				КонецЕсли;
				Если Спр.ЭтоГруппа()=0 Тогда
					Спр.СнятьПометкуУдаления(); 
				Иначе 
					НовыйКод(Спр);
					Спр.Записать();
					Спр.Новый();  
					Спр.Код=Код4; 
					Если ПустаяСтрока(ПолныйКод)=1 Тогда 
						Спр.Родитель="";
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Спр.Наименование= Наименование;
			ЗагрузитьРеквизиты(Спр);
			Спр.Записать();
		КонецЕсли;
		
		Состояние("Загружается справочник ""Общероссийский классификатор основных фондов""" + " "+ПолныйКод+ " "+ Наименование );
	КонецЦикла;
КонецПроцедуры   // ЗагрузитьСправочник(Справочник,Файл);
//*****************************************************************************
// 
Процедура Загрузка() 
	Файл = "okof.txt";
	РезультатЗагрузки=1;
	ИсхКаталог=КаталогИБ();
	Если Прав(ИсхКаталог,1)<>"\" Тогда
		ИсхКаталог=ИсхКаталог+"\";
	КонецЕсли;	
	ФайлЗагрузки=ИсхКаталог+Файл;
	Если ФС.СуществуетФайл(ФайлЗагрузки)= 0 Тогда
		Если РежимЗагрузки=1 Тогда
			Предупреждение("Загрузка справочника ОКОФ не произведена !"+ РазделительСтрок+
			"В каталоге базы данных отсутствует файл "+Файл); 
			Возврат;
		КонецЕсли;
		Если ФС.ВыбратьФайл(0, Файл, ИсхКаталог, "Выберите файл загрузки справочника "+Справочник,
		"Текстовые файлы (*.txt) |*.txt") = 1 Тогда 
			ФайлЗагрузки=ИсхКаталог+"\"+Файл; 
			
			Если ФС.СуществуетФайл(ФайлЗагрузки)= 0 Тогда
				Предупреждение("Загрузка справочника ОКОФ не произведена !"+ РазделительСтрок+
				"В указанном каталоге отсутствует файл "+Файл); 
				Возврат;
			КонецЕсли;			
		Иначе
			Возврат; 
		КонецЕсли;
	КонецЕсли;           
	
	Реквизиты = СоздатьОбъект("СписокЗначений");
	Если ПрочитатьСлужебнуюСтроку() = 0 Тогда 
		Возврат; 
	КонецЕсли;
	
	Попытка
		НачатьТранзакцию();  
		ЗначенияРеквизитов= СоздатьОбъект("СписокЗначений");
		СписокЗагрузки	= СоздатьОбъект("СписокЗначений");
		
		ЗагрузитьСправочник();
		ТекстЗагружаемыеДанные.ЗакрытьФайл();
		ЗафиксироватьТранзакцию(); 
	Исключение 
		ОтменитьТранзакцию();   
		Сообщить(ОписаниеОшибки());
		Предупреждение("Обновление классификатора ""Общероссийский классификатор основных фондов"" невозможно.");
		РезультатЗагрузки=0;
	КонецПопытки;
КонецПроцедуры   

//******
Процедура ЗагрузитьКлассификатор()
	Если МонопольныйРежим() = 1 Тогда
		Если  "Да" <> Вопрос("Загрузить справочник ОКОФ?", "Да+Нет") Тогда
			СтатусВозврата(0); 
			Возврат;
		КонецЕсли;
	ИначеЕсли "Да" <> Вопрос("Загрузку справочника рекомендуется выполнять
							 |в монопольном режиме. Продолжить?", "Да+Нет") Тогда
		СтатусВозврата(0); 
		Возврат;
	КонецЕсли;

	Если ЗагрузитьВнешнююКомпоненту(КаталогИБ() + "v7plus.dll") <> 1 Тогда
		Если ЗагрузитьВнешнююКомпоненту(КаталогИБ() + "ExtForms\" + "v7plus.dll") <> 1 Тогда
			Если ЗагрузитьВнешнююКомпоненту(КаталогПрограммы() + "v7plus.dll") <> 1 Тогда
				Предупреждение("Компонента v7plus.dll не найдена!");
				СтатусВозврата(0); Возврат;
			КонецЕсли;
		КонецЕсли;    
	КонецЕсли;
	
	Попытка
		ТекстЗагружаемыеДанные	= СоздатьОбъект("AddIn.V7TextFile");    
	Исключение
		Сообщить(ОписаниеОшибки());
		Предупреждение("Необходимо обновить версию компоненты v7plus.dll !");
		СтатусВозврата(0); Возврат;
	КонецПопытки;
	ТекстЗагружаемыеДанные.КодоваяСтраница(0);
	ТекстЗагружаемыеДанные.РазмерБуфера();
	Загрузка();
КонецПроцедуры;

