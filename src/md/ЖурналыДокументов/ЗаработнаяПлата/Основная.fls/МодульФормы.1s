Перем СписокДействий;

//******************************************************************************
Функция СуммаВыплаты()
	
	Сум = 0;
    Если ТекущийДокумент.Выбран() = 1 Тогда
		Если ТекущийДокумент.Вид() = "ВыплатаЗПКасса" Тогда
			Сум = ТекущийДокумент.Итог("КВыдаче");
		КонецЕсли;
	КонецЕсли;
	Возврат Формат(Сум, "Ч015.2.,");
	
КонецФункции // СуммаВыплаты()

//******************************************************************************
Процедура ОткрытьПутеводитель()

	ОткрытьФорму("Обработка.Путеводитель", "РасчетЗарплаты");

КонецПроцедуры // ОткрытьПутеводитель()

//******************************************************************************
Процедура ПолучитьПравовуюСправку()
	
	Меню = СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("nal_DFL", "Налог на доходы физический лиц");
	Меню.ДобавитьЗначение("nal_EDS", "Единый социальный налог (взнос)");
	Меню.ДобавитьЗначение("Z_SEV_NADB", "Северные надбавки и районные коэффициенты");
	Меню.ДобавитьЗначение("Z_K_ZOT", "Кодекс законов о труде");
	Ссылка = "";
	Если Меню.ВыбратьЗначение(Ссылка,,,,1) = 1 Тогда
	    Норм_ПолучитьСправку(Ссылка);
	КонецЕсли;
	
КонецПроцедуры // ВыбратьПравовуюСправку()

//******************************************************************************
Процедура СформироватьРасчетнуюВедомость()
	
	Если ТекущийДокумент.Вид() = "НачислениеЗП" Тогда
		Параметр = СоздатьОбъект("СписокЗначений");
		Параметр.Установить("Печать", 1);
		ОткрытьФорму(ТекущийДокумент, Параметр,);
	КонецЕсли;
	
КонецПроцедуры // СформироватьРасчетнуюВедомость()

//******************************************************************************
Процедура СформироватьТ53()
	
	Если ТекущийДокумент.Вид() = "ВыплатаЗПКасса" Тогда
		Параметр = СоздатьОбъект("СписокЗначений");
		Параметр.Установить("Печать", 1);
		ОткрытьФорму(ТекущийДокумент, Параметр,);
	КонецЕсли;
	
КонецПроцедуры // СформироватьТ53()

//******************************************************************************
Процедура СформироватьВедомость()
	
	Если ТекущийДокумент.Выбран() = 0 Тогда
		Предупреждение ("Не выбран документ.");
		
	ИначеЕсли ТекущийДокумент.Вид() = "НачислениеЗП" Тогда
		СформироватьРасчетнуюВедомость();
	
	Иначе //Если ТекущийДокумент.Вид() = "ВыплатаЗПКасса"
		СформироватьТ53();
	КонецЕсли;
	
КонецПроцедуры // СформироватьВедомость()

//******************************************************************************
Процедура СформироватьЛистки()
	
	Если ТекущийДокумент.Выбран() = 0 Тогда
		Предупреждение ("Не выбран документ.");
		
	ИначеЕсли ТекущийДокумент.Вид() = "НачислениеЗП" Тогда
		Параметр = СоздатьОбъект("СписокЗначений");
		Параметр.Установить("Печать", 2);
		ОткрытьФорму(ТекущийДокумент, Параметр,);
	КонецЕсли;
	
КонецПроцедуры // СформироватьЛистки()

//******************************************************************************
Процедура ПостроитьОтчет()
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "АнализСчетаПоСубконто");
    Расшифровка.Установить("РазделительУчета", БухИтоги.ИспользоватьРазделительУчета());
    Расшифровка.Установить("Счет", СчетПоКоду("70"));
    Расшифровка.Установить("ДанныеПоСубсчетам", 0);
   	Расшифровка.Установить("ВидСубконто1", ВидыСубконто.Сотрудники);
   	Расшифровка.Установить("ОтборСубконто1", 1);
   	Расшифровка.Установить("ПоГруппам1", 0);
    Расшифровка.Установить("Валюта", "");
    Расшифровка.Установить("ПоВалюте", 0);
    
	Если ТекущийДокумент.Выбран() = 1 Тогда
		Расшифровка.Установить("Дата1", НачМесяца(ТекущийДокумент.ДатаДок));
		Расшифровка.Установить("Дата2", КонМесяца(ТекущийДокумент.ДатаДок));

	Иначе
		Расшифровка.Установить("Дата1", НачалоПериодаБИ());
		Расшифровка.Установить("Дата2", КонецПериодаБИ());
	КонецЕсли;
    
    глРасшифровка = Расшифровка;
	глОбновить = 2;
	глТаблица = "";
	глФлагРасшифровки = 1;
	ОткрытьФорму("Отчет.АнализСчетаПоСубконто");
	глФлагРасшифровки = 0;
	глОбновить = 0;
	
КонецПроцедуры // ПостроитьОтчет()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Форма.Кн_Справка.Видимость(?(ТипЗначения(Норм_Компонента) = 0, 0, 1));
	
	Если ТипЗначенияСтр(Форма.Параметр) = "Документ" Тогда
		глАктивизироватьДокумент(Контекст, Форма.Параметр);
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриПовторномОткрытии()
	
	Если ТипЗначенияСтр(Форма.Параметр) = "Документ" Тогда
		глАктивизироватьДокумент(Контекст, Форма.Параметр);
	КонецЕсли;

КонецПроцедуры // ПриПовторномОткрытии()

//******************************************************************************
СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Ввести на основании");