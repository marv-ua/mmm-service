Перем СписокДействий;
Перем ДатаВыписки;

//******************************************************************************
Функция ПодписьРасчетногоСчета()
	
	ВозвратПодписиРасчетногоСчета="";
	Док=ТекущийДокумент;
	Если ПустоеЗначение(Док)=0 Тогда
		Если Док.Вид()="ОперацияПоРасчСчету" Тогда
			Если (Док.ПлательщикСчет.Вид() = "БанковскиеСчета") Тогда
				ВозвратПодписиРасчетногоСчета="Р/с № "+СокрЛП(Док.ПлательщикСчет.Номер)+" в банке: "+СокрЛП(Док.ПлательщикСчет.БанкОрганизации.Наименование);
			КонецЕсли;
			Если (Док.ПолучательСчет.Вид() = "БанковскиеСчета") Тогда
				Если ПустоеЗначение(ВозвратПодписиРасчетногоСчета)=0 Тогда
					ВозвратПодписиРасчетногоСчета=ВозвратПодписиРасчетногоСчета+РазделительСтрок;
				КонецЕсли;
				ВозвратПодписиРасчетногоСчета=ВозвратПодписиРасчетногоСчета+"Р/с № "+СокрЛП(Док.ПолучательСчет.Номер)+" в банке: "+СокрЛП(Док.ПолучательСчет.БанкОрганизации);
			КонецЕсли;
		Иначе
			ВозвратПодписиРасчетногоСчета="Р/с № "+СокрЛП(Док.РасчетныйСчет.Номер)+" в банке: "+СокрЛП(Док.РасчетныйСчет.БанкОрганизации);
		КонецЕсли;
	КонецЕсли;
	Возврат ВозвратПодписиРасчетногоСчета;
	
КонецФункции // ПодписьРасчетногоСчета()

//******************************************************************************
Функция НомерПиктограммы()
	
	Перем НомПикт;

	Если ТекущийДокумент.Выбран() = 1 Тогда
	    НомПикт = 2;
		ДатаВыписки = "";
	    
		ПодчДок = СоздатьОбъект("Документ");
		Если ПодчДок.ВыбратьПодчиненныеДокументы(ТекущийДокумент.ДатаДок,ТекущийДокумент.ДатаДок+15,ТекущийДокумент) = 1 Тогда
			Пока ПодчДок.ПолучитьДокумент() = 1 Цикл
				Если ПодчДок.Вид() = "Выписка" Тогда
		    		НомПикт = 1;
					ДатаВыписки = ПодчДок.ДатаДок;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		НомПикт = 0;
	КонецЕсли;

	Возврат НомПикт;
	
КонецФункции // НомерПиктограммы()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Форма.Кн_Справка.Видимость(?(ТипЗначения(Норм_Компонента) = 0, 0, 1));
	
	Если ТипЗначенияСтр(Форма.Параметр) = "Документ" Тогда
		глАктивизироватьДокумент(Контекст, Форма.Параметр);
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриПовторномОткрытии()
	
	Если ТипЗначенияСтр(Форма.Параметр) = "Документ" Тогда
		глАктивизироватьДокумент(Контекст, Форма.Параметр);
	КонецЕсли;

КонецПроцедуры // ПриПовторномОткрытии()

//******************************************************************************
СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Ввести на основании");