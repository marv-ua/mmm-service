Перем КонтекстДокумента;
//_____________________________________________________________________________
Функция ПодтверждениеВыбора(Док="")
	Если  Вопрос("Банковский счет выбранного документа "+Док+"
		|не соответствует указанному в выписке.
		|Продолжить?", "Да+Нет") = "Нет" Тогда
			Возврат 1;
	КонецЕсли;
	Возврат 0;
КонецФункции //ПодтверждениеВыбора()
//_____________________________________________________________________________
Функция НомерПиктограммы()
	Перем НомПикт;

	Если ТекущийДокумент.Выбран() = 1 Тогда
		НомПикт = 2;
		ДатаВыписки = "";
	    
		ПодчДок = СоздатьОбъект("Документ");
		Если ПодчДок.ВыбратьПодчиненныеДокументы(ТекущийДокумент.ДатаДок,ТекущийДокумент.ДатаДок+15,ТекущийДокумент) = 1 Тогда
			Пока ПодчДок.ПолучитьДокумент() = 1 Цикл
				Если ПодчДок.Вид() = "Выписка" Тогда
		    		НомПикт = 1;
					ДатаВыписки = ПодчДок.ДатаДок;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		НомПикт = 0;
	КонецЕсли;

	Возврат НомПикт;
КонецФункции //НомерПиктограммы
//_____________________________________________________________________________
Процедура ОК()
	Форма.ВыполнитьВыбор(ВыбрДок);
КонецПроцедуры
//_____________________________________________________________________________
Процедура Удалить()
	Если ВыбрДок.ТекущаяСтрока() > 0 Тогда
	    ВыбрДок.УдалитьСтроку(ВыбрДок.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры
//_____________________________________________________________________________
Процедура Очистить()
	ВыбрДок.УдалитьСтроки();
КонецПроцедуры
//_____________________________________________________________________________
Функция СуммаУчтеннаяРанееПоДокументу(Док,ТипДвижения="")
	Перем Сумма;
	Перем ТекущийНомерСтроки;

	Сумма = 0;
	ПодчДок = СоздатьОбъект("Документ");
	Если ПодчДок.ВыбратьПодчиненныеДокументы(Док.ДатаДок, КонтекстДокумента.ДатаДок, Док) = 1 Тогда
		Пока ПодчДок.ПолучитьДокумент() = 1 Цикл
			Если (ПодчДок.ТекущийДокумент() <> КонтекстДокумента.ТекущийДокумент()) и 
				 (ПодчДок.Вид() = "Выписка") Тогда
				//Предупреждение("Документ "+Док+" учтен в выписке "+ПодчДок.ТекущийДокумент());
				ПодчДок.ВыбратьСтроки();
				Пока ПодчДок.ПолучитьСтроку() = 1 Цикл
					Если ПодчДок.ПервичныйДокумент = Док Тогда
						Если ТипДвижения="Приход" Тогда
							Сумма = Сумма + ПодчДок.Приход;
						ИначеЕсли ТипДвижения="Расход" Тогда
							Сумма = Сумма + ПодчДок.Расход;
						Иначе
							Сумма = Сумма + ПодчДок.Расход + ПодчДок.Приход;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	КонтекстДокумента.ВыбратьСтроки();
	Пока КонтекстДокумента.ПолучитьСтроку() = 1 Цикл
		Если КонтекстДокумента.ПервичныйДокумент = Док Тогда
			Если ТипДвижения="Приход" Тогда
				Сумма = Сумма + КонтекстДокумента.Приход;
			ИначеЕсли ТипДвижения="Расход" Тогда
				Сумма = Сумма + КонтекстДокумента.Расход;
			Иначе
				Сумма = Сумма + КонтекстДокумента.Расход + КонтекстДокумента.Приход;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Сумма;
КонецФункции //СуммаУчтеннаяРанееПоДокументу
//_____________________________________________________________________________
Процедура Заполнить()
	Очистить();
	Док = СоздатьОбъект("Документ");
	Док.ИспользоватьЖурнал("ПлатежныеДокументы");
	Если Док.Вид()="ОперацияПоРасчСчету" Тогда
		Док.ВыбратьДокументы(КонтекстДокумента.ДатаДок, КонтекстДокумента.ДатаДок);
	Иначе
		Док.ВыбратьДокументы(КонтекстДокумента.ДатаДок-10, КонтекстДокумента.ДатаДок);
	КонецЕсли;
	Пока Док.ПолучитьДокумент() = 1 Цикл
		ТипДвижения="";
		Если Док.Вид()="ОперацияПоРасчСчету" Тогда
			Если (Док.ПлательщикСчет <> КонтекстДокумента.БанковскийСчет) и 
			(Док.ПолучательСчет <> КонтекстДокумента.БанковскийСчет) 
			Тогда
				Продолжить;
			КонецЕсли;
			Если (Док.ПлательщикСчет = КонтекстДокумента.БанковскийСчет) Тогда
				ТипДвижения="Расход";
			Иначе
				ТипДвижения="Приход";
			КонецЕсли;
		ИначеЕсли Док.Вид() = "РеестрДокументовНаИнкассо" Тогда
			Продолжить;
		Иначе
			Если Док.РасчетныйСчет <> КонтекстДокумента.БанковскийСчет Тогда
			    Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		СуммаДок = Док.Сумма - СуммаУчтеннаяРанееПоДокументу(Док.ТекущийДокумент(),ТипДвижения);
		Если СуммаДок > 0 Тогда
			ВыбрДок.НоваяСтрока();
			ВыбрДок.ПлатДок = Док.ТекущийДокумент();
			ВыбрДок.СуммаДок = СуммаДок;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры //Заполнить
//_____________________________________________________________________________
Процедура ПриОткрытии() //предопределенная
	КонтекстДокумента = Форма.Параметр;
	ВыбрДок.НоваяКолонка("ПлатДок",,,,"Выбранный документ",25);
	ВыбрДок.НоваяКолонка("СуммаДок",,,,"Сумма",10,"Ч20.2.,",2);
	Форма.ОбработкаВыбораСтроки(1);
КонецПроцедуры //ПриОткрытии
//_____________________________________________________________________________
Процедура ПриВыбореСтроки() //предопределенная
	СтатусВозврата(0);
    
	ТипДвижения="";
	Док = ТекущийДокумент.ТекущийДокумент();
	Если Док.Вид()="ОперацияПоРасчСчету" Тогда
		Если (Док.ПлательщикСчет <> КонтекстДокумента.БанковскийСчет) и 
		(Док.ПолучательСчет <> КонтекстДокумента.БанковскийСчет) 
		Тогда
			Если  ПодтверждениеВыбора(Док)=1 Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Если (Док.ПлательщикСчет = КонтекстДокумента.БанковскийСчет) Тогда
			ТипДвижения="Расход";
		Иначе
			ТипДвижения="Приход";
		КонецЕсли;
	ИначеЕсли Док.Вид() = "РеестрДокументовНаИнкассо" Тогда
		Предупреждение("Документ вида ""Реестр документов на инкассо"" не может быть основанием для строки выписки.");
		Возврат;
	Иначе
		Если Док.РасчетныйСчет <> КонтекстДокумента.БанковскийСчет Тогда
			Если  ПодтверждениеВыбора(Док)=1 Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	НомСтр = 0;
	ВыбрДок.НайтиЗначение(Док, НомСтр, "ПлатДок");
	Если НомСтр = 0 Тогда
		ВыбрДок.НоваяСтрока();
		ВыбрДок.ПлатДок = Док;
		ВыбрДок.СуммаДок = Док.Сумма - СуммаУчтеннаяРанееПоДокументу(Док,ТипДвижения);
	Иначе
		ВыбрДок.ПолучитьСтрокуПоНомеру(НомСтр);
	КонецЕсли;
КонецПроцедуры //ПриВыбореСтроки
