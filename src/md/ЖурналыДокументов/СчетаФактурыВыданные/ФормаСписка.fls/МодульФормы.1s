////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем БухИт, ДатаБИ;
Перем Сч76_АВ, Сч76_Н_1;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//******************************************************************************
// Отчет()
//
// Вызывается из формул элементов диалога:
//  КнопкаПечатьКарточки.
//
// Описание:
//  Вызывается стандартный отчет "Карточка счета".
//
Процедура Отчет()
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "КарточкаСчета");
	Расшифровка.Установить("РазделительУчета", БухИтоги.ИспользоватьРазделительУчета());
	Расшифровка.Установить("Дата1", НачМесяца(РабочаяДата()));
	Расшифровка.Установить("Дата2", КонМесяца(РабочаяДата()));
	
	Если ПустоеЗначение(ТекущийДокумент) = 0 Тогда
		Если НачМесяца(ТекущийДокумент.ДатаДок) < КонМесяца(РабочаяДата()) Тогда
			Расшифровка.Установить("Дата1", НачМесяца(ТекущийДокумент.ДатаДок));
		КонецЕсли;
		
		Если ТекущийДокумент.Аванс = 1 Тогда
			Расшифровка.Установить("Счет", Сч76_АВ);
			
		Иначе
			Расшифровка.Установить("Счет", Сч76_Н_1);
		КонецЕсли;

		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.Контрагенты);
		Расшифровка.Установить("ВидСубконто2", ВидыСубконто.СчетаФактурыВыданные);
		Расшифровка.Установить("Субконто1", ТекущийДокумент.Контрагент);
		Расшифровка.Установить("Субконто2", ТекущийДокумент);
		Расшифровка.Установить("ОтборСубконто1", 2);
		Расшифровка.Установить("ОтборСубконто2", 2);
	КонецЕсли;

	глРасшифровка = Расшифровка;
	глОбновить = 2;
	глТаблица = "";
	глФлагРасшифровки = 1;
	ОткрытьФорму("Отчет.КарточкаСчета");
	глФлагРасшифровки = 0;
	глОбновить = 0;
	
КонецПроцедуры // Отчет()

//******************************************************************************
// Субсчет76()
//
// Возвращаемое значение:
//  Счет.Основной - Счет учета НДС.
//
// Описание:
//  Определяет субсчет на счете 76 для текущего документа.
// 
Функция Субсчет76()
	
	Если ПустоеЗначение(ТекущийДокумент) = 0 Тогда
	    Вид = ТекущийДокумент.Вид();
		Док = ТекущийДокумент;
		
		Если Вид = "СчетФактура" Тогда
		    Если Док.Аванс = 1 Тогда
		        Возврат Сч76_АВ;
				
			ИначеЕсли Док.СчетНДС = 2 Тогда
				Возврат Сч76_Н_1;
		    КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	
	Возврат ПолучитьПустоеЗначение("Счет.Основной"); 
	
КонецФункции // Субсчет76()

//******************************************************************************
// Сальдо76()
//
// Возвращаемое значение:
//  Строка - сальдо по счету 76.
//
// Описание:
//  Определяет сальдо по счету 76 для текущего документа.
// 
Функция Сальдо76()
	
	Если КонМесяца(ДатаБИ) > КонецРассчитанногоПериодаБИ() Тогда 
		Возврат "";
		
	ИначеЕсли ПустоеЗначение(ТекущийДокумент) = 0 Тогда
		СубсчетНДС = Субсчет76();
		Если СубсчетНДС = Сч76_Н_1 Тогда
			СуммаНДС = БухИт.СКК(СубсчетНДС,,,ТекущийДокумент.Контрагент,ТекущийДокумент);
		Иначе
			СуммаНДС = БухИт.СКД(СубсчетНДС,,,ТекущийДокумент.Контрагент,ТекущийДокумент);
		КонецЕсли;
		Возврат СокрЛ(Формат(СуммаНДС,"Ч015.2.,"));
		
	Иначе
		Возврат ""; 
	КонецЕсли;
	
КонецФункции // Сальдо76()

//******************************************************************************
Функция ЗначениеАванса()
	Стр = "";
	Если ТекущийДокумент.Выбран()=1 Тогда
		Если ТекущийДокумент.Вид() = "СчетФактура" Тогда
		    Стр = ?(ТекущийДокумент.Аванс = 1, "с/ф на аванс","");
		КонецЕсли;	    
	КонецЕсли;
	Возврат Стр;
КонецФункции

//******************************************************************************
// ИнформационнаяСтрока()
//
// Возвращаемое значение:
//  Строка - сведения о незачтенном НДС.
//
// Вызывается из формул элементов диалога:
//  Текст "ИнформационнаяСтрока()".
//
// Описание:
//  Формирует строку с датой расчета бухгалтерских итогов и конечным дебетовым
// сальдо на счете 76.
//  
Функция ИнформационнаяСтрока()
	
	Стр = "";
	
	Если КонМесяца(ДатаБИ) > КонецРассчитанногоПериодаБИ() Тогда
		Стр = "На " + Формат(ДатаБИ, "Д (0)ДДММГГГГ") + " бухгалтерские итоги не рассчитаны!"+РазделительСтрок+
				"Расчет итогов выполняется в режиме ""Операции - Управление бухгалтерскими итогами"".";

	Иначе
		Если ПустоеЗначение(ТекущийДокумент) = 0 Тогда
			СтрокаСчета = Субсчет76();
			Если СтрокаСчета = Сч76_Н_1 Тогда
				СтрокаСчета = " по кредиту счета " + СтрокаСчета;
			ИначеЕсли СтрокаСчета = Сч76_АВ Тогда
				СтрокаСчета = " по дебету счета " + СтрокаСчета;
			Иначе
				СтрокаСчета = "";
			КонецЕсли;
			Стр = "Сальдо на конец дня " + Формат(КонМесяца(ДатаБИ), "Д") + СтрокаСчета + " : " + Сальдо76();
		КонецЕсли;
	КонецЕсли;

	Возврат Стр;
	
КонецФункции // ИнформационнаяСтрока()

//******************************************************************************
// ВвестиДокумент()
//
// Вызывается из формул элементов диалога:
//  Кнопка "Новый документ".
//
// Описание:
//  Создает новый выданный счет-фактуру.
//
Процедура ВвестиДокумент()

	Перем Отбр;
	ПолучитьОтбор("Контрагент", Отбр);
	Парам = СоздатьОбъект("СписокЗначений");
	Парам.Установить("Контрагент", Отбр);
	Парам.Установить("КонтекстЖурнала", Контекст);
	Журнал = Метаданные.Журнал("СчетаФактурыВыданные");
	Если Журнал.Графа(1).Выбран() <> 0 Тогда
		Если Журнал.Графа(1).Ссылки.Количество() = 1 Тогда
			ОткрытьФорму(Журнал.Графа(1).Ссылки.Получить(1).Родитель().ПолныйИдентификатор(), Парам);
		ИначеЕсли Журнал.Графа(1).Ссылки.Количество() > 1 Тогда
			Док = СоздатьОбъект("СписокЗначений");
			Если ПустоеЗначение(ТекущийДокумент) = 0 Тогда
			    ВидДокумента = ТекущийДокумент.Вид();
			Иначе
				ВидДокумента = "";
			КонецЕсли;
			Для а = 1 по Журнал.Графа(1).Ссылки.Количество() Цикл
				Представление = Журнал.Графа(1).Ссылки.Получить(а).Родитель().Синоним +
							"(" + Журнал.Графа(1).Ссылки.Получить(а).Родитель().Комментарий + ")";
				Док.ДобавитьЗначение(Журнал.Графа(1).Ссылки.Получить(а).Родитель().ПолныйИдентификатор(), Представление);
			КонецЦикла;
			Если Док.ВыбратьЗначение(ВидДокумента, "Выбор вида документа") = 1 Тогда
				ОткрытьФорму(ВидДокумента, Парам);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВвестиДокумент()

//******************************************************************************
// ПриВыбореПоказаОстатковВКолонке()
//
// Вызывается из формул элементов диалога:
//  Флажок "Показывать остатки в колонке".
//
// Описание:
//  Управляет видимостью колонки остатка.
// 
Процедура ПриВыбореПоказаОстатковВКолонке()
	
	Если ФлагОстаткиВКолонке = 0 Тогда
		Форма.КолонкаОстатка.Видимость(0);
	Иначе
		Форма.КолонкаОстатка.Видимость(1);
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореПоказаОстатковВКолонке()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()

	ДоговорСПокупателем = "";
	Если ПустоеЗначение(Форма.Параметр) = 0 Тогда
		Если ТипЗначенияСтр(Форма.Параметр) = "Справочник" Тогда
			Если Форма.Параметр.Вид() = "Контрагенты" Тогда
				Контрагент_Покупатель = Форма.Параметр;
			КонецЕсли;
			
		ИначеЕсли ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений" Тогда
			Контрагент_Покупатель = Форма.Параметр.Получить("Контрагент");
			ДатаБИ = Форма.Параметр.Получить("ДатаБИ");
		КонецЕсли;
	КонецЕсли;
	
	Если ПустоеЗначение(Контрагент_Покупатель) = 1 Тогда
	    Спр = СоздатьОбъект("Справочник.Контрагенты");
		Если Спр.Выбрать("Укажите покупателя",) = 1 Тогда
		    Контрагент_Покупатель = Спр.ТекущийЭлемент();
		Иначе
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Если Форма.МодальныйРежим() = 1 Тогда
		Форма.ОбработкаВыбораСтроки(1);
	КонецЕсли;
	УстановитьОтбор("Покупатель", Контрагент_Покупатель);
	ВидыОтбора("");
	
	Если ПустоеЗначение(ДатаБИ) = 1 Тогда
		ДатаБИ = РабочаяДата();
	КонецЕсли;
	ФлагОстаткиВКолонке = ВосстановитьЗначение("СчетаФактурыВыданные_ФлагОстаткиВКолонке");
    БухИт = СоздатьОбъект("БухгалтерскиеИтоги");
	БухИт.ПериодМ(ДатаБИ);
	Сч76_АВ = СчетПоКоду("76.АВ");
	Сч76_Н_1 = СчетПоКоду("76.Н.1");
	
	ПриВыбореПоказаОстатковВКолонке();
	
	глСписокОбщихЖурналов.Пометка(глСписокОбщихЖурналов.НайтиЗначение("СчетаФактурыВыданные"), 1);

КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВыбореСтроки()
    
    Форма.Параметр = ТекущийДокумент;
    
КонецПроцедуры // ПриВыбореСтроки()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	СохранитьЗначение("СчетаФактурыВыданные_ФлагОстаткиВКолонке", ФлагОстаткиВКолонке);
	глСписокОбщихЖурналов.Пометка(глСписокОбщихЖурналов.НайтиЗначение("СчетаФактурыВыданные"), 0);
	
КонецПроцедуры // ПриЗакрытии()