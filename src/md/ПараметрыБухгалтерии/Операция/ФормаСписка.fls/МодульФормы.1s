//_____________________________________________________________________________
Процедура ВвестиДокумент()
	ВидыДокументов = СоздатьОбъект("СписокЗначений");
	Для Индекс = 1 По Метаданные.Документ() Цикл
		Если (Метаданные.Документ(Индекс).РазрешитьПроведение = 0)
			и (Константа.ВсеДокументыВЖурналОпераций = Нет) Тогда
			Продолжить;
		КонецЕсли;
		
		Значение = Метаданные.Документ(Индекс).Идентификатор;
		Представление = Метаданные.Документ(Индекс).Представление();
		КомментарийДокумента = Метаданные.Документ(Индекс).Комментарий;
		Если ПустаяСтрока(КомментарийДокумента) = 0 Тогда
			Представление = Представление + " (" + КомментарийДокумента +")";
		КонецЕсли;
		ВидыДокументов.ДобавитьЗначение(Значение, Представление);
	КонецЦикла;
	
	ВидДок = "";
	Если ВидыДокументов.ВыбратьЗначение(ВидДок, "Выбор вида документа",,,) = 1 Тогда
		ОткрытьФорму("Документ."+ВидДок);
	КонецЕсли;
КонецПроцедуры //ВвестиДокумент
//_____________________________________________________________________________
Процедура ВвестиОперацию(Параметр)
	ОткрытьФорму("Операция",,Параметр);
КонецПроцедуры //ВвестиТиповуюОперацию
//_____________________________________________________________________________
Процедура ПоследниеТиповыеОперации()
	СписокТиповыхОпераций = СоздатьОбъект("СписокЗначений");
	ВосстановитьЗначение("СписокТиповыхОпераций", СписокТиповыхОпераций);
	Если СписокТиповыхОпераций.РазмерСписка() > 0 Тогда
		ТиповаяОперация = "";
		Позиция = "";
		Если СписокТиповыхОпераций.ВыбратьЗначение(ТиповаяОперация,, Позиция,, 1) = 1 Тогда
		    СписокТиповыхОпераций.УдалитьЗначение(Позиция);
			СписокТиповыхОпераций.ВставитьЗначение(1, ТиповаяОперация);
			СохранитьЗначение("СписокТиповыхОпераций", СписокТиповыхОпераций);
			ОткрытьФорму("Операция",, ТиповаяОперация);
		КонецЕсли;
	Иначе
		ОткрытьФорму("Операция",, "Имя_несуществующей_типовой_операции");
	КонецЕсли;
КонецПроцедуры //ПоследниеТиповыеОперации
//_____________________________________________________________________________
Процедура Сторно()
	Если ПустоеЗначение(Документ) = 0 Тогда
		ОткрытьФорму("Документ.Сторно",, Документ);
	Иначе
		Предупреждение("Сторнируемый документ или ручная операция не выбраны.");
	КонецЕсли;
КонецПроцедуры //Сторно

//_____________________________________________________________________________
Функция ДатаИзВарианта(Вариант, КонецИнтервала)
	ДатаГраницы = "";
	
	Если ТипЗначенияСтр(Вариант) = "Дата" Тогда
	    ДатаГраницы = Вариант;
	
	Иначе //Если ТипЗначенияСтр(Вариант) = "Строка" Тогда
		Если Вариант = "Месяц" Тогда
			Если КонецИнтервала = 0 Тогда //начало интервала
				ДатаГраницы = НачМесяца(РабочаяДата());
			Иначе //Если КонецИнтервала = 1 Тогда //конец интервала
				ДатаГраницы = КонМесяца(РабочаяДата());
			КонецЕсли;

		ИначеЕсли Вариант = "Квартал" Тогда
			Если КонецИнтервала = 0 Тогда //начало интервала
				ДатаГраницы = НачКвартала(РабочаяДата());
			Иначе //Если КонецИнтервала = 1 Тогда //конец интервала
				ДатаГраницы = КонКвартала(РабочаяДата());
			КонецЕсли;

		ИначеЕсли Вариант = "Год" Тогда
			Если КонецИнтервала = 0 Тогда //начало интервала
				ДатаГраницы = НачГода(РабочаяДата());
			Иначе //Если КонецИнтервала = 1 Тогда //конец интервала
				ДатаГраницы = КонГода(РабочаяДата());
			КонецЕсли;
		
		Иначе //Если Вариант = "День" Тогда
			ДатаГраницы = РабочаяДата();
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДатаГраницы;
КонецФункции //ДатаИзВарианта

//_____________________________________________________________________________
Процедура ПриУстановкеИнтервала(ДатаНач, ДатаКон)
	Если (ДатаНач < ДатаИзВарианта(НачалоСтандартногоИнтервала(), 0))
		или (ДатаКон > ДатаИзВарианта(КонецСтандартногоИнтервала(), 1)) Тогда
	    Предупреждение("Указанный интервал выходит за границы, установленные в параметрах
						|системы для журналов документов, операций и проводок. Он не сохранится
						|после закрытия журнала.
						|Установка интервала для всех журналов выполняется из меню Сервис
						|в настройке параметров системы (Параметры) на закладке Журналы.");
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Форма.Кн_Видеокурс.Видимость( ?(ТипЗначения(Видео_Компонента)=0,0,1 ));
	ТекОпер = ВосстановитьЗначение("ТекОпер");
	АктивизироватьОбъект(ТекОпер);
	глСписокОбщихЖурналов.Пометка(глСписокОбщихЖурналов.НайтиЗначение("ЖурналОпераций"), 1);
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриПовторномОткрытии()
	
	Если ТипЗначенияСтр(Форма.Параметр) = "Документ" Тогда
		глАктивизироватьДокумент(Контекст, Форма.Параметр);
	КонецЕсли;

КонецПроцедуры // ПриПовторномОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	СохранитьЗначение("ТекОпер", Документ.ТекущийДокумент());
	глСписокОбщихЖурналов.Пометка(глСписокОбщихЖурналов.НайтиЗначение("ЖурналОпераций"), 0);
	
КонецПроцедуры // ПриЗакрытии()

Процедура Печать()
	Перем ИмяОтбора;
	Перем ЗначениеОтбора;
	Перем Док;
	Перем ВклПроводки;
	Док = 0;

	Меню=СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение(0,"Не выводить проводки");
	Меню.ДобавитьЗначение(1,"Выводить проводки");
	Если Меню.ВыбратьЗначение(ВклПроводки,"",,, 1) = 0 Тогда
		Возврат;
	КонецЕсли;

	ПолучитьОтбор(ИмяОтбора,ЗначениеОтбора);
	Дата1=НачалоИнтервала();
	Дата2=КонецИнтервала();
	Опер=СоздатьОбъект("Операция");
	Если Строка(ИмяОтбора)="" Тогда
		Опер.ВыбратьОперации(Дата1,Дата2);
	ИначеЕсли (ИмяОтбора="Содержание") ИЛИ (ИмяОтбора="СуммаОперации") Тогда
		Опер.ВыбратьПоЗначению(Дата1,Дата2,ИмяОтбора,ЗначениеОтбора);
	ИначеЕсли (ИмяОтбора="Контрагент") или (ИмяОтбора="Покупатель")
	      или (ИмяОтбора="Продавец")   или (ИмяОтбора="Договор") Тогда
		Док=СоздатьОбъект("Документ");
		Док.ВыбратьПоЗначению(Дата1,Дата2,ИмяОтбора,ЗначениеОтбора);
	Иначе
		Док=СоздатьОбъект("Документ."+ИмяОтбора);
		Док.ВыбратьДокументы(Дата1,Дата2);
	КонецЕсли;
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("ЖурналОпераций");
	Таб.ВывестиСекцию("Отчет");
	Таб.ВывестиСекцию(?(ВклПроводки=1,"ШапкаП","Шапка"));
	Таб.Опции(0,0,Таб.ВысотаТаблицы(),0);
	НПП=0;
	Если ТипЗначенияСтр(Док) <> "Документ" Тогда
		Пока Опер.ПолучитьОперацию()=1 Цикл
			Состояние("Вывод "+Опер.ДатаОперации);
			НПП=НПП+1;
			Таб.ВывестиСекцию("Операция");
			Если ВклПроводки=1 Тогда
				Опер.ВыбратьПроводки();
				Пока Опер.ПолучитьПроводку()=1 Цикл
					Таб.ВывестиСекцию("Проводка");
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Пока Док.ПолучитьДокумент()=1 Цикл
			Если Док.СуществуетОперация()=1 Тогда
				Опер=Док.Операция;
				Состояние("Вывод "+Опер.ДатаОперации);
				НПП=НПП+1;
				Таб.ВывестиСекцию("Операция");
				Если ВклПроводки=1 Тогда
					Опер.ВыбратьПроводки();
					Пока Опер.ПолучитьПроводку()=1 Цикл
						Таб.ВывестиСекцию("Проводка");
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Таб.ВывестиСекцию("КонецОтчета");
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Журнал операций","");
КонецПроцедуры
