//_____________________________________________________________________________
Процедура ОткрытьФормуСубконто(ВидСубк)
	ИдентификаторСубк = ВидСубк.Идентификатор();
	ТипСубк = Метаданные.ВидСубконто(ИдентификаторСубк).Тип;
	Если ТипСубк = "Справочник" Тогда
		ОткрытьФорму(ВидСубк.ТипСубконто());
		
	ИначеЕсли ТипСубк = "Перечисление" Тогда
		ВидПеречисления = Метаданные.ВидСубконто(ИдентификаторСубк).Вид;
		СписокЗначенийПеречисления = СоздатьОбъект("ТаблицаЗначений");
		СписокЗначенийПеречисления.НоваяКолонка("Значение",,,,"Значения субконто "+Строка(ВидСубк));
		Для i=1 по Перечисление.ПолучитьАтрибут(ВидПеречисления).КоличествоЗначений() Цикл
			СписокЗначенийПеречисления.НоваяСтрока();
			СписокЗначенийПеречисления.Значение = Перечисление.ПолучитьАтрибут(ВидПеречисления).ЗначениеПоНомеру(i);
		КонецЦикла;
		ОткрытьФорму("Обработка.ВыводТаблицыЗначений#"+"Значения субконто "+Строка(ВидСубк),СписокЗначенийПеречисления);
		СписокЗначенийПеречисления.Форма.Заголовок("Просмотр значений субконто "+Строка(ВидСубк), 0);
	
	ИначеЕсли ТипСубк = "Документ" Тогда
		ОткрытьФорму("Журнал."+ИдентификаторСубк);
	КонецЕсли;
КонецПроцедуры //ОткрытьФормуСубконто
//_____________________________________________________________________________
Процедура ПоказатьСубконто()
	Если ТекущийСчет().КоличествоСубконто() = 0 Тогда
		Предупреждение("Счет "+ТекущийСчет().Код+" не имеет субконто.");
		Возврат;
	КонецЕсли;
	
	Если ТекущийСчет().КоличествоСубконто() = 1 Тогда
		ОткрытьФормуСубконто(ТекущийСчет().ВидСубконто(1));
	Иначе
		Меню = СоздатьОбъект("СписокЗначений");
		Для i=1 по ТекущийСчет().КоличествоСубконто() Цикл
			Меню.ДобавитьЗначение(ТекущийСчет().ВидСубконто(i));
		КонецЦикла;
		ВидСубк = "";
		Если Меню.ВыбратьЗначение(ВидСубк,,,,1) = 1 Тогда
			ОткрытьФормуСубконто(ВидСубк);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры //ПоказатьСубконто
//_____________________________________________________________________________
Процедура Подробно(Сч)
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Описание");
	ИмяСекции = СтрЗаменить(Сч.Код, ".", "_");
	Таб.ВывестиСекцию("Отступ|Счет");		
	Таб.ПрисоединитьСекцию("Подробно|Счет");		
	Попытка 
		Таб.ВывестиСекцию("Отступ|"+ИмяСекции);		
		Таб.ПрисоединитьСекцию("Подробно|"+ИмяСекции);		
	Исключение
		Таб.ВывестиСекцию("Отступ|НетОписания");		
		Таб.ПрисоединитьСекцию("Подробно|НетОписания");		
	КонецПопытки; 
	Если Сч.Уровень() > 1 Тогда
		Таб.ВывестиСекцию("Отступ|Ссылка");		
		Таб.ПрисоединитьСекцию("Подробно|Ссылка");		
	КонецЕсли;
	
	Таб.Опции(0, 0, 0, 0, "ОпцииПечатиИнформацииО_Счете", "ОкноИнформацииО_Счете");
	Таб.ОбластьПечати(1, 2);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Описание счета " + Сч.Код);
КонецПроцедуры //Подробно
//_____________________________________________________________________________
Процедура ОбработкаЯчейкиТаблицы(Значение, ФлагСтандОбраб, Таблица, Адрес)
	Подробно(СчетПоКоду(Значение));
	ФлагСтандОбраб = 0;
КонецПроцедуры
//_____________________________________________________________________________
Процедура Печать(Стиль=0)
	Сч=СоздатьОбъект("Счет");
	Сч.ИспользоватьПланСчетов(ИспользоватьПланСчетов());
	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Описание");
	Таб.ВывестиСекцию("Отчет");
	Сч.ВыбратьСчета();
	Счетчик = 1;
	Пока Сч.ПолучитьСчет()=1 Цикл
		Если Сч.ЭтоГруппа()=1 Тогда
			Таб.ВывестиСекцию("Группа");
		Иначе
			Таб.ВывестиСекцию("Строка");
		КонецЕсли;
		Если Стиль = 1 Тогда
			ИмяСекции = СтрЗаменить(Сч.Код, ".", "_");
			Попытка
				Высота = Таб.ВысотаТаблицы();
				Таб.ВывестиСекцию(ИмяСекции);
				Таб.ВывестиСекцию("Пробел");
				Таб.Область(Высота, 2, Таб.ВысотаТаблицы(), 2).РамкаОбвести(2,2,2,2);
				Таб.Область(Высота, 3, Таб.ВысотаТаблицы(), 10).РамкаОбвести(2,2,2,2);
			Исключение
			КонецПопытки; 
		КонецЕсли;
		Состояние("Обработано "+ Счетчик + " счетов");
		Счетчик = Счетчик + 1;
	КонецЦикла;
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0,0,2,0);
	Таб.Показать("План счетов");
КонецПроцедуры //Сформировать
//_____________________________________________________________________________
Процедура Сформировать()
	Перем Стиль;
	Меню = СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение(0, "Простой список");
	Меню.ДобавитьЗначение(1, "С подробными описаниями");
	Если Меню.ВыбратьЗначение(Стиль,,,,1) = 1 Тогда
		Печать(Стиль);
	КонецЕсли;
КонецПроцедуры
//_____________________________________________________________________________
Процедура ПостроитьОтчет()
	Меню = СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("КарточкаСчета","Карточка счета "+ТекущийСчет().Код);
	Меню.ДобавитьЗначение("АнализСчета","Анализ счета "+ТекущийСчет().Код);
	Меню.ДобавитьЗначение("АнализСчетаПоДатам","Анализ счета "+ТекущийСчет().Код+" по датам");
	Меню.ДобавитьЗначение("АнализСчетаПоСубконто","Анализ счета "+ТекущийСчет().Код+" по субконто");
	Меню.ДобавитьЗначение("ОборотноСальдоваяВедомостьПоСчету","Оборотно сальдовая ведомость по счету "+ТекущийСчет().Код);
	Меню.ДобавитьЗначение("ЖурналОрдер","Журнал-ордер (ведомость) по счету "+ТекущийСчет().Код);
	Меню.ДобавитьЗначение("ГлавнаяКнига","Обороты счета "+ТекущийСчет().Код+" (Главная книга)");

	ВидОтчета = "";
	Если Меню.ВыбратьЗначение(ВидОтчета,,,,1)=1 Тогда
		Расшифровка = СоздатьОбъект("СписокЗначений");
		Расшифровка.Установить("Отчет", ВидОтчета);
		Расшифровка.Установить("РазделительУчета", БухИтоги.ИспользоватьРазделительУчета());
		Расшифровка.Установить("Счет", ТекущийСчет());
		Расшифровка.Установить("Дата1", НачалоПериодаБИ());
		Расшифровка.Установить("Дата2", КонецПериодаБИ());

		Если ВидОтчета = "КарточкаСчета" Тогда
			Для i=1 по ТекущийСчет().КоличествоСубконто() Цикл
				Расшифровка.Установить("ВидСубконто"+i, ТекущийСчет().ВидСубконто(i));
			КонецЦикла;

		ИначеЕсли ВидОтчета = "АнализСчета" Тогда
			Расшифровка.Установить("ДанныеПоСубсчетам", ТекущийСчет().ЭтоГруппа());
			Расшифровка.Установить("ДанныеПоВалютам", ТекущийСчет().Валютный);

		ИначеЕсли ВидОтчета = "АнализСчетаПоДатам" Тогда
			Расшифровка.Установить("ДанныеПоСубсчетам", ТекущийСчет().ЭтоГруппа());
			Расшифровка.Установить("Валюта", "");
			Расшифровка.Установить("ПоВалюте", 0);

		ИначеЕсли ВидОтчета = "АнализСчетаПоСубконто" Тогда
			Расшифровка.Установить("ДанныеПоСубсчетам", ТекущийСчет().ЭтоГруппа());
			Для i=1 по ТекущийСчет().КоличествоСубконто() Цикл
				Расшифровка.Установить("ВидСубконто"+i, ТекущийСчет().ВидСубконто(i));
				Расшифровка.Установить("ОтборСубконто"+i, 1);
				Расшифровка.Установить("ПоГруппам"+i, 1);
			КонецЦикла;
			Расшифровка.Установить("Валюта", "");
			Расшифровка.Установить("ПоВалюте", 0);

		ИначеЕсли ВидОтчета = "ОборотноСальдоваяВедомостьПоСчету" Тогда
			Для i=1 по ТекущийСчет().КоличествоСубконто() Цикл
				Расшифровка.Установить("ВидСубконто"+i, ТекущийСчет().ВидСубконто(i));
				Расшифровка.Установить("ОтборСубконто"+i, 1);
				Расшифровка.Установить("ПоГруппам"+i, 1);
			КонецЦикла;

		ИначеЕсли ВидОтчета = "ЖурналОрдер" Тогда
			Расшифровка.Установить("ДанныеПоСубсчетам", ТекущийСчет().ЭтоГруппа());
			Расшифровка.Установить("Валюта", "");
			Расшифровка.Установить("ПоВалюте", 0);
			Расшифровка.Установить("ВидПериода", 2);
			Расшифровка.Установить("ПериодыСНулевымиОборотами", 0);
			Расшифровка.Установить("ОстНачД", 1);
			Расшифровка.Установить("ОстНачК", 1);
			Расшифровка.Установить("ОбД", 1);
			Расшифровка.Установить("ОбК", 1);
			Расшифровка.Установить("КорОбД", 1);
			Расшифровка.Установить("КорОбК", 1);
			Расшифровка.Установить("ОстКонД", 1);
			Расшифровка.Установить("ОстКонК", 1);

		ИначеЕсли ВидОтчета = "ГлавнаяКнига" Тогда
			Расшифровка.Установить("ДанныеПоСубсчетам", ТекущийСчет().ЭтоГруппа());
			Расшифровка.Установить("Валюта", "");
			Расшифровка.Установить("ПоВалюте", 0);
			Расшифровка.Установить("ОстНачД", 1);
			Расшифровка.Установить("ОстНачК", 1);
			Расшифровка.Установить("ОбД", 1);
			Расшифровка.Установить("ОбК", 1);
			Расшифровка.Установить("КорОбД", 1);
			Расшифровка.Установить("КорОбК", 1);
			Расшифровка.Установить("ОстКонД", 1);
			Расшифровка.Установить("ОстКонК", 1);
		КонецЕсли;

		глРасшифровка = Расшифровка;
		глФлагРасшифровки = 1;
		глОбновить = 0;
		ОткрытьФорму("Отчет."+ВидОтчета);
		глФлагРасшифровки = 0;
	КонецЕсли;
КонецПроцедуры //ПостроитьОтчет
//_____________________________________________________________________________
Процедура ПриОткрытии()
	Форма.Кн_Справка.Видимость( ?(ТипЗначения(Норм_Компонента)=0,0,1 ));
	Форма.Кн_Видеокурс.Видимость( ?(ТипЗначения(Видео_Компонента)=0,0,1 ));
КонецПроцедуры //ПриОткрытии
//_____________________________________________________________________________