Перем НачальнаяДатаДокумента;
Перем Новый;
Перем СписокДействий;

//******************************************************************************
// ПриВыбореВидаОбъектаУчета()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  СписокВидовОбъекта.
//
// Описание:
//  Обрабатывается ситуция перевыбора вида объекта учета (НМА или НИОКР).
//
Процедура ПриВыбореВидаОбъектаУчета()
	
	Если СписокВидовОбъекта.НайтиЗначение(ВидОбъектаУчета) <> СписокВидовОбъекта.ТекущаяСтрока() Тогда
	    ВидОбъектаУчета = СписокВидовОбъекта.ПолучитьЗначение(СписокВидовОбъекта.ТекущаяСтрока());
		
		// Если вид выбранного в документе объекта (НМА или НИОКР)
		// не соответствует утановленному, то очистим в документе
		// поле в котором указывается объект.
		Если НМА.Выбран() = 1 Тогда
		    Если НМА.ВидОбъектаУчета <> ВидОбъектаУчета Тогда
		        НМА = 0;
		    КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПриВыбореВидаОбъектаУчета()

//******************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;

КонецПроцедуры // ВводНового()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии() //предопределенная
	
	ПриЗаписиПерепроводить(1);
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.КнопкаОК.Доступность(0);
	КонецЕсли;

	НачальнаяДатаДокумента = ДатаДок;
	
	СписокВидовОбъекта.ДобавитьЗначение(0, "Нематериальный актив");
	СписокВидовОбъекта.ДобавитьЗначение(1, "Расходы на НИОКР");
	СписокВидовОбъекта.ТекущаяСтрока(СписокВидовОбъекта.НайтиЗначение(ВидОбъектаУчета));
	
КонецПроцедуры //ПриОткрытии

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, ФлагСтандОбр)
	Если ИдентЭлемДиалога = "СтатьяПрочихДоходовИРасходов" Тогда
		Если ВидОбъектаУчета = 1 Тогда
			ОткрытьФорму("Справочник.ПрочиеДоходыИРасходы", Перечисление.ВидыПрочихДоходовИРасходов.ПрочиеВнереализационныеДоходыРасходы);
			ФлагСтандОбр = 0;
			
		Иначе
			ОткрытьФорму("Справочник.ПрочиеДоходыИРасходы", Перечисление.ВидыПрочихДоходовИРасходов.ВыбытиеНМА);
			ФлагСтандОбр = 0;
		КонецЕсли;
		
	ИначеЕсли ИдентЭлемДиалога = "НМА" Тогда
		Если ВидОбъектаУчета = 1 Тогда
			ОткрытьФорму("Справочник.НематериальныеАктивы.ДляВыбора", "НИОКР");
			ФлагСтандОбр = 0;
			
		Иначе
			ОткрытьФорму("Справочник.НематериальныеАктивы.ДляВыбора", "НМА");
			ФлагСтандОбр = 0;
		КонецЕсли;
	    
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи() //предопределенная
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	Если НМА.Выбран() = 0 Тогда
		Если Вопрос("Не выбран объект списания.
					|Вернуться к редактированию документа?", "Да+Нет") = "Да" Тогда
			Активизировать("НМА");
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Операция.Содержание = "Списание НМА: "+НМА;
КонецПроцедуры //ПриЗаписи

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//*****************************************************************************
Новый = 0;

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Ввести на основании");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");
