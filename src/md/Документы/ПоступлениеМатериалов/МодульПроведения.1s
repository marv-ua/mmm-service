////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем СчетРасчетовСПоставщиком;
Перем СчетАвансовВыданных;
Перем ОплатаДоговора;
Перем ЦеныВДоговоре;
Перем ВестиУчетРасчетовУЕ;
Перем Валюта;
Перем Кратность;
Перем ОбъектыАналитикиНУ;
Перем АвансПоДоговору;
Перем АвансБезДоговора;
Перем АвансПоДоговоруРуб;
Перем АвансБезДоговораРуб;
Перем БезДоговора; 
Перем ЗадолженностьПоРасчетамВУЕРуб, ЗадолженностьПоРасчетамВУЕВал;
Перем НПРКурсоваяРазница;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//******************************************************************************
// РассчитатьСуммуАванса()
//
Процедура РассчитатьСуммуАванса()
	
	АвансБезДоговора = 0;
	АвансБезДоговораРуб = 0;
	АвансПоДоговору = 0;
	АвансПоДоговоруРуб = 0;
	
	Если ЗачитыватьАванс <> 1 Тогда //Зачитывать аванс
		СписокДоговоров = СоздатьОбъект("СписокЗначений");
		СписокДоговоров.ДобавитьЗначение(Договор);
		БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
		Если (ЗачитыватьАванс = 0) и (ПустоеЗначение(БезДоговора) = 0) Тогда //Зачитывать аванс без договора
			СписокДоговоров.ДобавитьЗначение(БезДоговора);
		КонецЕсли;                                  
		
		БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Контрагенты, Контрагент, 2);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Договоры, СписокДоговоров, 2);
		Если ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда	
				БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"60.22",,Валюта,,,"СВ");
			Иначе
				БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"60.22",,Валюта,,,"В");
			КонецЕсли;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
		    БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"60.7",,Валюта,,,"СВ");
			
		Иначе
			БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"60.2",,,,,"С");
		КонецЕсли;

		Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, БезДоговора) = 1 Тогда
			Если ОплатаДоговора = 2 Тогда
			    АвансБезДоговора = БухИт.СКД("В");
				Если ДатаДок >= '01.01.2008' Тогда	
					АвансБезДоговораРуб = БухИт.СКД("С");
				КонецЕсли;
				
			ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
				АвансБезДоговора = БухИт.СКД("В");
				АвансБезДоговораРуб = БухИт.СКД("С");
				
			Иначе
				АвансБезДоговора = БухИт.СКД("С");
			КонецЕсли;
		КонецЕсли;
		
		Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, Договор) = 1 Тогда
			Если ОплатаДоговора = 2 Тогда
			    АвансПоДоговору = БухИт.СКД("В"); 
				Если ДатаДок >= '01.01.2008' Тогда	
					АвансПоДоговоруРуб = БухИт.СКД("С");
				КонецЕсли;
				
			ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
				АвансПоДоговору = БухИт.СКД("В");
				АвансПоДоговоруРуб = БухИт.СКД("С");
				
			Иначе
				АвансПоДоговору = БухИт.СКД("С");
			КонецЕсли;
		КонецЕсли;
		
		АвансБезДоговора = Макс(АвансБезДоговора, 0);
		АвансБезДоговораРуб = Макс(АвансБезДоговораРуб, 0);
		АвансПоДоговору = Макс(АвансПоДоговору, 0);
		АвансПоДоговоруРуб = Макс(АвансПоДоговоруРуб, 0);
	КонецЕсли;
	
КонецПроцедуры // РассчитатьСуммуАванса()

//*****************************************************************************
// ЗачестьАванс(ЗачетАванса, ДоговорАванса)
//
Процедура ЗачестьАванс(ЗачетАванса, ЗачетАвансаРуб, ДоговорАванса)
	
	Если ЗачетАванса > 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "МТ";
		Операция.СодержаниеПроводки = "Зачтен аванс";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = СчетРасчетовСПоставщиком;
		Операция.Дебет.Контрагенты = Контрагент;
		Операция.Дебет.Договоры = Договор;
		Операция.Кредит.Счет = СчетАвансовВыданных;
		Операция.Кредит.Контрагенты = Контрагент;
		Операция.Кредит.Договоры = ДоговорАванса;
        
		Если ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Если (АвансПоДоговору + АвансБезДоговора) = ЗачетАванса Тогда
					ЗачетАвансаРуб = АвансПоДоговоруРуб + АвансБезДоговораРуб;
				Иначе
					КурсЗачетаАванса = Окр((АвансПоДоговоруРуб + АвансБезДоговораРуб)/(АвансПоДоговору + АвансБезДоговора), 4, 1);
					ЗачетАвансаРуб = ЗачетАванса*КурсЗачетаАванса;
				КонецЕсли;
			Иначе
				ЗачетАвансаРуб = ЗачетАванса*Курс/Кратность;
			КонецЕсли;
			Операция.Сумма = ЗачетАвансаРуб;
			Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			Операция.Сумма = ЗачетАвансаРуб;
			Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса; 
			
			ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб - Операция.Сумма;
			ЗадолженностьПоРасчетамВУЕВал = ЗадолженностьПоРасчетамВУЕВал - Операция.ВалСумма;
			
		Иначе
			Операция.Сумма = ЗачетАванса;
		КонецЕсли;
		
		Если ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ВЛ";
				Операция.СодержаниеПроводки = "Зачтена предоплата";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Кредит.Счет = СчетПоКоду("ВАЛ.60");
				Операция.Кредит.Контрагенты = Контрагент;
				Операция.Кредит.Договоры = ДоговорАванса;
				Операция.Сумма = ЗачетАванса*Курс/Кратность;
				Операция.Валюта = Валюта;
				Операция.ВалСумма = ЗачетАванса;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ЗачестьАванс()

//******************************************************************************
// СформироватьПроводкиДляЦелейНалоговогоУчета()
//
Процедура СформироватьПроводкиДляЦелейНалоговогоУчета(Знач СтоимостьМатериалов = 0, БИ_НУ = "", ПереченьСтатейЗатрат)
	
	Если ВидПоступления = 0 Тогда // поступление материалов
		Если (СтоимостьМатериалов > 0) или (Количество <> 0) Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		    Операция.НомерЖурнала = "НУ";
			Операция.СодержаниеПроводки = "Поступили материалы";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетПоКоду("Н02.01");
			Операция.Дебет.Материалы = Материал;
			Операция.Дебет.Основание = Договор;
			Операция.Дебет.УсловияПоступленияИВыбытия = Перечисление.УсловияПоступленияИВыбытия.ЗаПлату;
			Операция.Сумма = СтоимостьМатериалов;
			Операция.Количество = Количество;
		КонецЕсли;
		
	ИначеЕсли ВидПоступления = 1 Тогда // возврат из переработки
		
		// Отражение поступления материалов из переработки для целей налогового учета.
		Если (СчетЗатрат.ПринадлежитГруппе(СчетПоКоду("10")) = 1) 
		   и (Субконто1 = Материал) и (Константа.РаздельныйУчетМатериаловСкладПереработка.Получить(ДатаДок)=Нет) Тогда // возврат на склад (не отражается в налоговом учете)
		Иначе
		    // Проверим хватает ли на складе товара по данным налогового учета.
			СуммаНаСкладахНУ = 0;
	        КоличествоНаСкладахНУ = 0;
			Если БИ_НУ.ПолучитьСубконто(ВидыСубконто.Материалы,,Материал) = 1 Тогда
				Если Константа.РаздельныйУчетМатериаловСкладПереработка.Получить(ДатаДок)=Да Тогда
					Если БИ_НУ.ПолучитьСубконто(ВидыСубконто.Основание,,Договор) = 1 Тогда
						СуммаНаСкладахНУ = Макс(БИ_НУ.СКД("С"), 0);
						КоличествоНаСкладахНУ = БИ_НУ.СКД("К");
					КонецЕсли;
				Иначе
					СуммаНаСкладахНУ = Макс(БИ_НУ.СКД("С"), 0);
					КоличествоНаСкладахНУ = БИ_НУ.СКД("К");
				КонецЕсли;
			КонецЕсли;
			
		   	Если КоличествоНаСкладахНУ < Количество Тогда

		   		Если Константа.РаздельныйУчетМатериаловСкладПереработка.Получить(ДатаДок)=Нет Тогда
		   			ТекстСообщения = "По данным налогового учета на складах "+КоличествоНаСкладахНУ+" "+Материал.ЕдиницаИзмерения+
								  	    " из необходимых "+Количество+" "+Материал.ЕдиницаИзмерения+" материала "+Материал;
				Иначе
					ТекстСообщения = "По данным налогового учета в переработке по договору "+Договор+" "+КоличествоНаСкладахНУ+" "+Материал.ЕдиницаИзмерения+
								  	    " из необходимых "+Количество+" "+Материал.ЕдиницаИзмерения+" материала "+Материал;
				КонецЕсли;

				Если Константа.КонтрольОтрицательныхОстатков = Да Тогда
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
					
				Иначе
					глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), -1);
				КонецЕсли;
			КонецЕсли;
			
			Если Количество >= КоличествоНаСкладахНУ Тогда
				СуммаПередаваемыхМатериаловНУ = СуммаНаСкладахНУ;
			Иначе
				СуммаПередаваемыхМатериаловНУ = Количество*(СуммаНаСкладахНУ/КоличествоНаСкладахНУ);
			КонецЕсли; 
			
			ОбъектыАналитикиНУ = глСчетИАналитикаРасходовНУ(Контекст, СчетЗатрат, Субконто1, Субконто2, Субконто3, ПереченьСтатейЗатрат, Материал.СубСчет10);
			СчетНУ = ОбъектыАналитикиНУ.Получить("Счет");
			
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
	    	Операция.НомерЖурнала = "НУ";
			Операция.СодержаниеПроводки = "Поступ. матер. из переработки";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Если Константа.РаздельныйУчетМатериаловСкладПереработка.Получить(ДатаДок)=Да Тогда
				Операция.Кредит.Счет = СчетПоКоду("Н02.09");
				Операция.Кредит.Субконто(2,Договор);				
			Иначе
				Операция.Кредит.Счет = СчетПоКоду("Н02.01");
			КонецЕсли;
			Операция.Кредит.Материалы = Материал;
			Если ПустоеЗначение(СчетНУ) = 0 Тогда
				Операция.Дебет.Счет = СчетНУ;
				Для СчетчикЦикла = 1 По СчетНУ.КоличествоСубконто() Цикл
					ИдентификаторСубконто = СчетНУ.ВидСубконто(СчетчикЦикла,,).Идентификатор();
					Операция.Дебет.Субконто(СчетчикЦикла, ОбъектыАналитикиНУ.Получить(ИдентификаторСубконто)); 
				КонецЦикла;
			КонецЕсли;
			Операция.Сумма = СуммаПередаваемыхМатериаловНУ;
			Операция.Количество = Количество;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры // СформироватьПроводкиДляЦелейНалоговогоУчета()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()
	
	Сч19_3 = СчетПоКоду("19.3");
	Сч60_1 = СчетПоКоду("60.1");
	Сч60_2 = СчетПоКоду("60.2");
	Сч60_6 = СчетПоКоду("60.6");
	Сч60_7 = СчетПоКоду("60.7");
	Сч60_11 = СчетПоКоду("60.11");
	Сч60_22 = СчетПоКоду("60.22");
	Сч10_7 = СчетПокоду("10.7"); 
	Сч68_2 = СчетПокоду("68.2");
	
	СчУЕ_60 = СчетПоКоду("УЕ.60");
	СчВАЛ_60 = СчетПоКоду("ВАЛ.60");
	СчНПР = СчетПоКоду("НПР.10.1");
	
	ЗадолженностьПоРасчетамВУЕРуб = 0;
	ЗадолженностьПоРасчетамВУЕВал = 0; 
	НПРКурсоваяРазница = 0;
	
	Если ВидПоступления = 1 Тогда
		ПереченьСтатейЗатрат = глПолучитьПереченьСтатейЗатрат(ДатаДок); 
		
	КонецЕсли;
	
	Если ВидПоступления = 0 Тогда
		ЦеныВДоговоре = 1; // в рублях
		ВестиУчетРасчетовУЕ = 0;
		Если Договор.Выбран() = 1 Тогда
		    Если ПустоеЗначение(Договор.ВалютаДоговора) = 0 Тогда
				ЦеныВДоговоре = 2; // в валюте
			КонецЕсли;
			ОплатаДоговора = Договор.ОплатаДоговора; // 1 - в рублях, 2 - в валюте
			ВалютаДоговора = Договор.ВалютаДоговора;
			ВестиУчетРасчетовУЕ = Договор.ВестиУчетРасчетовУЕ;
		КонецЕсли;
		
		Если ЦеныВДоговоре = 2 Тогда
			Валюта = Договор.ВалютаДоговора;
			Кратность = Валюта.Кратность.Получить(ДатаДок);
			Кратность = ?(Кратность=0, 1, Кратность);
			Если ВестиУчетРасчетовУЕ = 1 Тогда
				Кратность = Кратность * 100 / (100 + Договор.ПроцентКорректировкиКурсаУЕ);
			КонецЕсли;
			Если Курс = 0 Тогда
				ТекстСообщения = "Не указан курс валюты.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если ОплатаДоговора = 2 Тогда
			СчетРасчетовСПоставщиком = Сч60_11;
			СчетАвансовВыданных = Сч60_22;
			Если ДатаДок >= '01.01.2008' Тогда
				СчетАвансовВыданныхПереоценка = СчВАЛ_60;
			Иначе
				СчетАвансовВыданныхПереоценка = Сч60_22;
			КонецЕсли;
			
			глТаблицаСчетов.УдалитьСтроки();
	
			глТаблицаСчетов.НоваяСтрока();
			глТаблицаСчетов.Счет = Сч60_11;
			глТаблицаСчетов.Субконто1 = Контрагент;
			глТаблицаСчетов.Субконто2 = Договор;
			глТаблицаСчетов.Субконто3 = "";
			глТаблицаСчетов.Валюта = Валюта;
			глТаблицаСчетов.Курс = Курс;
			
			Если ЗачитыватьАванс = 1 Тогда
			Иначе
				глТаблицаСчетов.НоваяСтрока();
				глТаблицаСчетов.Счет = СчетАвансовВыданныхПереоценка;
				глТаблицаСчетов.Субконто1 = Контрагент;
				глТаблицаСчетов.Субконто2 = Договор;
				глТаблицаСчетов.Субконто3 = "";
				глТаблицаСчетов.Валюта = Валюта;
				глТаблицаСчетов.Курс = Курс;
				Если ЗачитыватьАванс = 0 Тогда
					БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
					Если ПустоеЗначение(БезДоговора) = 0 Тогда
						глТаблицаСчетов.НоваяСтрока();
						глТаблицаСчетов.Счет = СчетАвансовВыданныхПереоценка;
						глТаблицаСчетов.Субконто1 = Контрагент;
						глТаблицаСчетов.Субконто2 = БезДоговора;
						глТаблицаСчетов.Субконто3 = "";
						глТаблицаСчетов.Валюта = Валюта;
						глТаблицаСчетов.Курс = Курс;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
	
			глПереоценкаСчетов(Контекст, глТаблицаСчетов,, 0);
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 0 Тогда
			СчетРасчетовСПоставщиком = Сч60_1;
			СчетАвансовВыданных = Сч60_2;
			
		Иначе
			СчетРасчетовСПоставщиком = Сч60_6;
			СчетАвансовВыданных = Сч60_7;
		КонецЕсли;
	    
		// Заранее расчитаем сумму зачтенного аванса.
		СуммаЗачтенногоАванса = 0;
		СуммаЗачтенногоАвансаРуб = 0;
		КурсАванса = 0;
		ВыделенныйНДС = 0;
		ВыделенныйНДСРуб = 0;
		СуммоваяРазницаДляНДС = 0;
		СуммоваяРазницаДляНП = 0;
		СуммоваяРазницаДляСтоимости = 0;
		КолСтрокДокумента = КоличествоСтрок();
		
		РассчитатьСуммуАванса();
		
		ЗачетАвансаПоДоговору  = 0;
		ЗачетАвансаБезДоговора = 0;
		ЗачетАвансаПоДоговоруРуб  = 0;
		ЗачетАвансаБезДоговораРуб = 0;
		
		СуммаПоступления = Итог("Всего");
		
		Если (ЦеныВДоговоре = 2) и (ВестиУчетРасчетовУЕ = 0) Тогда
			Если ОплатаДоговора = 2 Тогда
			    СуммаВал = Итог("Всего");
			
			Иначе
				СуммаВал = Окр(Итог("Всего") * Курс / Кратность, 2, 1);
			КонецЕсли;
			
		Иначе
			СуммаВал = Итог("Всего");
		КонецЕсли;
		
		Если (ВестиУчетРасчетовУЕ = 1)
		или ((ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008')) Тогда
			ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, СуммаВал);
			
			ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, СуммаВал - ЗачетАвансаПоДоговору);
			
			Аванс = АвансПоДоговору + АвансБезДоговора;
			ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
			
			Если ЗачетАванса = Аванс Тогда
				ЗачетАвансаПоДоговоруРуб = АвансПоДоговоруРуб;
				ЗачетАвансаБезДоговораРуб = АвансБезДоговораРуб;
				
			ИначеЕсли ЗачетАванса > АвансПоДоговору Тогда
				ЗачетАвансаПоДоговоруРуб = АвансПоДоговоруРуб;
				ЗачетАвансаБезДоговораРуб = Окр(ЗачетАвансаБезДоговора * Окр(АвансБезДоговораРуб / ?(АвансБезДоговора = 0, 1, АвансБезДоговора), 4, 1), 2, 1);
				
			Иначе
				ЗачетАвансаПоДоговоруРуб = Окр(ЗачетАвансаПоДоговору * Окр(АвансПоДоговоруРуб / ?(АвансПоДоговору = 0, 1, АвансПоДоговору), 4, 1), 2, 1);
				ЗачетАвансаБезДоговораРуб = 0;
			КонецЕсли;
			
			// Запомним сумму зачет для дальнейшего определения рублевой стоимости товара.
			СуммаЗачтенногоАванса = ЗачетАванса;
			СуммаЗачтенногоАвансаРуб = ЗачетАвансаБезДоговораРуб + ЗачетАвансаПоДоговоруРуб;
			КурсАванса = ?(СуммаЗачтенногоАванса = 0, 0, Окр(СуммаЗачтенногоАвансаРуб / СуммаЗачтенногоАванса, 4, 1));
		
		Иначе
			ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, СуммаВал);
			ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, СуммаВал - ЗачетАвансаПоДоговору);
		КонецЕсли;
		
		РаспределеноАванса = 0;
		РаспределеноАвансаРуб = 0;
		
		ТаблицаОплатыТоваров = СоздатьОбъект("ТаблицаЗначений");
		ТаблицаОплатыТоваров.НоваяКолонка("Товар");
		ТаблицаОплатыТоваров.НоваяКолонка("СтавкаНДС", "Число");
		ТаблицаОплатыТоваров.НоваяКолонка("Сумма", "Число");
		ТаблицаОплатыТоваров.НоваяКолонка("Всего", "Число");
		ТаблицаОплатыТоваров.НоваяКолонка("НДС", "Число");
		ТаблицаОплатыТоваров.НоваяКолонка("СуммаРуб", "Число");
		ТаблицаОплатыТоваров.НоваяКолонка("ВсегоРуб", "Число");
		ТаблицаОплатыТоваров.НоваяКолонка("НДСРуб", "Число");
		
		К = СуммаЗачтенногоАванса / ?(СуммаПоступления = 0, 1, СуммаПоступления); 
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл 
			
			Если ПустоеЗначение(Материал) = 1 Тогда
				ТекстСообщения = "В строке " + НомерСтроки + "не указан материал.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			ИначеЕсли Количество = 0 Тогда 
				ТекстСообщения = "Не указано количество материала: " + Материал + " ("+Материал.СубСчет10+").";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;

			// Определим долю зачтенного аванса, приходящуюся на эту строку.
			Если ((ВестиУчетРасчетовУЕ = 1)
			или ((ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008')))
			и (СуммаПоступления <> 0) Тогда
				ЧастьСуммыЗачтенногоАванса = Окр(СуммаЗачтенногоАванса * Всего / СуммаПоступления, 2, 1);
				ЧастьСуммыЗачтенногоАвансаРуб = Окр(СуммаЗачтенногоАвансаРуб * Всего / СуммаПоступления, 2, 1);
				
				РаспределеноАванса = РаспределеноАванса + ЧастьСуммыЗачтенногоАванса;
				РаспределеноАвансаРуб = РаспределеноАвансаРуб + ЧастьСуммыЗачтенногоАвансаРуб;
				
				Если КолСтрокДокумента = НомерСтроки Тогда
					Если РаспределеноАванса <> СуммаЗачтенногоАванса Тогда
						ЧастьСуммыЗачтенногоАванса = ЧастьСуммыЗачтенногоАванса + СуммаЗачтенногоАванса - РаспределеноАванса;
					КонецЕсли;
					
					Если РаспределеноАвансаРуб <> СуммаЗачтенногоАвансаРуб Тогда
						ЧастьСуммыЗачтенногоАвансаРуб = ЧастьСуммыЗачтенногоАвансаРуб + СуммаЗачтенногоАвансаРуб - РаспределеноАвансаРуб;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если (ВестиУчетРасчетовУЕ = 1)
			или ((ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008')) Тогда
			    ВсегоРубПоКурсуОтгрузки = Окр(Всего * Курс / Кратность, 2, 1);
				ОплаченоПоКурсуОтгрузки = Окр(К * ВсегоРубПоКурсуОтгрузки, 2, 1);
			    ВсегоРуб = ВсегоРубПоКурсуОтгрузки + ЧастьСуммыЗачтенногоАвансаРуб - ОплаченоПоКурсуОтгрузки;
				
				Если НДСвключатьВСтоимость = 1 Тогда
					ОплаченоНДСПоКурсуОтгрузки   = 0;
					НДСвЧастиЗачтенногоАвансаРуб = 0;
					НДСРуб                       = 0;
					
				Иначе
					НДСРубПоКурсуОтгрузки = Окр(НДС * Курс / Кратность, 2, 1);
					ОплаченоНДСПоКурсуОтгрузки = Окр(К * НДСРубПоКурсуОтгрузки, 2, 1);
					Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
						НДСвЧастиЗачтенногоАвансаРуб = Окр(К * НДС * Курс / Кратность, 2, 1);
					Иначе
						НДСвЧастиЗачтенногоАвансаРуб = Окр(К * НДС * КурсАванса, 2, 1);
					КонецЕсли;
					НДСРуб = НДСРубПоКурсуОтгрузки + НДСвЧастиЗачтенногоАвансаРуб - ОплаченоНДСПоКурсуОтгрузки;
				КонецЕсли;
				
				НПРубПоКурсуОтгрузки = Окр(НП * Курс / Кратность, 2, 1);
				ОплаченоНППоКурсуОтгрузки = Окр(К * НПРубПоКурсуОтгрузки, 2, 1);
				НПвЧастиЗачтенногоАвансаРуб = Окр(К * НП * КурсАванса, 2, 1);
				НПРуб = НПРубПоКурсуОтгрузки + НПвЧастиЗачтенногоАвансаРуб - ОплаченоНППоКурсуОтгрузки;
				
				Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
					СуммоваяРазницаДляВсего = 0;
					ТекСуммоваяРазницаДляНДС = 0;
					СуммоваяРазницаДляНП = 0;
					СуммоваяРазницаДляСтоимости = 0; 
				Иначе
					Если (Договор.ОтражатьСуммовуюРазницуПриЗачетеАванса = 1) и (ВестиУчетРасчетовУЕ = 1)  Тогда
						СуммоваяРазницаДляВсего = ЧастьСуммыЗачтенногоАвансаРуб - ОплаченоПоКурсуОтгрузки;
						ТекСуммоваяРазницаДляНДС = НДСвЧастиЗачтенногоАвансаРуб - ОплаченоНДСПоКурсуОтгрузки;
						СуммоваяРазницаДляНП = НПвЧастиЗачтенногоАвансаРуб - ОплаченоНППоКурсуОтгрузки;
						СуммоваяРазницаДляСтоимости = СуммоваяРазницаДляВсего - ТекСуммоваяРазницаДляНДС;
						
					Иначе
						СуммоваяРазницаДляВсего = 0;
						ТекСуммоваяРазницаДляНДС = 0;
						СуммоваяРазницаДляНП = 0;
						СуммоваяРазницаДляСтоимости = 0;
					КонецЕсли;
					
					СуммоваяРазницаДляНДС = СуммоваяРазницаДляНДС + ТекСуммоваяРазницаДляНДС;
				КонецЕсли;
				
			ИначеЕсли (ЦеныВДоговоре = 2) и (ОплатаДоговора = 1) Тогда
				ВсегоРуб = Окр(Всего * Курс / Кратность, 2, 1);
				НДСРуб = Окр(НДС * Курс / Кратность, 2, 1);
				
			Иначе
				ВсегоРуб = Всего;
				НДСРуб = НДС;
			КонецЕсли;
			
			ТаблицаОплатыТоваров.НоваяСтрока();
			ТаблицаОплатыТоваров.Товар = Материал;
			ТаблицаОплатыТоваров.Всего = Всего;
			ТаблицаОплатыТоваров.Сумма = Всего - НДС - НП;
			ТаблицаОплатыТоваров.НДС = НДС;
			ТаблицаОплатыТоваров.ВсегоРуб = ВсегоРуб;
			ТаблицаОплатыТоваров.СуммаРуб = ВсегоРуб - НДСРуб - НПРуб;
			ТаблицаОплатыТоваров.НДСРуб = НДСРуб;
		
			Если НДСвключатьВСтоимость <> 1 Тогда
				Стоимость = Всего - НДС;
				ВыделенныйНДС = ВыделенныйНДС + НДС;
				
				Если (ВестиУчетРасчетовУЕ = 1)
				или ((ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008')) Тогда
					СтоимостьРуб = ВсегоРуб - НДСРуб;
					ВыделенныйНДСРуб = ВыделенныйНДСРуб + НДСРуб;
				КонецЕсли;

			Иначе 
				Стоимость = Всего;
				
				Если (ВестиУчетРасчетовУЕ = 1)
				или ((ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008')) Тогда
					СтоимостьРуб = ВсегоРуб;
				КонецЕсли;
			КонецЕсли;
			
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "МТ";
			Операция.СодержаниеПроводки = "Поступили материалы";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = Материал.СубСчет10;
			Операция.Дебет.Материалы = Материал;
			Операция.Дебет.МестаХранения = МестоХранения;
			Операция.Кредит.Счет = СчетРасчетовСПоставщиком;
			Операция.Кредит.Контрагенты = Контрагент;
			Операция.Кредит.Договоры = Договор;
			Операция.Количество = Количество;
			
			Если ЦеныВДоговоре = 2 Тогда
			    Если ВестиУчетРасчетовУЕ = 1 Тогда
					Операция.Сумма = СтоимостьРуб - СуммоваяРазницаДляСтоимости;
					
				ИначеЕсли (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
					Операция.Сумма = СтоимостьРуб;
					
				Иначе
					Операция.Сумма = Стоимость*Курс/Кратность;
				КонецЕсли;
					
				Если (ОплатаДоговора = 2) или (ВестиУчетРасчетовУЕ = 1) Тогда
				    Операция.Валюта = Валюта;
					Операция.ВалСумма = Стоимость;
				КонецЕсли; 
				
				Если ВестиУчетРасчетовУЕ = 1 Тогда
					ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб + Операция.Сумма;
					ЗадолженностьПоРасчетамВУЕВал = ЗадолженностьПоРасчетамВУЕВал + Операция.ВалСумма;
				КонецЕсли;
			
			Иначе
				Операция.Сумма = Стоимость;
			КонецЕсли;
			
			Если СуммоваяРазницаДляСтоимости <> 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "МТ";
				Операция.СодержаниеПроводки = "Суммовая разница";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = Материал.СубСчет10;
				Операция.Дебет.Материалы = Материал;
				Операция.Дебет.МестаХранения = МестоХранения;
				Операция.Кредит.Счет = СчетРасчетовСПоставщиком;
				Операция.Кредит.Контрагенты = Контрагент;
				Операция.Кредит.Договоры = Договор;
				Операция.Сумма = СуммоваяРазницаДляСтоимости;
				Операция.Валюта = Валюта;     
				
				ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб + Операция.Сумма;
				
			КонецЕсли;
			
			// Отражение в налоговом учете поступления материалов.
			Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
				Если ЦеныВДоговоре = 2 Тогда
					Если ВестиУчетРасчетовУЕ = 1 Тогда
					    СтоимостьДляНалоговогоУчета = СтоимостьРуб - СуммоваяРазницаДляСтоимости;
						
					Иначе
						СтоимостьДляНалоговогоУчета = Стоимость*Курс/Кратность;
						Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
							НПРКурсоваяРазница =  Окр(СтоимостьРуб, 2) - Окр(СтоимостьДляНалоговогоУчета, 2);
						КонецЕсли;
					КонецЕсли;
					
				Иначе
					СтоимостьДляНалоговогоУчета = Стоимость;
				КонецЕсли;    
				
				
				СформироватьПроводкиДляЦелейНалоговогоУчета(СтоимостьДляНалоговогоУчета, , ПереченьСтатейЗатрат);
				глОтражениеСуммовыхРазницВНаловомУчете(Контекст, -СуммоваяРазницаДляСтоимости, 0);
				
				//********************************************************************
				//Проводки по расчетам в вал.
				Если (Константа.ПрименяетсяПБУ18.Получить(ДатаДок) = Да) Тогда
					Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
						Если НПРКурсоваяРазница <> 0 Тогда
							Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
							Операция.НомерЖурнала = "НУ";
							Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
							Операция.Дебет.Счет = СчНПР;
							Операция.Дебет.Субконто(1, Материал);
							Операция.Сумма = НПРКурсоваяРазница;
							Операция.СодержаниеПроводки = "Разница в оценке материалов"; 
						КонецЕсли;	
					КонецЕсли;	
				КонецЕсли;	
				//********************************************************************
			КонецЕсли;
		КонецЦикла;
	
		ИтогоНДС = Итог("НДС");
		Если ВерсияОбъекта < "7.70.421" Тогда
			// Документы, созданные в редакции 4.0 не формируют проводок
			// по НДС для неотфактурованных поставок.
			Если ПустоеЗначение(ДатаНомерСчетаФактуры) = 1 Тогда
				ИтогоНДС = 0;
			КонецЕсли;
		КонецЕсли;
		
		Если (ИтогоНДС > 0) и (НДСвключатьВСтоимость = 0) Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "МТ";
			Операция.СодержаниеПроводки = "Выделен НДС";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = Сч19_3;
			Операция.Дебет.Контрагенты = Контрагент;
			Если ПустоеЗначение(ДатаНомерСчетаФактуры) = 0 Тогда
				Операция.Дебет.СчетаФактурыПолученные = ТекущийДокумент();
			КонецЕсли;
			Операция.Кредит.Счет = СчетРасчетовСПоставщиком;
			Операция.Кредит.Контрагенты = Контрагент;
			Операция.Кредит.Договоры = Договор;
			Если ЦеныВДоговоре = 2 Тогда
			    Если ВестиУчетРасчетовУЕ = 1 Тогда
					Операция.Сумма = ВыделенныйНДСРуб - СуммоваяРазницаДляНДС;
					
				Иначе
					Операция.Сумма = ИтогоНДС*Курс/Кратность;
				КонецЕсли;
				
				Если (ОплатаДоговора = 2) или (ВестиУчетРасчетовУЕ = 1) Тогда
				    Операция.Валюта = Валюта;
					Операция.ВалСумма = ИтогоНДС;
				КонецЕсли;
				
				Если ВестиУчетРасчетовУЕ = 1 Тогда					
					ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб + Операция.Сумма;
					ЗадолженностьПоРасчетамВУЕВал = ЗадолженностьПоРасчетамВУЕВал + Операция.ВалСумма;
				КонецЕсли; 
				
			Иначе
				Операция.Сумма = ИтогоНДС;
			КонецЕсли;
			
			Если СуммоваяРазницаДляНДС <> 0  Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "МТ";
				Операция.СодержаниеПроводки = "Суммовая разница";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = Сч19_3;
				Операция.Дебет.Контрагенты = Контрагент;
				Если ПустоеЗначение(ДатаНомерСчетаФактуры) = 0 Тогда
					Операция.Дебет.СчетаФактурыПолученные = ТекущийДокумент();
				КонецЕсли;
				Операция.Кредит.Счет = СчетРасчетовСПоставщиком;
				Операция.Кредит.Контрагенты = Контрагент;
				Операция.Кредит.Договоры = Договор;
				Операция.Сумма = СуммоваяРазницаДляНДС;
				Операция.Валюта = Валюта; 
				
				ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб + Операция.Сумма;
				
			КонецЕсли;
			
			Если Договор.Выбран() = 1 Тогда
				Если (Договор.АвтоОбработкаНДС = 0) и (ВключатьВКнигуПокупок = 1) Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "МТ";
					Операция.СодержаниеПроводки = "НДС принят к зачету";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					
					Операция.Дебет.Счет = Сч68_2;
					Операция.Дебет.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
					
					Операция.Кредит.Счет = Сч19_3;
					Операция.Кредит.Контрагенты = Контрагент;
					Операция.Кредит.СчетаФактурыПолученные = ТекущийДокумент();
					
					Если ЦеныВДоговоре = 2 Тогда
						Если ВестиУчетРасчетовУЕ = 1 Тогда
							Операция.Сумма = ВыделенныйНДСРуб;
							
						Иначе
							Операция.Сумма = ИтогоНДС*Курс/Кратность;
						КонецЕсли;
						
					Иначе
						Операция.Сумма = ИтогоНДС;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Отразим зачет аванса.
		ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
		Если ЗачетАванса <> 0 Тогда
	
			ЗачестьАванс(ЗачетАвансаПоДоговору, ЗачетАвансаПоДоговоруРуб, Договор);
			ЗачестьАванс(ЗачетАвансаБезДоговора, ЗачетАвансаБезДоговораРуб, БезДоговора);
			
			Если (АвансПоДоговору + АвансБезДоговора) <> 0 Тогда
				ТекстСообщения = "=> Зачет аванса:" + РазделительСтрок
				               + "   поступило на сумму: "+СуммаВал+", зачтен аванс в размере: "+ЗачетАванса + РазделительСтрок
							   + "   - определен аванс по договору "+Договор+" в сумме: "+АвансПоДоговору+", зачтен аванс в размере: "+ЗачетАвансаПоДоговору + РазделительСтрок;
				
				Если ЗачитыватьАванс = 0 Тогда
					ТекстСообщения = ТекстСообщения
								   + "   - определен аванс без указания договора в сумме: "+АвансБезДоговора+", зачтен аванс в размере: "+ЗачетАвансаБезДоговора;
				Иначе
					ТекстСообщения = ТекстСообщения
					               + "   - аванс без указания договора не учтен";
				КонецЕсли;
				глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ВидПоступления = 1 Тогда
		Если СчетЗатрат.Выбран() = 0 Тогда
			ТекстСообщения = "Не указан счет затрат.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		СписокМатериалов = СоздатьОбъект("СписокЗначений");
		ВыгрузитьТабличнуюЧасть(СписокМатериалов, "Материал");
		БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Материалы, СписокМатериалов, 2);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Контрагенты, Контрагент);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Договоры, Договор);
		БухИт.ВключатьСубсчета(-1);
		БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"10",,,,,"СК");
		
		// Подготовим бух. итоги и определим счет отнесения стоимости
		// материалов для целей налогового учета.
		Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
			БИ_НУ = СоздатьОбъект("БухгалтерскиеИтоги");  БИ_НУ.ИспользоватьРазделительУчета(ЮрЛицо);
			БИ_НУ.ИспользоватьСубконто(ВидыСубконто.Материалы, СписокМатериалов, 2);
			Если Константа.РаздельныйУчетМатериаловСкладПереработка.Получить(ДатаДок)=Нет Тогда
				БИ_НУ.ВыполнитьЗапрос(,ТекущийДокумент(), "Н02.01",,,,, "СК");      				
			Иначе
				БИ_НУ.ИспользоватьСубконто(ВидыСубконто.Основание, Договор, 1);
				БИ_НУ.ВыполнитьЗапрос(,ТекущийДокумент(), "Н02.09",,,,, "СК");
			КонецЕсли;
		КонецЕсли;
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			
			Если ПустоеЗначение(Материал) = 1 Тогда
				ТекстСообщения = "В строке " + НомерСтроки + "не указан материал.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			ИначеЕсли Количество = 0 Тогда 
				ТекстСообщения = "Не указано количество материала: " + Материал + " ("+Материал.СубСчет10+").";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;

			// Проверяем остаток материала.
			СуммаВПереработке = 0;
			КоличествоВПереработке = 0;
			КоличествоНаСкладе = 0;
			Если БухИт.ПолучитьСчет(,Сч10_7) = 1 Тогда
				Если БухИт.ПолучитьСубконто(ВидыСубконто.Материалы,,Материал) = 1 Тогда
					Если БухИт.ПолучитьСубконто(ВидыСубконто.Контрагенты,,Контрагент) = 1 Тогда
						Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,,Договор) = 1 Тогда
							СуммаВПереработке = БухИт.СКД("С"); // Суммовой остаток, потребуется для расчета средней цены
							КоличествоВПереработке = БухИт.СКД("К");
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
	
			Если КоличествоВПереработке  < Количество Тогда
				ТекстСообщения = "В переработке по договору "+ Договор + " с "+Контрагент +" "+
								КоличествоВПереработке+" "+Материал.ЕдиницаИзмерения+
								" из необходимых "+Количество+" "+Материал.ЕдиницаИзмерения+" материала "+Материал+
								" ("+Материал.СубСчет10+")";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
	
			Если Количество = КоличествоВПереработке Тогда
				// Если материал отпускается полностью,
				// полностью списываем сумму
				СуммаПоступления = СуммаВПереработке;
			Иначе
				СуммаПоступления = Окр(Количество * СуммаВПереработке/КоличествоВПереработке,2,1);
			КонецЕсли;
	       	Стр = ""+НомерСтроки+". "+Материал+" (";
		   	Стр = Стр + " вид " + Сч10_7.ПолнНаименование+ " ,счет "+Сч10_7.Код;
		   	//Если Материал.СубСчет10.Выбран() = 1 Тогда
		   	//    Стр = Стр + "вид "+Материал.СубСчет10.Наименование+", счет "+Материал.СубСчет10;
		   	//Иначе
		   	//	Стр = Стр + "вид материала не указан";
		   	//КонецЕсли;
		   	Стр = Стр+"): в переработке по договору "+Договор+" с "+Контрагент+" "+КоличествоВПереработке+" "+Материал.ЕдиницаИзмерения+",";
		   	Стр = Стр+" на сумму "+СуммаВПереработке+",";
		   	Стр = Стр+" средняя стоимость "+Окр(СуммаВПереработке/КоличествоВПереработке,2,1);
		   	
		   	ТекстСообщения = Стр;
			глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
            Операция.НомерЖурнала = "МТ";
			Операция.СодержаниеПроводки = "Поступ. матер. из перераб. в пр-во";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			
			Операция.Дебет.Счет = СчетЗатрат;
			Операция.Дебет.Субконто(1,Субконто1);
			Операция.Дебет.Субконто(2,Субконто2);
			Операция.Дебет.Субконто(3,Субконто3);
			
			Операция.Кредит.Счет = Сч10_7;
			Операция.Кредит.Материалы = Материал;
			Операция.Кредит.Контрагенты = Контрагент;
			Операция.Кредит.Договоры = Договор;
			Операция.Количество = Количество;
			Операция.Сумма = СуммаПоступления;
			Сумма = СуммаПоступления;
			// установка реквизитов т.ч.
			Цена = СуммаПоступления / Количество;
			НДС = 0;
			НП = 0;
			Всего = СуммаПоступления;
			
			// Отражение в налоговом учете.
			Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
				СформироватьПроводкиДляЦелейНалоговогоУчета(, БИ_НУ, ПереченьСтатейЗатрат);
				Если СтатусВозврата() = 0 Тогда // произошла ошибка при формировании проводок для целей налогового учета.
				    Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	    Операция.СуммаОперации = Итог("Всего");
	КонецЕсли;
	
	Если (ПустоеЗначение(ДатаНомерСчетаФактуры) = 0)и(Договор.АвтоОбработкаНДС=1)и(ВидПоступления<1) Тогда
	    глДляЗаполненияКнигиПокупок(Контекст, ТаблицаОплатыТоваров);
	КонецЕсли;  
	
	//********************************************************************
	//Проводки по расчетам в у.е. 
	Если ДатаДок >= '01.01.2007' Тогда
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			Если (ЗадолженностьПоРасчетамВУЕРуб + ЗадолженностьПоРасчетамВУЕВал) <> 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "УЕ";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Кредит.Счет = СчУЕ_60;
				Операция.Кредит.Субконто(1, Контрагент);
				Операция.Кредит.Субконто(2, Договор);			
				Операция.Валюта = Валюта;
				Операция.Сумма = ЗадолженностьПоРасчетамВУЕРуб;
				Операция.ВалСумма = ЗадолженностьПоРасчетамВУЕВал;
				Операция.СодержаниеПроводки = "Задолженность по приобретению в у.е.";
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	//********************************************************************
	
	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения()

//******************************************************************************
// ОбработкаУдаленияПроведения()
//
// Описание:
//  Если документом отражается возврат из преработки,
//  то необходимо очистить реквизит "Всего, который
//  заполняется при проведении документа.
//
Процедура ОбработкаУдаленияПроведения()
	
	Если ВидПоступления = 1 Тогда
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Всего = 0;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаУдаленияПроведения() 