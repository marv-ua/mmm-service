//******************************************************************************
Процедура ОбработкаПроведения(РежимПроведения=0)
	
	Если РежимПроведения = 1 Тогда
	    ОчиститьДвижения("Справочник");
	Иначе
		ОчиститьДвижения();
		
		Если БалансоваяСтоимость = 0 Тогда
			ТекстСообщения = "Не указана первоначальная стоимость основного средства.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
			
		ИначеЕсли ПустоеЗначение(ОбъектВнеоборотныхАктивов) = 1 Тогда
			ТекстСообщения = "Не указан объект внеоборотных активов.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
	
		ИначеЕсли КоличествоСтрок() = 0 Тогда
			ТекстСообщения = "Не указаны основные средства, вводимые в эксплуатацию.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли; 
		
		Если (ДатаДок>='01.01.2006')  И (СписатьНаЗатраты=1) Тогда
			Если ПустоеЗначение(Субконто1)=1 Тогда
				ТекстСообщения="На закладке ""Бухгалтерский учет"" не указан материал на который производится переквалификация актива!";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			ИначеЕсли ПустоеЗначение(Субконто2)=1 Тогда
				ТекстСообщения="На закладке ""Бухгалтерский учет"" не указано место хранения актива!";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		//Проверка принадлежности ОС к ЕНВД	
		Если (Константа.ОрганизацияЯвляетсяПлательщикомЕНВД = Да) и (глНовыеПравилаВеденияНУ(ДатаДок) = 1) Тогда
			ОС_ЕНВД = 0;
			Если СчетЗатрат = СчетПоКоду("20") Тогда
				Если Субконто1.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.УслугаЕНВД Тогда
					ОС_ЕНВД = 1;
				КонецЕсли;
			ИначеЕсли СчетЗатрат = СчетПоКоду("44.1.2") Тогда
				ОС_ЕНВД = 1;					
			КонецЕсли;
			Если (ОС_ЕНВД = 1) и (ПринятьК_НалоговомуУчету <> 3) Тогда
				ТекстСообщения = "Основное средство принимается к учету по виду деятельности, облагаемому ЕНВД. 
								 |Переключатель ""Для целей налогового учета"" на закладке ""Общие сведения"" следует установить в положение ""Не принимать к налоговому учету"".";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
		КонецЕсли;

		Сч01_1  = СчетПоКоду("01.1");
		Сч08    = СчетПоКоду("08." + Строка(ВидВложений));
		СчМЦ_01 = СчетПоКоду("МЦ.01");
		Сч10_9 = СчетПоКоду("10.9");  
		СчН02_МЦ = СчетПоКоду("Н02.МЦ");
		
		КоличествоВА = 1;
		
		БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИт.ИспользоватьСубконто(ВидыСубконто.ОбъектыСтроительства, ОбъектВнеоборотныхАктивов, 2);
		БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), Сч08);
		Если ВидВложений = 4 Тогда
			КоличествоВА = БухИт.СКД("К");
			Если (КоличествоСтрок() > КоличествоВА) и (ВерсияОбъекта >= "7.70.421") Тогда
				ТекстСообщения = "Недостаточно """ + ОбъектВнеоборотныхАктивов + """ по счету " + Сч08;
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
			СтоимостьОбъектовВнеоборотныхАктивов = БухИт.СКД("С");
			Если СтоимостьОбъектовВнеоборотныхАктивов = 0 Тогда
				ТекстСообщения = "Не найдено остатка по счету " + Сч08;
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
		Иначе
			СтоимостьОбъектовВнеоборотныхАктивов = БухИт.СКД("С");
		КонецЕсли;

		Если БалансоваяСтоимость * КоличествоСтрок() > СтоимостьОбъектовВнеоборотныхАктивов Тогда
			ТекстСообщения = "Недостаточно """ + ОбъектВнеоборотныхАктивов + """ по счету " + Сч08;
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
	
		// Рассчитаем постоянные разницы, приходящиеся на внеоборотные активы вводимые в эксплуатацию.
		Если Константа.ПрименяетсяПБУ18.Получить(ДатаДок) = Да Тогда
			БухИт.ИспользоватьСубконто(ВидыСубконто.ОбъектыСтроительства, ОбъектВнеоборотныхАктивов, 2);
			БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "НПР.08");
			ВсегоПостоянныхРазниц = БухИт.СКД();
			ВведеноВЭксплуатациюПостоянныхРазниц = Окр(?(КоличествоВА = 0, 0, КоличествоСтрок()*ВсегоПостоянныхРазниц/КоличествоВА), 2);
		КонецЕсли;
	КонецЕсли;
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если РежимПроведения <> 1 Тогда
			СпрОС = СоздатьОбъект("Справочник.ОсновныеСредства");
			Если СпрОС.НайтиЭлемент(ОсновноеСредство) = 0 Тогда
				ТекстСообщения = "В стороке №" + НомерСтроки + " не задано основное средство.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
			Если (ДатаДок>='01.01.2006') И (СписатьНаЗатраты=1) Тогда
				Если ПустоеЗначение(СпрОС.ДатаВводаВЭксплуатацию)=1 Тогда
					СпрОС.ДатаВводаВЭксплуатацию="";
				КонецЕсли;
			Иначе
				СпрОС.ДатаВводаВЭксплуатацию   = ДатаДок;
			КонецЕсли;                                   
			
			СпрОС.ПодлежитАмортизации      = ПодлежитАмортизации;
			СпрОС.КоэффициентУскорения     = КоэффициентУскорения;
			
			Если ПустоеЗначение(Группа) = 0 Тогда
				СпрОС.Группа = Группа;    
			КонецЕсли;
			
			Если ПустоеЗначение(СпособНачисленияАмортизации) = 0 Тогда
				СпрОС.СпособНачисленияАмортизации = СпособНачисленияАмортизации;
			КонецЕсли;
			
			Если ПустоеЗначение(ШифрПоЕНАОФ) = 0 Тогда
				СпрОС.ШифрПоЕНАОФ = ШифрПоЕНАОФ;
			КонецЕсли;
			
			Если ПринятьК_НалоговомуУчету = 1 Тогда
				Если ПустоеЗначение(МетодНачисленияАмортизации) = 0 Тогда
					СпрОС.МетодНачисленияАмортизации = МетодНачисленияАмортизации;
				КонецЕсли;
				
				Если ПустоеЗначение(АмортизационнаяГруппа) = 0 Тогда
					СпрОС.АмортизационнаяГруппа	= АмортизационнаяГруппа;	
				КонецЕсли;
			Иначе
				СпрОС.МетодНачисленияАмортизации = "";	
				СпрОС.АмортизационнаяГруппа	= "";	
			КонецЕсли;
					    
			Если ПустоеЗначение(СпрОС.ПрочиеСведения) = 1 Тогда
				СпрОС.ПрочиеСведения = ПрочиеСведения;
			КонецЕсли;
			СпрОС.МатериалДляОтраженияВСоставеМПЗ= "";
			СпрОС.КодПоОКОФ = КодПоОКОФ;
			СпрОС.Записать();
		КонецЕсли;
		
		УстановитьРеквизитСправочника(ОсновноеСредство, "ПервоначальнаяСтоимость", БалансоваяСтоимость, ДатаДок);
		УстановитьРеквизитСправочника(ОсновноеСредство, "Подразделение", Подразделение, ДатаДок);
		УстановитьРеквизитСправочника(ОсновноеСредство, "МОЛ", МОЛ, ДатаДок);
		УстановитьРеквизитСправочника(ОсновноеСредство, "НачислятьАмортизацию", НачислятьАмортизацию, ДатаДок);
		УстановитьРеквизитСправочника(ОсновноеСредство, "КоэффициентАмортизации", КоэффициентАмортизации, ДатаДок);
		УстановитьРеквизитСправочника(ОсновноеСредство, "СрокПолезногоИспользования", СрокПолезногоИспользования, ДатаДок);
		УстановитьРеквизитСправочника(ОсновноеСредство, "Состояние", Перечисление.СостоянияОС.В_Эксплуатации, ДатаДок);
		УстановитьРеквизитСправочника(ОсновноеСредство, "ОбщийОбъемПродукцииРабот", ОбщийОбъемПродукцииРабот, ДатаДок);
		УстановитьРеквизитСправочника(ОсновноеСредство, "СтавкаНалогаНаИмущество", СтавкаНалогаНаИмущество, ДатаДок);
		
		Если СписатьНаЗатраты = 0 Тогда
			УстановитьРеквизитСправочника(ОсновноеСредство, "СчетЗатрат", СчетЗатрат, ДатаДок);
			УстановитьРеквизитСправочника(ОсновноеСредство, "Субконто1", Субконто1, ДатаДок, СчетЗатрат.ВидСубконто(1));
			УстановитьРеквизитСправочника(ОсновноеСредство, "Субконто2", Субконто2, ДатаДок, СчетЗатрат.ВидСубконто(2));
			УстановитьРеквизитСправочника(ОсновноеСредство, "Субконто3", Субконто3, ДатаДок, СчетЗатрат.ВидСубконто(3));
		КонецЕсли;

		Если ПринятьК_НалоговомуУчету = 1 Тогда
			УстановитьРеквизитСправочника(ОсновноеСредство, "ПервоначальнаяСтоимостьН", ПервоначальнаяСтоимостьН, ДатаДок);
			УстановитьРеквизитСправочника(ОсновноеСредство, "СрокПолезногоИспользованияН", СрокПолезногоИспользованияН, ДатаДок);
			УстановитьРеквизитСправочника(ОсновноеСредство, "СпециальныйКоэффициент", СпециальныйКоэффициент, ДатаДок);
			УстановитьРеквизитСправочника(ОсновноеСредство, "ВидРасхода", ВидРасхода, ДатаДок);        
			УстановитьРеквизитСправочника(ОсновноеСредство, "Объект", Объект, ДатаДок);
			ИмяТипа = "";
			Если ПустоеЗначение(ЭлементРасхода) = 0 Тогда
				ИмяТипа = "Перечисление."+ ЭлементРасхода.Вид();
			КонецЕсли;
			УстановитьРеквизитСправочника(ОсновноеСредство, "ЭлементРасхода", ЭлементРасхода, ДатаДок, ИмяТипа);
		КонецЕсли;

		Если РежимПроведения <> 1 Тогда
			Если (ДатаДок>='01.01.2006') И (СписатьНаЗатраты=1) Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ОС";
				Операция.СодержаниеПроводки = "ОС отражено в составе МПЗ";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = Сч10_9;
				Операция.Дебет.Материалы = Субконто1;
				Операция.Дебет.МестаХранения = Субконто2;
				Операция.Кредит.Счет = Сч08;
				Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
				Операция.Сумма = БалансоваяСтоимость;
				Операция.Количество = 1;
				СпрОс.МатериалДляОтраженияВСоставеМПЗ= Субконто1;
				СпрОс.Записать();
			Иначе
				Если ВидВложений = 4 Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ОС";
					Операция.СодержаниеПроводки = "Введено в эксплуатацию ОС";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = Сч01_1;
					Операция.Дебет.ОсновныеСредства = ОсновноеСредство;
					Операция.Кредит.Счет = Сч08;
					Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
					Операция.Сумма = БалансоваяСтоимость;
					Если ВерсияОбъекта >= "7.70.421" Тогда
						Операция.Количество = 1;
					КонецЕсли;
					
				Иначе
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ОС";
					Операция.СодержаниеПроводки = "Введено в эксплуатацию ОС";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = Сч01_1;
					Операция.Дебет.ОсновныеСредства = ОсновноеСредство;
					Операция.Кредит.Счет = Сч08;
					Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
					Операция.Сумма = БалансоваяСтоимость;
				КонецЕсли;
				
				Если СписатьНаЗатраты = 1 Тогда
					Если СчетЗатрат.Выбран() = 1 Тогда
						Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
						Операция.НомерЖурнала = "ОС";
						Операция.СодержаниеПроводки = "ОС списано на затраты";
						Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
						Операция.Дебет.Счет = СчетЗатрат;
						Операция.Дебет.Субконто(1, Субконто1);
						Операция.Дебет.Субконто(2, Субконто2);
						Операция.Дебет.Субконто(3, Субконто3);
						Операция.Кредит.Счет = Сч01_1;
						Операция.Кредит.ОсновныеСредства = ОсновноеСредство;
						Операция.Сумма = БалансоваяСтоимость;
						
					Иначе
						ТекстСообщения = "Нет возможности списать стоимость основного средства "+ОсновноеСредство+" на затраты на производство "+
						"(расходы на продажу). Не указан счет отнесения затрат.";
						глНеПроводить(Контекст, ТекстСообщения);
						Возврат;
					КонецЕсли;
					
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ОС";
					Операция.СодержаниеПроводки = "ОС списано на затраты";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = СчМЦ_01;
					Операция.Дебет.Субконто(1, ОсновноеСредство);
					Операция.Сумма = БалансоваяСтоимость;
					
				КонецЕсли;
			КонецЕсли;
			    
			// Отразим постоянные разницы
			Если Константа.ПрименяетсяПБУ18.Получить(ДатаДок) = Да Тогда
				Если ВведеноВЭксплуатациюПостоянныхРазниц <> 0 Тогда
					ПостояннаяРазница = Окр(ВведеноВЭксплуатациюПостоянныхРазниц/КоличествоСтрок()*НомерСтроки, 2)
					                  - Окр(ВведеноВЭксплуатациюПостоянныхРазниц/КоличествоСтрок()*(НомерСтроки-1), 2);
					Если ПостояннаяРазница <> 0 Тогда
                        
						Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
						Операция.НомерЖурнала = "ПР";
						Операция.СодержаниеПроводки = "Списание постоянных разниц";
						
						Если СписатьНаЗатраты = 1 Тогда
							
							Если ДатаДок>='01.01.2006' Тогда
								СчетНПР = СчетПоКоду("НПР.10.1");
							Иначе
								СчетНПР = глПолучитьСчетДебетаНПР(СчетЗатрат, Субконто1);
							КонецЕсли;
							
							Если ПустоеЗначение(СчетНПР) = 0 Тогда
								Операция.Дебет.Счет = СчетНПР;
								Операция.Дебет.Субконто(1, Субконто1);
								Операция.Дебет.Субконто(2, Субконто2);
								Операция.Дебет.Субконто(3, Субконто3);
							КонецЕсли;
							
						Иначе
							Операция.Дебет.Счет = СчетПоКоду("НПР.01");
							Операция.Дебет.ОсновныеСредства = ОсновноеСредство;
						КонецЕсли;
						
						Операция.Кредит.Счет = СчетПоКоду("НПР.08");
						Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
						Операция.Сумма = ПостояннаяРазница;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		
			Если (ДатаДок >= '01.01.2002') 
			   и ((ПринятьК_НалоговомуУчету < 3) или (глНовыеПравилаВеденияНУ(ДатаДок) = 1)) Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ОС";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Кредит.Счет = ?(ВидВложений = 4, СчетПоКоду("Н01.01"), СчетПоКоду("Н01.09"));
				Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
				Операция.Сумма = ПервоначальнаяСтоимостьН;
				Если (глНовыеПравилаВеденияНУ(ДатаДок) = 1)
				   и (ВидВложений = 4) Тогда
					Операция.Количество = 1;
				КонецЕсли;
				
				// Примем объеткт к налоговому учету, как амортизируемое имущество.
				Если ПринятьК_НалоговомуУчету = 1 Тогда
					Операция.СодержаниеПроводки = "Введено в эксплуатацию ОС";

					Если (ДатаДок>='01.01.2006') И (СписатьНаЗатраты=1) Тогда
						Операция.Дебет.Счет = СчетПоКоду("Н05.МЦ");
					Иначе
						Операция.Дебет.Счет = СчетПоКоду("Н05.01");
					КонецЕсли;
					
					Операция.Дебет.ОсновныеСредства = ОсновноеСредство;

					// Учтем стоимость в составе расходов.
				ИначеЕсли ПринятьК_НалоговомуУчету = 2 Тогда
					
					СчетИАналитикаДляОтнесенияРасходов = глПолучитьСчетРасходовДляЦелейНалоговогоУчета(ВидРасхода, ЭлементРасхода, Объект, ДатаДок);
					СчетРасходов = СчетИАналитикаДляОтнесенияРасходов.Получить("Счет");
					СчетРасходов = глПолучитьСчетУчетаКосвенныхРасходовНУ(ДатаДок, СчетЗатрат, Субконто1, СчетРасходов);
					Если ПустоеЗначение(СчетРасходов) = 1 Тогда
						ТекстСообщения = "На закладке ""Налоговый учет"" неверно указано направление отнесения расходов для целей налогового учета.";
						глНеПроводить(Контекст, ТекстСообщения);
						Возврат;
					КонецЕсли;
					Операция.СодержаниеПроводки = "ОС включено в расходы";
					Операция.Дебет.Счет = СчетРасходов;
					Операция.Дебет.Субконто(1, СчетИАналитикаДляОтнесенияРасходов.Получить("Субконто1"));
					Операция.Дебет.Субконто(2, СчетИАналитикаДляОтнесенияРасходов.Получить("Субконто2"));
					Операция.Дебет.Субконто(3, СчетИАналитикаДляОтнесенияРасходов.Получить("Субконто3"));
					                                         
				// Не принимаются к налоговому учету
				ИначеЕсли ПринятьК_НалоговомуУчету = 3 Тогда 
					Операция.Кредит.УсловияПоступленияИВыбытия = Перечисление.УсловияПоступленияИВыбытия.Другие;
				КонецЕсли;
				
				Если (ДатаДок>='01.01.2006') И (СписатьНаЗатраты=1) Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "НУ";
					Операция.СодержаниеПроводки = "ОС отражено в составе МПЗ";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = СчН02_МЦ;
					Операция.Дебет.Материалы = Субконто1;
					Операция.Количество = 1; 
				КонецЕсли;
				
				Если (ВключитьКапитальныеВложенияВРасходы = 1) И (((СписатьНаЗатраты = 0) И  (ДатаДок<'01.01.2006'))
				ИЛИ (ДатаДок>='01.01.2006')) И (ПринятьК_НалоговомуУчету = 1) Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ОС";  
					Операция.СодержаниеПроводки = "Включение кап.вложений в расходы";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = СчетПоКоду("Н05.КВ");
					Операция.Дебет.ОсновныеСредства = ОсновноеСредство;
					Операция.Сумма = КапитальныеВложенияВключенныеВРасходы;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если РежимПроведения <> 1 Тогда
		Операция.Записать();
		ТекстСообщения = "Основные средства введены в эксплуатацию.";
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		
		ТекстСообщения = "Документ проведен.";
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		глПриПроведении(Контекст);
	КонецЕсли;

КонецПроцедуры // ОбработкаПроведения()