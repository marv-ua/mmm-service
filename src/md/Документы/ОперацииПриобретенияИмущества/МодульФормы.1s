////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем НачальнаяДатаДокумента; 
Перем Новый;
Перем СписокДействий;
Перем СписокДокументов;
Перем СписокВидовРасходов, СписокЭлементовРасходов, СписокВнереализационныхРасходов;
Перем СписокВидовДокументовДляВыбора;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//*****************************************************************************
//
// ИтоговаяСтрока()
// 
// Вызывается из формул элементов диалога:
//  Текст ИтоговаяСтрока.
//
// Описание:
//  Формирует строку, содержащую итоговые суммы колонок табличной части.
//
Функция ИтоговаяСтрока()
	
	Возврат "Итого на сумму: " + СокрЛ(Формат(Итог("Сумма"),"Ч20.2-,"));
	
КонецФункции // ИтоговаяСтрока()

//******************************************************************************
//
// ПриИзмененииВидаРасхода()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Графа "Вид расхода".
//
// Описание:
//  Обработка изменения условия операции.
//
Процедура ПриИзмененииВидаРасхода()
	
	ТипЭлементаРасхода = "";
	ВидыОбъекта = "";
	Если ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеТоваров Тогда
		ВидыОбъекта = "Номенклатура";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеМатериалов Тогда
		ВидыОбъекта = "Материалы";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеОсновныхСредств Тогда
		ВидыОбъекта = "ОбъектыСтроительства,Оборудование";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеНематериальныхАктивов Тогда
		ВидыОбъекта = "ОбъектыСтроительства";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеЦенныхБумаг Тогда
		ВидыОбъекта = "ЦенныеБумаги";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеПрав Тогда
		ВидыОбъекта = "Договоры";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ТранспортныеРасходы Тогда
		Объект = "";
		Форма.Объект.Доступность(0);
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПредставительскиеРасходы Тогда
		Объект = "";
		Форма.Объект.Доступность(0);
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РасходыНаРекламу Тогда
		Объект = "";
		Форма.Объект.Доступность(0);
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РасходыБудущихПериодов Тогда
		ВидыОбъекта = "РасходыБудущихПериодов";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ГарантийныйРемонтИОбслуживание Тогда
		ВидыОбъекта = "Номенклатура";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.НИОКР Тогда
		ВидыОбъекта = "РасходыБудущихПериодов";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ОсвоениеПриродныхРесурсов Тогда
		ВидыОбъекта = "РасходыБудущихПериодов";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.КосвенныеРасходы Тогда
		ТипЭлементаРасхода = "Перечисление.ГруппыВидыРасходов";
		Объект = "";
		Форма.Объект.Доступность(0);
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ВнереализационныеРасходы Тогда
		ТипЭлементаРасхода = "Перечисление.ВнереализационныеРасходы";
		Объект = "";
		Форма.Объект.Доступность(0);
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РеализацияОсновныхСредств Тогда
		ВидыОбъекта = "ОсновныеСредства";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РеализацияНематериальныхАктивов Тогда
		ВидыОбъекта = "НематериальныеАктивы";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РеализацияПрочегоИмущества Тогда
		ВидыОбъекта = "Материалы,Оборудование,ОбъектыСтроительства,ЦенныеБумаги";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ВыбытиеИмущества Тогда
		ВидыОбъекта = "Материалы,Оборудование,ОсновныеСредства,НематериальныеАктивы,ОбъектыСтроительства,ЦенныеБумаги";

	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.БезвозмезднаяПередачаИмущества Тогда
		Объект = "";
		Форма.Объект.Доступность(0);
		
	Иначе
		Объект = "";
		Форма.Объект.Доступность(0);
	КонецЕсли;
	
	НазначитьТип("ЭлементРасхода", ТипЭлементаРасхода);
	Если ПустоеЗначение(ТипЭлементаРасхода) = 1 Тогда
		Форма.ЭлементРасхода.Доступность(0);
	Иначе
		Форма.ЭлементРасхода.Доступность(1);
	КонецЕсли;
	
	Если ПустоеЗначение(ВидыОбъекта) = 0 Тогда
		Если Найти("," + ВидыОбъекта + ",", "," + Объект.Вид() + ",") = 0 Тогда
			Объект = "";
		КонецЕсли;
		Объект.ВидыДляВыбора(ВидыОбъекта);
		Форма.Объект.Доступность(1);
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииВидаРасхода()

//******************************************************************************
// ПриИзмененииУсловияПолучения()
//
// Вызывается из формул элементов диалога:
//  Графа "Условие операции".
//
// Описание:
//  Обработка изменения условия операции.
//
Процедура ПриИзмененииУсловияПолучения()
	
	ВидыОбъекта = "";
	Если УсловиеПолучения = Перечисление.УсловияПолученияИмущества.ВРамкахЦелевогоФинансирования Тогда
		ВидРасхода = "";
		Форма.ВидРасхода.Доступность(0);
		ЭлементРасхода = "";
		Форма.ЭлементРасхода.Доступность(0);
		ВидыОбъекта = "Номенклатура,Материалы,ЦенныеБумаги";
		
	ИначеЕсли УсловиеПолучения = Перечисление.УсловияПолученияИмущества.Безвозмездно Тогда
		ВидРасхода = "";
		Форма.ВидРасхода.Доступность(0);
		ЭлементРасхода = "";
		Форма.ЭлементРасхода.Доступность(0);
		ВидыОбъекта = "Номенклатура,Материалы,ЦенныеБумаги,Оборудование,ОбъектыСтроительства,";
	
	Иначе
		Форма.ВидРасхода.Доступность(1);
		Форма.Объект.Доступность(0);
		ПриИзмененииВидаРасхода();
	КонецЕсли;
	
	Если ПустоеЗначение(ВидыОбъекта) = 0 Тогда
		Если Найти("," + ВидыОбъекта + ",", "," + Объект.Вид() + ",") = 0 Тогда
			Объект = "";
		КонецЕсли;
		Объект.ВидыДляВыбора(ВидыОбъекта);
		Форма.Объект.Доступность(1);
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииУсловияПолучения()

//******************************************************************************
// Заполнить()
//
// Вызывается из формул элементов диалога:
//  Кнопка "Заполнить".
//
// Описание:
//  Заполняет табличную часть документа по данным учета.
//
Процедура Заполнить()

	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.Установить("КонтекстДокумента", Контекст);
	ОткрытьФормуМодально("Обработка.ФормированиеНалоговыхДокументов", СписокПараметров);
	
КонецПроцедуры // Заполнить()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	Если Копирование = 1 Тогда //копирование документа
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ВводНового()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	                   
	Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
		Предупреждение("Данные, введенные в документ не будут влиять на
		               |налоговый учет, т.к. установлен способ ведения
					   |налогового учета - ""Одновременно с бухгалтерским учетом"" !");
	КонецЕсли;
				
	НачальнаяДатаДокумента = ДатаДок;
	ПриЗаписиПерепроводить(1);
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаЗаполнить.Доступность(0);
	КонецЕсли;
	
	Форма.УсловиеПолучения.ВыполнятьФормулуТолькоПриИзменении(1);
	
	СписокВидовРасходов = СоздатьОбъект("СписокЗначений");
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПриобретениеТоваров);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПриобретениеМатериалов);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПриобретениеОсновныхСредств);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПриобретениеНематериальныхАктивов);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПриобретениеЦенныхБумаг);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПриобретениеПрав);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ТранспортныеРасходы);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПредставительскиеРасходы);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.РасходыНаРекламу);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.РасходыБудущихПериодов);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ГарантийныйРемонтИОбслуживание);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.НИОКР);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ОсвоениеПриродныхРесурсов);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.КосвенныеРасходы);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ВнереализационныеРасходы);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.РеализацияОсновныхСредств);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.РеализацияНематериальныхАктивов);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.РеализацияПрочегоИмущества);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ВыбытиеИмущества);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.БезвозмезднаяПередачаИмущества);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.НеПринимаемые);	
	
	СписокЭлементовРасходов = СоздатьОбъект("СписокЗначений");
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.МатериальныеРасходы);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.НалогиИСборы);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ПользованиеПравамиНаРезультатыИнтеллектуальнойДеятельности);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.РасходыНаКомандировки);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.РасходыНаРекламуБезНорм);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ИнформационныеУслуги);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ИзучениеКонъюнктурыРынка);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.РемонтОсновныхСредств);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ПодготовкаИПереподготовкаКадров);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ОхранаИмущества);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.УслугиПоУправлению);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.АудиторскиеУслуги);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ПредоставлениеРаботниковСтороннимиОрганизациями);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.СоциальнаяЗащитаИнвалидов);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ДругиеРасходы);
	
	СписокВнереализационныхРасходов = СоздатьОбъект("СписокЗначений");
	СписокВнереализационныхРасходов.ДобавитьЗначение(Перечисление.ВнереализационныеРасходы.СодержаниеИмуществаПереданногоВАренду);
	СписокВнереализационныхРасходов.ДобавитьЗначение(Перечисление.ВнереализационныеРасходы.УслугиБанков);
	СписокВнереализационныхРасходов.ДобавитьЗначение(Перечисление.ВнереализационныеРасходы.УбыткиПрошлыхЛет);
	СписокВнереализационныхРасходов.ДобавитьЗначение(Перечисление.ВнереализационныеРасходы.ПрочиеВнереализационныеРасходы);
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриРедактированииНовойСтроки()
	
	ПриИзмененииУсловияПолучения();
	ДокументБУ.ВидыДляВыбора(СписокВидовДокументовДляВыбора);
	
КонецПроцедуры // ПриРедактированииНовойСтроки()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеРедактированияСтроки()
	
	Если (Форма.ТекущаяКолонка() = "ОткрытьДокумент") Тогда
		Если ДокументБУ.Выбран()=0 Тогда
			Предупреждение("Не могу открыть документ, отражающий данную операцию
			                |в бухгалтеском учете: документ не указан!",60);
		Иначе     
			ОткрытьФорму(ДокументБУ);
		КонецЕсли;
		
	Иначе
		ПриИзмененииУсловияПолучения();
		ДокументБУ.ВидыДляВыбора(СписокВидовДокументовДляВыбора);
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеРедактированияСтроки()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, Флаг)
	
	Если ЭлементДиалога = "ВидРасхода" Тогда
		Флаг = 0;
		Позиция = СписокВидовРасходов.НайтиЗначение(ВидРасхода);
		ВыбранныйВидРасхода = ВидРасхода;
		Если СписокВидовРасходов.ВыбратьЗначение(ВыбранныйВидРасхода,, Позиция,, 2) = 1 Тогда
			Если ВидРасхода <> ВыбранныйВидРасхода Тогда
				ВидРасхода = ВыбранныйВидРасхода;
			    ПриИзмененииВидаРасхода();
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ЭлементДиалога = "ЭлементРасхода" Тогда
		Если ЭлементРасхода.Вид() = "ГруппыВидыРасходов" Тогда
			Флаг = 0;
			Позиция = СписокЭлементовРасходов.НайтиЗначение(ЭлементРасхода);
			СписокЭлементовРасходов.ВыбратьЗначение(ЭлементРасхода,, Позиция,, 2);
			
		ИначеЕсли ЭлементРасхода.Вид() = "ВнереализационныеРасходы" Тогда
			Флаг = 0;
			Позиция = СписокВнереализационныхРасходов.НайтиЗначение(ЭлементРасхода);
			СписокВнереализационныхРасходов.ВыбратьЗначение(ЭлементРасхода,, Позиция,, 2);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи() //предопределенная
	
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
		
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
		
	КонецЕсли;
	
	Операция.СуммаОперации = Итог("Сумма");
	Операция.Содержание = "Операции приобретения имущества";
	
КонецПроцедуры // ПриЗаписи()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Новый = 0;

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");

СписокВидовДокументовДляВыбора = "ПоступлениеНМА,ПоступлениеМатериалов,ПоступлениеОС,ПоступлениеТоваров,ПоступлениеОборудования,УслугиСтороннихОрганизаций,АвансовыйОтчет,Выписка,Операция";