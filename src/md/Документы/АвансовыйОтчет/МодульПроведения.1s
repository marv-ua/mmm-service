//_____________________________________________________________________________
Процедура ОбработкаПроведения()
    ТипОтчета = Валюта.Выбран() + 1;
	Сч19 = СчетПоКоду("19.3");

	Если ТипОтчета = 2 Тогда
		Сч71 = СчетПоКоду("71.11");

		КурсВал = Валюта.Курс.Получить(ДатаДок);
		Кратность = Валюта.Кратность.Получить(ДатаДок);
		Кратность = ?(Кратность=0,1,Кратность);

		глТаблицаСчетов.УдалитьСтроки();
		глТаблицаСчетов.НоваяСтрока();
		глТаблицаСчетов.Счет = Сч71;
		глТаблицаСчетов.Субконто1 = Сотрудник;
		глТаблицаСчетов.Субконто2 = "";
		глТаблицаСчетов.Субконто3 = "";
		глТаблицаСчетов.Валюта = Валюта;
		глПереоценкаСчетов(Контекст, глТаблицаСчетов);

	Иначе
		Сч71 = СчетПоКоду("71.1");
	КонецЕсли; 
		ПереченьСтатейЗатрат = глПолучитьПереченьСтатейЗатрат(ДатаДок); 

	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(КоррСчет) = 1 Тогда
			ТекстСообщения = "В строке №" + НомерСтроки + " не указан корреспондирующий счет.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
			
		ИначеЕсли КоррСчет.Валютный = 1 Тогда
			ТекстСообщения = "В строке №" + НомерСтроки + " корреспондирующий счет не может быть валютным.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;

		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "БК";
		Операция.СодержаниеПроводки = КомуЗаЧто;
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = КоррСчет;
		Операция.Дебет.Субконто(1,Субконто1);
		Операция.Дебет.Субконто(2,Субконто2);
		Операция.Дебет.Субконто(3,Субконто3);
		Операция.Кредит.Счет = Сч71;
		Операция.Кредит.Сотрудники = Сотрудник;

		Если НДС > 0 Тогда
			Если ТипОтчета = 1 Тогда
				Операция.Сумма = Сумма - НДС;
			Иначе
				Операция.Сумма = Окр(Сумма*КурсВал/Кратность,2,1) - Окр(НДС*КурсВал/Кратность,2,1);
			КонецЕсли;
		Иначе
			Если ТипОтчета = 1 Тогда
				Операция.Сумма = Сумма;
			Иначе
				Операция.Сумма = Окр(Сумма*КурсВал/Кратность,2,1);
			КонецЕсли;
		КонецЕсли;

		Если КоррСчет.Количественный = 1 Тогда
		    Операция.Количество = Количество;
		КонецЕсли;

		Если ТипОтчета = 2 Тогда
		    Операция.Валюта = Валюта;
		    Операция.ВалСумма = Сумма - НДС;
		КонецЕсли;
		
		Если Операция.Дебет.Счет.Код = "20" Тогда
			Если Операция.Дебет.Субконто(1).ТипНоменклатуры = Перечисление.ТипыНоменклатуры.УслугаЕНВД Тогда  // расходы, связанные с услугами ЕНВД не отражаются  в НУ
				Продолжить; 
			КонецЕсли;
		КонецЕсли;  
		
		// Отражение операций в налоговом учете.
		ОбъектыАналитикиНУ = глСчетИАналитикаРасходовНУ(Контекст, КоррСчет, Субконто1, Субконто2, Субконто3, ПереченьСтатейЗатрат, Сч71);
		
		// Если опредлен налоговый счет, то необходимо
		// сформировать проводку для целей налогового учета.
		СчетНУ = ОбъектыАналитикиНУ.Получить("Счет");
		Если ПустоеЗначение(СчетНУ) = 0 Тогда
			СуммаРасходов = Операция.Сумма;
			
		    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		    Операция.НомерЖурнала = "НУ";
			Операция.СодержаниеПроводки = КомуЗаЧто;
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетНУ;
			Для СчетчикЦикла = 1 По СчетНУ.КоличествоСубконто() Цикл
				ИдентификаторСубконто = СчетНУ.ВидСубконто(СчетчикЦикла,,).Идентификатор();
				Операция.Дебет.Субконто(СчетчикЦикла, ОбъектыАналитикиНУ.Получить(ИдентификаторСубконто));
			КонецЦикла;
			
			Операция.Дебет.Субконто(ВидыСубконто.УсловияПоступленияИВыбытия, Перечисление.УсловияПоступленияИВыбытия.Другие);
			
			Операция.Сумма = СуммаРасходов;
			Если СчетНУ.Количественный = 1 Тогда
			    Операция.Количество = Количество;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Операция.Записать();
	
	ТекстСообщения = "Сотруднику "+Сотрудник+" зачтена сумма, выданная под отчет.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры
