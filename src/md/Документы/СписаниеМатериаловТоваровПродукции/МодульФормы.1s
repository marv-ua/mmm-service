////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем НачальнаяДатаДокумента; 
Перем Новый;

Перем МатериальныеРасходы;
Перем ВидыОбъекта;
Перем СписокВидовРасходов, СписокЭлементовРасходов;
Перем СписокДействий;
Перем СписокВидовДокументовДляВыбора;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//   
//******************************************************************************
//
// ПриИзмененииВидаРасхода()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Графа "Вид расхода".
//
// Описание:
//  Обработка изменения условия операции.
//
Процедура ПриИзмененииВидаРасхода()
	
	ТипЭлементаРасхода = "";
	ВидыОбъекта = "";             
	Если ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеТоваров Тогда
		ВидыОбъекта = "Номенклатура";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеМатериалов Тогда
		ВидыОбъекта = "Материалы";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеОсновныхСредств Тогда
		ВидыОбъекта = "ОбъектыСтроительства,Оборудование";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеНематериальныхАктивов Тогда
		ВидыОбъекта = "ОбъектыСтроительства";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеЦенныхБумаг Тогда
		ВидыОбъекта = "ЦенныеБумаги";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеПрав Тогда
		ВидыОбъекта = "Договоры";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ТранспортныеРасходы Тогда
		Объект = "";
		Форма.Объект.Доступность(0);
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПредставительскиеРасходы Тогда
		Объект = "";
		Форма.Объект.Доступность(0);
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РасходыНаРекламу Тогда
		Объект = "";
		Форма.Объект.Доступность(0);
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РемонтОсновныхСредств Тогда
		ВидыОбъекта = "ОсновныеСредства";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ГарантийныйРемонтИОбслуживание Тогда
		ВидыОбъекта = "Номенклатура";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.НИОКР Тогда
		ВидыОбъекта = "РасходыБудущихПериодов";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ОсвоениеПриродныхРесурсов Тогда
		ВидыОбъекта = "РасходыБудущихПериодов";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.КосвенныеРасходы Тогда
		ТипЭлементаРасхода = "ГруппыВидыРасходов";
		Объект = "";
		Форма.Объект.Доступность(0);

	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РеализацияОсновныхСредств Тогда
		ВидыОбъекта = "ОсновныеСредства";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РеализацияНематериальныхАктивов Тогда
		ВидыОбъекта = "НематериальныеАктивы";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РеализацияПрочегоИмущества Тогда
		ВидыОбъекта = "Материалы,Оборудование,ОбъектыСтроительства,ЦенныеБумаги";
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ВыбытиеИмущества Тогда
		ВидыОбъекта = "Материалы,Оборудование,ОсновныеСредства,НематериальныеАктивы,ОбъектыСтроительства,ЦенныеБумаги";

	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.БезвозмезднаяПередачаИмущества Тогда
		Объект = "";
		Форма.Объект.Доступность(0);
		
	ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПрямыеРасходы Тогда
	    ВидыОбъекта = "ВидыНоменклатуры";
		
	Иначе
		Объект = "";
		Форма.Объект.Доступность(0);
	КонецЕсли;
	
	Если ПустоеЗначение(ТипЭлементаРасхода) = 1 Тогда
		ЭлементРасхода = 0;
		Форма.ЭлементРасхода.Доступность(0);
		
	Иначе
		Форма.ЭлементРасхода.Доступность(1);
	КонецЕсли;
	
	Если ПустоеЗначение(ВидыОбъекта) = 0 Тогда
		Если Найти("," + ВидыОбъекта + ",", "," + Объект.Вид() + ",") = 0 Тогда
			Объект = "";
		КонецЕсли;
		Объект.ВидыДляВыбора(ВидыОбъекта);
		Форма.Объект.Доступность(1);
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииВидаРасхода()
    
//******************************************************************************
// УправлениеДиалогом()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура УправлениеДиалогом()
	
	Если ВидОбъектов = 0 Тогда // вид МПЗ - материал
		Форма.МПЗ.Заголовок("Материал");
		
	ИначеЕсли ВидОбъектов = 1 Тогда // товар
		Форма.МПЗ.Заголовок("Товар");
			
	Иначе // продукция
		Форма.МПЗ.Заголовок("Продукция");
	КонецЕсли;
	
КонецПроцедуры // УправлениеДиалогом()
//******************************************************************************
// ПриВыбореВидаОбъектов(ИзДиалога)
//
// Вызывается из формул элементов диалога:
//  Поле со списком "ВариантыВидовОбъектов".
//
// Описание:
//  Устанавливает значение реквизита "ВидОбъектов".
//
Процедура ПриВыбореВидаОбъектов()

	Если ВидОбъектов <> ВариантыВидовОбъектов.ПолучитьЗначение(ВариантыВидовОбъектов.ТекущаяСтрока()) Тогда

		// если есть строки, то для того, чтобы поменять вид объектов необходимо удалить их
		Если КоличествоСтрок() <> 0 Тогда
			Если Вопрос("Таблица будет очищена, а существующие строки удалены.
					|Продолжить?","Да+Нет") = "Да" Тогда
				УдалитьСтроки(); 
				
			Иначе
				ВариантыВидовОбъектов.ТекущаяСтрока(ВариантыВидовОбъектов.НайтиЗначение(ВидОбъектов));
				Возврат;
			КонецЕсли;
		КонецЕсли;    
		ВидОбъектов = ВариантыВидовОбъектов.ПолучитьЗначение(ВариантыВидовОбъектов.ТекущаяСтрока());
		УправлениеДиалогом();
	КонецЕсли;                                                                                  

КонецПроцедуры // ПриВыбореВидаОбъектов() 

//******************************************************************************
// Заполнить()
//
// Вызывается из формул элементов диалога:
//  Кнопка "Заполнить".
//
// Описание:
//  Заполняет табличную часть документа по данным учета.
//
Процедура Заполнить()

	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.Установить("КонтекстДокумента", Контекст);
	ОткрытьФормуМодально("Обработка.ФормированиеНалоговыхДокументов", СписокПараметров);
	
КонецПроцедуры // Заполнить()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	
	Если Копирование = 1 Тогда //копирование документа
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ВводНового()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
		Предупреждение("Данные, введенные в документ не будут влиять на
		               |налоговый учет, т.к. установлен способ ведения
					   |налогового учета - ""Одновременно с бухгалтерским учетом"" !");
	КонецЕсли;
	
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаЗаполнить.Доступность(0);
	КонецЕсли;
	
	СписокВидовРасходов = СоздатьОбъект("СписокЗначений");
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПрямыеРасходы);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПриобретениеТоваров);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПриобретениеМатериалов);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПриобретениеОсновныхСредств);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПриобретениеНематериальныхАктивов);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПриобретениеЦенныхБумаг);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПриобретениеПрав);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ПредставительскиеРасходы);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.РасходыНаРекламу);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.НИОКР);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ОсвоениеПриродныхРесурсов);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.КосвенныеРасходы);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.РеализацияОсновныхСредств);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.РеализацияНематериальныхАктивов);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.РеализацияПрочегоИмущества);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.ВыбытиеИмущества);	
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.БезвозмезднаяПередачаИмущества);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВидыРасходов.НеПринимаемые);
	
	СписокЭлементовРасходов = СоздатьОбъект("СписокЗначений");
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ПользованиеПравамиНаРезультатыИнтеллектуальнойДеятельности);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.МатериальныеРасходы);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.РасходыНаКомандировки);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.РасходыНаРекламуБезНорм);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ИнформационныеУслуги);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ИзучениеКонъюнктурыРынка);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.РемонтОсновныхСредств);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ПодготовкаИПереподготовкаКадров);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ОхранаИмущества);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.УслугиПоУправлению);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.АудиторскиеУслуги);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ПредоставлениеРаботниковСтороннимиОрганизациями);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.СоциальнаяЗащитаИнвалидов);
	СписокЭлементовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ДругиеРасходы);
	
	ВариантыВидовОбъектов.ДобавитьЗначение(0, "Материалы");
	ВариантыВидовОбъектов.ДобавитьЗначение(1, "Товары");
	ВариантыВидовОбъектов.ДобавитьЗначение(2, "Продукция");
	ВариантыВидовОбъектов.ТекущаяСтрока(ВариантыВидовОбъектов.НайтиЗначение(ВидОбъектов));
	УправлениеДиалогом();

КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриРедактированииНовойСтроки()
	
	ПриИзмененииВидаРасхода();
	ДокументБУ.ВидыДляВыбора(СписокВидовДокументовДляВыбора);
	
КонецПроцедуры // ПриРедактированииНовойСтроки()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеРедактированияСтроки()
	                 
	Если (Форма.ТекущаяКолонка() = "ОткрытьДокумент") Тогда
		Если ДокументБУ.Выбран()=0 Тогда
			Предупреждение("Не могу открыть документ, отражающий данную операцию
			                |в бухгалтеском учете: документ не указан!",60);
		Иначе     
			ОткрытьФорму(ДокументБУ);
		КонецЕсли;
		
	Иначе
		ПриИзмененииВидаРасхода();
		ДокументБУ.ВидыДляВыбора(СписокВидовДокументовДляВыбора);
	КонецЕсли;
		
КонецПроцедуры // ПриРедактированииНовойСтроки()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, Флаг)
	
	Если ЭлементДиалога = "ВидРасхода" Тогда
		Флаг = 0;
		Позиция = СписокВидовРасходов.НайтиЗначение(ВидРасхода);
		СписокВидовРасходов.ВыбратьЗначение(ВидРасхода,, Позиция,, 2);
		ПриИзмененииВидаРасхода();
		
	ИначеЕсли ЭлементДиалога = "ЭлементРасхода" Тогда
		
		Флаг = 0;
		Позиция = СписокЭлементовРасходов.НайтиЗначение(ЭлементРасхода);
		СписокЭлементовРасходов.ВыбратьЗначение(ЭлементРасхода,, Позиция,, 2);
		
	ИначеЕсли ЭлементДиалога = "МПЗ" Тогда
		
		Если ВидОбъектов = 0 Тогда // материал
		    МПЗ.ВидыДляВыбора("Материалы");
			
		ИначеЕсли ВидОбъектов = 1 Тогда // товары
			МПЗ.ВидыДляВыбора("Номенклатура");
			ОтборНоменклатуры = Перечисление.ТипыНоменклатуры.Товар;
			
		ИначеЕсли ВидОбъектов = 2 Тогда // продукция
			МПЗ.ВидыДляВыбора("Номенклатура");
			ОтборНоменклатуры = Перечисление.ТипыНоменклатуры.Продукция;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи() //предопределенная
	
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
		
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Новый = 0;               

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");
                                                
СписокВидовДокументовДляВыбора = "ТребованиеНакладная,ПоступлениеМатериалов,Операция";