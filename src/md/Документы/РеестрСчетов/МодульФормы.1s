////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;
Перем НачальнаяДатаДокумента;
Перем Новый;
Перем СписокДействий;

//*****************************************************************************
// ПриВыбореСчетаКонтрагента()
//
// Параметры:          
//		Нет
//
//	Вызывется из формул элементов диалога:
// 	"СчетКонтрагента".
//
// Описание
// 		Определяет соответствие расчетного счета контрагента
//		выбранному контрагенту, при несоответствии контрагент заменяется
//		
Процедура ПриВыбореСчетаКонтрагента()
	
	Если СчетКонтрагента.Выбран() = 1 Тогда
		Если СчетКонтрагента.Владелец <> Контрагент Тогда
			Контрагент = СчетКонтрагента.Владелец;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры //ПриВыбореСчетаКонтрагента

//*****************************************************************************
// ПриВыбореКонтрагента()
//
// Параметры:          
//		Нет
//
//	Вызывется из формул элементов диалога:
// 	"Контрагент".
//
// Описание
// 		Определяет соответствие контрагента выбранному расчетному счету контрагента,
//		при несоответствии или если счет не выбран устанавливается расчетный счет по умолчанию.
//		
Процедура ПриВыбореКонтрагента()
	
	Если СчетКонтрагента.Владелец <> Контрагент Тогда
		СчетКонтрагента = "";
	КонецЕсли;
	Если ПустоеЗначение(СчетКонтрагента) = 1 Тогда
	    СчетКонтрагента = глРасчетныйСчетКонтрагента(Контрагент);
		ПриВыбореСчетаКонтрагента();
	КонецЕсли;
	
КонецПроцедуры //ПриВыбореКонтрагента

//*****************************************************************************
// УстДату()
//
// Параметры:          
//		Нет
//
//	Вызывется из формул элементов диалога:
// 	"ДатаДок".
//
// Описание
// 		Устанавливает срок (дату) закрытия аккредитива
//		
Процедура УстДату()
	
	СрокПлатежа=ДатаДок;
	
КонецПроцедуры //УстДату

//*****************************************************************************
// Печать()
// Параметры:          
//		Нет
//
// Описание
// 		Осуществляет вывод на печать
//		
Процедура Печать()

	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	
	Если ПустаяСтрока(Константа.ОфициальноеНазваниеОрганизации)=1 Тогда
		Получатель = СокрЛП(Константа.НазваниеОрганизации);
	Иначе
		Получатель = СокрЛП(Константа.ОфициальноеНазваниеОрганизации);
	КонецЕсли;
	БанкПолучателя = РасчетныйСчет.БанкОрганизации;
	НомерСчетаПолучателя = РасчетныйСчет.Номер;
	
	Если ПустаяСтрока(Контрагент.ПолнНаименование)=1 Тогда
		Плательщик = СокрЛП(Контрагент.Наименование);
	Иначе
		Плательщик = СокрЛП(Контрагент.ПолнНаименование);
	КонецЕсли;
	НомерСчетаПлательщика = СчетКонтрагента.Номер;
	БанкПлательщика = СчетКонтрагента.БанкОрганизации;

	НаименованиеНомер = "РЕЕСТР СЧЕТОВ № "+глПреобразоватьНомерДок(НомерДок);
	ДатаДокумента     = глВыводДатыПлатежныхДокументов(ДатаДок, РасчетныйСчет);	// Месяц даты числом или прописью
	СуммаПрописью     = глВыводСуммыПлатежныхДокументовПрописью(Итог("Всего"), РасчетныйСчет);	// Сумма без 00 копеек (=) или (-00)
	СрокАккредитива   = ?(ПустоеЗначение(СрокПлатежа)=1, "", Формат(СрокПлатежа, "Д ДДММГГГГ"));
	
	Таб.ВывестиСекцию("Шапка");

	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;
	
	Таб.ВывестиСекцию("Подвал");

	Таб.Опции(0,0,0,0,"ОпцииПечатиРеестраСчетов","ПараметрыОкнаРеестраСчетов");
	Таб.ТолькоПросмотр(1);
	Таб.ПараметрыСтраницы(,,,,0,,0,,,);
	Таб.Показать("Печать реестра расчетов","");   
	
КонецПроцедуры

//******************************************************************************
// ПоКнопкеПечать()
// 
// Вызывается из формул элементов диалога:
//  Кнопка "кнПечать".
//
// Описание:
//  Определяется соответствующая печатная форма.
// 	
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		Печать();
		
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(Контекст, "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Вызывается из формул элементов диалога:
//  Кнопка "кнВыбПечать".
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Документ." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()

//*****************************************************************************
Процедура ВводНового(Копирование) //предопределенная
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	Если Копирование = 1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;

	СрокПлатежа = ДатаДок;
	Акцепт = 1;
	УсловиеОплаты = "";
	РасчетныйСчет = глЗначениеПоУмолчанию("ОсновнойБанковскийСчет");
	
КонецПроцедуры //ВводНового

//*****************************************************************************
Процедура ПриОткрытии() //предопределенная
	
	НачальнаяДатаДокумента=ДатаДок;
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.кнОК.Доступность(0);
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.КнопкаПоУмолчанию("кнЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;
	Форма.Кн_Справка.Видимость(?(ТипЗначения(Норм_Компонента) = 0, 0, 1));
	
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Документ." + Вид(),ТаблицаПечФорм);
	
КонецПроцедуры //ПриОткрытии

//*****************************************************************************
Процедура ПриЗаписи() //предопределенная

	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
       СтатусВозврата(0);Возврат;
	КонецЕсли;
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

КонецПроцедуры //ПриЗаписи

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	// глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Новый = 0;

ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");
	
// добавим информацию о встроенной форме
ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Реестр счетов";
ТаблицаПечФорм.Кнопка       = "Печать";

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Ввести на основании");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");