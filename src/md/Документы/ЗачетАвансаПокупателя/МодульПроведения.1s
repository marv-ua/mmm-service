////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем Таб;
Перем АвансБезДоговора, АвансБезДоговораРуб, АвансПоДоговору, АвансПоДоговоруРуб;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//*****************************************************************************
// ЗачестьАванс(ЗачетАванса, ДоговорАванса)
//
Процедура ЗачестьАванс(ЗачетАванса, ДоговорАванса)
	
	Если ЗачетАванса > 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "ТВ";
		Операция.СодержаниеПроводки = "Зачтена предоплата";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = Таб.СчетАвансовПолученных;
		Операция.Дебет.Контрагенты = Контрагент;
		Операция.Дебет.Договоры = ДоговорАванса;
		Операция.Кредит.Счет = Таб.СчетРасчетовСПокупателем;
		Операция.Кредит.Контрагенты = Контрагент;
		Операция.Кредит.Договоры = Таб.Договор;
		Если Таб.ОплатаДоговора = 2 Тогда 
			Если ДатаДок >= '01.01.2008' Тогда
				Если (АвансПоДоговору + АвансБезДоговора) = ЗачетАванса Тогда
					ЗачетАвансаРуб = АвансПоДоговоруРуб + АвансБезДоговораРуб;
				Иначе
					КурсЗачетаАванса = Окр((АвансПоДоговоруРуб + АвансБезДоговораРуб)/(АвансПоДоговору + АвансБезДоговора), 4, 1);
					ЗачетАвансаРуб = ЗачетАванса*КурсЗачетаАванса;
				КонецЕсли;
			Иначе
				ЗачетАвансаРуб = ЗачетАванса*Таб.Курс/Таб.Кратность;
			КонецЕсли;
			Операция.Сумма = ЗачетАвансаРуб;
		    Операция.Валюта = Таб.Валюта;
			Операция.ВалСумма = ЗачетАванса;
		Иначе
			Операция.Сумма = ЗачетАванса;
		КонецЕсли; 
		
		Если Таб.ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ТВ";
				Операция.СодержаниеПроводки = "Зачтена предоплата";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетПоКоду("ВАЛ.62");
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.Договоры = ДоговорАванса;
				Операция.Сумма = ЗачетАванса*Таб.Курс/Таб.Кратность;
				Операция.Валюта = Таб.Валюта;
				Операция.ВалСумма = ЗачетАванса;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ЗачетАванса()

//*****************************************************************************
// ЗачестьАвансЗаКомиссионныйТовар(ЗачетАванса, ДоговорАванса)
//
Процедура ЗачестьАвансЗаКомиссионныйТовар(ЗачетАванса, ДоговорАванса)
	
	Если ЗачетАванса > 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "ТВ";
		Операция.СодержаниеПроводки = "Зачтен аванс";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = Таб.СчетАвансовПолученных;
		Операция.Дебет.Контрагенты = Контрагент;
		Операция.Дебет.Договоры = ДоговорАванса;
		Операция.Кредит.Счет = Таб.СчетРасчетовЗаКомиссионныеТовары;
		Операция.Кредит.Контрагенты = Контрагент;
		Операция.Кредит.Договоры = Таб.Договор;
		Если Таб.ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Если (АвансПоДоговору + АвансБезДоговора) = ЗачетАванса Тогда
					ЗачетАвансаРуб = АвансПоДоговоруРуб + АвансБезДоговораРуб;
				Иначе
					КурсЗачетаАванса = Окр((АвансПоДоговоруРуб + АвансБезДоговораРуб)/(АвансПоДоговору + АвансБезДоговора), 4, 1);
					ЗачетАвансаРуб = ЗачетАванса*КурсЗачетаАванса;
				КонецЕсли;
			Иначе
				ЗачетАвансаРуб = ЗачетАванса*Таб.Курс/Таб.Кратность;
			КонецЕсли;
			Операция.Сумма = ЗачетАвансаРуб;
		    Операция.Валюта = Таб.Валюта;
			Операция.ВалСумма = ЗачетАванса;
		Иначе
			Операция.Сумма = ЗачетАванса;
		КонецЕсли;
		
		Если Таб.ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ВЛ";
				Операция.СодержаниеПроводки = "Зачтена предоплата";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетПоКоду("ВАЛ.62");
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.Договоры = ДоговорАванса;
				Операция.Сумма = ЗачетАванса*Таб.Курс/Таб.Кратность;
				Операция.Валюта = Таб.Валюта;
				Операция.ВалСумма = ЗачетАванса;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ЗачестьАвансЗаКомиссионныйТовар()


////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()
	
	Сч62_1 = СчетПоКоду("62.1");
	Сч62_4 = СчетПоКоду("62.4");
	Сч62_2 = СчетПоКоду("62.2");
	Сч62_11 = СчетПоКоду("62.11");
	Сч62_44 = СчетПоКоду("62.44");
	Сч62_22 = СчетПоКоду("62.22");
	
	СчВАЛ_62 = СчетПоКоду("ВАЛ.62");

	
	ВыгрузитьТабличнуюЧасть(Таб);
	Таб.Свернуть("Договор,Курс", "Сумма"); 
	Таб.НоваяКолонка("СчетРасчетовСПокупателем");
	Таб.НоваяКолонка("СчетРасчетовЗаКомиссионныеТовары");
	Таб.НоваяКолонка("СчетАвансовПолученных");
    Таб.НоваяКолонка("ОплатаДоговора");
	Таб.НоваяКолонка("Валюта");
	Таб.НоваяКолонка("Курс");
	Таб.НоваяКолонка("Кратность");
	Таб.НоваяКолонка("СуммаРуб");
	
	глТаблицаСчетов.УдалитьСтроки();
	
	Таб.ВыбратьСтроки();
	Пока Таб.ПолучитьСтроку() = 1 Цикл
		Таб.СчетРасчетовСПокупателем = Сч62_1;
		Таб.СчетРасчетовЗаКомиссионныеТовары = Сч62_4;
		Таб.СчетАвансовПолученных = Сч62_2;
			
		Таб.ОплатаДоговора = Таб.Договор.ОплатаДоговора; // 1 - врублях, 2 - в валюте
		Если Таб.ОплатаДоговора = 2 Тогда
			Таб.Валюта = Таб.Договор.ВалютаДоговора;
			Таб.Курс = Таб.Валюта.Курс.Получить(ДатаДок);
			Таб.Кратность = Таб.Валюта.Кратность.Получить(ДатаДок);
			Таб.Кратность = ?(Таб.Кратность=0, 1, Таб.Кратность);
		КонецЕсли;
		
		Если Таб.ОплатаДоговора = 2 Тогда
			Таб.СчетРасчетовСПокупателем = Сч62_11;
			Таб.СчетРасчетовЗаКомиссионныеТовары = Сч62_44;
			Таб.СчетАвансовПолученных = Сч62_22; 
			Если ДатаДок >= '01.01.2008' Тогда
				СчетАвансовПолученныхПереоценка = СчВАЛ_62;
			Иначе
				СчетАвансовПолученныхПереоценка = Сч62_22;
			КонецЕсли;
			
			глТаблицаСчетов.НоваяСтрока();
			глТаблицаСчетов.Счет = СчетАвансовПолученныхПереоценка;
			глТаблицаСчетов.Субконто1 = Контрагент;
			глТаблицаСчетов.Субконто2 = Таб.Договор;
			глТаблицаСчетов.Субконто3 = "";
			глТаблицаСчетов.Валюта = Таб.Валюта;
			глТаблицаСчетов.Курс = Таб.Курс;
			Если ЗачитыватьАванс = 0 Тогда
				БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
				Если ПустоеЗначение(БезДоговора) = 0 Тогда
					глТаблицаСчетов.НоваяСтрока();
					глТаблицаСчетов.Счет = СчетАвансовПолученныхПереоценка;
					глТаблицаСчетов.Субконто1 = Контрагент;
					глТаблицаСчетов.Субконто2 = БезДоговора;
					глТаблицаСчетов.Субконто3 = "";
					глТаблицаСчетов.Валюта = Таб.Валюта;
					глТаблицаСчетов.Курс = Таб.Курс;
				КонецЕсли;
			КонецЕсли;
			
			Если ТипРасчетов = 2 Тогда
		        глТаблицаСчетов.НоваяСтрока();
				глТаблицаСчетов.Счет = Сч62_44;
				глТаблицаСчетов.Субконто1 = Контрагент;
				глТаблицаСчетов.Субконто2 = Таб.Договор;
				глТаблицаСчетов.Субконто3 = "";
				глТаблицаСчетов.Валюта = Таб.Валюта;
				глТаблицаСчетов.Курс = Таб.Курс;
				
			Иначе
				глТаблицаСчетов.НоваяСтрока();
				глТаблицаСчетов.Счет = Сч62_11;
				глТаблицаСчетов.Субконто1 = Контрагент;
				глТаблицаСчетов.Субконто2 = Таб.Договор;
				глТаблицаСчетов.Субконто3 = "";
				глТаблицаСчетов.Валюта = Таб.Валюта;
				глТаблицаСчетов.Курс = Таб.Курс;
		    КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	глПереоценкаСчетов(Контекст, глТаблицаСчетов,, 0);
	МетодОпределенияВыручки = Константа.МетодОпределенияВыручки.Получить(ДатаДок);
	СписокДоговоров = СоздатьОбъект("СписокЗначений");
	Таб.Выгрузить(СписокДоговоров,,, "Договор");
	
	БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
	Если (ЗачитыватьАванс = 0) и (ПустоеЗначение(БезДоговора) = 0) Тогда // зачитывать аванс без договора
		СписокДоговоров.ДобавитьЗначение(БезДоговора);
	КонецЕсли;
	
	БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
	БухИт.ИспользоватьСубконто(ВидыСубконто.Контрагенты, Контрагент, 2);
	БухИт.ИспользоватьСубконто(ВидыСубконто.Договоры, СписокДоговоров, 2);
	БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"62.2, 62.22");
	
	ТабБезДоговора = СоздатьОбъект("ТаблицаЗначений");
	Таб.Выгрузить(ТабБезДоговора,,, "Валюта");
	ТабБезДоговора.НоваяКолонка("Сумма", "Число");
	ТабБезДоговора.НоваяКолонка("СуммаРуб", "Число");
	ТабБезДоговора.Свернуть("Валюта", "Сумма, СуммаРуб");      	
	
	Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, БезДоговора) = 1 Тогда
		Если БухИт.ПолучитьСчет(, "62.22") = 1 Тогда
			ТабБезДоговора.ВыбратьСтроки();
			Пока ТабБезДоговора.ПолучитьСтроку() = 1 Цикл
				Если БухИт.ПолучитьВалюту(, ТабБезДоговора.Валюта) = 1 Тогда
					ТабБезДоговора.Сумма = БухИт.СКК("В");
					Если ДатаДок >= '01.01.2008' Тогда	
						ТабБезДоговора.СуммаРуб = БухИт.СКК("С");
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		НомСтроки = 0;
		Если ТабБезДоговора.НайтиЗначение(ПолучитьПустоеЗначение(), НомСтроки, "Валюта") = 1 Тогда
			Если БухИт.ПолучитьСчет(, "62.2") = 1 Тогда
			    ТабБезДоговора.УстановитьЗначение(НомСтроки, "Сумма", БухИт.СКК("С"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Таб.ВыбратьСтроки();
	Пока Таб.ПолучитьСтроку() = 1 Цикл
		АвансПоДоговору = 0;
		АвансПоДоговоруРуб = 0;
		Если Таб.Сумма = 0 Тогда
		    Продолжить;
			
		ИначеЕсли БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, Таб.Договор) = 1 Тогда
			Если Таб.ОплатаДоговора = 2 Тогда
				Если БухИт.ПолучитьСчет(, "62.22") = 1 Тогда
					Если БухИт.ПолучитьВалюту(, Таб.Валюта) = 1 Тогда
						АвансПоДоговору = БухИт.СКК("В");
						Если ДатаДок >= '01.01.2008' Тогда	
							АвансПоДоговоруРуб = БухИт.СКК("С");
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				Если БухИт.ПолучитьСчет(, "62.2") = 1 Тогда
				    АвансПоДоговору = БухИт.СКК("С");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
				
		НомСтроки = 0;
		Если ТабБезДоговора.НайтиЗначение(Таб.Валюта, НомСтроки, "Валюта") = 1 Тогда
			АвансБезДоговора = ТабБезДоговора.ПолучитьЗначение(НомСтроки, "Сумма");
			АвансБезДоговораРуб = ТабБезДоговора.ПолучитьЗначение(НомСтроки, "СуммаРуб");
		Иначе
			АвансБезДоговора = 0; 
			АвансБезДоговораРуб = 0;
		КонецЕсли;
		
		Если (АвансПоДоговору + АвансБезДоговора) < Таб.Сумма Тогда
			СообщениеДоговораАванса = ?(ЗачитыватьАванс=0,"договорам ""Без договора (Служебный..."" и """+Таб.Договор+""".","этому договору.");
			ТекстСообщения = "Сумма зачета аванса по договору """+Таб.Договор+""" превышает аванс, полученный по "+СообщениеДоговораАванса;
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, Таб.Сумма);
		ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, Таб.Сумма - ЗачетАвансаПоДоговору);
    	ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
		
		Если ТипРасчетов = 1 Тогда
			ЗачестьАванс(ЗачетАвансаПоДоговору, Таб.Договор);
			ЗачестьАванс(ЗачетАвансаБезДоговора, БезДоговора);
			
			ТекстСообщения = "=> Зачет аванса." + РазделительСтрок
			               + "   отгружено на сумму: "+Таб.Сумма+", зачтен аванс в размере: "+ЗачетАванса+ РазделительСтрок
						   + "   - определен аванс по договору "+Таб.Договор+" в сумме: "+АвансПоДоговору+", зачтен аванс в размере: "+ЗачетАвансаПоДоговору + РазделительСтрок;
			
			Если ЗачитыватьАванс = 0 Тогда
				ТекстСообщения = ТекстСообщения
							   + "   - определен аванс без указания договора в сумме: "+АвансБезДоговора+", зачтен аванс в размере: "+ЗачетАвансаБезДоговора;
			Иначе
				ТекстСообщения = ТекстСообщения
				               + "   - аванс без указания договора не учтен";
			КонецЕсли;
			глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	        				
		Иначе
			ЗачестьАвансЗаКомиссионныйТовар(ЗачетАвансаПоДоговору, Таб.Договор);
			ЗачестьАвансЗаКомиссионныйТовар(ЗачетАвансаБезДоговора, БезДоговора);
			
			ТекстСообщения = "=> Зачет аванса, поступившего за комиссионные товары:" + РазделительСтрок
			               + "   отгружено на сумму: "+Таб.Сумма+", зачтен аванс в размере: "+ЗачетАванса + РазделительСтрок
						   + "   - определен аванс по договору "+Таб.Договор+" в сумме: "+АвансПоДоговору+", зачтен аванс в размере: "+ЗачетАвансаПоДоговору + РазделительСтрок;
			
			Если ЗачитыватьАванс = 0 Тогда
				ТекстСообщения = ТекстСообщения
							   + "   - определен аванс без указания договора в сумме: "+АвансБезДоговора+", зачтен аванс в размере: "+ЗачетАвансаБезДоговора;
			Иначе
				ТекстСообщения = ТекстСообщения
				               + "   - аванс без указания договора не учтен";
			КонецЕсли;
			глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		КонецЕсли;
		
		Если ТабБезДоговора.НайтиЗначение(Таб.Валюта, НомСтроки, "Валюта") = 1 Тогда
			ТабБезДоговора.УстановитьЗначение(НомСтроки, "Сумма", АвансБезДоговора - ЗачетАвансаБезДоговора);
		КонецЕсли;
	КонецЦикла;
	
	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения()