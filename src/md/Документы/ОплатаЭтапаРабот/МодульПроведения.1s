////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем Контрагент, Договор;
Перем СчВР_1, СчВР_6, СчВР_11;
Перем ОплатаДоговора;
Перем Валюта;
Перем ВестиУчетРасчетовУЕ;
Перем КурсВалюты;
Перем Кратность;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//*****************************************************************************
// ЗачетАвансаПоСчетуВР(ЗачетАванса, ЗачетАвансаРуб, ДоговорАванса)
//
Процедура ЗачетАвансаПоСчетуВР(ЗачетАванса, ЗачетАвансаРуб, ДоговорАванса)
	
	Если ЗачетАванса > 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "ТВ";
		Операция.СодержаниеПроводки = "Зачтена предоплата";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		
		Если ОплатаДоговора = 2 Тогда
			Операция.Дебет.Счет = СчВР_11;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = ДоговорАванса;
		    Операция.Сумма = ЗачетАванса*КурсВалюты/Кратность;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			Операция.Дебет.Счет = СчВР_6;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = ДоговорАванса;
		    Операция.Сумма = ЗачетАвансаРуб;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса;
			
		Иначе
			Операция.Дебет.Счет = СчВР_1;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = ДоговорАванса;
			Операция.Сумма = ЗачетАванса;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ЗачетАвансаПоСчетуВР()


////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()
	
	Сч46 = СчетПоКоду("46");
	Сч62_1 = СчетПоКоду("62.1");
	Сч62_6 = СчетПоКоду("62.6");
	Сч62_11 = СчетПоКоду("62.11");
	Сч62_2 = СчетПоКоду("62.2");
	Сч62_7 = СчетПоКоду("62.7");
	Сч62_22 = СчетПоКоду("62.22");
	Сч90_1_1 = СчетПоКоду("90.1.1");
	СчВР_1 = СчетПоКоду("ВР.1");
	СчВР_6 = СчетПоКоду("ВР.6");
	СчВР_11 = СчетПоКоду("ВР.11");
	
	Сч68_5 = СчетПоКоду("68.5");
	Сч90_6 = СчетПоКоду("90.6");
	
	глТаблицаСчетов.УдалитьСтроки();
	
	Если ДокументВыполненияЭтапаРабот.ЗавершитьРаботы <> 1 Тогда
		ТекстСообщения = "Выполненный этап работ "+ДокументВыполненияЭтапаРабот+" не является неоплаченным.";
		глНеПроводить(Контекст, ТекстСообщения, ДокументВыполненияЭтапаРабот);
		Возврат;
			
	ИначеЕсли ДокументВыполненияЭтапаРабот.Проведен() = 0 Тогда
		ТекстСообщения = "Акт о выполнении этапа работ "+ДокументВыполненияЭтапаРабот+" не проведен.";
		глНеПроводить(Контекст, ТекстСообщения, ДокументВыполненияЭтапаРабот);
		Возврат;
		
	Иначе
		ПодчДок = СоздатьОбъект("Документ");
		ПодчДок.ВыбратьПодчиненныеДокументы(,ДатаДок, ДокументВыполненияЭтапаРабот);
		Пока ПодчДок.ПолучитьДокумент() = 1 Цикл
			Если ПодчДок.Вид() = "ЗавершениеРабот" Тогда

			ИначеЕсли ПодчДок.Вид() = "ОплатаЭтапаРабот" Тогда
				Если ПодчДок.ТекущийДокумент() <> ТекущийДокумент() Тогда
					ТекстСообщения = "По Акту о выполнении этапа работ "+ДокументВыполненияЭтапаРабот+" работы оплачены документом "+ПодчДок.ТекущийДокумент();
					глНеПроводить(Контекст, ТекстСообщения, ДокументВыполненияЭтапаРабот);
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Контрагент = ДокументВыполненияЭтапаРабот.Контрагент;
	Договор = ДокументВыполненияЭтапаРабот.Договор;
	ДатаЭтапа = ДокументВыполненияЭтапаРабот.ДатаДок;
	
	Если Договор.ВалютаДоговора.Выбран() = 1 Тогда
	    ЦеныВДоговоре = 2;
		ВестиУчетРасчетовУЕ = Договор.ВестиУчетРасчетовУЕ;
		ОплатаДоговора =  Договор.ОплатаДоговора;
		Валюта = Договор.ВалютаДоговора;
		КурсВалюты = Валюта.Курс.Получить(ДатаДок);
		Кратность = Валюта.Кратность.Получить(ДатаДок);
		Кратность = ?(Кратность = 0, 1, Кратность);
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			Кратность = Кратность * 100 / (100 + Договор.ПроцентКорректировкиКурсаУЕ);
		КонецЕсли;
		
	Иначе
		ЦеныВДоговоре = 1;
		ВестиУчетРасчетовУЕ = 0;
	КонецЕсли;
	
	Если ОплатаДоговора = 2 Тогда
	    глТаблицаСчетов.НоваяСтрока();
		глТаблицаСчетов.Счет = СчВР_11;
		глТаблицаСчетов.Субконто1 = Контрагент;
		Если ВариантОплатыРабот = 1 Тогда
			глТаблицаСчетов.Субконто2 = Договор;
		Иначе
			глТаблицаСчетов.Субконто2 = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
		КонецЕсли;
		глТаблицаСчетов.Субконто3 = "";
		глТаблицаСчетов.Валюта = Валюта;
		глТаблицаСчетов.Курс = КурсВалюты;
	КонецЕсли;
	глПереоценкаСчетов(Контекст, глТаблицаСчетов, , 0);
	
	ТаблицаРеализации = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаРеализации.НоваяКолонка("Работа");
	ТаблицаРеализации.НоваяКолонка("ВидНоменклатуры");
	ТаблицаРеализации.НоваяКолонка("СтавкаНДС");
	ТаблицаРеализации.НоваяКолонка("СтавкаНП");
	ТаблицаРеализации.НоваяКолонка("Всего", "Число", 15, 2);
	ТаблицаРеализации.НоваяКолонка("ВалВсего", "Число", 15, 2);
	ТаблицаРеализации.НоваяКолонка("НП", "Число", 15, 2);
	ТаблицаРеализации.НоваяКолонка("НДС", "Число", 15, 2);
	ТаблицаРеализации.НоваяКолонка("Количество", "Число");
	
	ТаблицаРеализации.НоваяКолонка("ВыручкаРуб", "Число", 15, 2);
	ТаблицаРеализации.НоваяКолонка("ВыручкаНУ", "Число", 15, 2);
	ТаблицаРеализации.НоваяКолонка("СуммоваяРазница", "Число", 15, 2);
	
	Опер = СоздатьОбъект("Операция");
	СчН06_01 = СчетПоКоду("Н06.01");

	Опер.НайтиОперацию(ДокументВыполненияЭтапаРабот);
	ДокументВыполненияЭтапаРабот.ВыбратьСтроки();
	Пока ДокументВыполненияЭтапаРабот.ПолучитьСтроку() = 1 Цикл
		Работа = ДокументВыполненияЭтапаРабот.Работа;
		ТаблицаРеализации.НоваяСтрока();
		ТаблицаРеализации.Работа = Работа;
		ТаблицаРеализации.ВидНоменклатуры = Работа.ВидНоменклатуры;
		ТаблицаРеализации.СтавкаНДС = глСтавкаНалога(ДокументВыполненияЭтапаРабот, "НДС");
		ТаблицаРеализации.СтавкаНП = глСтавкаНалога(ДокументВыполненияЭтапаРабот, "НП");
		ТаблицаРеализации.ВидНоменклатуры = Работа.ВидНоменклатуры;
		
		Если (ЦеныВДоговоре = 2) и (ВестиУчетРасчетовУЕ = 0) Тогда
			ТаблицаРеализации.Всего = ДокументВыполненияЭтапаРабот.Всего*КурсВалюты/Кратность;
			ТаблицаРеализации.НП = ДокументВыполненияЭтапаРабот.НП*КурсВалюты/Кратность;
			ТаблицаРеализации.НДС = ДокументВыполненияЭтапаРабот.НДС*КурсВалюты/Кратность;
			Если ОплатаДоговора =2 Тогда
			    ТаблицаРеализации.ВалВсего = ДокументВыполненияЭтапаРабот.Всего;
			КонецЕсли;
			
		Иначе
			ТаблицаРеализации.Всего = ДокументВыполненияЭтапаРабот.Всего;
			ТаблицаРеализации.НП = ДокументВыполненияЭтапаРабот.НП;
			ТаблицаРеализации.НДС = ДокументВыполненияЭтапаРабот.НДС;
		КонецЕсли;
		
		Опер.ВыбратьПроводки();
		Пока Опер.ПолучитьПроводку() = 1 Цикл
		    Если Опер.Кредит.Счет = СчН06_01 Тогда
				Если Опер.Кредит.Субконто(ВидыСубконто.Номенклатура) = Работа Тогда
				    ТаблицаРеализации.ВыручкаНУ = ТаблицаРеализации.ВыручкаНУ + Опер.Сумма;
				КонецЕсли;
		    КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ЗачетАвансаПоДоговору = 0;
	ЗачетАвансаБезДоговора = 0;
	ЗачетАвансаПоДоговоруРуб = 0;
    ЗачетАвансаБезДоговораРуб = 0;
	
	АвансБезДоговора = 0;
	АвансБезДоговораРуб = 0;
	АвансПоДоговору = 0;
	АвансПоДоговоруРуб = 0;
	
	СписокДоговоров = СоздатьОбъект("СписокЗначений");
	СписокДоговоров.ДобавитьЗначение(Договор);
	БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
	Если (ВариантОплатыРабот = 0) и (ПустоеЗначение(БезДоговора) = 0) Тогда //работы были оплачены без указания договора
		СписокДоговоров.ДобавитьЗначение(БезДоговора);
	КонецЕсли;

	БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
	БухИт.ИспользоватьСубконто(ВидыСубконто.Контрагенты, Контрагент, 2);
	БухИт.ИспользоватьСубконто(ВидыСубконто.Договоры, СписокДоговоров, 2);
	Если ОплатаДоговора = 2 Тогда
		БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "62.22, ВР.11",, Валюта,,, "В");
		
	ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
		БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "62.7, ВР.6",, Валюта,,, "СВ");
		
	Иначе
		БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "62.2, ВР.1",,,,, "С");
	КонецЕсли;
	
	ЗачтенныйАвансПоДоговору = 0;
	ЗачтенныйАвансПоДоговоруРуб = 0;
	ЗачтенныйАвансБезДоговора = 0;
	ЗачтенныйАвансБезДоговораРуб = 0;
	
	Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, Договор) = 1 Тогда
		Если ОплатаДоговора = 2 Тогда
			Если БухИт.ПолучитьСчет(, "62.22") = 1 Тогда
				АвансПоДоговору = БухИт.СКК("В");
			КонецЕсли;
			Если БухИт.ПолучитьСчет(, "ВР.11") = 1 Тогда
				ЗачтенныйАвансПоДоговору = БухИт.СКД("В");
			КонецЕсли;
		
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			Если БухИт.ПолучитьСчет(, "62.7") = 1 Тогда
				АвансПоДоговору = БухИт.СКК("В");
				АвансПоДоговоруРуб = БухИт.СКК("С");
			КонецЕсли;
			Если БухИт.ПолучитьСчет(, "ВР.6") = 1 Тогда
				ЗачтенныйАвансПоДоговору = БухИт.СКД("В");
				ЗачтенныйАвансПоДоговоруРуб = БухИт.СКД("С");
			КонецЕсли;
		
		Иначе
			Если БухИт.ПолучитьСчет(, "62.2") = 1 Тогда
				АвансПоДоговору = БухИт.СКК("С");
			КонецЕсли;
			Если БухИт.ПолучитьСчет(, "ВР.1") = 1 Тогда
				ЗачтенныйАвансПоДоговору = БухИт.СКД("С");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, БезДоговора) = 1 Тогда
		Если ОплатаДоговора = 2 Тогда
			Если БухИт.ПолучитьСчет(, "62.22") = 1 Тогда
				АвансБезДоговора = БухИт.СКК("В");
			КонецЕсли;
			Если БухИт.ПолучитьСчет(, "ВР.11") = 1 Тогда
				ЗачтенныйАвансБезДоговора = БухИт.СКД("В");
			КонецЕсли;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			Если БухИт.ПолучитьСчет(, "62.7") = 1 Тогда
				АвансБезДоговора = БухИт.СКК("В");
				АвансБезДоговораРуб = БухИт.СКК("С");
			КонецЕсли;
			Если БухИт.ПолучитьСчет(, "ВР.6") = 1 Тогда
				ЗачтенныйАвансБезДоговора = БухИт.СКД("В");
				ЗачтенныйАвансБезДоговораРуб = БухИт.СКД("С");
			КонецЕсли;
			
		Иначе
			Если БухИт.ПолучитьСчет(, "62.2") = 1 Тогда
				АвансБезДоговора = БухИт.СКК("С");
			КонецЕсли;
			Если БухИт.ПолучитьСчет(, "ВР.1") = 1 Тогда
				ЗачтенныйАвансБезДоговора = БухИт.СКД("С");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	АвансПоДоговору = АвансПоДоговору - ЗачтенныйАвансПоДоговору;
	АвансБезДоговора = АвансБезДоговора - ЗачтенныйАвансБезДоговора;
	АвансПоДоговоруРуб = АвансПоДоговоруРуб - ЗачтенныйАвансПоДоговоруРуб;
	АвансБезДоговораРуб = АвансБезДоговораРуб - ЗачтенныйАвансБезДоговораРуб;
	
	Если ОплатаДоговора = 2 Тогда
	    ИтогВсего = ТаблицаРеализации.Итог("ВалВсего");
	
	Иначе
		ИтогВсего = ТаблицаРеализации.Итог("Всего");
	КонецЕсли;
		
	Если (АвансПоДоговору + АвансБезДоговора) < ИтогВсего Тогда
		РезультатПроверки = 0;
		ТекстСообщения = "Для отражения оплаты этапа работ недостаточно аванса.";
		глНеПроводить(Контекст, ТекстСообщения, ДокументВыполненияЭтапаРабот);
		Возврат;
	КонецЕсли;
	
    Если ВестиУчетРасчетовУЕ = 1 Тогда
		ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, ИтогВсего);
		ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, ИтогВсего - ЗачетАвансаПоДоговору);
		
		Аванс = АвансПоДоговору + АвансБезДоговора;
		ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
		
		Если ЗачетАванса = Аванс Тогда
		    ЗачетАвансаПоДоговоруРуб = АвансПоДоговоруРуб;
			ЗачетАвансаБезДоговораРуб = АвансБезДоговораРуб;
			
		ИначеЕсли ЗачетАванса > АвансПоДоговору Тогда
			ЗачетАвансаПоДоговоруРуб = АвансПоДоговоруРуб;
			ЗачетАвансаБезДоговораРуб = Окр(ЗачетАвансаБезДоговора * Окр(АвансБезДоговораРуб / ?(АвансБезДоговора = 0, 1, АвансБезДоговора), 4, 1), 2, 1);
			
		Иначе
			ЗачетАвансаПоДоговоруРуб = Окр(ЗачетАвансаПоДоговору * Окр(АвансПоДоговоруРуб / ?(АвансПоДоговору = 0, 1, АвансПоДоговору), 4, 1), 2, 1);
			ЗачетАвансаБезДоговораРуб = 0;
		КонецЕсли;
	
	Иначе
		ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, ИтогВсего);
		ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, ИтогВсего - ЗачетАвансаПоДоговору);
	КонецЕсли;
	
	СуммаЗачтенногоАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
	Если ВестиУчетРасчетовУЕ = 1 Тогда
		СуммаЗачтенногоАвансаРуб = ЗачетАвансаПоДоговоруРуб + ЗачетАвансаБезДоговораРуб;
	КонецЕсли;
	
	ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
	ЗачетАвансаПоСчетуВР(ЗачетАвансаПоДоговору, ЗачетАвансаПоДоговоруРуб, Договор);
	ЗачетАвансаПоСчетуВР(ЗачетАвансаБезДоговора, ЗачетАвансаБезДоговораРуб, БезДоговора);
    
	ТекстСообщения = "=> Зачет аванса:" + РазделительСтрок
	               + "   выполнены работы на сумму: "+ИтогВсего+", зачтен аванс в размере: "+ЗачетАванса + РазделительСтрок
				   + "   - определен аванс по договору "+Договор+" в сумме: "+АвансПоДоговору+", зачтен аванс в размере: "+ЗачетАвансаПоДоговору + РазделительСтрок;
	
	Если ВариантОплатыРабот = 0 Тогда
		ТекстСообщения = ТекстСообщения
					   + "   - определен аванс без указания договора в сумме: "+АвансБезДоговора+", зачтен аванс в размере: "+ЗачетАвансаБезДоговора;
	Иначе
		ТекстСообщения = ТекстСообщения
		               + "   - аванс без указания договора не учтен";
	КонецЕсли;
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	
	РаспределеноАвансаРуб = 0;
	КолСтрокТабОтгрузки = ТаблицаРеализации.КоличествоСтрок();
	КурсАванса = ?(СуммаЗачтенногоАванса = 0, 0, Окр(СуммаЗачтенногоАвансаРуб / СуммаЗачтенногоАванса, 4, 1));
	КурсВыполненияРабот = ?(СуммаЗачтенногоАванса = 0, 0, Окр(ДокументВыполненияЭтапаРабот.Курс/?((Кратность = 0) или (ПустоеЗначение(Кратность) = 1), 1, Кратность), 4, 1));
	ТаблицаРеализации.ВыбратьСтроки();
	Пока ТаблицаРеализации.ПолучитьСтроку() = 1 Цикл
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			Если ПустоеЗначение(ИтогВсего) = 0  Тогда
			    ЧастьСуммыЗачтенногоАванса = ТаблицаРеализации.Всего;
				ЧастьСуммыЗачтенногоАвансаРуб = Окр(СуммаЗачтенногоАвансаРуб * ТаблицаРеализации.Всего / ИтогВсего, 2, 1);
				
			Иначе
				ЧастьСуммыЗачтенногоАванса = 0;
				ЧастьСуммыЗачтенногоАвансаРуб = 0;
			КонецЕсли;
			
			РаспределеноАвансаРуб = РаспределеноАвансаРуб + ЧастьСуммыЗачтенногоАвансаРуб;
			
			Если КолСтрокТабОтгрузки = ТаблицаРеализации.НомерСтроки Тогда
				Если РаспределеноАвансаРуб <> СуммаЗачтенногоАвансаРуб Тогда
					ЧастьСуммыЗачтенногоАвансаРуб = ЧастьСуммыЗачтенногоАвансаРуб + СуммаЗачтенногоАвансаРуб - РаспределеноАвансаРуб;
				КонецЕсли;
			КонецЕсли;
			    
			ТаблицаРеализации.ВыручкаРуб = ЧастьСуммыЗачтенногоАвансаРуб;
			
            ВыручкаПоДокументуОплаты = Окр(ЧастьСуммыЗачтенногоАванса * КурсАванса, 2, 1);
			ВыручкаПоДокументуВыполненияЭтапаРабот = Окр(ТаблицаРеализации.Всего * КурсВыполненияРабот, 2, 1);
			СуммоваяРазница = ВыручкаПоДокументуОплаты - ВыручкаПоДокументуВыполненияЭтапаРабот;
			Конт = глВзятьКонтекст(Контекст);
			глУчестьСуммовыеРазницы(Конт, ДокументВыполненияЭтапаРабот.Договор, ДокументВыполненияЭтапаРабот, , , , , , , , , СуммоваяРазница, );

		КонецЕсли;
		
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "ТВ";
		Операция.СодержаниеПроводки = "Учтена выручка";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = Сч46;
		Операция.Дебет.Номенклатура = ТаблицаРеализации.Работа;
		Операция.Дебет.Контрагенты = Контрагент;
		Операция.Дебет.Договоры = Договор;
		Операция.Кредит.Счет = Сч90_1_1;
		Операция.Кредит.ВидыНоменклатуры= ТаблицаРеализации.ВидНоменклатуры;
		Если ВерсияОбъекта >= "7.70.421" Тогда
			Операция.Кредит.СтавкиНДС = ТаблицаРеализации.СтавкаНДС;
			Операция.Кредит.СтавкиНП = ТаблицаРеализации.СтавкаНП;
		КонецЕсли;
		
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			Операция.Сумма = ТаблицаРеализации.ВыручкаРуб;
		Иначе
			Операция.Сумма = ТаблицаРеализации.Всего;
		КонецЕсли;
	КонецЦикла;
	
	Если (ВерсияОбъекта >= "7.70.421") и (ТаблицаРеализации.Итог("НП") > 0) Тогда
		ТаблицаРеализации.Свернуть("ВидНоменклатуры, СтавкаНП","НП");
		ТаблицаРеализации.ВыбратьСтроки();
		Пока ТаблицаРеализации.ПолучитьСтроку() = 1 Цикл
			Если ТаблицаРеализации.НП > 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ТВ";
				Операция.СодержаниеПроводки = "Начислен НП";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = Сч90_6;
				Операция.Дебет.ВидыНоменклатуры = ТаблицаРеализации.ВидНоменклатуры;
				Операция.Дебет.СтавкиНП = ТаблицаРеализации.СтавкаНП;
				Операция.Кредит.Счет = Сч68_5;
				Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
				Если ВестиУчетРасчетовУЕ = 1 Тогда
				    Операция.Сумма = ТаблицаРеализации.НП*КурсВалюты/Кратность;
				Иначе
					Операция.Сумма = ТаблицаРеализации.НП;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения()