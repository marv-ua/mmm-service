//_____________________________________________________________________________
Процедура ОбработкаПроведения()

	Если ФормироватьПроводки = 0 Тогда
		ТекстСообщения = "Документ проведен.";
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		глПриПроведении(Контекст);
		Возврат;

	ИначеЕсли КоррСчет.Выбран() = 0 Тогда
		ТекстСообщения = "Не указан корреспондирующий счет";
		глНеПроводить(Контекст, ТекстСообщения);
		Возврат;
	КонецЕсли;  
	
	ПереченьСтатейЗатрат = глПолучитьПереченьСтатейЗатрат(ДатаДок);

	Сч62_1 = СчетПоКоду("62.1");
	Сч62_4 = СчетПоКоду("62.4");
	Сч62_11 = СчетПоКоду("62.11"); 
	Сч68_2 = СчетПоКоду("68.2");
	Сч68_5 = СчетПоКоду("68.5");
	Сч76_5 = СчетПоКоду("76.5");
	Сч76_Н_4 = СчетПоКоду("76.Н.4");
	Сч90_1_1 = СчетПоКоду("90.1.1");
	Сч90_3 = СчетПоКоду("90.3");
	Сч90_6 = СчетПоКоду("90.6");
	
	СчУЕ_62 = СчетПоКоду("УЕ.62");
	СчВАЛ_60 = СчетПоКоду("ВАЛ.60");
	СчВАЛ_62 = СчетПоКоду("ВАЛ.62");


	МетодОпределенияВыручки = Константа.МетодОпределенияВыручки.Получить(ДатаДок);

	Если Валютный = 2 Тогда
		СчетКассы = СчетПоКоду("50.11");

		Курс = Валюта.Курс.Получить(ДатаДок);
		Кратность = Валюта.Кратность.Получить(ДатаДок);
		Кратность = ?(Кратность=0,1,Кратность);

		глТаблицаСчетов.УдалитьСтроки();

		глТаблицаСчетов.НоваяСтрока();
		глТаблицаСчетов.Счет = СчетКассы;
		глТаблицаСчетов.Субконто1 = ДвижениеДенежныхСредств;
		глТаблицаСчетов.Субконто2 = "";
		глТаблицаСчетов.Субконто3 = "";
		глТаблицаСчетов.Валюта = Валюта;

		Если КоррСчет.Валютный = 1 Тогда
			глТаблицаСчетов.НоваяСтрока();
			глТаблицаСчетов.Счет = КоррСчет;
			глТаблицаСчетов.Субконто1 = Субконто1;
			глТаблицаСчетов.Субконто2 = Субконто2;
			глТаблицаСчетов.Субконто3 = Субконто3;
			глТаблицаСчетов.Валюта = Валюта;
		КонецЕсли;
		глПереоценкаСчетов(Контекст, глТаблицаСчетов);

	Иначе
		СчетКассы = СчетПоКоду("50.1");
	КонецЕсли;

	Если Сумма > 0 Тогда
		
		ДокПоставки = ДокументПоставки;
		
		БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИт.Рассчитать(,ТекущийДокумент(), "62.1,62.4,62.6,62.8,76.5,76.6,УЕ.62");
		Конт = глВзятьКонтекст(Контекст);
		ВалютаДоговора = ""; СуммаОплатыВал = 0; СуммаОплатыРуб = 0; СуммоваяРазница = 0; ВыбКурс = КурсОплаты;
		глУчестьСуммовыеРазницы(Конт, Субконто2, ДокПоставки, БухИт, КоррСчет, 1, Сумма, ВыбКурс, ВалютаДоговора, СуммаОплатыВал, СуммаОплатыРуб, СуммоваяРазница, ПереченьСтатейЗатрат);
		
		Если (РозничнаяВыручка = 1) Тогда
			Если Константа.РозницаОблагаетсяЕНВД.Получить(ДатаДок) = Перечисление.Булево.Да Тогда
				КоррСчет = СчетПоКоду("90.1.2");
			Иначе
				КоррСчет = СчетПоКоду("90.1.1");
			КонецЕсли;
		КонецЕсли;
		
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "БК";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.СодержаниеПроводки = "Приход:"+Приложение;
		Операция.Дебет.Счет = СчетКассы;
		Операция.Дебет.Субконто(1, ДвижениеДенежныхСредств);
		Операция.Кредит.Счет = КоррСчет;
		Операция.Кредит.Субконто(1,Субконто1);
		Операция.Кредит.Субконто(2,Субконто2);
		Операция.Кредит.Субконто(3,Субконто3);

		Если Валютный = 2 Тогда
			Операция.Валюта = Валюта;
			Операция.ВалСумма = Сумма;
			СуммаПоступления = Окр(Сумма*Курс/Кратность,2,1);
			
		ИначеЕсли (ПустоеЗначение(ВалютаДоговора) = 0) и (КоррСчет.Валютный = 1) Тогда
			Операция.Валюта = ВалютаДоговора;
			
			Если Сумма = СуммаОплатыРуб Тогда
				Операция.ВалСумма = СуммаОплатыВал;
				СуммаПоступления = СуммаОплатыРуб;
				
			Иначе
				Операция.ВалСумма = ?(ВыбКурс = 0, 0, Сумма / ВыбКурс);
				СуммаПоступления = Сумма;
			КонецЕсли;
	        
		Иначе
			СуммаПоступления = Сумма;
		КонецЕсли;
		Операция.Сумма = СуммаПоступления;
		Операция.СуммаОперации = Операция.Сумма;

		Если КоррСчет.Количественный = 1 Тогда
			Операция.Количество = Количество;
		КонецЕсли;
		
		Если РозничнаяВыручка = 0 Тогда
			
			// Отражение операций в налоговом учете.
			ОбъектыАналитикиНУ = глСчетИАналитикаДоходовНУ(Контекст, КоррСчет, Субконто1, Субконто2, Субконто3);
			
			// Если опредлен налоговый счет, то необходимо
			// сформировать проводку для целей налогового учета.
			СчетНУ = ОбъектыАналитикиНУ.Получить("Счет");
			Если ПустоеЗначение(СчетНУ) = 0 Тогда
				СуммаРасходов = Операция.Сумма;
				
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "НУ";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Кредит.Счет = СчетНУ;
				Для СчетчикЦикла = 1 По СчетНУ.КоличествоСубконто() Цикл
					ИдентификаторСубконто = СчетНУ.ВидСубконто(СчетчикЦикла,,).Идентификатор();
					Операция.Кредит.Субконто(СчетчикЦикла, ОбъектыАналитикиНУ.Получить(ИдентификаторСубконто));
				КонецЦикла;
				Операция.СодержаниеПроводки = "Приход:"+Приложение;
				Операция.Сумма = СуммаРасходов;
				Если СчетНУ.Количественный = 1 Тогда
					Операция.Количество = Количество;
				КонецЕсли;
			КонецЕсли;
			
			Если (НП > 0) и (ВерсияОбъекта >= "7.70.421") Тогда
				Если (КоррСчет = Сч62_1) или (КоррСчет = Сч62_4)  или (КоррСчет = Сч76_5) Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.СодержаниеПроводки = "Начислен НП";
					Операция.НомерЖурнала = "БК";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = Сч76_Н_4;
					Операция.Дебет.Контрагенты = Субконто1;
					Операция.Дебет.Договоры = Субконто2;
					Операция.Кредит.Счет = Сч68_5;
					Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
					Если Валютный = 2 Тогда
						Операция.Сумма = Окр(НП*Курс/Кратность,2,1);
					Иначе
						Операция.Сумма = НП;
					КонецЕсли;
					
				ИначеЕсли КоррСчет = Сч90_1_1 Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.СодержаниеПроводки = "Начислен НП";
					Операция.НомерЖурнала = "БК";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = Сч90_6;
					Операция.Дебет.ВидыНоменклатуры = Субконто1;
					Операция.Дебет.СтавкиНП = СтавкаНП;
					Операция.Кредит.Счет = Сч68_5;
					Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
					Если Валютный = 2 Тогда
						Операция.Сумма = Окр(НП*Курс/Кратность,2,1);
					Иначе
						Операция.Сумма = НП;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если ТорговаяТочка.Выбран() = 0 Тогда
				ТекстСообщения = "Не указана розничная торговая точка";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "БК";
			Операция.СодержаниеПроводки = "Розничная выручка";
			Операция.Дебет.Счет = СчетПоКоду("РВ.1");
			Операция.Дебет.Субконто(1, ТорговаяТочка);
			Операция.Сумма = Сумма - НДС - НП;
			Если НДС > 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = "Начислен НДС";
				Операция.НомерЖурнала = "БК";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = Сч90_3;
				Операция.Дебет.СтавкиНДС = СтавкаНДС;
				Операция.Кредит.Счет = Сч68_2;
				Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
				Операция.Сумма = НДС;
				
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "БК";
				Операция.СодержаниеПроводки = "НДС с розничной выручки";
				Операция.Дебет.Счет = СчетПоКоду("РВ.2");
				Операция.Дебет.Субконто(1, ТорговаяТочка);
				Операция.Дебет.Субконто(2, СтавкаНДС);
				Операция.Сумма = НДС;
			КонецЕсли;
			Если НП > 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = "Начислен НП";
				Операция.НомерЖурнала = "БК";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = Сч90_6;
				Операция.Дебет.СтавкиНП = СтавкаНП;
				Операция.Кредит.Счет = Сч68_5;
				Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
				Операция.Сумма = НП;
					
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "БК";
				Операция.СодержаниеПроводки = "НП с розничной выручки";
				Операция.Дебет.Счет = СчетПоКоду("РВ.3");
				Операция.Дебет.Субконто(1, ТорговаяТочка);
				Операция.Дебет.Субконто(2, СтавкаНП);
				Операция.Сумма = НП;
			КонецЕсли;
		КонецЕсли; 
		
		//********************************************************************
		//Проводки по расчетам в у.е. 	
		Если ПустоеЗначение(ДокументПоставки) = 0 Тогда			
			Если ДокументПоставки.Вид() <> "Счет" Тогда
				Если ДатаДок >= '01.01.2007' Тогда
					Если Найти("62.6,76.6",КоррСчет.Код) > 0 Тогда 
						//Расчет курсовых разниц 	            
						глТаблицаСчетов.УдалитьСтроки();
						глТаблицаСчетов.НоваяСтрока();
						глТаблицаСчетов.Счет = КоррСчет;
						глТаблицаСчетов.Субконто1 = Субконто1;
						глТаблицаСчетов.Субконто2 = Субконто2;
						глТаблицаСчетов.Субконто3 = Субконто3;
						глТаблицаСчетов.Валюта = Субконто2.ВалютаДоговора;
						глПереоценкаСчетов(Контекст, глТаблицаСчетов);
						
					КонецЕсли;                                        				
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Найти("62.6,62.8,76.6",КоррСчет.Код) > 0 Тогда
			ТекстСообщения = "=> Не указан документ поставки. Суммовые разницы по расчетам в условных единицах с контрагентом: " + Субконто1 + " по договору: " + Субконто2 + " не рассчитаны."; 	
			глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), -1); 
		КонецЕсли; 
		//********************************************************************
		
		//********************************************************************
		//Проводки по расчетам в валюте 	
		Если ДатаДок >= '01.01.2008' Тогда	
			Если Валютный = 2 Тогда
				Если Найти("60.22,62.22",КоррСчет.Код) > 0 Тогда 
					глТаблицаСчетов.УдалитьСтроки(); 
					
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ВЛ";
					Операция.СодержаниеПроводки = "Аванс в валюте";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Валюта = Валюта;
					
					//Расчет курсовых разниц 	      
					глТаблицаСчетов.НоваяСтрока();
					глТаблицаСчетов.Субконто1 = Субконто1;                
					глТаблицаСчетов.Субконто2 = Субконто2;
					глТаблицаСчетов.Субконто3 = Субконто3;
					глТаблицаСчетов.Валюта = Субконто2.ВалютаДоговора;
					Если КоррСчет.Код = "62.22" Тогда
						глТаблицаСчетов.Счет = СчВАЛ_62;
						Операция.Кредит.Счет = СчВАЛ_62;
					Иначе
						глТаблицаСчетов.Счет = СчВАЛ_60;						
						Операция.Кредит.Счет = СчВАЛ_60;
					КонецЕсли;
					
					Операция.Кредит.Субконто(1, Субконто1);
					Операция.Кредит.Субконто(2, Субконто2);
					Операция.Кредит.Субконто(3, Субконто3);

					СуммаОперацииВал = Сумма;
					СуммаОперацииРуб = Окр(Сумма*Курс/Кратность,2,1);
					
					
					Операция.Сумма = СуммаОперацииРуб;
					Операция.ВалСумма = СуммаОперацииВал;
					
					Если глТаблицаСчетов.количествоСтрок() <> 0 Тогда
						глПереоценкаСчетов(Контекст, глТаблицаСчетов, 1);
						глТаблицаСчетов.УдалитьСтроки();
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		//********************************************************************

		
	КонецЕсли;  
	
	Если (ПустоеЗначение(Операция.Содержание) = 1) и (Операция.СуммаОперации = 0) Тогда
	Операция.Содержание = "Поступл.в кассу: "+Основание;
		Если Валютный = 2 Тогда
			Курс = Валюта.Курс.Получить(ДатаДок);
			Кратность = Валюта.Кратность.Получить(ДатаДок);
			Кратность = ?(Кратность=0,1,Кратность);
			СуммаРКО = Окр(Сумма*Курс/Кратность,2,1);
		Иначе
			Валюта = "";
			СуммаРКО = Сумма;
		КонецЕсли;
		Операция.СуммаОперации = СуммаРКО;
	КонецЕсли;  
    
	Операция.Записать(); 
	
	ТекстСообщения = "Учтено поступление денежных средств в кассу.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры
//_____________________________________________________________________________ 