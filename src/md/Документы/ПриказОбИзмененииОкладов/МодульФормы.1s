Перем НачальнаяДатаДокумента; 
Перем Новый;
Перем СписокДействий;

//******************************************************************************
// ПриВыбореСотрудника()
//
Процедура ПриВыбореСотрудника()
	Если Сотрудник.Выбран()=1 Тогда
	    Оклад = Сотрудник.Оклад.Получить(ДатаДок);
	КонецЕсли;
КонецПроцедуры //ПриВыбореСотрудника

//******************************************************************************
// ПриСменеДаты()
//
Процедура ПриСменеДаты()
	Если КоличествоСтрок() > 0 Тогда
		Если Вопрос("Обновить оклады в Приказе на дату документа?","Да+Нет")="Да" Тогда
			ВыбратьСтроки();
			Пока ПолучитьСтроку()=1 Цикл
				Если Сотрудник.Выбран()=1 Тогда
					Оклад = Сотрудник.Оклад.Получить(ДатаДок);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры //ПриСменеДаты

//******************************************************************************
// Заполнить()
//
Процедура Заполнить()
	Если ВыбратьСтроки() = 0 Тогда
	ИначеЕсли Вопрос("Перед заполнением таблица будет очищена,"+РазделительСтрок+"а существующие строки удалены. Продолжать?","Да+Нет") = "Нет" Тогда
		Возврат;
	Иначе
		УдалитьСтроки();
	КонецЕсли;

	Сотр = СоздатьОбъект("Справочник.Сотрудники");
    Сотр.ПорядокНаименований();
	Сотр.ВыбратьЭлементы(0);
	Пока Сотр.ПолучитьЭлемент() = 1 Цикл
		Если Сотр.ЭтоГруппа() = 1 Тогда
			Продолжить;
		КонецЕсли;
		
		Если глСотрудникЧислитсяРаботающим(Сотр, ДатаДок) = 0 Тогда
				Продолжить;
		КонецЕсли;
		
		НоваяСтрока();
		Сотрудник = Сотр.ТекущийЭлемент();
		Оклад = Сотр.Оклад.Получить(ДатаДок);
	КонецЦикла;
	Если КоличествоСтрок() = 0 Тогда
		Предупреждение("Не обнаружено ни одного работающего"+РазделительСтрок+"сотрудника на "+Формат(ДатаДок,"Д ДДММММГГГГ"));
	КонецЕсли;
КонецПроцедуры //Заполнить

//******************************************************************************
// Подбор()
//
Процедура Подбор()
	
	ОткрытьПодбор("Справочник.Сотрудники","ДляВыбора");
	
КонецПроцедуры // Подбор()

//******************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Если Константа.УчетЗарплатыВоВнешнейПрограмме = Да Тогда
		Предупреждение("Документ ""Приказ об изменении окладов"" не используется в режиме
		               |расчета зарплаты во внешней программе");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Новый = 1;
	Если Копирование = 1 Тогда //копирование документа
	    Возврат;
	КонецЕсли;
	
КонецПроцедуры //ВводНового

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	ПриЗаписиПерепроводить(1);
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.КнопкаЗаполнить.Доступность(0);
		Форма.КнопкаПодбор.Доступность(0);
		Форма.КнопкаОчистить.Доступность(0);
	КонецЕсли;
	НачальнаяДатаДокумента = ДатаДок;
	
КонецПроцедуры //ПриОткрытии

//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПодбора(ВыбрСотрудник) //предопределенная
	
	НовыйОклад = ВыбрСотрудник.Оклад.Получить(ДатаДок);
	Если ВвестиЧисло(НовыйОклад,"Введите новый оклад",10,0)=0 Тогда
		Возврат;
	КонецЕсли;
	Если НовыйОклад > 0 Тогда
	    НоваяСтрока();
	    Сотрудник = ВыбрСотрудник;
	    Оклад = НовыйОклад;
		АктивизироватьСтроку();
	КонецЕсли;
	
КонецПроцедуры //ОбработкаПодбора

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи()
	
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	Если ВыбратьСтроки()=0 Тогда
		Предупреждение("В документе не указано ни одного сотрудника."+РазделительСтрок+
		"Документ не может быть записан.");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	Пока ПолучитьСтроку()=1 Цикл
		Если Сотрудник.Выбран()=0 Тогда
			Предупреждение("В строке "+НомерСтроки+" не указан сотрудник."+РазделительСтрок+
			"Документ не может быть записан.");
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЦикла;

	Если Константа.ВсеДокументыВЖурналОпераций = Да Тогда
	    СуществуетОперация(1);
		Операция.СуммаОперации = Итог("Оклад");
		Операция.Содержание = "Приказ об изменении окладов";
	Иначе
	    СуществуетОперация(0);
	КонецЕсли;
	
КонецПроцедуры //ПриЗаписи

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Новый = 0;

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Ввести на основании");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");
