//*****************************************************************************
// Предопределенная процедура
//
// Описание:
//  Формирует проводки операции документа.
//
Процедура ОбработкаПроведения()
	
	Если ФормироватьПроводки = 0 Тогда
		ТекстСообщения = "Документ проведен.";
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		глПриПроведении(Контекст);
		Возврат;
	
	ИначеЕсли ДокументОприходования.Выбран() = 1 Тогда
		Если (ДокументОприходования.Вид() = "АвансовыйОтчет") или  (ДокументОприходования.Вид() = "Выписка") или (ДокументОприходования.Вид() = "РасходныйОрдер") Тогда
		ИначеЕсли ПустоеЗначение(ДокументОприходования.ДатаНомерСчетаФактуры) = 0 Тогда
			ТекстСообщения = "Полученный счет-фактура уже зарегистрирован документом оприходования.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЦеныВДоговоре = 1; // в рублях
	Если Договор.Выбран() = 1 Тогда
	    Если ПустоеЗначение(Договор.ВалютаДоговора) = 0 Тогда
			ЦеныВДоговоре = 2; // в валюте
			
			Если (Договор.ОплатаДоговора = 2) или (Договор.ВестиУчетРасчетовУЕ = 1) Тогда
				Если (Счет.Выбран()=1) и (Счет.Валютный = 0) и (СчетФактураНаВозврат = 0) 
				и (ВидОперации <> Перечисление.ВидыОперацийСчетаФактурыПолученного.Аванс) Тогда
					ТекстСообщения = "Взаиморасчеты по договору " + Договор + " ведутся в валюте. Счет " + Счет.Код + " не предназначен для ведения валютного учета.";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				КонецЕсли;
			КонецЕсли;

		КонецЕсли;
		ОплатаДоговора = Договор.ОплатаДоговора; // 1 - в рублях, 2 - в валюте		
		ВалютаДоговора = Договор.ВалютаДоговора;
	КонецЕсли;
	
	Если ЦеныВДоговоре = 2 Тогда
		Валюта = Договор.ВалютаДоговора;
		Кратность = Валюта.Кратность.Получить(ДатаДок);
		Кратность = ?(Кратность=0, 1, Кратность);
		Если Договор.ВестиУчетРасчетовУЕ = 1 Тогда
			Кратность = Кратность * 100 / (100 + Договор.ПроцентКорректировкиКурсаУЕ);
		КонецЕсли;
		Если Курс = 0 Тогда
		    ТекстСообщения = "Не указан курс валюты.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВидОперации <> Перечисление.ВидыОперацийСчетаФактурыПолученного.Аванс Тогда
		Если Счет.Выбран()=0 Тогда
			ТекстСообщения = "Не указан счет кредита.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;

	СуммаЗачетаВал = 0;
	СуммаЗачетаРуб = 0;
	ЗадолженностьВал = 0;
	ЗадолженностьРуб = 0;
	
	Сч60_6 = СчетПоКоду("60.6");
	Сч60_7 = СчетПоКоду("60.7");
	Сч68_2 = СчетПоКоду("68.2");
	
	Если ДокументОприходования.Выбран() = 1 Тогда
		Док = ДокументОприходования;
		
		Если ДокументОприходования.Вид() = "УслугиСтороннихОрганизаций" Тогда
		    Если ДокументОприходования.ДокументПоступления.Выбран() = 1 Тогда
		        Док = ДокументОприходования.ДокументПоступления;
		    КонецЕсли;
		КонецЕсли;
		
	    Если Док.Вид() = "ПоступлениеМатериалов" Тогда
			НомЖур = "МТ";
		ИначеЕсли Док.Вид() = "ПоступлениеТоваров" Тогда
			НомЖур = "ТВ";
		ИначеЕсли Док.Вид() = "ПоступлениеОС" Тогда
			НомЖур = "ОС";
		ИначеЕсли Док.Вид() = "ПоступлениеНМА" Тогда
			НомЖур = "НА";
		ИначеЕсли Док.Вид() = "АвансовыйОтчет" Тогда
			НомЖур = "БК";
		ИначеЕсли Док.Вид() = "ПоступлениеОборудования" Тогда
			НомЖур = "ОС";
		ИначеЕсли Док.Вид() = "УслугиСтороннихОрганизаций" Тогда
			НомЖур = "СО";
		КонецЕсли;
		
		// Определим курс валюты операции.
		Если (ЦеныВДоговоре = 2) и (ОплатаДоговора = 1) Тогда
			ВестиУчетРасчетовУЕ = Договор.ВестиУчетРасчетовУЕ;
			Если (ВестиУчетРасчетовУЕ = 1) и (ДокументОприходования.Выбран() = 1) Тогда // поиск зачтенного аванса
			    Опер = СоздатьОбъект("Операция");
				Если Опер.НайтиОперацию(ДокументОприходования) = 1 Тогда
				    Опер.ВыбратьПроводки();
					Пока Опер.ПолучитьПроводку() = 1 Цикл
					    Если (Опер.Дебет.Счет = Сч60_6) и (Опер.Кредит.Счет = Сч60_7) Тогда
					        СуммаЗачетаВал = СуммаЗачетаВал + Опер.ВалСумма;
							СуммаЗачетаРуб = СуммаЗачетаРуб + Опер.Сумма;
						КонецЕсли;
						
						Если Опер.Кредит.Счет = Сч60_6 Тогда
							ЗадолженностьВал = ЗадолженностьВал + Опер.ВалСумма;
							ЗадолженностьРуб = ЗадолженностьРуб + Опер.Сумма;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	Иначе // если документ не указан, то попытаемся определить по счету
		Если Субсчет19 = СчетПоКоду("19.1") Тогда
		    НомЖур = "ОС";
		ИначеЕсли Субсчет19 = СчетПоКоду("19.2") Тогда
		    НомЖур = "НА";
		Иначе
			НомЖур = "ТВ";
		КонецЕсли;			
	КонецЕсли;
	
	ДоляОплаченнойОтгрузки = ?(ЗадолженностьВал = 0, 0, СуммаЗачетаВал / ЗадолженностьВал);
	КурсОплаты = ?(СуммаЗачетаВал = 0, 0, Окр(СуммаЗачетаРуб / СуммаЗачетаВал, 4, 1));
	КурсСФ = ?(ЦеныВДоговоре = 2, Курс / Кратность, 1);
	
	ТаблицаОплаты = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаОплаты.НоваяКолонка("Товар");
	ТаблицаОплаты.НоваяКолонка("СтавкаНДС", "Число");
	ТаблицаОплаты.НоваяКолонка("Сумма", "Число");
	ТаблицаОплаты.НоваяКолонка("Всего", "Число");
	ТаблицаОплаты.НоваяКолонка("НДС", "Число");
	ТаблицаОплаты.НоваяКолонка("СуммаРуб", "Число");
	ТаблицаОплаты.НоваяКолонка("ВсегоРуб", "Число");
	ТаблицаОплаты.НоваяКолонка("НДСРуб", "Число");
	
	ТаблицаОплаты.НоваяСтрока();
	ТаблицаОплаты.СтавкаНДС = 20;
	
	Если ЦеныВДоговоре = 2 Тогда
		ОплСумма = Окр(СуммаБезНДС20 * ДоляОплаченнойОтгрузки, 2, 1);
		ТаблицаОплаты.СуммаРуб = Окр(ОплСумма * КурсОплаты + (СуммаБезНДС20 - ОплСумма) * КурсСФ, 2, 1);
		
		ОплСумма = Окр(НДС20 * ДоляОплаченнойОтгрузки, 2, 1);
		ТаблицаОплаты.НДСРуб = Окр(ОплСумма * КурсОплаты + (НДС20 - ОплСумма) * КурсСФ, 2, 1);
		
		ТаблицаОплаты.ВсегоРуб =ТаблицаОплаты.СуммаРуб + ТаблицаОплаты.НДСРуб;
		
	Иначе
		ТаблицаОплаты.ВсегоРуб = СуммаБезНДС20 + НДС20;
		ТаблицаОплаты.СуммаРуб = СуммаБезНДС20;
		ТаблицаОплаты.НДСРуб = НДС20;
	КонецЕсли;
	
	ТаблицаОплаты.НоваяСтрока();
	ТаблицаОплаты.СтавкаНДС = 10;
	
	Если ЦеныВДоговоре = 2 Тогда
		ОплСумма = Окр(СуммаБезНДС10 * ДоляОплаченнойОтгрузки, 2, 1);
		ТаблицаОплаты.СуммаРуб = Окр(ОплСумма * КурсОплаты + (СуммаБезНДС10 - ОплСумма) * КурсСФ, 2, 1);
		
		ОплСумма = Окр(НДС10 * ДоляОплаченнойОтгрузки, 2, 1);
		ТаблицаОплаты.НДСРуб = Окр(ОплСумма * КурсОплаты + (НДС10 - ОплСумма) * КурсСФ, 2, 1);
		
		ТаблицаОплаты.ВсегоРуб =ТаблицаОплаты.СуммаРуб + ТаблицаОплаты.НДСРуб;
		
	Иначе
		ТаблицаОплаты.ВсегоРуб = СуммаБезНДС10 + НДС10;
		ТаблицаОплаты.СуммаРуб = СуммаБезНДС10;
		ТаблицаОплаты.НДСРуб = НДС10;
	КонецЕсли;
	
	ТаблицаОплаты.НоваяСтрока();
	ТаблицаОплаты.СтавкаНДС = 0;
	                             
	Если НДСПоСтавкеНольПроцентов = 0 Тогда //Освобождаемые
		Если ЦеныВДоговоре = 2 Тогда
			ОплСумма = Окр(СуммаСовсемБезНДС * ДоляОплаченнойОтгрузки, 2, 1);
			ТаблицаОплаты.СуммаРуб = Окр(ОплСумма * КурсОплаты + (СуммаСовсемБезНДС - ОплСумма) * КурсСФ, 2, 1);
			ТаблицаОплаты.НДСРуб = 0;
			ТаблицаОплаты.ВсегоРуб =ТаблицаОплаты.СуммаРуб + ТаблицаОплаты.НДСРуб;
			
		Иначе
			ТаблицаОплаты.ВсегоРуб = СуммаСовсемБезНДС;
			ТаблицаОплаты.СуммаРуб = СуммаСовсемБезНДС;
			ТаблицаОплаты.НДСРуб = 0;
		КонецЕсли; 
	Иначе // по ставке 0 процентов   
		ТаблицаОплаты.СуммаРуб = 0;
		ТаблицаОплаты.НДСРуб = 0;
		Если ЦеныВДоговоре = 2 Тогда
			ОплСумма = Окр(Всего * ДоляОплаченнойОтгрузки, 2, 1);			
			ТаблицаОплаты.ВсегоРуб =Окр(ОплСумма * КурсОплаты + (Всего - ОплСумма) * КурсСФ, 2, 1);
			
		Иначе
			ТаблицаОплаты.ВсегоРуб = Всего;			
		КонецЕсли;
	КонецЕсли;
	
	Если ((НДС10 + НДС20) <> 0) и (ДокументОприходования.Выбран() = 1) и (ВидОперации <> Перечисление.ВидыОперацийСчетаФактурыПолученного.Возврат) 
	и (ВидОперации <> Перечисление.ВидыОперацийСчетаФактурыПолученного.Аванс) Тогда
		
		// Сторнируем НДС, выделенный при проведении документа оприходования.
		Если ДокументОприходования.Вид() = "АвансовыйОтчет" Тогда
		ИначеЕсли ВерсияОбъекта < "7.70.421" Тогда
		ИначеЕсли ПустоеЗначение(ДокументОприходования.ДатаНомерСчетаФактуры) = 1 Тогда
			
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = НомЖур;
			Операция.СодержаниеПроводки = "Сторнирован НДС";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Кредит.Счет = Счет;
			Операция.Кредит.Субконто(1, Субконто1);
			Операция.Кредит.Субконто(2, Субконто2);
			Операция.Кредит.Субконто(3, Субконто3);
			Операция.Дебет.Счет = Субсчет19;
			Операция.Дебет.Контрагенты = Контрагент;
			Если ЦеныВДоговоре = 2 Тогда
				Операция.Сумма = - ТаблицаОплаты.Итог("НДСРуб");
				
				Если (ОплатаДоговора = 2) или (Договор.ВестиУчетРасчетовУЕ = 1) Тогда
					Операция.Валюта = Валюта;
					Операция.ВалСумма = - (НДС10 + НДС20);
				КонецЕсли;
				
			Иначе
				Операция.Сумма = - (НДС10 + НДС20);
			КонецЕсли;
		КонецЕсли;
					
	ИначеЕсли ((НДС10 + НДС20) <> 0) и (ДокументОприходования.Выбран() = 1) и (ВидОперации = Перечисление.ВидыОперацийСчетаФактурыПолученного.Возврат) Тогда
		
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = НомЖур;
		Операция.СодержаниеПроводки = "Сторнирован НДС";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = Счет;				
		Операция.Дебет.Субконто(1, Субконто1);
		Операция.Дебет.Субконто(2, Субконто2);
		Операция.Дебет.Субконто(3, Субконто3);
		
		Операция.Кредит.Счет = Субсчет19;
		Операция.Кредит.Контрагенты = Контрагент;
		Если ЦеныВДоговоре = 2 Тогда
			Операция.Сумма = - ТаблицаОплаты.Итог("НДСРуб");
			
			Если ((ОплатаДоговора = 2) или (Договор.ВестиУчетРасчетовУЕ = 1)) и (СчетФактураНаВозврат = 0) Тогда
				Операция.Валюта = Валюта;
				Операция.ВалСумма = НДС10 + НДС20;
			КонецЕсли;
			
		Иначе
			Операция.Сумма = НДС10 + НДС20;
		КонецЕсли;
		
	КонецЕсли;
	
	// Формируем проводки по НДС поставщика.
	Если ВидОперации <> Перечисление.ВидыОперацийСчетаФактурыПолученного.Аванс Тогда
		Если ((НДС10 + НДС20) <> 0) и (ПустоеЗначение(Счет)=0) Тогда
			Если СчетФактураНаВозврат = 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = НомЖур;
				Операция.СодержаниеПроводки = "Выделен НДС";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Кредит.Счет = Счет;
				Операция.Кредит.Субконто(1, Субконто1);
				Операция.Кредит.Субконто(2, Субконто2);
				Операция.Кредит.Субконто(3, Субконто3);
				Операция.Дебет.Счет = Субсчет19;
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.СчетаФактурыПолученные = ТекущийДокумент();
				
				Если ЦеныВДоговоре = 2 Тогда
					Операция.Сумма = ТаблицаОплаты.Итог("НДСРуб");
					
					Если ((ОплатаДоговора = 2) или (Договор.ВестиУчетРасчетовУЕ = 1)) и (СчетФактураНаВозврат = 0) Тогда
						Операция.Валюта = Валюта;
						Операция.ВалСумма = НДС10 + НДС20;
					КонецЕсли;
					
				Иначе
					Операция.Сумма = НДС10 + НДС20;
				КонецЕсли;
			Иначе
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = НомЖур;
				Операция.СодержаниеПроводки = "Выделен НДС";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = Счет;
				Операция.Дебет.Субконто(1, Субконто1);
				Операция.Дебет.Субконто(2, Субконто2);
				Операция.Дебет.Субконто(3, Субконто3);
				Операция.Кредит.Счет = Субсчет19;
				Операция.Кредит.Контрагенты = Контрагент;
				Операция.Кредит.СчетаФактурыПолученные = ТекущийДокумент();
				Если ЦеныВДоговоре = 2 Тогда
					Операция.Сумма = - ТаблицаОплаты.Итог("НДСРуб");
				Иначе  
					Операция.Сумма = -(НДС10 + НДС20);
				КонецЕсли; 
			КонецЕсли; 
			
			Если Договор.Выбран() = 1 Тогда
				Если (Договор.АвтоОбработкаНДС = 0) и (ВключатьВКнигуПокупок = 1) Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = НомЖур;
					Операция.СодержаниеПроводки = "НДС принят к зачету";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					
					Операция.Дебет.Счет = Сч68_2;
					Операция.Дебет.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
					
					Операция.Кредит.Счет = Субсчет19;
					Операция.Кредит.Контрагенты = Контрагент;
					Операция.Кредит.СчетаФактурыПолученные = ТекущийДокумент();
					
					Если ЦеныВДоговоре = 2 Тогда
						Операция.Сумма = ТаблицаОплаты.Итог("НДСРуб");
						
					Иначе
						Операция.Сумма = НДС10 + НДС20;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Если в табличную часть документа введены строки, то оприходуем импортные товары.
		// Сформируем проводки по забалансовому счету ГТД.
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Если (Товар.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Товар) и (Товар.СтранаПроисхожд = 0) Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ТВ";
				Операция.СодержаниеПроводки = "Поступил импортный товар";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетПоКоду("ГТД");
				Операция.Дебет.Номенклатура = Товар;
				Операция.Дебет.ГТД = ГТД;
				Операция.Количество = Количество;
			Иначе
				ТекстСообщения = "Товар "+Товар+" не является импортным (указана страна происхождения Россия).";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
	// Формируем проводки по НДС с авансов
	Если ВидОперации = Перечисление.ВидыОперацийСчетаФактурыПолученного.Аванс Тогда
		Если (НДС10 + НДС20) <> 0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "ВА";
			Операция.СодержаниеПроводки = "НДС с авансов выданных";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			
			Операция.Дебет.Счет = Сч68_2;
			Операция.Дебет.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
			
			Операция.Кредит.Счет = СчетПоКоду("76.ВА");
			Операция.Кредит.Контрагенты = Контрагент;
			Операция.Кредит.СчетаФактурыПолученные = ТекущийДокумент();

			Операция.Сумма = НДС10 + НДС20;
			
		КонецЕсли;
	КонецЕсли;
	

	
	Если (Договор.АвтоОбработкаНДС=1) И (ВидОперации <> Перечисление.ВидыОперацийСчетаФактурыПолученного.Аванс) Тогда
	    глДляЗаполненияКнигиПокупок(Контекст, ТаблицаОплаты);
	КонецЕсли;
	
	Если (ПустоеЗначение(Операция.Содержание) = 1) и (Операция.СуммаОперации = 0) Тогда
		Операция.СуммаОперации = Всего;
		Операция.Содержание = "Сч.-факт. поставщика: "+Контрагент; 
	КонецЕсли;
	
	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения()