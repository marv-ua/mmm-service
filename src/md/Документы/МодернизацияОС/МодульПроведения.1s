Перем БухИт;

//******************************************************************************
// ДвиженияДляЦелейБУ()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Формируются движения для целей бухгалтерского учета.
//
Процедура ДвиженияДляЦелейБУ()
	
	Сч01_1 = СчетПоКоду("01.1");
	Сч08_3 = СчетПоКоду("08.3");
	
	Если СуммаРасходов = 0 Тогда
		Возврат;
	КонецЕсли;
	              
	ВсегоРасходов = 0;
	Если БухИт.ПолучитьСчет(, Сч08_3) = 1 Тогда
	    ВсегоРасходов = БухИт.СКД();
	КонецЕсли;
	
	Если ВсегоРасходов < СуммаРасходов Тогда
		ТекстСообщения = "Недостаточно расходов по внеоборотному активу """ + ОбъектВнеоборотныхАктивов+ """ на счете " + Сч08_3;
		глНеПроводить(Контекст, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СуммаРасходовНаОбъект = Окр(СуммаРасходов/КоличествоСтрок(), 2);
	СуммаРасходовНаПоследнийОбъект = СуммаРасходов - СуммаРасходовНаОбъект*(КоличествоСтрок() - 1);

	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		
		Если ПустоеЗначение(ОсновноеСредство.МатериалДляОтраженияВСоставеМПЗ)=0 Тогда
			ТекстСообщения = "Инв.№ "+ОсновноеСредство.Код+" "+ОсновноеСредство.Наименование+". Основное средство отражено в составе МПЗ и не может быть модернизировано!";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
			Прервать;
		КонецЕсли;
		
		СуммаЗатрат = ?(НомерСтроки <> КоличествоСтрок(), СуммаРасходовНаОбъект, СуммаРасходовНаПоследнийОбъект);
		Если СуммаЗатрат > 0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "ОС";
			Операция.СодержаниеПроводки = "Затраты по модернизации";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = Сч01_1;
			Операция.Дебет.ОсновныеСредства = ОсновноеСредство;
			Операция.Кредит.Счет = Сч08_3;
			Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
			Операция.Сумма = СуммаЗатрат;
		КонецЕсли;
		
		// Установка периодических реквизитов для целей бухгалтерского учета
		УстановитьРеквизитСправочника(ОсновноеСредство,"ПервоначальнаяСтоимость", 
			СуммаЗатрат + ОсновноеСредство.ПервоначальнаяСтоимость.Получить(ДатаДок));
		УстановитьРеквизитСправочника(ОсновноеСредство,"СрокПолезногоИспользования", СрокПолезногоИспользования);
		УстановитьРеквизитСправочника(ОсновноеСредство,"ОбъектМодернизирован", 1);
		
		Если ОбщийОбъемПродукцииРабот > 0 Тогда
		    УстановитьРеквизитСправочника(ОсновноеСредство,"ОбщийОбъемПродукцииРабот", ОбщийОбъемПродукцииРабот);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ДвиженияДляЦелейБУ()

//******************************************************************************
// ДвиженияДляЦелейБУ()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Формируются движения для целей налогового учета.
//
Процедура ДвиженияДляЦелейНУ()
	
	СчН01_09 = СчетПоКоду("Н01.09");
	СчН05_01 = СчетПоКоду("Н05.01");
	
	Если СуммаРасходовН = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВсегоРасходов = 0;
	Если БухИт.ПолучитьСчет(, СчН01_09) = 1 Тогда
	    ВсегоРасходов = БухИт.СКД();
	КонецЕсли;
	
	Если ВсегоРасходов < СуммаРасходовН Тогда
		ТекстСообщения = "Недостаточно расходов по внеоборотному активу """ + ОбъектВнеоборотныхАктивов+ """ на счете " + СчН01_09;
		глНеПроводить(Контекст, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	СуммаРасходовНаОбъект = Окр(СуммаРасходовН/КоличествоСтрок(), 2);
	СуммаРасходовНаПоследнийОбъект = СуммаРасходовН - СуммаРасходовНаОбъект*(КоличествоСтрок() - 1);
	КапитальныеВложенияВключенныеВРасходыНаОбъект = Окр(КапитальныеВложенияВключенныеВРасходы/КоличествоСтрок(), 2);
	КапитальныеВложенияВключенныеВРасходыНаПоследнийОбъект = КапитальныеВложенияВключенныеВРасходы - КапитальныеВложенияВключенныеВРасходыНаОбъект*(КоличествоСтрок() - 1);  	
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		
		СуммаЗатрат = ?(НомерСтроки <> КоличествоСтрок(), СуммаРасходовНаОбъект, СуммаРасходовНаПоследнийОбъект);
		Если СуммаЗатрат > 0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "ОС";
			Операция.СодержаниеПроводки = "Затраты по модернизации";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчН05_01;
			Операция.Дебет.ОсновныеСредства = ОсновноеСредство;
			Операция.Кредит.Счет = СчН01_09;
			Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
			Операция.Сумма = СуммаЗатрат; 
			
			КапитальныеВложенияВключенныеВРасходыНаОбъект = ?(НомерСтроки <> КоличествоСтрок(), КапитальныеВложенияВключенныеВРасходыНаОбъект, КапитальныеВложенияВключенныеВРасходыНаПоследнийОбъект);
			Если (ВключитьКапитальныеВложенияВРасходы = 1) И (КапитальныеВложенияВключенныеВРасходыНаОбъект <> 0) Тогда 
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ОС"; 
				Операция.СодержаниеПроводки = "Включение кап.вложений в расходы";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетПоКоду("Н05.КВ");
				Операция.Дебет.ОсновныеСредства = ОсновноеСредство;
				Операция.Сумма = КапитальныеВложенияВключенныеВРасходыНаОбъект;
			КонецЕсли;
		КонецЕсли;
		
		// Установка периодических реквизитов для целей налогового учета
		УстановитьРеквизитСправочника(ОсновноеСредство,"ПервоначальнаяСтоимостьН",
			СуммаЗатрат + ОсновноеСредство.ПервоначальнаяСтоимостьН.Получить(ДатаДок - 1));
		УстановитьРеквизитСправочника(ОсновноеСредство,"СрокПолезногоИспользованияН", СрокПолезногоИспользованияН);
		
	КонецЦикла;
	
КонецПроцедуры // ДвиженияДляЦелейНУ()

//******************************************************************************
Процедура ОбработкаПроведения()
	
	БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
	БухИт.ИспользоватьСубконто(ВидыСубконто.ОбъектыСтроительства, ОбъектВнеоборотныхАктивов, 2);
	БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "08.3, Н01.09");
	
	ДвиженияДляЦелейБУ();
	Если СтатусВозврата() = 0 Тогда
	    Возврат;
	КонецЕсли;
	
	ДвиженияДляЦелейНУ();
	Если СтатусВозврата() = 0 Тогда
	    Возврат;
	КонецЕсли;
	
	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
КонецПроцедуры
//_____________________________________________________________________________
