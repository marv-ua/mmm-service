//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()
	
	СчетУчетаНалогов = СчетПоКоду("Н15");
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(ВидНалога) = 1 Тогда
			ТекстСообщения = "В строке №" + НомерСтроки + " не указан вид налога.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
			
		ИначеЕсли (ПустоеЗначение(ВидРасхода) = 1) и (Найти(глФондыЕСН, ВидНалога.Код) > 0) Тогда
			ТекстСообщения = "В строке №" + НомерСтроки + " не указан вид расхода.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
			
		ИначеЕсли (ВидРасхода = Перечисление.ВидыРасходов.НеПринимаемые)
		        и (глНовыеПравилаВеденияНУ(ДатаДок) = 0) Тогда
			Продолжить;
		КонецЕсли;
		
		ПривязыватьСтроку(НомерСтроки);

		Операция.НоваяПроводка();  Операция.РазделительУчета = ЮрЛицо;
		Операция.Кредит.Счет = СчетУчетаНалогов;
		Операция.Кредит.НалогиИОтчисления = ВидНалога;
		Операция.Кредит.СрокиУплаты        = СрокУплаты;
		
		Операция.СодержаниеПроводки = "" + ВидНалога;
		Операция.НомерЖурнала = "НУ";
		Операция.Сумма = СуммаНалога;

		Если ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеТоваров Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
					
		    ИначеЕсли Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Товар Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н02.02.1");
				Операция.Дебет.Номенклатура = Объект;
				
			Иначе
				ТекстСообщения = "В строке №" + НомерСтроки + " указан объект учета, не являющийся товаром.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеМатериалов Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н02.01");
			Операция.Дебет.Материалы = Объект;
		    
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеОсновныхСредств Тогда
			Если Объект.Вид() = "ОбъектыСтроительства" Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н01.01");
				Операция.Дебет.ОбъектыСтроительства = Объект;
				
			ИначеЕсли Объект.Вид() = "Оборудование" Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н01.02");
				Операция.Дебет.Оборудование = Объект;
				
			Иначе
				ТекстСообщения = "В строке №" + НомерСтроки + " указан объекта учета не являющийся внеоборотным активом или оборудованием.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.СтроительствоОсновныхСредств Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект налогового учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			Операция.Дебет.Счет = СчетПоКоду("Н01.09");
			Операция.Дебет.ОбъектыСтроительства = Объект;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеНематериальныхАктивов Тогда
			Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н01.08");
			
			Иначе
				Операция.Дебет.Счет = СчетПоКоду("Н01.01");
			КонецЕсли;
			Операция.Дебет.ОбъектыСтроительства = Объект;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РасходыБудущихПериодов Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
			Если Объект.НазначениеСтатьиРасходов = 1 Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н04.11");
				Операция.Дебет.РасходыБудущихПериодов = Объект;
				Операция.Дебет.НалогиИОтчисления = ВидНалога;
			Иначе
				Операция.Дебет.Счет = СчетПоКоду("Н04.09");    
				Операция.Дебет.РасходыБудущихПериодов = Объект;
				Операция.Дебет.УсловияПоступленияИВыбытия = Перечисление.УсловияПоступленияИВыбытия.Другие;				
			КонецЕсли;
			
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.НИОКР Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н01.03");
			Операция.Дебет.ОбъектыСтроительства = Объект;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ОсвоениеПриродныхРесурсов Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н01.04");
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПрямыеРасходы Тогда
			Если (ВидНалога.Код = "ЕСН_ФБ") или (ВидНалога.Код = "ФСС")
			 или (ВидНалога.Код = "ФФОМС" ) или (ВидНалога.Код = "ТФОМС") 
			 или (((ВидНалога.Код = "ПФР_нак") или (ВидНалога.Код = "ПФР_страх")) И (ДатаГод(ДатаДок) >= 2010)) Тогда
			 	ГруппаВидРасходов = Перечисление.ГруппыВидыРасходов.ЕСН;
			 	
			ИначеЕсли (ВидНалога.Код = "ФСС_НС") или (((ВидНалога.Код = "ПФР_нак") или (ВидНалога.Код = "ПФР_страх")) И (ДатаГод(ДатаДок) < 2010))Тогда
			 	ГруппаВидРасходов = Перечисление.ГруппыВидыРасходов.ДругиеРасходы;
			 	
			Иначе
				ГруппаВидРасходов = Перечисление.ГруппыВидыРасходов.НалогиИСборы;
			КонецЕсли;
			
			Если ПустоеЗначение(Объект) = 1 Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н01.06");
				Операция.Дебет.ГруппыВидыРасходов = ГруппаВидРасходов;
				
			ИначеЕсли (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.УслугаЕНВД) Тогда
				Сообщить("В строке №" + НомерСтроки + " указаны расходы, не принимаемые к налоговому учету!");
				СтатусВозврата(0); Возврат;
			
			ИначеЕсли (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Продукция)
				или (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Полуфабрикат)
				или (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Услуга)
				или (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Работа) Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н01.05");
				Операция.Дебет.ГруппыВидыРасходов = ГруппаВидРасходов;
				Операция.Дебет.ВидыНоменклатуры = Объект;
				
			Иначе
				ТекстСообщения = "В строке №" + НомерСтроки + " указан объект учета, не являющийся видом номенклатуры, относящимся к продукции, работам, услугам.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.КосвенныеРасходы Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н07.04.1");
			Если (ВидНалога.Код = "ЕСН_ФБ") или (ВидНалога.Код = "ФСС")
			 или (ВидНалога.Код = "ФФОМС" ) или (ВидНалога.Код = "ТФОМС") 
			 или (((ВидНалога.Код = "ПФР_нак") или (ВидНалога.Код = "ПФР_страх")) И (ДатаГод(ДатаДок) >= 2010)) Тогда
			 	Операция.Дебет.ГруппыВидыРасходов = Перечисление.ГруппыВидыРасходов.ЕСН;
			 	
			ИначеЕсли (ВидНалога.Код = "ФСС_НС") или (((ВидНалога.Код = "ПФР_нак") или (ВидНалога.Код = "ПФР_страх")) И (ДатаГод(ДатаДок) < 2010)) Тогда
			 	Операция.Дебет.ГруппыВидыРасходов = Перечисление.ГруппыВидыРасходов.ДругиеРасходы;
			 	
			Иначе
				Операция.Дебет.ГруппыВидыРасходов = Перечисление.ГруппыВидыРасходов.НалогиИСборы;
			КонецЕсли;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РеализацияОсновныхСредств Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н07.07");
			Операция.Дебет.ОсновныеСредства = Объект;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РеализацияНематериальныхАктивов Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н07.08");
			Операция.Дебет.НематериальныеАктивы = Объект;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ВнереализационныеРасходы Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н09");
			Операция.Дебет.ВнереализационныеРасходы = Перечисление.ВнереализационныеРасходы.ПрочиеВнереализационныеРасходы;
			
		ИначеЕсли ПустоеЗначение(ВидРасхода) = 1 Тогда
			//  Для прочих налогов (дороги, налог на имущество), если не
			// указан вид расходов, то по умолчанию относим на косвенные расходы.
			Операция.Дебет.Счет = глПолучитьСчетУчетаКосвенныхРасходовНУ(ДатаДок,,,, 1);
			Операция.Дебет.ГруппыВидыРасходов = Перечисление.ГруппыВидыРасходов.НалогиИСборы;    
			
		КонецЕсли;
	КонецЦикла;

	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);

КонецПроцедуры // ОбработкаПроведения()
