////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем НачальнаяДатаДокумента; 
Перем Новый;
Перем СписокДействий;
Перем СписокВременныхЕдиницРасчета;
Перем ДатаНач;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//******************************************************************************
// УстановкаДаты()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Здесь описывается возвращаемое значение.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается функция.
//
Функция УстановкаДаты()
	
	ДатаДок = КонМесяца(ДатаДок);
	ДатаНач = НачМесяца(ДатаДок);

КонецФункции // УстановкаДаты()


//******************************************************************************
// ПриИзмененииВалютнойСуммы()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Валютная сумма.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ПриИзмененииВалютнойСуммы()
	
    Если ВалСумма = 0 Тогда
		Если ПустоеЗначение(Валюта) = 0 Тогда
		    Валюта = "";
		КонецЕсли;
		Форма.Валюта.Доступность(0);
		
	Иначе
		Форма.Валюта.Доступность(1);
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииВалютнойСуммы()

//******************************************************************************
// РасчетСуммы()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура РасчетСуммы()
	
	Если НРег(СокрП(ВременнаяЕдиницаРасчета)) = "день" Тогда
		Если (ПустоеЗначение(НачалоПериода) = 0) и (ПустоеЗначение(ОкончаниеПериода) = 0) Тогда
		    Сумма = (ОкончаниеПериода - НачалоПериода + 1) * База * Ставка / 100;
		КонецЕсли;
		
	ИначеЕсли НРег(СокрП(ВременнаяЕдиницаРасчета)) = "месяц" Тогда
		Если (НачалоПериода = НачМесяца(ДатаДок)) и (ОкончаниеПериода = КонМесяца(ДатаДок)) Тогда
		    Сумма = База * Ставка / 100;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // РасчетСуммы()

//******************************************************************************
// ЗаполнитьРеквизитыПоДоговору()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ЗаполнитьРеквизитыПоДоговору()
	
  Если Договор.Выбран() = 1 Тогда
	Если Договор.ДатаНачалаНачисленияШтрафныхСанкций < ДатаНач Тогда
		НачалоПериода = ДатаНач;
	Иначе
		НачалоПериода = Договор.ДатаНачалаНачисленияШтрафныхСанкций;
	КонецЕсли;
	
	Если (ПустоеЗначение(Договор.ДатаПрекращенияНачисленияШтрафныхСанкций) = 1) 
		или (Договор.ДатаПрекращенияНачисленияШтрафныхСанкций > ДатаДок) Тогда
		ОкончаниеПериода = ДатаДок;
	Иначе
		ОкончаниеПериода = Договор.ДатаПрекращенияНачисленияШтрафныхСанкций;
	КонецЕсли;
	
	Ставка = Договор.СтавкаШтрафныхСанкций;

  	Если ПустоеЗначение(Договор.ВременнаяЕдиницаРасчетаШтрафныхСанкций) = 1 Тогда
  	    ВременнаяЕдиницаРасчета = "День";
  	Иначе
  		ВременнаяЕдиницаРасчета = Договор.ВременнаяЕдиницаРасчетаШтрафныхСанкций;
  	КонецЕсли;
  КонецЕсли;	
	
КонецПроцедуры // ЗаполнитьРеквизитыПоДоговору()

//*********************************************************************************
//
Процедура Заполнить()

	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.Установить("КонтекстДокумента", Контекст);
	ОткрытьФормуМодально("Обработка.ФормированиеНалоговыхДокументов", СписокПараметров);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	УстановкаДаты();

	Если Копирование = 1 Тогда //копирование документа
	    Возврат;
	КонецЕсли;

КонецПроцедуры // ВводНового()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	НачальнаяДатаДокумента = ДатаДок;
	ПриЗаписиПерепроводить(1);
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаЗаполнить.Доступность(0);
	КонецЕсли;

	Форма.НачалоПериода.ВыполнятьФормулуТолькоПриИзменении(1);
	Форма.ОкончаниеПериода.ВыполнятьФормулуТолькоПриИзменении(1);
	Форма.База.ВыполнятьФормулуТолькоПриИзменении(1);
	Форма.Ставка.ВыполнятьФормулуТолькоПриИзменении(1);
	
	СписокВременныхЕдиницРасчета = СоздатьОбъект("СписокЗначений");
	СписокВременныхЕдиницРасчета.ДобавитьЗначение("День");
	СписокВременныхЕдиницРасчета.ДобавитьЗначение("Рабочий день");
	СписокВременныхЕдиницРасчета.ДобавитьЗначение("Неделя");
	СписокВременныхЕдиницРасчета.ДобавитьЗначение("Месяц");

	ДатаНач = НачМесяца(ДатаДок);

КонецПроцедуры // ПриОткрытии()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, Флаг)
	
	Если ЭлементДиалога = "ВременнаяЕдиницаРасчета" Тогда
		Флаг = 0;
		ВыбраннаяВременнаяЕдиницаРасчета = СокрП(ВременнаяЕдиницаРасчета);
		Если СписокВременныхЕдиницРасчета.ВыбратьЗначение(ВыбраннаяВременнаяЕдиницаРасчета,,,, 2) = 1 Тогда
			Если ВременнаяЕдиницаРасчета <> ВыбраннаяВременнаяЕдиницаРасчета Тогда
			    ВременнаяЕдиницаРасчета = ВыбраннаяВременнаяЕдиницаРасчета;
				РасчетСуммы();
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ЭлементДиалога = "СтатьяДоходовИРасходов" Тогда
		Флаг = 0;
		ОткрытьФорму("Справочник.ПрочиеДоходыИРасходы", Перечисление.ВидыПрочихДоходовИРасходов.ШтрафыПоДоговорам);
	КонецЕсли;

КонецПроцедуры //ПриНачалеВыбораЗначения()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи() //предопределенная
	
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
		
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	Операция.СуммаОперации = Итог("Сумма");
	Операция.Содержание = "Начисление штрафных санкций за " + Формат(ДатаДок,"Д ММММГГГГ");
	
КонецПроцедуры // ПриЗаписи()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Новый = 0;

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");