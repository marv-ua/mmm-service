////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()
    
	Сч76_АВ = СчетПоКоду("76.АВ");
	Сч68_2  = СчетПоКоду("68.2");
	
	Если (ЗаписьДопЛистаПК = 1) и (ПустоеЗначение(ДатаДопЛистаПК) = 1) Тогда
		ТекстСообщения = "Не указана дата записи дополнительного листа книги покупок.";
		глНеПроводить(Контекст, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если (ФормироватьПроводки = 0) или (НДСпоСтавкеНольПроцентов = 1) Тогда
		ТекстСообщения = "Документ проведен.";
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		глПриПроведении(Контекст);
		Возврат;
	КонецЕсли;
	
	Если КоррСчет.Выбран() = 0 Тогда
		ТекстСообщения = "Не выбран корреспондирующий счет.";
		глНеПроводить(Контекст, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ЦеныВДоговоре = 1; // в рублях
	Если Договор.Выбран() = 1 Тогда
	    Если ПустоеЗначение(Договор.ВалютаДоговора) = 0 Тогда
			ЦеныВДоговоре = 2; // в валюте
		КонецЕсли;
		ОплатаДоговора = Договор.ОплатаДоговора; // 1 - в рублях, 2 - в валюте
		Валюта = Договор.ВалютаДоговора;
		Кратность = Валюта.Кратность.Получить(ДатаДок);
		Кратность = ?(Кратность=0, 1, Кратность);
	КонецЕсли;
	
	Если ОплатаДоговора = 2 Тогда
		СуммаНДС = (НДС10 + НДС20)*Курс/Кратность;
		СуммаНДС = Окр(СуммаНДС, 2);
		
		// В НДС на счете 19 могла возникнуть курсовая разница
		Если (ДокументОснование.Выбран() = 1) и (Аванс = 0) Тогда
			КурсДокОсн = ДокументОснование.Курс;
			КратностьДокОсн = Валюта.Кратность.Получить(ДокументОснование.ДатаДок);
			КратностьДокОсн = ?(КратностьДокОсн=0, 1, КратностьДокОсн);

			глТаблицаСчетов.УдалитьСтроки();
			глТаблицаСчетов.НоваяСтрока();
			глТаблицаСчетов.Счет = Субсчет19;
			глТаблицаСчетов.Субконто1 = Контрагент;
			глТаблицаСчетов.Субконто2 = ДокументОснование;
			глТаблицаСчетов.Субконто3 = "";
			глТаблицаСчетов.Валюта = Валюта;
			глТаблицаСчетов.Курс = Курс;
			глТаблицаСчетов.РублевыйОстаток = Окр((НДС10 + НДС20)*КурсДокОсн/КратностьДокОсн, 2);
			глТаблицаСчетов.ВалютныйОстаток = НДС10 + НДС20;
			
			глПереоценкаСчетов(Контекст, глТаблицаСчетов, 0, 0);
			Операция.ЗаписатьПроводки();
		КонецЕсли;
		
	Иначе
		СуммаНДС = НДС10 + НДС20;
	КонецЕсли;
	
	БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
	Если СуммаНДС = 0 Тогда
	Иначе
		Если Аванс = 1 Тогда
			БухИт.ИспользоватьСубконто(ВидыСубконто.СчетаФактурыВыданные, ДокументОснование, 2);
		    БухИт.ВыполнитьЗапрос(НачМесяца(ДатаДок), ТекущийДокумент(), Сч76_АВ);
			
			Если БухИт.СКД() < СуммаНДС Тогда
				ТекстСообщения = "Сумма НДС превышает сальдо по дебету счета 76.АВ.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;

			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "БК";
			Операция.СодержаниеПроводки = "Восстановлен НДС с аванса";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = Сч68_2;
			Операция.Дебет.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
			Операция.Кредит.Счет = Сч76_АВ;
			Операция.Кредит.Субконто(1, Контрагент);
			Операция.Кредит.Субконто(2, ДокументОснование);
			Операция.Сумма = СуммаНДС;
			
		Иначе 
			
			СчетФактура=СоздатьОбъект("Документ");
			Выбран = 0; 
			Если ДокументОснование.Вид() = "ПоступлениеТоваров" Тогда
				Если ((ДокументОснование.ВидПоступления=4) или (ДокументОснование.ВидПоступления=13)) и (ДокументОснование.ПокупателемВыставляетсяСчетФактураНаВозврат = 0) и (ДокументОснование.ВариантОтраженияВозврата = 1) Тогда
					Если ПустоеЗначение(ДокументОснование.ДокументОснование) = 0 Тогда
						СчетФактура.ВыбратьПодчиненныеДокументы(ДокументОснование.ДокументОснование.ДатаДок,ДокументОснование.ДатаДок,ДокументОснование.ДокументОснование);
						Пока СчетФактура.ПолучитьДокумент()=1 Цикл
							Если СчетФактура.Вид()="СчетФактура" Тогда
								Выбран = 1;
								Прервать;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли; 
				КонецЕсли; 
			КонецЕсли; 

			Если СуммаНДС >=0 Тогда
				БухИт.ИспользоватьСубконто(ВидыСубконто.Контрагенты, Контрагент, 2);
				БухИт.ИспользоватьСубконто(ВидыСубконто.СчетаФактурыПолученные, ?(Выбран = 1, СчетФактура, ДокументОснование), 2);
				БухИт.ВыполнитьЗапрос(НачМесяца(ДатаДок), ТекущийДокумент(), Субсчет19);
				Если БухИт.СКД() < СуммаНДС Тогда
					ТекстСообщения = "Сумма НДС превышает сальдо по дебету счета "+Субсчет19;
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "ФР";
			Операция.СодержаниеПроводки = "НДС принят к зачету";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = КоррСчет;
			Операция.Дебет.Субконто(1, Субконто1);
			Операция.Дебет.Субконто(2, Субконто2);
			Операция.Дебет.Субконто(3, Субконто3);
			Операция.Кредит.Счет = Субсчет19;
			Операция.Кредит.Субконто(1, Контрагент);
			Операция.Кредит.Субконто(2, ?(Выбран = 1, СчетФактура.ТекущийДокумент(), ДокументОснование));
			Операция.Сумма = СуммаНДС;
		КонецЕсли;
	КонецЕсли;    
	
	Если (ПустоеЗначение(Операция.Содержание) = 1) и (Операция.СуммаОперации = 0) Тогда
		Если ОплатаДоговора = 2 Тогда
			Операция.СуммаОперации = Всего*Курс/Кратность;
			
		Иначе
			Операция.СуммаОперации = Всего;
		КонецЕсли;
		Операция.Содержание = "Запись книги покупок";
	КонецЕсли;
		
	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения()