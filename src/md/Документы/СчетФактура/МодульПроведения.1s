//*****************************************************************************
// Предопределенная процедура
//
// Описание:
//  Формирует проводки операции документа.
//
Процедура ОбработкаПроведения()
	
	СчГТД = СчетПоКоду("ГТД");
	Сч90_3 = СчетПоКоду("90.3");
	Сч91_2 = СчетПоКоду("91.2");
	Сч62_6 = СчетПоКоду("62.6");
	Сч62_7 = СчетПоКоду("62.7");
	Сч68_2 = СчетПоКоду("68.2");
	Сч68_3 = СчетПоКоду("68.3");
	Сч68_5 = СчетПоКоду("68.5");
	Сч76_Н_1 = СчетПоКоду("76.Н.1");
	Сч76_Н_2 = СчетПоКоду("76.Н.2");
	Сч76_Н_4 = СчетПоКоду("76.Н.4");
	Сч76_АВ = СчетПоКоду("76.АВ");
	СчВР_6 = СчетПоКоду("ВР.6");
	
	// Определим курс валюты операции.
	СуммаЗачетаВал = 0;
	СуммаЗачетаРуб = 0;
	ЗадолженностьВал = 0;
	ЗадолженностьРуб = 0;
	
	ЦеныВДоговоре = 1; // в рублях
	ОплатаДоговора = 1; // в рублях
	Если Договор.Выбран() = 1 Тогда
	    Если ПустоеЗначение(Договор.ВалютаДоговора) = 0 Тогда
			ЦеныВДоговоре = 2; // в валюте
			ОплатаДоговора = Договор.ОплатаДоговора; // 1 - врублях, 2 - в валюте
		КонецЕсли;
		
		ВестиУчетРасчетовУЕ = Договор.ВестиУчетРасчетовУЕ;
		
		Если ДокументОснование.Выбран() = 1 Тогда // поиск зачтенного аванса
		    Опер = СоздатьОбъект("Операция");
			Если Опер.НайтиОперацию(ДокументОснование) = 1 Тогда
			    Опер.ВыбратьПроводки();
				Пока Опер.ПолучитьПроводку() = 1 Цикл
					Если Опер.Дебет.Счет = СчВР_6 Тогда
					    СуммаЗачетаВал = СуммаЗачетаВал + Опер.ВалСумма;
						СуммаЗачетаРуб = СуммаЗачетаРуб + Опер.Сумма;
						ЗадолженностьВал = ЗадолженностьВал + Опер.ВалСумма;
						ЗадолженностьРуб = ЗадолженностьРуб + Опер.Сумма;
					
					Иначе
					    Если (Опер.Дебет.Счет = Сч62_7) и (Опер.Кредит.Счет = Сч62_6) Тогда
					        СуммаЗачетаВал = СуммаЗачетаВал + Опер.ВалСумма;
							СуммаЗачетаРуб = СуммаЗачетаРуб + Опер.Сумма;
						КонецЕсли;
						
						Если Опер.Дебет.Счет = Сч62_6 Тогда
							ЗадолженностьВал = ЗадолженностьВал + Опер.ВалСумма;
							ЗадолженностьРуб = ЗадолженностьРуб + Опер.Сумма;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ВерсияОбъекта < "7.70.421" Тогда
		Если Валюта.Выбран() = 1 Тогда
			ЦеныВДоговоре = 2;
			Курс = Валюта.Курс.Получить(ДатаДок);
		КонецЕсли;
	Иначе
		Валюта = Договор.ВалютаДоговора;
	КонецЕсли;
	
	Если ЦеныВДоговоре = 2 Тогда
		Кратность = Валюта.Кратность.Получить(ДатаДок);
		Кратность = ?(Кратность=0, 1, Кратность);
		Если Договор.ВестиУчетРасчетовУЕ = 1 Тогда
			Кратность = Кратность * 100 / (100 + Договор.ПроцентКорректировкиКурсаУЕ);
		КонецЕсли;
	КонецЕсли;
	
	// Выгрузим табличную часть документа в таблицу значений.
	Таб = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(Таб);
	Таб.НоваяКолонка("ВидТовара", "Строка");
	Таб.НоваяКолонка("НДСВал", "Число");
	Таб.НоваяКолонка("АкцизВал", "Число");
	Таб.НоваяКолонка("НПВал", "Число");

	
	НомСтроки = 1;
	Пока НомСтроки <= Таб.КоличествоСтрок() Цикл
		Таб.ПолучитьСтрокуПоНомеру(НомСтроки);
		Если ТипЗначенияСтр(Таб.Товар) = "Справочник" Тогда
			Таб.ВидТовара = Таб.Товар.Вид();
			Если Таб.Товар.Вид() = "Номенклатура" Тогда
				Если (Таб.Товар.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Товар)
				   и (Таб.Товар.ТипТовара = Перечисление.ТипыТоваров.НаКомиссии) Тогда
					Таб.УдалитьСтроку(); // для товаров на комиссии проводок не формируем
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		       
		Если ПустоеЗначение(Таб.Счет) = 1 Тогда
			Если ((Таб.НДС <> 0) и (ОтключитьНДС = 0) и (Аванс = 0))
			 или ((Таб.АкцизСумма <> 0) и (ОтключитьАкциз = 0))
			 или ((Таб.НП <> 0) и (ОтключитьНП = 0) и (Аванс = 0)) Тогда
			 	ТекстСообщения = "На закладке ""Корр.счет и номер ГТД"" в строке №" + НомСтроки + " не указан корр. счет.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		НомСтроки = НомСтроки + 1;
	КонецЦикла;
	
	ДоляОплаченнойОтгрузки = ?(ЗадолженностьВал = 0, 0, СуммаЗачетаВал / ЗадолженностьВал);
	КурсОплаты = ?(СуммаЗачетаВал = 0, 0, Окр(СуммаЗачетаРуб / СуммаЗачетаВал, 4, 1));
	КурсСФ = ?(((ЦеныВДоговоре = 2) и (Аванс = 0)) или (ОплатаДоговора = 2), Курс / Кратность, 1);
	
	Если ВидОперации = Перечисление.ВидыОперацийСчетаФактурыВыданного.СуммоваяРазница Тогда
		КурсСФ = 1;
	КонецЕсли;
	
	Таб.ВыбратьСтроки();
	Пока Таб.ПолучитьСтроку()= 1 Цикл
		
		Если ЦеныВДоговоре = 2 Тогда
			Таб.НДСВал = Таб.НДС;
			Таб.АкцизВал = Таб.АкцизСумма;
			Таб.НПВал = Таб.НП;
		КонецЕсли;

		ОплСумма = Таб.Сумма * ДоляОплаченнойОтгрузки;
		Таб.Сумма = Окр(ОплСумма * КурсОплаты + (Таб.Сумма - ОплСумма) * КурсСФ, 2, 1);
		
		ОплНДС = Таб.НДС * ДоляОплаченнойОтгрузки;
		Таб.НДС = Окр(ОплНДС * КурсОплаты + (Таб.НДС - ОплНДС) * КурсСФ, 2, 1);
		
		ОплНП = Таб.НП * ДоляОплаченнойОтгрузки;
		Таб.НП = Окр(ОплНП * КурсОплаты + (Таб.НП - ОплНП) * КурсСФ, 2, 1);
		
		ОплАкцизСумма = Окр(Таб.АкцизСумма * ДоляОплаченнойОтгрузки, 2, 1);
		Таб.АкцизСумма = Окр(ОплАкцизСумма * КурсОплаты + (Таб.АкцизСумма - ОплАкцизСумма) * КурсСФ, 2, 1);
		
	КонецЦикла;
		
	Таб.Свернуть("ВидТовара,Счет,Субконто1,Субконто2,Субконто3,СтавкаНДС","Сумма,НДС,НП,АкцизСумма,НДСВал,АкцизВал,НПВал");

	// По каждой из строк полученной таблицы формируем проводки.
	Таб.ВыбратьСтроки();
	Пока Таб.ПолучитьСтроку()= 1 Цикл
		
		// Определим номер журнала проводок.
		Если Таб.ВидТовара = "Номенклатура" Тогда
			НомЖур = "ТВ";
		ИначеЕсли Таб.ВидТовара = "ОсновныеСредства" Тогда
			НомЖур = "ОС";
		ИначеЕсли Таб.ВидТовара = "НематериальныеАктивы" Тогда
			НомЖур = "НА";
		ИначеЕсли Таб.ВидТовара = "Материалы" Тогда
			НомЖур = "МТ";
		КонецЕсли;
		
		// Проводка НДС.
		Если (Таб.НДС <> 0) и (ОтключитьНДС = 0) Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Если Аванс = 0 Тогда
				Операция.НомерЖурнала = НомЖур;
				Операция.СодержаниеПроводки = "Начислен НДС";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = Таб.Счет;
				Операция.Дебет.Субконто(1, Таб.Субконто1);
				Операция.Дебет.Субконто(2, Таб.Субконто2);
				Операция.Дебет.Субконто(3, Таб.Субконто3);
				Если (ЦеныВДоговоре = 2) и (Таб.Счет.Валютный=1) Тогда
					Операция.Валюта=Валюта;
					Операция.ВалСумма=Таб.НДСВал;
				КонецЕсли;
			Иначе
				Операция.СодержаниеПроводки = "Начислен НДС с предоплаты";
				Операция.НомерЖурнала = "БК";
				Операция.Дебет.Счет = Сч76_АВ;
				Операция.Дебет.Субконто(1, Контрагент);
				Операция.Дебет.Субконто(2, ТекущийДокумент());
			КонецЕсли;
			Если СчетНДС = 2 Тогда
				Операция.Кредит.Счет = Сч76_Н_1;
				Операция.Кредит.Субконто(1, Контрагент);
				Операция.Кредит.Субконто(2, ТекущийДокумент());
			Иначе
				Операция.Кредит.Счет = Сч68_2;
				Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
			КонецЕсли;
			Операция.Сумма = Таб.НДС;		
		КонецЕсли;
		          
		// Проводка акциза.
		Если (Таб.АкцизСумма <> 0) и (ОтключитьАкциз = 0) Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = НомЖур;
			Операция.СодержаниеПроводки = "Начислен акциз";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			
			Если Таб.Счет = СчетПоКоду("90.3") Тогда
				Операция.Дебет.Счет = СчетПоКоду("90.4");
			Иначе
				Операция.Дебет.Счет = Таб.Счет;
			КонецЕсли;
			Операция.Дебет.Субконто(1, Таб.Субконто1);
			Операция.Дебет.Субконто(2, Таб.Субконто2);
			Операция.Дебет.Субконто(3, Таб.Субконто3);
			Операция.Кредит.Счет = Сч68_3;
			Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
			Если СчетАкциза = 2 Тогда
				Операция.Кредит.Счет = Сч76_Н_2;
				Операция.Кредит.Субконто(1, Контрагент);
				Операция.Кредит.Субконто(2, ТекущийДокумент());
			КонецЕсли;
			Операция.Сумма = Таб.АкцизСумма; 
			Если (ЦеныВДоговоре = 2) и (Таб.Счет.Валютный=1) Тогда
				Операция.Валюта=Валюта; 
				Операция.ВалСумма=Таб.АкцизВал;
			КонецЕсли;
		КонецЕсли;
		
		// Если указан, это то счет-фактура ред. 4.0 и надо формировать проводки.
		Если (Таб.НП <> 0) и (ОтключитьНП = 0) и (Аванс = 0) и (ВерсияОбъекта < "7.70.421") Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = НомЖур;
			Операция.СодержаниеПроводки = "Начислен НП";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Если Таб.Счет = СчетПоКоду("90.3") Тогда
				Операция.Дебет.Счет = СчетПоКоду("90.6");
			Иначе
				Операция.Дебет.Счет = Таб.Счет;
			КонецЕсли;
			Операция.Дебет.Субконто(1, Таб.Субконто1);
			Операция.Дебет.Субконто(2, Таб.Субконто2);
			Операция.Дебет.Субконто(3, Таб.Субконто3);
			Операция.Кредит.Счет = Сч68_5;
			Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
			Если СчетНП = 2 Тогда
				Операция.Кредит.Счет = Сч76_Н_4;
				Операция.Кредит.Субконто(1, Контрагент);
			КонецЕсли;
			Операция.Сумма = Таб.НП;
			Если (ЦеныВДоговоре = 2) и (Таб.Счет.Валютный=1) Тогда
				Операция.Валюта=Валюта;
				Операция.ВалСумма=Таб.НПВал;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
		
	// Для импортных товаров сформируем проводки по счету ГТД.
	Если ВидОперации = Перечисление.ВидыОперацийСчетаФактурыВыданного.Реализация Тогда
		БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИт.Рассчитать(, ТекущийДокумент(), СчГТД);
	
		КоличествоИмпортныхТоваров = 0;
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Если ТипЗначения(Товар) = 2 Тогда
				Продолжить;
			ИначеЕсли Товар.Вид() = "Номенклатура" Тогда
				Если (Товар.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Товар) и 
					(Товар.СтранаПроисхожд = 0) Тогда
					ОстатокПоГТД = БухИт.СКД(СчГТД, "К",, Товар, ГТД);
					Если Количество > ОстатокПоГТД Тогда
						ТекстСообщения = "По счету ГТД определен остаток "+ОстатокПоГТД+" "+Товар.ЕдиницаИзмерения+
										" товара "+Товар+" из необходимых "+Количество+" "+Товар.ЕдиницаИзмерения+" (строка №"+НомерСтроки+").";
						глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), -1);
					КонецЕсли;
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ТВ";
					Операция.СодержаниеПроводки = "Продан импортный товар";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Кредит.Счет = СчГТД;
					Операция.Кредит.Номенклатура = Товар;
					Операция.Кредит.ГТД = ГТД;
					Операция.Количество = Количество;
					
					КоличествоИмпортныхТоваров = КоличествоИмпортныхТоваров + 1;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если (ВерсияОбъекта < "7.70.421") и (Аванс = 0) Тогда
		ТаблицаКомиссии = СоздатьОбъект("ТаблицаЗначений");
		ВыгрузитьТабличнуюЧасть(ТаблицаКомиссии);
		ТаблицаКомиссии.Свернуть("Счет,Субконто1,Комитент", "НДС_Вознаграждения");
		ТаблицаКомиссии.ВыбратьСтроки();
		Пока ТаблицаКомиссии.ПолучитьСтроку() = 1 Цикл
			Если ТаблицаКомиссии.НДС_Вознаграждения = 0 Тогда
			Иначе
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = НомЖур;
				Операция.СодержаниеПроводки = "Начислен НДС";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = ТаблицаКомиссии.Счет;
				Операция.Дебет.Субконто(1, ТаблицаКомиссии.Субконто1);
				Операция.Кредит.Счет = Сч68_2;
				Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
				Если СчетНДС = 2 Тогда
					Операция.Кредит.Счет = СчетПоКоду("76.Н.4");
					Операция.Кредит.Субконто(1, ТаблицаКомиссии.Комитент);
				КонецЕсли;
			    Операция.Сумма = ТаблицаКомиссии.НДС_Вознаграждения*?(ЦеныВДоговоре = 2, Курс/Кратность, 1);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если (Договор.АвтоОбработкаНДС = 1) и
		 ((СчетНДС = 2) или (ДатаДок >= '01.01.2006')) и
		 (Аванс = 0) Тогда
		
		БезНДС20=0;
		БезНДС10=0;
		НДС20=0;
		НДС10=0;
		НДС0=0;
		Освобождаемые=0;
		
		Если НДСпоСтавкеНольПроцентов=1 Тогда
			НДС0 = Таб.Итог("Сумма");
			
		Иначе
			Таб.Свернуть("СтавкаНДС", "Сумма,НДС");
			Таб.ВыбратьСтроки();
			Пока Таб.ПолучитьСтроку()=1 Цикл
				Если Таб.СтавкаНДС.Ставка > 17 Тогда
					БезНДС20 = БезНДС20 + Таб.Сумма;
					НДС20 = НДС20 + Таб.НДС; 
				ИначеЕсли Таб.СтавкаНДС.Ставка = 10 Тогда
					БезНДС10 = БезНДС10 + Таб.Сумма;
					НДС10 = НДС10 + Таб.НДС;
				ИначеЕсли Таб.СтавкаНДС.Ставка = 0 Тогда
					Освобождаемые = Освобождаемые + Таб.Сумма;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если БезНДС20>0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "АВ";
			Операция.Кредит.Счет = СчетПоКоду("ЗПР.20.Б");
			Операция.Кредит.Субконто(1,Контрагент);
			Операция.Кредит.Субконто(2,Договор);
			Операция.Кредит.Субконто(3,ТекущийДокумент());
			Операция.Сумма = БезНДС20;
			Операция.СодержаниеПроводки = "Данные для автоматического учета НДС";
		КонецЕсли;
		
		Если НДС20>0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "АВ";
			Операция.Кредит.Счет = СчетПоКоду("ЗПР.20.Н");
			Операция.Кредит.Субконто(1,Контрагент);
			Операция.Кредит.Субконто(2,Договор);
			Операция.Кредит.Субконто(3,ТекущийДокумент());
			Операция.Сумма = НДС20;
			Операция.СодержаниеПроводки = "Данные для автоматического учета НДС";
		КонецЕсли;
		
		Если БезНДС10>0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "АВ";
			Операция.Кредит.Счет = СчетПоКоду("ЗПР.10.Б");
			Операция.Кредит.Субконто(1,Контрагент);
			Операция.Кредит.Субконто(2,Договор);
			Операция.Кредит.Субконто(3,ТекущийДокумент());
			Операция.Сумма = БезНДС10;
			Операция.СодержаниеПроводки = "Данные для автоматического учета НДС";
		КонецЕсли;
		
		Если НДС10>0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "АВ";
			Операция.Кредит.Счет = СчетПоКоду("ЗПР.10.Н");
			Операция.Кредит.Субконто(1,Контрагент);
			Операция.Кредит.Субконто(2,Договор);
			Операция.Кредит.Субконто(3,ТекущийДокумент());
			Операция.Сумма = НДС10;
			Операция.СодержаниеПроводки = "Данные для автоматического учета НДС";
		КонецЕсли;
		
		Если НДС0>0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "АВ";
			Операция.Кредит.Счет = СчетПоКоду("ЗПР.0");
			Операция.Кредит.Субконто(1,Контрагент);
			Операция.Кредит.Субконто(2,Договор);
			Операция.Кредит.Субконто(3,ТекущийДокумент());
			Операция.Сумма = НДС0;
			Операция.СодержаниеПроводки = "Данные для автоматического учета НДС";
		КонецЕсли;
		
		Если Освобождаемые>0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "АВ";
			Операция.Кредит.Счет = СчетПоКоду("ЗПР.БН");
			Операция.Кредит.Субконто(1,Контрагент);
			Операция.Кредит.Субконто(2,Договор);
			Операция.Кредит.Субконто(3,ТекущийДокумент());
			Операция.Сумма = Освобождаемые;
			Операция.СодержаниеПроводки = "Данные для автоматического учета НДС";
		КонецЕсли;
	КонецЕсли;

	Если (ПустоеЗначение(Операция.Содержание) = 1) и (Операция.СуммаОперации = 0) Тогда
		Операция.СуммаОперации = Итог("Всего");
		Операция.Содержание = "Сч.-факт. покупателю: "+Контрагент;
	КонецЕсли;
	
	Операция.Записать();
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения()