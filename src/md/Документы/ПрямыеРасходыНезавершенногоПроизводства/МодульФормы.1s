////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем НачальнаяДатаДокумента; 
Перем Новый;
Перем СписокВидовРасходов;
Перем СписокДействий;
Перем СписокВидовДокументовДляВыбора;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//****************************************************************************** 
// УправлениеВидимостьюКолонок(СпособРасчета)
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Устанавливает видимость нужных колонок табличной части в зависимости от
//  выбранного способа расчета
//
Процедура УправлениеВидимостьюКолонок(СпособРасчета)

	Если СпособРасчета = 1 Тогда
		Форма.Сумма.Видимость(0);
		Форма.Сумма.Доступность(0);
		Форма.Количество.Видимость(1);
		Форма.Количество.Доступность(1);
		Форма.ТехПотери.Видимость(1);
		Форма.ТехПотери.Доступность(1);
		
		Форма.Количество.Заголовок("Кол. НЗП");

	Иначе
		Форма.Сумма.Видимость(1);
		Форма.Сумма.Доступность(1);
		Форма.Количество.Видимость(0);
		Форма.Количество.Доступность(0);
		Форма.ТехПотери.Видимость(0);
		Форма.ТехПотери.Доступность(0);
	
		Форма.Сумма.Заголовок("Сумма НЗП");

	КонецЕсли;
	
	Если (СпособРасчета = 2)
	 или (СпособРасчета = 3) Тогда
		// Для второго и третьего способов исходные данные по НЗП вводятся по номенклатуре
	 	Форма.Номенклатура.Видимость(1);
		Форма.Номенклатура.Доступность(1);
		Форма.ВидНоменклатуры.Видимость(0);
		Форма.ВидНоменклатуры.Доступность(0);
		
	Иначе
		// Для первого и четвертого способов исходные данные по НЗП вводятся по видам номенклатуры
		Форма.Номенклатура.Видимость(0);
		Форма.Номенклатура.Доступность(0);
		Форма.ВидНоменклатуры.Видимость(1);
		Форма.ВидНоменклатуры.Доступность(1);
	    
	КонецЕсли;
	
	Если (СпособРасчета = 4) Тогда
		Форма.ВидРасхода.Видимость(1);
		Форма.ВидРасхода.Доступность(1);

	Иначе
		Форма.ВидРасхода.Видимость(0);
		Форма.ВидРасхода.Доступность(0);
	    
	КонецЕсли;

КонецПроцедуры // УправлениеВидимостьюКолонок()

//****************************************************************************** 
// ПриИзмененииСпособаРасчета()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  СпособыРасчета
//
// Описание:
//  Выполняет все действия связанные с изменением способа расчета НЗП
//
Процедура ПриИзмененииСпособаРасчета()

	Если СпособРасчета <> СпособыРасчета.ТекущаяСтрока() Тогда
		// изменилось значение текущего способа расчета НЗП
		Если КоличествоСтрок() <> 0 Тогда
			Если Вопрос("При изменении способа расчета табличная часть будет очищена! Продолжить?", "Да+Нет", 60) <> "Да" Тогда
				// устанавливаем старый способ расчета и больше ничего не меняем
				СпособыРасчета.ТекущаяСтрока(СпособРасчета);
				Возврат;

			Иначе
				УдалитьСтроки();
				
			КонецЕсли;
		    
		КонецЕсли;

		// сохраняем выбранный способ расчета
		СпособРасчета = СпособыРасчета.ТекущаяСтрока();

		// перестраиваем структуру табличной части
		УправлениеВидимостьюКолонок(СпособРасчета);		

	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииСпособаРасчета()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	
	Если Копирование = 1 Тогда //копирование документа
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ВводНового()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	                   
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.КнопкаОК.Доступность(0);
	КонецЕсли;

	СпособыРасчета.ДобавитьЗначение("Для производства, связанного с обработкой сырья");
	СпособыРасчета.ДобавитьЗначение("Для производства, связанного с оказ. услуг (выполн. работ)");
	СпособыРасчета.ДобавитьЗначение("Для прочих видов производства");
	СпособыРасчета.ДобавитьЗначение("Оценка производится вручную");
	
	Если ПустоеЗначение(СпособРасчета) = 1 Тогда
		СпособРасчета = 4;
	КонецЕсли;
	СпособыРасчета.ТекущаяСтрока(СпособРасчета);
	// перестраиваем структуру табличной части
	УправлениеВидимостьюКолонок(СпособРасчета);		
	
	СписокВидовРасходов = СоздатьОбъект("СписокЗначений");
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ОплатаТруда);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.МатериальныеРасходы);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.Амортизация);
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ЕСН); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.НалогиИСборы); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.РасходыНаРекламуБезНорм); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ИнформационныеУслуги); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ИзучениеКонъюнктурыРынка); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.РемонтОсновныхСредств); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ПодготовкаИПереподготовкаКадров); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ОхранаИмущества); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.УслугиПоУправлению); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.АудиторскиеУслуги); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ПредоставлениеРаботниковСтороннимиОрганизациями); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ОсвоениеПриродныхРесурсов); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.СоциальнаяЗащитаИнвалидов); 
	СписокВидовРасходов.ДобавитьЗначение(Перечисление.ГруппыВидыРасходов.ДругиеРасходы); 
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриРедактированииНовойСтроки()
	
	ДокументБУ.ВидыДляВыбора(СписокВидовДокументовДляВыбора);
	
КонецПроцедуры // ПриРедактированииНовойСтроки()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеРедактированияСтроки()
	                  
	Если (Форма.ТекущаяКолонка() = "ОткрытьДокумент") Тогда
		Если ДокументБУ.Выбран()=0 Тогда
			Предупреждение("Не могу открыть документ, отражающий данную операцию
			                |в бухгалтеском учете: документ не указан!",60);
		Иначе     
			ОткрытьФорму(ДокументБУ);
		КонецЕсли;
		
	Иначе
		ДокументБУ.ВидыДляВыбора(СписокВидовДокументовДляВыбора);
	КонецЕсли;
	
КонецПроцедуры // ПриРедактированииНовойСтроки()        

//*****************************************************************************
// Предопределенная процедура
//
//Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, Флаг)
//	
//	Если ЭлементДиалога = "ВидРасхода" Тогда
//		Флаг = 0;
//		Позиция = СписокВидовРасходов.НайтиЗначение(ВидРасхода);
//		СписокВидовРасходов.ВыбратьЗначение(ВидРасхода,, Позиция,, 2);
//	КонецЕсли;
//	
//КонецПроцедуры // ПриНачалеВыбораЗначения()

//*****************************************************************************
// Предопределенная процедура
//

Процедура ПриЗаписи() //предопределенная
	
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
		
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
		
	КонецЕсли;
	Если Константа.ВсеДокументыВЖурналОпераций = Да Тогда
	    СуществуетОперация(1);
		Операция.СуммаОперации = Итог("Сумма");
		Операция.Содержание = "Незавершенное производство";
	Иначе
	    СуществуетОперация(0);
	КонецЕсли;
КонецПроцедуры // ПриЗаписи()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Новый = 0;               

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности"); 
СписокДействий.ДобавитьЗначение("Перейти  в журнал");

СписокВидовДокументовДляВыбора = "НезавершенноеПроизводство";