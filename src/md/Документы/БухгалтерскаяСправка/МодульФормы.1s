Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;
Перем НачальнаяДатаДокумента; 
Перем Новый;
Перем СписокДействий;

//******************************************************************************
Функция ИнформацияБУ(Показатель)
	Стр = Показатель+".";
	Счет = ПолучитьАтрибут("Счет"+Показатель);
	Если ПустоеЗначение(Счет) = 0 Тогда
	    Стр = Стр+" "+Счет.Код;
		Для н = 1 по Счет.КоличествоСубконто() Цикл
			Субконто = ПолучитьАтрибут("Субконто"+Показатель+Строка(н));
			Если ПустоеЗначение(Субконто) = 0 Тогда
				Стр = Стр+" / "+Субконто;
			Иначе     
				Стр = Стр+" / "+"...";
			КонецЕсли;	
		КонецЦикла;
	Иначе
		Стр = " - ";
	КонецЕсли;
	
	Возврат Стр;
КонецФункции

//******************************************************************************
Процедура Пересчет()
	Если Валюта.Выбран() = 1 Тогда
		Курс = Валюта.Курс.Получить(ДатаДок);
		Кратность = Валюта.Кратность.Получить(ДатаДок);
		Кратность = ?(Кратность = 0 ,1 ,Кратность); 
	    Сумма = ВалСумма * Курс / Кратность;
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
Процедура ПриВыбореСчета(ИдентификаторКолонки = "")
	
	Если ПустоеЗначение(ИдентификаторКолонки) = 1 Тогда
		ИдентификаторКолонки = Форма.ТекущаяКолонка();
	КонецЕсли;
	
	Счет = ПолучитьАтрибут(ИдентификаторКолонки);
	ИмяАтрибутаСубконто = СтрЗаменить(ИдентификаторКолонки, "Счет", "Субконто");
	НазначитьТип(ИмяАтрибутаСубконто + "1", Счет.ВидСубконто(1));
	НазначитьТип(ИмяАтрибутаСубконто + "2", Счет.ВидСубконто(2));
	НазначитьТип(ИмяАтрибутаСубконто + "3", Счет.ВидСубконто(3));
	Если ПустоеЗначение(Счет) = 0 Тогда
		Если (Константа.ИспользоватьСписокКорректныхПроводок = Да) Тогда
			Если ((ИдентификаторКолонки = "СчетДт") и (СчетКт.Выбран()=1)) или
				((ИдентификаторКолонки = "СчетКт") и (СчетДт.Выбран()=1)) Тогда
				глПроверкаКорректныхПроводок(СчетДт,СчетКт);
			КонецЕсли;  
		КонецЕсли;
	КонецЕсли;
	Форма.Количество.Доступность(Мин(СчетДт.Количественный + СчетКт.Количественный,1));
	Форма.КоличествоН.Доступность(Мин(СчетДтН.Количественный + СчетКтН.Количественный,1));
	Форма.Валюта.Доступность(Мин(СчетДт.Валютный + СчетКт.Валютный,1));
	Форма.ВалСумма.Доступность(Мин(СчетДт.Валютный + СчетКт.Валютный,1));
	Если Форма.Количество.Доступность() = 0 Тогда
	    Количество = "";
	КонецЕсли;          
	Если Форма.КоличествоН.Доступность() = 0 Тогда
	    КоличествоН = "";
	КонецЕсли;
	Если Форма.Валюта.Доступность() = 0 Тогда
	    Валюта = "";
	КонецЕсли;
	Если Форма.ВалСумма.Доступность() = 0 Тогда
	    ВалСумма = "";
	КонецЕсли;
	
КонецПроцедуры

//******************************************************************************
Процедура ПриВыбореСубконто()
	
	ИдентификаторРеквизита = Форма.ТекущаяКолонка();
	ЗначениеРеквизита = ПолучитьАтрибут(ИдентификаторРеквизита);
	НомерСубконто = Число(Прав(ИдентификаторРеквизита, 1));
	Если ТипЗначенияСтр(ЗначениеРеквизита) = "Справочник" Тогда
		Если ЗначениеРеквизита.Вид() = "Контрагенты" Тогда
			Если НомерСубконто < 3 Тогда
				ИдентификаторСледующегоРеквизита = Лев(ИдентификаторРеквизита, СтрДлина(ИдентификаторРеквизита)-1) + (НомерСубконто + 1);
				ЗначениеСледующегоРеквизита = ПолучитьАтрибут(ИдентификаторСледующегоРеквизита);
				Если ТипЗначенияСтр(ЗначениеСледующегоРеквизита) = "Справочник" Тогда
					Если ЗначениеСледующегоРеквизита.Вид() = "Договоры" Тогда
						Если ЗначениеРеквизита <> ЗначениеСледующегоРеквизита.Владелец Тогда
						    УстановитьАтрибут(ИдентификаторСледующегоРеквизита, ЗначениеРеквизита.ОсновнойДоговор);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ЗначениеРеквизита.Вид() = "Договоры" Тогда
			Если (ПустоеЗначение(ЗначениеРеквизита) = 0) и (НомерСубконто > 1) Тогда
				ИдентификаторПредыдущегоРеквизита = Лев(ИдентификаторРеквизита, СтрДлина(ИдентификаторРеквизита)-1) + (НомерСубконто - 1);
				ЗначениеПредыдущегоРеквизита = ПолучитьАтрибут(ИдентификаторПредыдущегоРеквизита);
				Если ТипЗначенияСтр(ЗначениеПредыдущегоРеквизита) = "Справочник" Тогда
					Если ЗначениеПредыдущегоРеквизита.Вид() = "Контрагенты" Тогда
						Если ЗначениеРеквизита.Владелец <> ЗначениеПредыдущегоРеквизита Тогда
						    УстановитьАтрибут(ИдентификаторПредыдущегоРеквизита, ЗначениеРеквизита.Владелец);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
Процедура Печать()
	
	Заголовок = "Бухгалтерская справка №"+НомерДок+" от "+ДатаДок;
    Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	Таб.ВывестиСекцию("ОтчетДок");
	Таб.ВывестиСекцию("ШапкаП");
	НП = 1;
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ((СчетДт.Выбран() = 1) или (СчетКт.Выбран() = 1)) Тогда 
			АналитикаДт = Строка(СубконтоДт1) + РазделительСтрок + Строка(СубконтоДт2) + РазделительСтрок + Строка(СубконтоДт3);
			АналитикаКт = Строка(СубконтоКт1) + РазделительСтрок + Строка(СубконтоКт2) + РазделительСтрок + Строка(СубконтоКт3);
			
			Таб.ВывестиСекцию("Проводка");
			НП = НП+1;                                                  
		КонецЕсли;
	КонецЦикла;
	Таб.ВывестиСекцию("КонецОтчета");
	Таб.ПараметрыСтраницы(2,,,,,,,,,,,);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Бухгалтерская справка","");
	
КонецПроцедуры

//******************************************************************************
// ПоКнопкеПечать()
// 
// Вызывается из формул элементов диалога:
//  Кнопка "кнПечать".
//
// Описание:
//  Определяется соответствующая печатная форма.
// 	
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		Печать();
		
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(Контекст, "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Вызывается из формул элементов диалога:
//  Кнопка "кнВыбПечать".
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Документ." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриВыбореЗакладки(Номер, Значение)
    
	Если Номер = 1 Тогда
        БУ = 1;
		НУ = 0;
	Иначе
		БУ = 0;
		НУ = 1;
    КонецЕсли;
	Форма.СчетДт.Видимость(БУ);
	Форма.СубконтоДт1.Видимость(БУ);
	Форма.СубконтоДт2.Видимость(БУ);
	Форма.СубконтоДт3.Видимость(БУ);
	Форма.СчетКт.Видимость(БУ);
	Форма.СубконтоКт1.Видимость(БУ);
	Форма.СубконтоКт2.Видимость(БУ);
	Форма.СубконтоКт3.Видимость(БУ); 
	
	Форма.Валюта.Видимость(БУ);
	Форма.ВалСумма.Видимость(БУ);
	Форма.Сумма.Видимость(БУ);
	Форма.НЖ.Видимость(БУ);
	Форма.Количество.Видимость(БУ);
	
	Форма.СчетДтН.Видимость(НУ);
	Форма.СубконтоДтН1.Видимость(НУ);
	Форма.СубконтоДтН2.Видимость(НУ);
	Форма.СубконтоДтН3.Видимость(НУ);
	Форма.СчетКтН.Видимость(НУ);
	Форма.СубконтоКтН1.Видимость(НУ);
	Форма.СубконтоКтН2.Видимость(НУ);
	Форма.СубконтоКтН3.Видимость(НУ);
	
	Форма.КоличествоН.Видимость(НУ);
	Форма.СуммаН.Видимость(НУ);
	Форма.Информация.Видимость(НУ);
	
	Активизировать("НомерСтроки",0);
КонецПроцедуры

//******************************************************************************
Процедура Заполнить()
	
	Если  КоличествоСтрок() > 0 Тогда
		Если
			Вопрос("При заполнении старые данные на закладке ""Налоговый учет"" будут удалены
				   |Продолжить?","Да+Нет") <> "Да" Тогда
						
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
		ПереченьСтатейЗатрат = глПолучитьПереченьСтатейЗатрат(ДатаДок); 
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		
		// Дебет
	    ОбъектыАналитикиНУ = глСчетИАналитикаРасходовНУ(Контекст, СчетДт, СубконтоДт1, СубконтоДт2, СубконтоДт3, ПереченьСтатейЗатрат, СчетКт);
		СчетДтН = ОбъектыАналитикиНУ.Получить("Счет");
		ПриВыбореСчета("СчетДтН");
		СубконтоДтН1 = ОбъектыАналитикиНУ.Получить(СчетДтН.ВидСубконто(1).Идентификатор());
		СубконтоДтН2 = ОбъектыАналитикиНУ.Получить(СчетДтН.ВидСубконто(2).Идентификатор());
		СубконтоДтН3 = ОбъектыАналитикиНУ.Получить(СчетДтН.ВидСубконто(3).Идентификатор());
		        
		// Кредит
		ОбъектыАналитикиНУ = глСчетИАналитикаДоходовНУ(Контекст, СчетКт, СубконтоКт1, СубконтоКт2, СубконтоКт3);
		СчетКтН = ОбъектыАналитикиНУ.Получить("Счет");
		Если ПустоеЗначение(СчетКтН) = 1 Тогда
			ОбъектыАналитикиНУ = глСчетИАналитикаРасходовНУ(Контекст, СчетКт, СубконтоКт1, СубконтоКт2, СубконтоКт3, ПереченьСтатейЗатрат, СчетДт);
			СчетКтН = ОбъектыАналитикиНУ.Получить("Счет");
		КонецЕсли;
		
		ПриВыбореСчета("СчетКтН");
		СубконтоКтН1 = ОбъектыАналитикиНУ.Получить(СчетКтН.ВидСубконто(1).Идентификатор());
		СубконтоКтН2 = ОбъектыАналитикиНУ.Получить(СчетКтН.ВидСубконто(2).Идентификатор());
		СубконтоКтН3 = ОбъектыАналитикиНУ.Получить(СчетКтН.ВидСубконто(3).Идентификатор());
		
		Если (ПустоеЗначение(СчетДтН) = 0) или (ПустоеЗначение(СчетКтН) = 0) Тогда
			КоличествоН = Количество;
			СуммаН = Сумма;    
		КонецЕсли;
		
	КонецЦикла;
	
	Если Форма.Закладки.ТекущаяСтрока() = 1 Тогда
		Форма.Закладки.ТекущаяСтрока(2);
		ПриВыбореЗакладки(2,"Налоговый учет");    
	КонецЕсли;

КонецПроцедуры

//*****************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	
КонецПроцедуры // ВводНового()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	ПриЗаписиПерепроводить(1);
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.кнОК.Доступность(0);
		Форма.кнЗаписать.Доступность(0);
		Форма.кнЗаполнить.Доступность(0);
		Форма.кнОчистить.Доступность(0);
	КонецЕсли;
	
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Бухгалтерский учет");
	Форма.Закладки.ДобавитьЗначение("Налоговый учет");
	ПриВыбореЗакладки(1,"Бухгалтерский учет");
	
	НачальнаяДатаДокумента = ДатаДок;
	
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Документ." + Вид(),ТаблицаПечФорм);
	
КонецПроцедуры

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеРедактированияСтроки()
	Форма.Количество.Доступность(Мин(СчетДт.Количественный + СчетКт.Количественный,1));
	Форма.КоличествоН.Доступность(Мин(СчетДтН.Количественный + СчетКтН.Количественный,1));
	Форма.Валюта.Доступность(Мин(СчетДт.Валютный + СчетКт.Валютный,1));
	Форма.ВалСумма.Доступность(Мин(СчетДт.Валютный + СчетКт.Валютный,1));
КонецПроцедуры

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, ФлагСтандОбр)
	Если (Константа.ИспользоватьСписокКорректныхПроводок = Да) Тогда
		Если (ИдентЭлемДиалога = "СчетДт") и (СчетКт.Выбран()=1) Тогда
			СписокКорректныхПроводок = СоздатьОбъект("СписокЗначений");
			СписокКорректныхПроводок.Установить("Счет", СчетКт);
			СписокКорректныхПроводок.Установить("Корреспонденция", 0);
			глЗначениеОтбора = СписокКорректныхПроводок;
		ИначеЕсли (ИдентЭлемДиалога = "СчетКт") и (СчетДт.Выбран()=1) Тогда
			СписокКорректныхПроводок = СоздатьОбъект("СписокЗначений");
			СписокКорректныхПроводок.Установить("Счет", СчетДт);
			СписокКорректныхПроводок.Установить("Корреспонденция", 1);
			глЗначениеОтбора = СписокКорректныхПроводок;
		КонецЕсли;  
	КонецЕсли;
КонецПроцедуры

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи() //предопределенная
	
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
		
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

//******************************************************************************
Новый = 0;

ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");
	
// добавим информацию о встроенной форме
ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Печать";
ТаблицаПечФорм.Кнопка       = "Печать";

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Ввести на основании");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");