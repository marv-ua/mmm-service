////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;
Перем НачальнаяДатаДокумента;
Перем Новый;
Перем СписокДействий;

//_____________________________________________________________________________
Процедура ПриВыбореСчетаКонтрагента()
	Если СчетКонтрагента.Выбран() = 1 Тогда
		Если СчетКонтрагента.Владелец <> Контрагент Тогда
			Контрагент = СчетКонтрагента.Владелец;
		КонецЕсли;
		Если (ПустоеЗначение(Содержание) = 1) и (ПустоеЗначение(СчетКонтрагента.Назначение)=0) Тогда 
			Содержание=СчетКонтрагента.Назначение;
		КонецЕсли;               
	КонецЕсли;
КонецПроцедуры //ПриВыбореСчетаКонтрагента
//_____________________________________________________________________________
Процедура ПриВыбореКонтрагента()
	Если СчетКонтрагента.Владелец <> Контрагент Тогда
		СчетКонтрагента = "";
	КонецЕсли;
	Если ПустоеЗначение(СчетКонтрагента) = 1 Тогда
	    СчетКонтрагента = глРасчетныйСчетКонтрагента(Контрагент);
		ПриВыбореСчетаКонтрагента();
	КонецЕсли;
КонецПроцедуры //ПриВыбореКонтрагента
//_____________________________________________________________________________
Процедура ПриУстановкеАкцепта()
   	Форма.Уполномоченный.Доступность(Акцепт);
    Форма.УсловиеОплаты.Доступность(Акцепт);
	Если Акцепт = 0 Тогда
	    УсловиеОплаты = "";
	КонецЕсли;
КонецПроцедуры
//_____________________________________________________________________________
Функция ДоступностьСчета()
	Если ВидАккредитива = Перечисление.ВидыАккредитива.ОтзывныйПокрытый Тогда
    	Возврат 1;
	ИначеЕсли ВидАккредитива = Перечисление.ВидыАккредитива.БезотзывныйПокрытый Тогда
    	Возврат 1;
	ИначеЕсли ВидАккредитива = Перечисление.ВидыАккредитива.ОтзывныйНепокрытый Тогда
    	Возврат 0;
	ИначеЕсли ВидАккредитива = Перечисление.ВидыАккредитива.БезотзывныйНепокрытый Тогда
    	Возврат 0;
	КонецЕсли;
КонецФункции
//_____________________________________________________________________________
Процедура ПриВыбореВидАккредитива()
	Форма.Счет.Доступность(ДоступностьСчета());
	Форма.СчетПоставщика.Доступность(ДоступностьСчета());
КонецПроцедуры
//-----------------------------------------------
Процедура РасчетНДС()
	Если СтавкаНДС.Выбран() = 1 Тогда
	    СтНДС = СтавкаНДС.Ставка;
	Иначе
		СтНДС = 0;
	КонецЕсли;
	НДС = Сумма*СтНДС/(100+СтНДС);
КонецПроцедуры
//-----------------------------------------------
Процедура УстДату()
	СрокПлатежа = ДатаДок;
КонецПроцедуры
//_____________________________________________________________________________
Процедура Печать()
	
	Если (Прав(Строка(НомерДок),3) = "000") Тогда
		Если Вопрос("Номер аккредитива не может оканчиваться на ""000"".
					|Вернуться к редактированию документа?", "Да+Нет") = "Да" Тогда
	    	Активизировать("НомерДок");
			Возврат;
	    КонецЕсли;
	КонецЕсли;
	
	Таб=СоздатьОбъект("Таблица");
	ИмяФайлаПечатнойФормы = КаталогИБ()+"ExtForms\PrnForms\1cbaccr.mxl";
	Если ФС.СуществуетФайл(ИмяФайлаПечатнойФормы) = 1 Тогда
		Таб.ИсходнаяТаблица(ИмяФайлаПечатнойФормы);
	Иначе
		Таб.ИсходнаяТаблица("Таблица");
	КонецЕсли;
	Таб.Вывести();
	
	Таб.Область("НаименованиеНомер").Текст = "АККРЕДИТИВ № "+глПреобразоватьНомерДок(НомерДок, 0, 0);
	Таб.Область("ДатаДокумента").Текст = глВыводДатыПлатежныхДокументов(ДатаДок,РасчетныйСчет);	// Месяц даты числом или прописью
	Таб.Область("УсловиеОплаты").Текст = ?(Акцепт=1,"С акцептом","Без акцепта")+РазделительСтрок+СокрЛП(УсловиеОплаты);
	Таб.Область("ВидПлатежа").Текст = ВидПлатежа;
	Таб.Область("ВидАккредитива").Текст = ВидАккредитива;
	
	Таб.Область("СуммаЧислом").Текст = глВыводСуммыПлатежныхДокументов(Сумма,РасчетныйСчет);	// Сумма без 00 копеек (=) или (-00)
	Таб.Область("СуммаПрописью").Текст = глВыводСуммыПлатежныхДокументовПрописью(Сумма,РасчетныйСчет);	// Сумма без 00 копеек (=) или (-00)
	Таб.Область("СрокПлатежа").Текст = ?(ПустоеЗначение(СрокПлатежа)=1,"",Формат(СрокПлатежа,"Д ДДММГГГГ"));
	
	Плательщик = "";БанкПлательщика = "";НомерСчетаПлательщика = "";БИКБанкаПлательщика = "";СчетБанкаПлательщика = "";
	глПлатежныеРеквизиты(РасчетныйСчет, Плательщик, БанкПлательщика, НомерСчетаПлательщика, БИКБанкаПлательщика, СчетБанкаПлательщика);
	Таб.Область("Плательщик").Текст = Плательщик;
	Таб.Область("БанкПлательщика").Текст = БанкПлательщика;
	Таб.Область("НомерСчетаПлательщика").Текст = НомерСчетаПлательщика;
	Таб.Область("БИКБанкаПлательщика").Текст = БИКБанкаПлательщика;
	Таб.Область("СчетБанкаПлательщика").Текст = СчетБанкаПлательщика;
	
	Получатель = "";БанкПолучателя = "";НомерСчетаПолучателя = "";БИКБанкаПолучателя = "";СчетБанкаПолучателя = "";
	глПлатежныеРеквизиты(СчетКонтрагента, Получатель, БанкПолучателя, НомерСчетаПолучателя, БИКБанкаПолучателя, СчетБанкаПолучателя);
	Таб.Область("Поставщик").Текст = Получатель;
	Таб.Область("БанкПоставщика").Текст = БанкПолучателя;
	
	Таб.Область("НомерСчетаПоставщика").Текст = ?(ДоступностьСчета()=1,СчетПоставщика,"");
	Таб.Область("БИКБанкаПоставщика").Текст = БИКБанкаПолучателя;
	Таб.Область("СчетБанкаПоставщика").Текст = СчетБанкаПолучателя;

	Таб.Область("НазначениеПлатежа").Текст = СокрЛП(Содержание);
	Таб.Область("ВидДокумента").Текст = "Платеж по представлению (вид документа) "+СокрЛП(ВидДокумента);
	Таб.Область("ДополнительныеУсловия").Текст = "Дополнительные условия "+СокрЛП(ДополнУсловия);
	Таб.Область("НомерСчетаПоставщикаТекст").Текст = "№ сч. получателя "+ СокрЛП(НомерСчетаПолучателя);

	Таб.Опции(0,0,0,0,"ОпцииПечатиЗаявленияНаАккредитив","ПараметрыОкнаЗаявленияНаАккредитив");
	Таб.ТолькоПросмотр(1);
	Таб.ПараметрыСтраницы(,,,,0,,0,,,);
	Таб.Показать("Печать аккредитива","");   
	
КонецПроцедуры // Печать

//******************************************************************************
// ПоКнопкеПечать()
// 
// Вызывается из формул элементов диалога:
//  Кнопка "кнПечать".
//
// Описание:
//  Определяется соответствующая печатная форма.
// 	
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		Печать();
		
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(Контекст, "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Вызывается из формул элементов диалога:
//  Кнопка "кнВыбПечать".
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Документ." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()

//_____________________________________________________________________________
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	Если Копирование = 1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;
	РасчетныйСчет = глЗначениеПоУмолчанию("ОсновнойБанковскийСчет");
	СтавкаНДС = глЗначениеПоУмолчанию("ОсновнаяСтавкаНДС");
	ВидПлатежа = Перечисление.ВидыПлатежа.Электронно;
	Акцепт = 1;
	СрокПлатежа = ДатаДок;
	УсловиеОплаты = "";
	ВидАккредитива = Перечисление.ВидыАккредитива.ОтзывныйПокрытый;
	СохрСчетПоставщика = СчетПоставщика;
КонецПроцедуры
//_____________________________________________________________________________
Процедура ПриОткрытии() //предопределенная
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.кнВыбораРасчетногоСчета.Доступность(0);
		Форма.кнОК.Доступность(0);
		Форма.КнопкаПоУмолчанию("кнЗакрыть");
		Форма.КнопкаЗаписать.Доступность(0);
		
		Форма.кнОчиститьУсловия.Доступность(0); 
		Форма.кнОчиститьСодержание.Доступность(0); 
		Форма.кнОчиститьВидПлатежа.Доступность(0); 
		Форма.кнОчиститьВидАккредитива.Доступность(0); 
		Форма.кнОчиститьВидДокумента.Доступность(0); 
	Иначе
		Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;
	Форма.Уполномоченный.Доступность(Акцепт);
	Форма.УсловиеОплаты.Доступность(Акцепт);
	ПриВыбореВидАккредитива();
	Форма.Кн_Справка.Видимость(?(ТипЗначения(Норм_Компонента) = 0, 0, 1));
	НачальнаяДатаДокумента = ДатаДок;
	
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Документ." + Вид(),ТаблицаПечФорм);
	
КонецПроцедуры //ПриОткрытии
//_____________________________________________________________________________
Процедура ПриЗаписи() //предопределенная
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	ИначеЕсли (ДатаДок >= '15.02.2000') и (Прав(Строка(НомерДок),3) = "000") Тогда
		Если Вопрос("Номер аккредитива не может оканчиваться на ""000"".
					|Вернуться к редактированию документа?", "Да+Нет") = "Да" Тогда
	    	Активизировать("НомерДок");
			СтатусВозврата(0);
			Возврат;
	    КонецЕсли;
	КонецЕсли;

	Если Константа.ВсеДокументыВЖурналОпераций = Да Тогда
	    СуществуетОперация(1);
		Операция.СуммаОперации = Сумма;
		Операция.Содержание = "Заявление на аккредитив";
	Иначе
	    СуществуетОперация(0);
	КонецЕсли;
КонецПроцедуры //ПриЗаписи

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//*****************************************************************************
Новый = 0;
                                                     
ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");
	
// добавим информацию о встроенной форме
ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Печать аккредитива";
ТаблицаПечФорм.Кнопка       = "Печать";

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Ввести на основании"); 
СписокДействий.ДобавитьЗначение("Перейти  в журнал");