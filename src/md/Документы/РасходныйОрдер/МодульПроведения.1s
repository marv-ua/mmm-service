//******************************************************************************
Процедура ОбработкаПроведения()

	Если ФормироватьПроводки = 0 Тогда
		ТекстСообщения = "Документ проведен.";
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		глПриПроведении(Контекст);
		Возврат;

	ИначеЕсли КоррСчет.Выбран() = 0 Тогда
		ТекстСообщения = "Не указан корреспондирующий счет";
		глНеПроводить(Контекст, ТекстСообщения);
		Возврат;
	КонецЕсли; 
	
	ПереченьСтатейЗатрат = глПолучитьПереченьСтатейЗатрат(ДатаДок);

	Если Валютный = 2 Тогда
		СчетКассы = СчетПоКоду("50.11");

		Курс = Валюта.Курс.Получить(ДатаДок);
		Кратность = Валюта.Кратность.Получить(ДатаДок);
		Кратность = ?(Кратность=0,1,Кратность);

		глТаблицаСчетов.УдалитьСтроки();

		глТаблицаСчетов.НоваяСтрока();
		глТаблицаСчетов.Счет = СчетКассы;
		глТаблицаСчетов.Субконто1 = ДвижениеДенежныхСредств;
		глТаблицаСчетов.Субконто2 = "";
		глТаблицаСчетов.Субконто3 = "";
		глТаблицаСчетов.Валюта = Валюта;

		Если КоррСчет.Валютный = 1 Тогда
			глТаблицаСчетов.НоваяСтрока();
			глТаблицаСчетов.Счет = КоррСчет;
			глТаблицаСчетов.Субконто1 = Субконто1;
			глТаблицаСчетов.Субконто2 = Субконто2;
			глТаблицаСчетов.Субконто3 = Субконто3;
			глТаблицаСчетов.Валюта = Валюта;
		КонецЕсли;

		глПереоценкаСчетов(Контекст, глТаблицаСчетов);

	Иначе
		СчетКассы = СчетПоКоду("50.1");
	КонецЕсли;

	Если Сумма > 0 Тогда
		
		БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИт.Рассчитать(,ТекущийДокумент(), "60.1,60.6,76.5,76.6,УЕ.60");
		Конт = глВзятьКонтекст(Контекст);
		ВалютаДоговора = ""; СуммаОплатыВал = 0; СуммаОплатыРуб = 0; СуммоваяРазница = 0; ВыбКурс = КурсОплаты;
		глУчестьСуммовыеРазницы(Конт, Субконто2, ДокументПоставки, БухИт, КоррСчет, 2, Сумма, ВыбКурс, ВалютаДоговора, СуммаОплатыВал, СуммаОплатыРуб, СуммоваяРазница, ПереченьСтатейЗатрат);
		
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "БК";
		Операция.СодержаниеПроводки = "Расход: "+Приложение;
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = КоррСчет;
		Операция.Дебет.Субконто(1,Субконто1);
		Операция.Дебет.Субконто(2,Субконто2);
		Операция.Дебет.Субконто(3,Субконто3);
		Операция.Кредит.Счет = СчетКассы;
		Операция.Кредит.Субконто(1, ДвижениеДенежныхСредств);

		Если Валютный = 2 Тогда
			Операция.Валюта = Валюта;
			Операция.ВалСумма = Сумма;
			СуммаРасхода = Окр(Сумма*Курс/Кратность,2,1);
			
		ИначеЕсли (ПустоеЗначение(ВалютаДоговора) = 0) и (КоррСчет.Валютный = 1) Тогда
			Операция.Валюта = ВалютаДоговора;
			
			Если Сумма = СуммаОплатыРуб Тогда
				Операция.ВалСумма = СуммаОплатыВал;
				СуммаРасхода = СуммаОплатыРуб;
				
			Иначе
				Операция.ВалСумма = ?(ВыбКурс = 0, 0, Сумма / ВыбКурс);
				СуммаРасхода = Сумма;
			КонецЕсли;
			
		Иначе
			СуммаРасхода = Сумма;
		КонецЕсли;
		Операция.Сумма = СуммаРасхода;
		Операция.СуммаОперации = Операция.Сумма;

		Если КоррСчет.Количественный = 1 Тогда
		    Операция.Количество = Количество;
		КонецЕсли;
		
		// Отражение операций в налоговом учете.
		ОбъектыАналитикиНУ = глСчетИАналитикаРасходовНУ(Контекст, КоррСчет, Субконто1, Субконто2, Субконто3, ПереченьСтатейЗатрат, СчетКассы);
		
		// Если опредлен налоговый счет, то необходимо
		// сформировать проводку для целей налогового учета.
		СчетНУ = ОбъектыАналитикиНУ.Получить("Счет");
		Если ПустоеЗначение(СчетНУ) = 0 Тогда
			СуммаРасходов = Операция.Сумма;
			
		    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		    Операция.НомерЖурнала = "НУ";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетНУ;
			Для СчетчикЦикла = 1 По СчетНУ.КоличествоСубконто() Цикл
				ИдентификаторСубконто = СчетНУ.ВидСубконто(СчетчикЦикла,,).Идентификатор();
				Операция.Дебет.Субконто(СчетчикЦикла, ОбъектыАналитикиНУ.Получить(ИдентификаторСубконто));
			КонецЦикла;
			Операция.СодержаниеПроводки = "Расход: "+Приложение;
			Операция.Сумма = СуммаРасходов;
			Если СчетНУ.Количественный = 1 Тогда
			    Операция.Количество = Количество;
			КонецЕсли;
		КонецЕсли;  
		
		//********************************************************************
		//Проводки по расчетам в у.е. 	
		Если ПустоеЗначение(ДокументПоставки) = 0 Тогда			
			Если ДатаДок >= '01.01.2007' Тогда
				Если Найти("60.6,76.6",КоррСчет.Код) > 0 Тогда 
				
					СчУЕ_60 = СчетПоКоду("УЕ.60");
					//Расчет курсовых разниц 	            
					глТаблицаСчетов.УдалитьСтроки();
					глТаблицаСчетов.НоваяСтрока();
					глТаблицаСчетов.Счет = КоррСчет;
					глТаблицаСчетов.Субконто1 = Субконто1;
					глТаблицаСчетов.Субконто2 = Субконто2;
					глТаблицаСчетов.Субконто3 = Субконто3;
					глТаблицаСчетов.Валюта = Субконто2.ВалютаДоговора;
					глПереоценкаСчетов(Контекст, глТаблицаСчетов);
					
				КонецЕсли;                                        				
			КонецЕсли;
		ИначеЕсли Найти("60.6,76.6",КоррСчет.Код) > 0 Тогда
			ТекстСообщения = "=> Не указан документ поставки. Суммовые разницы по расчетам в условных единицах с контрагентом: " + Субконто1 + " по договору: " + Субконто2 + " не рассчитаны."; 	
			глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), -1); 
		КонецЕсли; 
		//********************************************************************  
		
		//********************************************************************
		//Проводки по расчетам в валюте 	
		Если ДатаДок >= '01.01.2008' Тогда	
			Если Валютный = 2 Тогда
				Если Найти("60.22,62.22",КоррСчет.Код) > 0 Тогда 
					СчВАЛ_60 = СчетПоКоду("ВАЛ.60");
					СчВАЛ_62 = СчетПоКоду("ВАЛ.62");

					глТаблицаСчетов.УдалитьСтроки(); 
					
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ВЛ";
					Операция.СодержаниеПроводки = "Аванс в валюте";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Валюта = Валюта;
					
					//Расчет курсовых разниц 	            
					глТаблицаСчетов.НоваяСтрока();
					глТаблицаСчетов.Субконто1 = Субконто1;
					глТаблицаСчетов.Субконто2 = Субконто2;
					глТаблицаСчетов.Субконто3 = Субконто3;
					глТаблицаСчетов.Валюта = Субконто2.ВалютаДоговора;
					Если КоррСчет.Код = "60.22" Тогда
						глТаблицаСчетов.Счет = СчВАЛ_60;
						Операция.Дебет.Счет = СчВАЛ_60;
					Иначе                               						
						глТаблицаСчетов.Счет = СчВАЛ_62;
						Операция.Дебет.Счет = СчВАЛ_62;
					КонецЕсли;
										
					Операция.Дебет.Субконто(1, Субконто1);
					Операция.Дебет.Субконто(2, Субконто2);
					Операция.Дебет.Субконто(3, Субконто3);
					
					СуммаОперацииВал = Сумма;
					СуммаОперацииРуб = Окр(Сумма*Курс/Кратность,2,1);
					
					
					Операция.Сумма = СуммаОперацииРуб;
					Операция.ВалСумма = СуммаОперацииВал;
					
					Если глТаблицаСчетов.количествоСтрок() <> 0 Тогда
						глПереоценкаСчетов(Контекст, глТаблицаСчетов, 1);
						глТаблицаСчетов.УдалитьСтроки();
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		//********************************************************************
		
	КонецЕсли;
	

	Если (ПустоеЗначение(Операция.Содержание) = 1) и (Операция.СуммаОперации = 0) Тогда
		Операция.Содержание = "Выдача из кассы: "+Основание;
		Если Валютный = 2 Тогда
			Курс = Валюта.Курс.Получить(ДатаДок);
			Кратность = Валюта.Кратность.Получить(ДатаДок);
			Кратность = ?(Кратность=0,1,Кратность);
			СуммаРКО = Окр(Сумма*Курс/Кратность,2,1);
		Иначе
			Валюта = "";
			СуммаРКО = Сумма;
		КонецЕсли;
		Операция.СуммаОперации = СуммаРКО;
	КонецЕсли;
	
	Операция.Записать();

	ТекстСообщения = "Учтена выдача денежных средств из кассы.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
КонецПроцедуры
//_____________________________________________________________________________ 