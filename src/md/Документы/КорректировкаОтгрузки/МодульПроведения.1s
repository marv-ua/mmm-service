//*****************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()
	
	Если ВидРеализации = 1 Тогда
	    СчетВыручки = СчетПоКоду("90.1.1");
		СчетНП = СчетПоКоду("90.6");
	Иначе
		СчетВыручки = СчетПоКоду("91.1");
		СчетНП = СчетПоКоду("91.2");
	КонецЕсли;
	
	НомЖур = "ТВ";
	Если ДокументОтгрузки.Выбран() = 1 Тогда
		Если ДокументОтгрузки.Вид() = "ОтпускМатериаловНаСторону" Тогда
			НомЖур = "МТ";
		ИначеЕсли ДокументОтгрузки.Вид() = "ПередачаОС" Тогда
			НомЖур = "ОС";
		ИначеЕсли ДокументОтгрузки.Вид() = "ПередачаНМА" Тогда
			НомЖур = "НА";
		КонецЕсли;
	КонецЕсли;
	
	ЦеныВДоговоре = 1; //в рублях
	Если Договор.Выбран() = 1 Тогда
	    Если ПустоеЗначение(Договор.ВалютаДоговора) = 0 Тогда
			ЦеныВДоговоре = 2; //в валюте
		КонецЕсли;
		Валюта = Договор.ВалютаДоговора;
		ОплатаДоговора = Договор.ОплатаДоговора; //1 - врублях, 2 - в валюте
	КонецЕсли;
	
	Если ЦеныВДоговоре = 2 Тогда
		Кратность = Валюта.Кратность.Получить(ДатаДок);
		Кратность = ?(Кратность=0, 1, Кратность);
	КонецЕсли;
	
	Если ОплатаДоговора = 2 Тогда
		ТекстСообщения = "Для оплаты договора с покупателем указана иностранная валюта.";
		глНеПроводить(Контекст, ТекстСообщения);
		Возврат;
	Иначе
		СчетРасчетовСПокупателем = СчетПоКоду("62.1");
		СчетАвансов = СчетПоКоду("62.2");
	КонецЕсли;
	
	СчетРасчетовСКомитентом = СчетПоКоду("76.5");
	СчетРасчетовЗаКомиссионныеТовары = СчетПоКоду("62.4");
	СчетРасчетовПоНП = СчетПоКоду("76.Н.4");
	
	ТаблицаДокумента = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(ТаблицаДокумента);
	ТаблицаДокумента.Свернуть("Субконто1,СтавкаНДС,СтавкаНП,Комитент,ДоговорКомиссии", "СуммаОтгрузки,НП,Всего");
	ТаблицаДокумента.ВыбратьСтроки();
	Пока ТаблицаДокумента.ПолучитьСтроку() = 1 Цикл
	    Если ТаблицаДокумента.НП = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Скорректируем задолженность покупателя за собственные товары.
		Если (ПустоеЗначение(ТаблицаДокумента.Комитент) = 1) и (ВидРеализации = 1) Тогда
			
			// Сторнируем дебиторскую задолженность, начисленную при отгрузке.
			Операция.НоваяПроводка();
			Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = НомЖур;
			Операция.СодержаниеПроводки = "Сторн. выручка без НП";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетРасчетовСПокупателем;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;
			Операция.Кредит.Счет = СчетВыручки;
			Операция.Кредит.Субконто(1, ТаблицаДокумента.Субконто1);
		    Операция.Кредит.СтавкиНДС = ТаблицаДокумента.СтавкаНДС;
			Операция.Кредит.СтавкиНП = глБезНалога("НП");
			Если ЦеныВДоговоре = 2 Тогда
				Операция.Сумма =  - ТаблицаДокумента.СуммаОтгрузки*Курс/Кратность;
			Иначе
				Операция.Сумма = - ТаблицаДокумента.СуммаОтгрузки;
			КонецЕсли;
			
			// Начислим дебиторскую задолженность по новому варианту реализации.
	    	Операция.НоваяПроводка();
			Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = НомЖур;
			Операция.СодержаниеПроводки = "Учтена выручка с НП";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетРасчетовСПокупателем;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;
			Операция.Кредит.Счет = СчетВыручки;
			Операция.Кредит.Субконто(1, ТаблицаДокумента.Субконто1);
		    Операция.Кредит.СтавкиНДС = ТаблицаДокумента.СтавкаНДС;
			Операция.Кредит.СтавкиНП = ТаблицаДокумента.СтавкаНП;
			Если ЦеныВДоговоре = 2 Тогда
				Операция.Сумма = (ТаблицаДокумента.НП + ТаблицаДокумента.СуммаОтгрузки)*Курс/Кратность;
			Иначе
				Операция.Сумма = ТаблицаДокумента.НП + ТаблицаДокумента.СуммаОтгрузки;
			КонецЕсли;
			
		// Скорректируем задолженность покупателя за реализацию прочих активов.
		ИначеЕсли ВидРеализации = 2 Тогда
			Операция.НоваяПроводка();
			Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = НомЖур;
			Операция.СодержаниеПроводки = "Доначислена выручка";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетРасчетовСПокупателем;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;
			Операция.Кредит.Счет = СчетВыручки;
			Операция.Кредит.Субконто(1, ТаблицаДокумента.Субконто1);
			
			Если ЦеныВДоговоре = 2 Тогда
				Операция.Сумма = (ТаблицаДокумента.НП)*Курс/Кратность;
			Иначе
				Операция.Сумма = ТаблицаДокумента.НП;
			КонецЕсли;

		// Скорректируем задолженность покупателя за комиссионные товары.
		ИначеЕсли (ПустоеЗначение(ТаблицаДокумента.Комитент) = 0) и (ВидРеализации = 1) Тогда
			Операция.НоваяПроводка();
			Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = НомЖур;
			Операция.СодержаниеПроводки = "Доначислена выручка";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетРасчетовЗаКомиссионныеТовары;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;
			Операция.Кредит.Счет = СчетРасчетовСКомитентом;
			Операция.Кредит.Контрагенты = ТаблицаДокумента.Комитент;
			Операция.Кредит.Договоры = ТаблицаДокумента.ДоговорКомиссии;
			Если ЦеныВДоговоре = 2 Тогда
			    Операция.Сумма = ТаблицаДокумента.НП*Курс/Кратность;
			Иначе
				Операция.Сумма = ТаблицаДокумента.НП;
			КонецЕсли;
		КонецЕсли;
		
		// Начисление НП.
		Операция.НоваяПроводка();
		Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = НомЖур;
		Операция.СодержаниеПроводки = "Начислен налог с продаж";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = СчетНП;
		Операция.Дебет.Субконто(1, ТаблицаДокумента.Субконто1);
		Если ВидРеализации = 1 Тогда
		    Операция.Дебет.Субконто(2, ТаблицаДокумента.СтавкаНП);
		КонецЕсли;
		Операция.Кредит.Счет = СчетРасчетовПоНП;
		Операция.Кредит.Контрагенты = Контрагент;
		Операция.Кредит.Договоры = Договор;
		Если ЦеныВДоговоре = 2 Тогда
		    Операция.Сумма = ТаблицаДокумента.НП*Курс/Кратность;
		Иначе
			Операция.Сумма = ТаблицаДокумента.НП;
		КонецЕсли;
		
		// При продаже комиссионного товара налог с продаж будет удержан у комитента.
		Если ТаблицаДокумента.Комитент.Выбран() = 1 Тогда
			Операция.НоваяПроводка();
			Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = НомЖур;
			Операция.СодержаниеПроводки = "Удержан налог с продаж";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетРасчетовСКомитентом;
			Операция.Дебет.Контрагенты = ТаблицаДокумента.Комитент;
			Операция.Дебет.Договоры = ТаблицаДокумента.ДоговорКомиссии;
			Операция.Кредит.Счет = СчетВыручки;
			Операция.Кредит.ВидыНоменклатуры = ТаблицаДокумента.Субконто1;
			Операция.Кредит.СтавкиНДС = ТаблицаДокумента.СтавкаНДС;
			Операция.Кредит.СтавкиНП = ТаблицаДокумента.СтавкаНП;
            Если ЦеныВДоговоре = 2 Тогда
                Операция.Сумма = ТаблицаДокумента.НП*Курс/Кратность;
			Иначе
				Операция.Сумма = ТаблицаДокумента.НП;
            КонецЕсли;
		КонецЕсли;
	КонецЦикла;
    
	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения()