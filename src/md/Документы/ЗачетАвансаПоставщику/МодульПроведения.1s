////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем Таб;
Перем АвансБезДоговора, АвансБезДоговораРуб, АвансПоДоговору, АвансПоДоговоруРуб;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//*****************************************************************************
// ЗачестьАванс(ЗачетАванса, ДоговорАванса)
//
Процедура ЗачестьАванс(ЗачетАванса, ДоговорАванса)
	
	Если ЗачетАванса > 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "ТВ";
		Операция.СодержаниеПроводки = "Зачтена предоплата";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = Таб.СчетРасчетовСПоставщиком;
		Операция.Дебет.Контрагенты = Контрагент;
		Операция.Дебет.Договоры = Таб.Договор;
		Операция.Кредит.Счет = Таб.СчетАвансовВыданных;
		Операция.Кредит.Контрагенты = Контрагент;
		Операция.Кредит.Договоры = ДоговорАванса;
		Если Таб.ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Если (АвансПоДоговору + АвансБезДоговора) = ЗачетАванса Тогда
					ЗачетАвансаРуб = АвансПоДоговоруРуб + АвансБезДоговораРуб;
				Иначе
					КурсЗачетаАванса = Окр((АвансПоДоговоруРуб + АвансБезДоговораРуб)/(АвансПоДоговору + АвансБезДоговора), 4, 1);
					ЗачетАвансаРуб = ЗачетАванса*КурсЗачетаАванса;
				КонецЕсли;
			Иначе
				ЗачетАвансаРуб = ЗачетАванса*Таб.Курс/Таб.Кратность;
			КонецЕсли; 
			Операция.Сумма = ЗачетАвансаРуб;
		    Операция.Валюта = Таб.Валюта;
			Операция.ВалСумма = ЗачетАванса;
		Иначе
			Операция.Сумма = ЗачетАванса;
		КонецЕсли; 
		
		Если Таб.ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ВЛ";
				Операция.СодержаниеПроводки = "Зачтена предоплата";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Кредит.Счет = СчетПоКоду("ВАЛ.60");
				Операция.Кредит.Контрагенты = Контрагент;
				Операция.Кредит.Договоры = ДоговорАванса;
				Операция.Сумма = ЗачетАванса*Таб.Курс/Таб.Кратность;
				Операция.Валюта = Таб.Валюта;
				Операция.ВалСумма = ЗачетАванса;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ЗачетАванса()


////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()

	Сч60_1 = СчетПоКоду("60.1");
	Сч60_2 = СчетПоКоду("60.2");
	Сч60_11 = СчетПоКоду("60.11");
	Сч60_22 = СчетПоКоду("60.22");
	
	СчВАЛ_60 = СчетПоКоду("ВАЛ.60");

	Таб = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(Таб);
	Таб.Свернуть("Договор", "Сумма"); 
	Таб.НоваяКолонка("СчетРасчетовСПоставщиком");
	Таб.НоваяКолонка("СчетАвансовВыданных");
    Таб.НоваяКолонка("ОплатаДоговора");
	Таб.НоваяКолонка("Валюта");
	Таб.НоваяКолонка("Курс");
	Таб.НоваяКолонка("Кратность");
	Таб.НоваяКолонка("СуммаРуб");
	
	глТаблицаСчетов.УдалитьСтроки();
	
	Таб.ВыбратьСтроки();
	Пока Таб.ПолучитьСтроку() = 1 Цикл
		Таб.СчетРасчетовСПоставщиком = Сч60_1;
		Таб.СчетАвансовВыданных = Сч60_2;
			
		Таб.ОплатаДоговора = Таб.Договор.ОплатаДоговора; // 1 - врублях, 2 - в валюте
		Если Таб.ОплатаДоговора = 2 Тогда
			Таб.Валюта = Таб.Договор.ВалютаДоговора;
			Таб.Курс = Таб.Валюта.Курс.Получить(ДатаДок);
			Таб.Кратность = Таб.Валюта.Кратность.Получить(ДатаДок);
			Таб.Кратность = ?(Таб.Кратность=0, 1, Таб.Кратность);
		КонецЕсли;
		
		Если Таб.ОплатаДоговора = 2 Тогда
			Таб.СчетРасчетовСПоставщиком = Сч60_11;
			Таб.СчетАвансовВыданных = Сч60_22;
			Если ДатаДок >= '01.01.2008' Тогда
				СчетАвансовВыданныхПереоценка = СчВАЛ_60;
			Иначе
				СчетАвансовВыданныхПереоценка = Сч60_22;
			КонецЕсли;
	
			глТаблицаСчетов.НоваяСтрока();
			глТаблицаСчетов.Счет = СчетАвансовВыданныхПереоценка;
			глТаблицаСчетов.Субконто1 = Контрагент;
			глТаблицаСчетов.Субконто2 = Таб.Договор;
			глТаблицаСчетов.Субконто3 = "";
			глТаблицаСчетов.Валюта = Таб.Валюта;
			глТаблицаСчетов.Курс = Таб.Курс;
			Если ЗачитыватьАванс = 0 Тогда
				БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
				Если ПустоеЗначение(БезДоговора) = 0 Тогда
					глТаблицаСчетов.НоваяСтрока();
					глТаблицаСчетов.Счет = СчетАвансовВыданныхПереоценка;
					глТаблицаСчетов.Субконто1 = Контрагент;
					глТаблицаСчетов.Субконто2 = БезДоговора;
					глТаблицаСчетов.Субконто3 = "";
					глТаблицаСчетов.Валюта = Таб.Валюта;
					глТаблицаСчетов.Курс = Таб.Курс;
				КонецЕсли;
			КонецЕсли;
			
			глТаблицаСчетов.НоваяСтрока();
			глТаблицаСчетов.Счет = Сч60_11;
			глТаблицаСчетов.Субконто1 = Контрагент;
			глТаблицаСчетов.Субконто2 = Таб.Договор;
			глТаблицаСчетов.Субконто3 = "";
			глТаблицаСчетов.Валюта = Таб.Валюта;
			глТаблицаСчетов.Курс = Таб.Курс;
		КонецЕсли;
	КонецЦикла;
	
	глПереоценкаСчетов(Контекст, глТаблицаСчетов,, 0);

	СписокДоговоров = СоздатьОбъект("СписокЗначений");
	Таб.Выгрузить(СписокДоговоров,,, "Договор");
	БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
	Если (ЗачитыватьАванс = 0) и (ПустоеЗначение(БезДоговора) = 0) Тогда
		СписокДоговоров.ДобавитьЗначение(БезДоговора);
	КонецЕсли;

	БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
	БухИт.ИспользоватьСубконто(ВидыСубконто.Контрагенты, Контрагент, 2);
	БухИт.ИспользоватьСубконто(ВидыСубконто.Договоры, СписокДоговоров, 2);
	БухИт.ВыполнитьЗапрос(,ТекущийДокумент(), "60.2,60.22");

	ТабБезДоговора = СоздатьОбъект("ТаблицаЗначений");
	Таб.Выгрузить(ТабБезДоговора,,, "Валюта");
	ТабБезДоговора.НоваяКолонка("Сумма", "Число");
	ТабБезДоговора.НоваяКолонка("СуммаРуб", "Число");
	ТабБезДоговора.Свернуть("Валюта", "Сумма, СуммаРуб");
	
	Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, БезДоговора) = 1 Тогда
		Если БухИт.ПолучитьСчет(, "60.22") = 1 Тогда
			ТабБезДоговора.ВыбратьСтроки();
			Пока ТабБезДоговора.ПолучитьСтроку() = 1 Цикл
				Если БухИт.ПолучитьВалюту(, ТабБезДоговора.Валюта) = 1 Тогда
					ТабБезДоговора.Сумма = БухИт.СКД("В");
					Если ДатаДок >= '01.01.2008' Тогда	
						ТабБезДоговора.СуммаРуб = БухИт.СКД("С");
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		НомСтроки = 0;
		Если ТабБезДоговора.НайтиЗначение(ПолучитьПустоеЗначение(), НомСтроки, "Валюта") = 1 Тогда
			Если БухИт.ПолучитьСчет(, "60.2") = 1 Тогда
			    ТабБезДоговора.УстановитьЗначение(НомСтроки, "Сумма", БухИт.СКД("С"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Таб.ВыбратьСтроки();
	Пока Таб.ПолучитьСтроку() = 1 Цикл
		АвансПоДоговору = 0; 
		АвансПоДоговоруРуб = 0;
		Если Таб.Сумма = 0 Тогда
		    Продолжить;
			
		ИначеЕсли БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, Таб.Договор) = 1 Тогда
			Если Таб.ОплатаДоговора = 2 Тогда
				Если БухИт.ПолучитьСчет(, "60.22") = 1 Тогда
					Если БухИт.ПолучитьВалюту(, Таб.Валюта) = 1 Тогда
						АвансПоДоговору = БухИт.СКД("В"); 
						Если ДатаДок >= '01.01.2008' Тогда	
							АвансПоДоговоруРуб = БухИт.СКД("С");
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				Если БухИт.ПолучитьСчет(, "60.2") = 1 Тогда
				    АвансПоДоговору = БухИт.СКД("С");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		НомСтроки = 0;
		Если ТабБезДоговора.НайтиЗначение(Таб.Валюта, НомСтроки, "Валюта") = 1 Тогда
			АвансБезДоговора = ТабБезДоговора.ПолучитьЗначение(НомСтроки, "Сумма");
			АвансБезДоговораРуб = ТабБезДоговора.ПолучитьЗначение(НомСтроки, "СуммаРуб");
		Иначе
			АвансБезДоговора = 0;
		КонецЕсли;
		
		Если (АвансПоДоговору + АвансБезДоговора) < Таб.Сумма Тогда
			СообщениеДоговораАванса = ?(ЗачитыватьАванс=0,"договорам ""Без договора (Служебный..."" и """+Таб.Договор+""".","данному договору.");
			ТекстСообщения = "Сумма зачета аванса по договору """+Таб.Договор+""" превышает аванс, выплаченный по "+СообщениеДоговораАванса;
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, Таб.Сумма);
		ЗачетАвансаБезДоговора = Таб.Сумма - ЗачетАвансаПоДоговору;
		ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
		Если ТабБезДоговора.НайтиЗначение(Таб.Валюта, НомСтроки, "Валюта") = 1 Тогда
			ТабБезДоговора.УстановитьЗначение(НомСтроки, "Сумма", АвансБезДоговора - ЗачетАвансаБезДоговора);
		КонецЕсли;
		
		ЗачестьАванс(ЗачетАвансаПоДоговору, Таб.Договор);
		ЗачестьАванс(ЗачетАвансаБезДоговора, БезДоговора);
		
		ТекстСообщения = "=> " + ""+Таб.НомерСтроки+". Зачтен аванс в размере "+ЗачетАванса+", договор реализации "+Таб.Договор +":" + РазделительСтрок
					   + "   - определен аванс по договору "+Договор+" в сумме: "+АвансПоДоговору+", зачтен аванс в размере: "+ЗачетАвансаПоДоговору + РазделительСтрок;
		
		Если ЗачитыватьАванс = 0 Тогда
			ТекстСообщения = ТекстСообщения
						   + "   - определен аванс без указания договора в сумме: "+АвансБезДоговора+", зачтен аванс в размере: "+ЗачетАвансаБезДоговора;
		Иначе
			ТекстСообщения = ТекстСообщения
			               + "   - аванс без указания договора не учтен";
		КонецЕсли;
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		
	КонецЦикла;

	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения()
