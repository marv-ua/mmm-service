////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;
Перем НачальнаяДатаДокумента;
Перем Новый;
Перем СписокДействий;

//******************************************************************************
Процедура Подбор()
	
	ОткрытьПодбор("Справочник.ОсновныеСредства","ДляВыбора");
	
КонецПроцедуры  

//******************************************************************************
Процедура ОбработкаПодбора(ВыбрОС)
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
	    Если ОсновноеСредство.Код = ВыбрОС.Код Тогда
	        Предупреждение("Основное средство уже выбрано", 10);
			Возврат;
	    КонецЕсли;
	КонецЦикла;
	НоваяСтрока();
	ОсновноеСредство = ВыбрОС;
	АктивизироватьСтроку();
КонецПроцедуры 

//******************************************************************************
Процедура Заполнить()  
	НачОС = "";                            
	СписокКодов = СоздатьОбъект("СписокЗначений");
    Если КоличествоСтрок() = 0 Тогда
        ВвестиЗначение(НачОС,"Выберите элемент, входящий в группу однотипных основных средств","Справочник.ОсновныеСредства");
		Если ПустоеЗначение(НачОС) = 1 Тогда
		    Возврат;
		Иначе
			НоваяСтрока();
			ОсновноеСредство = НачОС;
		КонецЕсли;
	КонецЕсли;
	ВыбратьСтроки();
	ПолучитьСтроку();
	НаимОС = ОсновноеСредство.Наименование;       
	СписокКодов.ДобавитьЗначение(ОсновноеСредство.Код);
	ОСРодитель = ОсновноеСредство.Родитель;
	КС = КоличествоСтрок();
	Пока ПолучитьСтроку() = 1 Цикл
	    Если ОсновноеСредство.Наименование <> НаимОС Тогда
	        Предупреждение("В табличной части указаны объекты с разными наименованиями."+
			РазделительСтрок+"                Заполнение отменено",60);
			Возврат;
		Иначе
			СписокКодов.ДобавитьЗначение(ОсновноеСредство.Код);
	    КонецЕсли;
	КонецЦикла;
	Спр = СоздатьОбъект("Справочник.ОсновныеСредства");
	Спр.ИспользоватьРодителя(ОСРодитель);
	Спр.ВыбратьЭлементы(1);
	Пока Спр.ПолучитьЭлемент() = 1 Цикл
		Если Спр.ЭтоГруппа() = 1 Тогда
		    Продолжить;
		КонецЕсли;
		Если Спр.Наименование <> НаимОС Тогда
		    Продолжить;
		КонецЕсли;
	    Если СписокКодов.НайтиЗначение(Спр.Код) = 0 Тогда
	        НоваяСтрока();
			ОсновноеСредство = Спр.ТекущийЭлемент();
	    КонецЕсли;
	КонецЦикла;
	Если КС = КоличествоСтрок() Тогда
	    Предупреждение("Других объектов с указанным наименованием не найдено",60);
	КонецЕсли;
КонецПроцедуры

//_____________________________________________________________________________
Процедура ПриВыбореТипаСписания()
	Если ТипСписания = 1 Тогда
		Форма.СтатьяПрочихДоходовИРасходов.Видимость(0);
		Форма.ПодпСтатьи.Видимость(0);
	Иначе
		Форма.СтатьяПрочихДоходовИРасходов.Видимость(1);
		Форма.ПодпСтатьи.Видимость(1);
	КонецЕсли;
КонецПроцедуры //ПриВыбореТипаСписания
//_____________________________________________________________________________
Процедура РассчитатьБалансовуюСтоимость()
	
	Если ДатаДок > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("На " + ДатаДок + " бухгалтерские итоги не рассчитаны!
		    |Расчет итогов выполняется в режиме
		    |""Операции - Управление бухгалтерскими итогами"".");
		
	ИначеЕсли (Выбран() = 0) или ((Выбран() = 1) и (ДатаДок <> ТекущийДокумент().ДатаДок)) Тогда
		Предупреждение("Для получения суммы первоначальной стоимости по данным
					   |бухгалтерского учета документ необходимо записать."); 
			
	ИначеЕсли ОсновноеСредство.Выбран() = 0 Тогда
		Предупреждение("Не выбрано основное средство.");
		
	ИначеЕсли Проведен() = 1 Тогда
		Предупреждение("Для получения балансовой стоимости и начисленной 
						|амортизации сделайте документ непроведенным.");
		
	Иначе
		СведенияОбОС = глРасчетАмортизацииОС(ОсновноеСредство, ДатаДок);
		БалансоваяСтоимость        = СведенияОбОС.Получить("БалансоваяСтоимостьКон");
		РассчитаннаяАмортизация    = СведенияОбОС.Получить("РассчитаннаяАмортизацияБух");
		НачисленнаяАмортизацияКон  = СведенияОбОС.Получить("НачисленнаяАмортизацияКон");
		НачисленнаяАмортизация     = РассчитаннаяАмортизация + НачисленнаяАмортизацияКон;
	КонецЕсли;
	
КонецПроцедуры //РассчитатьБалансовуюСтоимость
//_____________________________________________________________________________
Процедура Печать()
	Если ДатаДок<Дата("23.03.2003") Тогда
		Таб = СоздатьОбъект("Таблица");
		Таб.ИсходнаяТаблица("ОС-4_97");
		Руководитель = Константа.Руководитель.Получить(ДатаДок);
		ПредставлениеГода = Лев(ДатаГод(ДатаДок),2);
		Таб.Вывести();
		Таб.ТолькоПросмотр(1);
		Таб.Опции(0,0,0,0,"ОпцииПечатиОС4","ОкноОС4");
		Таб.Показать();
	Иначе
			
		Организация = СокрЛП(Константа.НазваниеОрганизации);
		Подразделение = ОсновноеСредство.Подразделение.Получить(ДатаДок-1);
		МОЛ = ОсновноеСредство.МОЛ.Получить(ДатаДок-1); 
		Руководитель = "("+ФИО(Константа.Руководитель.Получить(ДатаДок))+")";
		ГлавБух = "("+ФИО(Константа.ГлБухгалтер.Получить(ДатаДок))+")";
		
		Если (ОсновноеСредство.Группа = Перечисление.ГруппыОС.ТранспортныеСредства) Тогда
			ТранспортныеСредства = 1;
		Иначе
			ТранспортныеСредства = 0;
		КонецЕсли;
				
		Если (КоличествоСтрок() > 1) и (ТранспортныеСредства = 0) Тогда
			Таб = СоздатьОбъект("Таблица");
			Таб.ИсходнаяТаблица("ОС-4б");
		КонецЕсли;
		
		КоличествоОСВСоставеМПЗ = 0;

		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл  
			Если ПустоеЗначение(ОсновноеСредство.МатериалДляОтраженияВСоставеМПЗ)=0 Тогда
				Сообщить("Основное средство инв. №"+ОсновноеСредство.Код+" "+ОсновноеСредство.Наименование+" отражено в составе МПЗ!");
				КоличествоОСВСоставеМПЗ = КоличествоОСВСоставеМПЗ + 1;
				Продолжить;
			КонецЕсли;

			Если (КоличествоСтрок() = 1) или (ТранспортныеСредства = 1) Тогда
				Таб = СоздатьОбъект("Таблица");
				Если (ТранспортныеСредства = 1) Тогда
					Таб.ИсходнаяТаблица("ОС-4а");
				Иначе
					Таб.ИсходнаяТаблица("ОС-4");
				КонецЕсли;
			КонецЕсли;
	
			НаимОС = ОсновноеСредство.Наименование;
			ДатаВвода = ОсновноеСредство.ДатаВводаВЭксплуатацию;
			ИнвНомер = ОсновноеСредство.Код; 
			
			Экспл = СоздатьОбъект("Периодический");
			Экспл.ИспользоватьОбъект("Состояние", ОсновноеСредство);
			СрокЭкспл=0;
			НачалоЭкспл =0;
			Экспл.ВыбратьЗначения(ДатаВвода, ДатаДок-1);
			Пока Экспл.ПолучитьЗначение() = 1 Цикл
				Если Экспл.Значение = Перечисление.СостоянияОС.В_Эксплуатации Тогда
					СрокЭкспл= СрокЭкспл + ?(НачалоЭкспл=0,0,Экспл.ДатаЗнач - НачалоЭкспл);
					НачалоЭкспл = Экспл.ДатаЗнач;
				Иначе
					СрокЭкспл= СрокЭкспл + ?(НачалоЭкспл=0,0,Экспл.ДатаЗнач - НачалоЭкспл);
					НачалоЭкспл = 0;
				КонецЕсли;
			КонецЦикла;
			СрокЭкспл = СрокЭкспл + ?(НачалоЭкспл=0,0,ДатаДок - НачалоЭкспл);
			СрокЭкспл = ?(Окр(СрокЭкспл/30.5,0,1)>0,Строка(Окр(СрокЭкспл/30.5,0,1))+" мес.","");
			НачСтоимость = Формат(БалансоваяСтоимость,"Ч015.2");
			НачАмортизация = Формат(НачисленнаяАмортизация,"Ч015.2");
			ОстСтоимость = Формат(БалансоваяСтоимость - НачисленнаяАмортизация,"Ч015.2");
			
			Если (КоличествоСтрок() > 1) и (ТранспортныеСредства = 0) Тогда
				Нп = НомерСтроки;
				Если Нп = 1 Тогда
					Таб.ВывестиСекцию("Шапка0");
					Таб.ВывестиСекцию("Шапка1");
				ИначеЕсли ((Нп-1)/15) = Цел((Нп-1)/15) Тогда
					Таб.НоваяСтраница();
					//Таб.ВывестиСекцию("Шапка2");
					//Таб.НоваяСтраница();
					Таб.ВывестиСекцию("Шапка1");
				КонецЕсли;
				Таб.ВывестиСекцию("Строка1");
			Иначе
				Таб.Вывести();
				Таб.Опции(0,0,0,0,"ОпцииПечатиОС_4","ОкноОС_4");
				Таб.ТолькоПросмотр(1);
				Таб.ПараметрыСтраницы(2);
				
				Если (ОсновноеСредство.Группа = Перечисление.ГруппыОС.ТранспортныеСредства) Тогда
					Таб.Показать("ОС-4а");
				Иначе
					Таб.Показать("ОС-4");
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если КоличествоОСВСоставеМПЗ>0 Тогда
			Возврат;
		КонецЕсли;
		
		
		Если (КоличествоСтрок() > 1) и (ТранспортныеСредства = 0)  Тогда
			Таб.НоваяСтраница();
			Таб.ВывестиСекцию("Шапка2");
			Таб.Опции(0,0,0,0,"ОпцииПечатиОС_4б","ОкноОС_4б");
			Таб.ТолькоПросмотр(1);
			Таб.ПараметрыСтраницы(2);
			
			Таб.Показать("Акт ОС-4б");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры //Печать

//******************************************************************************
// ПоКнопкеПечать()
// 
// Вызывается из формул элементов диалога:
//  Кнопка "кнПечать".
//
// Описание:
//  Определяется соответствующая печатная форма.
// 	
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		Печать();
		
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(Контекст, "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Вызывается из формул элементов диалога:
//  Кнопка "кнВыбПечать".
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Документ." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()

//_____________________________________________________________________________
Процедура ВводНового(Копирование) //предопределенная
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	Если Копирование = 1 Тогда
	    Возврат;
	КонецЕсли;
	ТипСписания = 2;
	
КонецПроцедуры //ВводНового
//_____________________________________________________________________________
Процедура ПриОткрытии() //предопределенная
	НачальнаяДатаДокумента = ДатаДок;
	ПриЗаписиПерепроводить(1);
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаПоказать.Доступность(0);
	КонецЕсли;
	ПриВыбореТипаСписания();
	
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Документ." + Вид(),ТаблицаПечФорм);
	
КонецПроцедуры //ПриОткрытии
//_____________________________________________________________________________
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, ФлагСтандОбр)
	Если ИдентЭлемДиалога = "СтатьяПрочихДоходовИРасходов" Тогда
	    ОткрытьФорму("Справочник.ПрочиеДоходыИРасходы", Перечисление.ВидыПрочихДоходовИРасходов.ВыбытиеОС);
		ФлагСтандОбр = 0;
	КонецЕсли;
КонецПроцедуры
//_____________________________________________________________________________
Процедура ПриЗаписи() //предопределенная
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Если ОсновноеСредство.Выбран() = 0 Тогда
		Если Вопрос("Не выбрано выбывающее основное средство.
					|Вернуться к редактированию документа?","Да+Нет") = "Да" Тогда
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Операция.Содержание = "Списание ОС";
	Операция.СуммаОперации = БалансоваяСтоимость * КоличествоСтрок();
	ВсегоБалансоваяСтоимость = БалансоваяСтоимость * КоличествоСтрок();
КонецПроцедуры //ПриЗаписи

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//*****************************************************************************
Новый = 0;

ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");
	
// добавим информацию о встроенной форме
ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Акт на списание основных средств";
ТаблицаПечФорм.Кнопка       = "Акт ОС-4";

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Ввести на основании"); 
СписокДействий.ДобавитьЗначение("Перейти  в журнал");
