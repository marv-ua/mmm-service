////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем СчетРасчетовСПокупателем;
Перем СчетАвансовПолученных;
Перем ОплатаДоговора;
Перем ЦеныВДоговоре;
Перем ВестиУчетРасчетовУЕ;
Перем Валюта;
Перем Кратность;
Перем ИтогВсего;
Перем АвансПоДоговору;
Перем АвансБезДоговора;
Перем АвансПоДоговоруРуб;
Перем АвансБезДоговораРуб;
Перем БезДоговора;
Перем ЗадолженностьПоРасчетамВУЕРуб, ЗадолженностьПоРасчетамВУЕВал;
Перем НПРКурсоваяРазница;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//******************************************************************************
// РассчитатьСуммуАванса()
//
// Описание:
//  Выполняет расчет бухгалтерских итогов и определяет текущую сумму аванса.
//
Процедура РассчитатьСуммуАванса()
	
	АвансБезДоговора = 0;
	АвансБезДоговораРуб = 0;
	АвансПоДоговору = 0;
	АвансПоДоговоруРуб = 0;
	
	Если (ЗачитыватьАванс <> 1) и (ВидОтпуска = 0) Тогда //Зачитывать аванс
		СписокДоговоров = СоздатьОбъект("СписокЗначений");
		СписокДоговоров.ДобавитьЗначение(Договор);
		БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
		Если (ЗачитыватьАванс = 0) и (ПустоеЗначение(БезДоговора) = 0) Тогда //Зачитывать аванс без договора
			СписокДоговоров.ДобавитьЗначение(БезДоговора);
		КонецЕсли;                                  
		
		БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Контрагенты, Контрагент, 2);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Договоры, СписокДоговоров, 2);
		Если ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда	
				БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"62.22",,Валюта,,,"СВ");
			Иначе
				БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"62.22",,Валюта,,,"В");
			КонецЕсли;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
		    БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"62.7",,Валюта,,,"СВ");
			
		Иначе
			БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"62.2",,,,,"С");
		КонецЕсли;

		Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, БезДоговора) = 1 Тогда
			Если ОплатаДоговора = 2 Тогда
			    АвансБезДоговора = БухИт.СКК("В");
				Если ДатаДок >= '01.01.2008' Тогда	
					АвансБезДоговораРуб = БухИт.СКК("С");
				КонецЕсли;
				
			ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
				АвансБезДоговора = БухИт.СКК("В");
				АвансБезДоговораРуб = БухИт.СКК("С");
				
			Иначе
				АвансБезДоговора = БухИт.СКК("С");
			КонецЕсли;
		КонецЕсли;
		
		Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, Договор) = 1 Тогда
			Если ОплатаДоговора = 2 Тогда
			    АвансПоДоговору = БухИт.СКК("В");
				Если ДатаДок >= '01.01.2008' Тогда	
					АвансПоДоговоруРуб = БухИт.СКК("С");
				КонецЕсли;
				
			ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
				АвансПоДоговору = БухИт.СКК("В");
				АвансПоДоговоруРуб = БухИт.СКК("С");
				
			Иначе
				АвансПоДоговору = БухИт.СКК("С");
			КонецЕсли;
		КонецЕсли;
		
		АвансБезДоговора = Макс(АвансБезДоговора, 0);
		АвансБезДоговораРуб = Макс(АвансБезДоговораРуб, 0);
		АвансПоДоговору = Макс(АвансПоДоговору, 0);
		АвансПоДоговоруРуб = Макс(АвансПоДоговоруРуб, 0);
	КонецЕсли;
	
КонецПроцедуры // РассчитатьСуммуАванса()

//*****************************************************************************
// ЗачестьАванс(ЗачетАванса, ЗачетАвансаРуб, ДоговорАванса)
//
Процедура ЗачестьАванс(ЗачетАванса, ЗачетАвансаРуб, ДоговорАванса)
	
	Если ЗачетАванса > 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "МТ";
		Операция.СодержаниеПроводки = "Зачтена предоплата";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = СчетАвансовПолученных;
		Операция.Дебет.Контрагенты = Контрагент;
		Операция.Дебет.Договоры = ДоговорАванса;
		Операция.Кредит.Счет = СчетРасчетовСПокупателем;
		Операция.Кредит.Контрагенты = Контрагент;
		Операция.Кредит.Договоры = Договор;
		
		Если ОплатаДоговора = 2 Тогда 
			Если ДатаДок >= '01.01.2008' Тогда
				Если (АвансПоДоговору + АвансБезДоговора) = ЗачетАванса Тогда
					ЗачетАвансаРуб = АвансПоДоговоруРуб + АвансБезДоговораРуб;
				Иначе
					КурсЗачетаАванса = Окр((АвансПоДоговоруРуб + АвансБезДоговораРуб)/(АвансПоДоговору + АвансБезДоговора), 4, 1);
					ЗачетАвансаРуб = ЗачетАванса*КурсЗачетаАванса;
				КонецЕсли;
			Иначе
				ЗачетАвансаРуб = ЗачетАванса*Курс/Кратность;
			КонецЕсли;
			Операция.Сумма = ЗачетАвансаРуб;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			Операция.Сумма = ЗачетАвансаРуб;
			Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса; 
			
			ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб - Операция.Сумма;
			ЗадолженностьПоРасчетамВУЕВал = ЗадолженностьПоРасчетамВУЕВал - Операция.ВалСумма;
			
		Иначе
			Операция.Сумма = ЗачетАванса;
		КонецЕсли; 
		
		Если ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ВЛ";
				Операция.СодержаниеПроводки = "Зачтена предоплата";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетПоКоду("ВАЛ.62");
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.Договоры = ДоговорАванса;
				Операция.Сумма = ЗачетАванса*Курс/Кратность;
				Операция.Валюта = Валюта;
				Операция.ВалСумма = ЗачетАванса;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ЗачетАванса()

//******************************************************************************
// СформироватьПроводкиДляЦелейНалоговогоУчета()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура СформироватьПроводкиДляЦелейНалоговогоУчета(Знач ВыручкаБезНалогов, ТаблицаОстатков)
	
	Если КоличествоОтпущено >= ТаблицаОстатков.КоличествоНаСкладахНУ Тогда
		СуммаСписания = ТаблицаОстатков.СуммаНаСкладахНУ;
		
	Иначе
		СуммаСписания = Окр(КоличествоОтпущено*(ТаблицаОстатков.СуммаНаСкладахНУ/ТаблицаОстатков.КоличествоНаСкладахНУ), 2, 1);
	КонецЕсли;
	
	// Уменьшим остаток материала на списанное количество.
	ТаблицаОстатков.СуммаНаСкладахНУ = ТаблицаОстатков.СуммаНаСкладахНУ - СуммаСписания;
	ТаблицаОстатков.КоличествоНаСкладахНУ = ТаблицаОстатков.КоличествоНаСкладахНУ - КоличествоОтпущено;
	
	// Для определения условия списания материала необходимо
	// определить в счет аванса списание материала или нет.
		
	// Отразим списание стоимости материала.
	Если ВидОтпуска = 0 Тогда // продажа
	    
		Если СуммаСписания <> 0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "НУ";
			Операция.СодержаниеПроводки = "Списан материал";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Кредит.Счет = СчетПоКоду("Н02.01");
			Операция.Кредит.Субконто(1, Материал);
			Операция.Кредит.УсловияПоступленияИВыбытия = Перечисление.УсловияПоступленияИВыбытия.ЗаПлату;
			Операция.Кредит.Основание = Договор;
		    Операция.Дебет.Счет = СчетПоКоду("Н07.06");
			Операция.Сумма = СуммаСписания;
			Операция.Количество = КоличествоОтпущено;
		КонецЕсли;
		
	ИначеЕсли ВидОтпуска = 2 Тогда // возврат поставщику
		
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НУ";
		Операция.СодержаниеПроводки = "Возврат материала (по претензии)";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = СчетПоКоду("Н02.01");
		Операция.Дебет.Субконто(1, Материал);
		Операция.Дебет.Субконто(2, Перечисление.УсловияПоступленияИВыбытия.Возврат);
		Операция.Дебет.Субконто(3, Договор);
		Операция.Сумма = -ВыручкаБезНалогов;
		Операция.Количество = -КоличествоОтпущено;
		
	ИначеЕсли ВидОтпуска=1 Тогда //передача в переработку
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НУ";
		Операция.СодержаниеПроводки = "Передан в переработку материал";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Кредит.Счет = СчетПоКоду("Н02.01");
		Операция.Кредит.Субконто(1, Материал);
		Операция.Дебет.Счет = СчетПоКоду("Н02.09");
		Операция.Дебет.Субконто(1, Материал);
		Операция.Дебет.Основание = Договор; 
		Операция.Сумма = СуммаСписания;
		Операция.Количество = КоличествоОтпущено;
		//_____________________________________________________________________________  

	КонецЕсли;
	
КонецПроцедуры // СформироватьПроводкиДляЦелейНалоговогоУчета()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()
	
	Сч10_7   = СчетПоКоду("10.7"); // для отпуска в переработку на сторону
	Сч19_3   = СчетПоКоду("19.3");
	Сч62_2   = СчетПоКоду("62.2");
	Сч62_1   = СчетПоКоду("62.1");
	Сч62_7   = СчетПоКоду("62.7");
	Сч62_6   = СчетПоКоду("62.6");
	Сч62_22  = СчетПоКоду("62.22");
	Сч62_11  = СчетПоКоду("62.11");
	Сч68_5   = СчетПоКоду("68.5");
	Сч76_Н_4 = СчетПоКоду("76.Н.4");
	Сч76_2   = СчетПоКоду("76.2");
	Сч76_22  = СчетПоКоду("76.22");
	Сч91_1   = СчетПоКоду("91.1");
	Сч91_2   = СчетПоКоду("91.2");  
	
	СчУЕ_62 = СчетПоКоду("УЕ.62");
	СчВАЛ_62 = СчетПоКоду("ВАЛ.62");
	
	ЗадолженностьПоРасчетамВУЕРуб = 0;
	ЗадолженностьПоРасчетамВУЕВал = 0;
	НПРКурсоваяРазница = 0;

	
	СчетВыручкиНУ = СчетПоКоду("Н06.03");
	
	Если ВидОтпуска <> 1 Тогда // если не передача в переработку на сторону
		ЦеныВДоговоре = 1; // в рублях
		ВестиУчетРасчетовУЕ = 0;
		
		Если Договор.Выбран() = 1 Тогда
			Если ПустоеЗначение(Договор.ВалютаДоговора) = 0 Тогда
				ЦеныВДоговоре = 2; // в валюте
			КонецЕсли;
			ОплатаДоговора = Договор.ОплатаДоговора; // 1 - врублях, 2 - в валюте
			ВестиУчетРасчетовУЕ = Договор.ВестиУчетРасчетовУЕ;
		КонецЕсли;
	
		Если ЦеныВДоговоре = 2 Тогда
			Валюта = Договор.ВалютаДоговора;
			Кратность = Валюта.Кратность.Получить(ДатаДок);
			Кратность = ?(Кратность=0, 1, Кратность);
			Если ВестиУчетРасчетовУЕ = 1 Тогда
				Кратность = Кратность * 100 / (100 + Договор.ПроцентКорректировкиКурсаУЕ);
			КонецЕсли;
		
			Если (Курс = 0) и (ВидОтпуска = 1) Тогда
			    ТекстСообщения = "Не указан курс валюты.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ВидОтпуска = 0 Тогда
		Если ОплатаДоговора = 2 Тогда
			глТаблицаСчетов.УдалитьСтроки();
			
			СчетРасчетовСПокупателем = Сч62_11;
			СчетАвансовПолученных = Сч62_22;
			Если ДатаДок >= '01.01.2008' Тогда
				СчетАвансовПолученныхПереоценка = СчВАЛ_62;
			Иначе
				СчетАвансовПолученныхПереоценка = Сч62_22;
			КонецЕсли;
			
			глТаблицаСчетов.НоваяСтрока();
			глТаблицаСчетов.Счет = Сч62_11;
			глТаблицаСчетов.Субконто1 = Контрагент;
			глТаблицаСчетов.Субконто2 = Договор;
			глТаблицаСчетов.Субконто3 = "";
			глТаблицаСчетов.Валюта = Валюта;
			глТаблицаСчетов.Курс = Курс;
			
			Если ЗачитыватьАванс <> 1 Тогда
				глТаблицаСчетов.НоваяСтрока();
				глТаблицаСчетов.Счет = СчетАвансовПолученныхПереоценка;
				глТаблицаСчетов.Субконто1 = Контрагент;
				глТаблицаСчетов.Субконто2 = Договор;
				глТаблицаСчетов.Субконто3 = "";
				глТаблицаСчетов.Валюта = Валюта;
				глТаблицаСчетов.Курс = Курс;
				Если ЗачитыватьАванс = 0 Тогда
					БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
					Если ПустоеЗначение(БезДоговора) = 0 Тогда
						глТаблицаСчетов.НоваяСтрока();
						глТаблицаСчетов.Счет = СчетАвансовПолученныхПереоценка;
						глТаблицаСчетов.Субконто1 = Контрагент;
						глТаблицаСчетов.Субконто2 = БезДоговора;
						глТаблицаСчетов.Субконто3 = "";
						глТаблицаСчетов.Валюта = Валюта;
						глТаблицаСчетов.Курс = Курс;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			глПереоценкаСчетов(Контекст, глТаблицаСчетов,, 0);
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 0 Тогда
		    СчетРасчетовСПокупателем = Сч62_1;
			СчетАвансовПолученных = Сч62_2;
			
		Иначе
			СчетРасчетовСПокупателем = Сч62_6;
			СчетАвансовПолученных = Сч62_7;
		КонецЕсли;
	КонецЕсли;
	
	СписокМатериалов = СоздатьОбъект("СписокЗначений");
	ВыгрузитьТабличнуюЧасть(СписокМатериалов, "Материал");
	БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
	БухИт.ИспользоватьСубконто(ВидыСубконто.Материалы, СписокМатериалов, 2);
	БухИт.ИспользоватьСубконто(ВидыСубконто.МестаХранения);
	БухИт.ВключатьСубсчета(-1);
	БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"10",,,,,"СК");
	
	БИ_НУ = СоздатьОбъект("БухгалтерскиеИтоги");  БИ_НУ.ИспользоватьРазделительУчета(ЮрЛицо);
	БИ_НУ.ИспользоватьСубконто(ВидыСубконто.Материалы, СписокМатериалов, 2);
	БИ_НУ.ВыполнитьЗапрос(,ТекущийДокумент(), "Н02.01",,,,, "СК");
	
	ТаблицаОстатков = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(ТаблицаОстатков, "Материал, КоличествоОтпущено");
	ТаблицаОстатков.Свернуть("Материал", "КоличествоОтпущено");
	ТаблицаОстатков.НоваяКолонка("КоличествоНаСкладах", "Число");
	ТаблицаОстатков.НоваяКолонка("СуммаНаСкладах", "Число");
	ТаблицаОстатков.НоваяКолонка("СредняяСтоимость", "Число");
	ТаблицаОстатков.НоваяКолонка("КоличествоНаСкладахНУ", "Число");
	ТаблицаОстатков.НоваяКолонка("СуммаНаСкладахНУ", "Число");
	ТаблицаОстатков.ВыбратьСтроки();
	Пока ТаблицаОстатков.ПолучитьСтроку() = 1 Цикл
		КоличествоНаСкладе = 0;
		Если ТаблицаОстатков.Материал.Вид() = "Материалы" Тогда
			Если БухИт.ПолучитьСчет(, ТаблицаОстатков.Материал.СубСчет10) = 1 Тогда
				Если БухИт.ПолучитьСубконто(ВидыСубконто.Материалы,, ТаблицаОстатков.Материал) = 1 Тогда
					ТаблицаОстатков.СуммаНаСкладах = Макс(БухИт.СКД("С"), 0);
					ТаблицаОстатков.КоличествоНаСкладах = БухИт.СКД("К");
					Если БухИт.ПолучитьСубконто(ВидыСубконто.МестаХранения,, МестоХранения) = 1 Тогда
						КоличествоНаСкладе = БухИт.СКД("К");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Стр = ""+ТаблицаОстатков.НомерСтроки+". "+ТаблицаОстатков.Материал+": на складе "+МестоХранения+" "+КоличествоНаСкладе+" "+
			  ТаблицаОстатков.Материал.ЕдиницаИзмерения+", всего на складах "+ТаблицаОстатков.КоличествоНаСкладах+" "+
			  ТаблицаОстатков.Материал.ЕдиницаИзмерения+" на сумму "+ТаблицаОстатков.СуммаНаСкладах;
		
		Если ТаблицаОстатков.КоличествоНаСкладах > 0 Тогда
			ТаблицаОстатков.СредняяСтоимость = ТаблицаОстатков.СуммаНаСкладах/ТаблицаОстатков.КоличествоНаСкладах;
			Стр = Стр + ",  средняя стоимость " + Окр(ТаблицаОстатков.СредняяСтоимость, 2, 1);
		КонецЕсли;
		ТекстСообщения = Стр;
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		
		Если КоличествоНаСкладе < ТаблицаОстатков.КоличествоОтпущено Тогда
			ТекстСообщения = "На складе "+КоличествоНаСкладе+" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+
							" из необходимых "+ТаблицаОстатков.КоличествоОтпущено+" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+" "+
							ТаблицаОстатков.Материал + " ("+ТаблицаОстатков.Материал.СубСчет10+")";
			
			Если Константа.КонтрольОтрицательныхОстатков = Да Тогда
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			
			Иначе
				глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), -1);
			КонецЕсли;
		КонецЕсли;
		
		// Предварительные операции по налоговому учету.
		//Если (ВидОтпуска <> 1) и (глНовыеПравилаВеденияНУ(ДатаДок) = 1) Тогда
		Если (глНовыеПравилаВеденияНУ(ДатаДок) = 1) Тогда
			Если БИ_НУ.ПолучитьСубконто(1,, ТаблицаОстатков.Материал) = 1 Тогда
				ТаблицаОстатков.СуммаНаСкладахНУ = Макс(БИ_НУ.СКД("С"), 0);
				ТаблицаОстатков.КоличествоНаСкладахНУ = БИ_НУ.СКД("К");
			КонецЕсли;
			Если ТаблицаОстатков.КоличествоНаСкладахНУ < ТаблицаОстатков.КоличествоОтпущено Тогда
				ТекстСообщения = "По данным налогового учета на складах "+ТаблицаОстатков.КоличествоНаСкладахНУ+" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+
					" из необходимых "+ТаблицаОстатков.КоличествоОтпущено+" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+" "+
					ТаблицаОстатков.Материал;
				
				Если Константа.КонтрольОтрицательныхОстатков = Да Тогда
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				
				Иначе
					глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), -1);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ВидОтпуска = 0 Тогда // если продажа материалов, то рассчитаем сумму аванса полученного от покупателя.
		ЗачетАвансаПоДоговору = 0;
		ЗачетАвансаБезДоговора = 0;
		ЗачетАвансаПоДоговоруРуб = 0;
    	ЗачетАвансаБезДоговораРуб = 0;
		РассчитатьСуммуАванса(); // расчет итогов по авансам
	КонецЕсли;
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если КоличествоОтпущено=0 Тогда
			// Если не указано количество отпускаемного материала, выдаем сообщение и проводку не формируем.
			ТекстСообщения = "Строка " + НомерСтроки + ", не указано отпущенное количество материала""" + Материал;
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
			
		ИначеЕсли Материал.Выбран() = 0 Тогда
			ТекстСообщения = "Строка " + НомерСтроки + ", не указан материал.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		НомСтроки = 0;
		ТаблицаОстатков.НайтиЗначение(Материал, НомСтроки, "Материал");
		ТаблицаОстатков.ПолучитьСтрокуПоНомеру(НомСтроки);
		
		Если КоличествоОтпущено >= ТаблицаОстатков.КоличествоНаСкладах Тогда
			// Если материал отпускается полностью, полностью списываем сумму
			СуммаОтгрузки = ТаблицаОстатков.СуммаНаСкладах;
		
		Иначе
			СуммаОтгрузки = Окр(КоличествоОтпущено*ТаблицаОстатков.СредняяСтоимость, 2, 1);
		КонецЕсли;
		    
		// Уменьшим остаток материала на списанное количество.
		ТаблицаОстатков.СуммаНаСкладах = ТаблицаОстатков.СуммаНаСкладах - СуммаОтгрузки;
		ТаблицаОстатков.КоличествоНаСкладах = ТаблицаОстатков.КоличествоНаСкладах - КоличествоОтпущено;
       	
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Если ВидОтпуска = 0 Тогда  //отгрузка
			Операция.НомерЖурнала = "МТ";
			Операция.СодержаниеПроводки = "Списан материал";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = Сч91_2;
			Операция.Дебет.ПрочиеДоходыИРасходы = СтатьяПрочихДоходовИРасходов;
			Операция.Кредит.Счет = Материал.СубСчет10;
			Операция.Кредит.Материалы = Материал;
			Операция.Кредит.МестаХранения = МестоХранения;
			Операция.Количество = КоличествоОтпущено;
			Операция.Сумма = СуммаОтгрузки;
			
		ИначеЕсли ВидОтпуска = 1 Тогда  // передача на переработку 
			
			//устанавливаем реквизиты табличной части Цена, Сумма, Всего, НДС, НП
			Сумма = СуммаОтгрузки;
			Цена = СуммаОтгрузки / КоличествоОтпущено;
			НДС = 0;
			НП = 0;
			Всего = СуммаОтгрузки;
			Операция.СодержаниеПроводки = "Материал перед. в перераб.";
			Операция.НомерЖурнала = "МТ";
			Операция.Дебет.Счет = Сч10_7;
			Операция.Дебет.Материалы = Материал;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;
			Операция.Кредит.Счет = Материал.СубСчет10;
			Операция.Кредит.Материалы = Материал;
			Операция.Кредит.МестаХранения = МестоХранения;
			Операция.Количество = КоличествоОтпущено;
			Операция.Сумма = СуммаОтгрузки;
			
		Иначе  // возврат  поставщику
			Операция.СодержаниеПроводки = "Возврат поставщику";
			Операция.НомерЖурнала = "МТ";
			Операция.Дебет.Счет = Сч76_2;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;
			Операция.Кредит.Счет = Материал.СубСчет10;
			Операция.Кредит.Материалы = Материал;
			Операция.Кредит.МестаХранения = МестоХранения;
			Операция.Количество = КоличествоОтпущено;
			
			Если НДСвключатьВСтоимость = 1 Тогда
				Стоимость = ?(ЦеныВДоговоре = 2,Окр(Всего*Курс/Кратность,2,1),Всего);
			Иначе
				Стоимость = ?(ЦеныВДоговоре = 2,Окр(Всего*Курс/Кратность,2,1)-Окр(НДС*Курс/Кратность,2,1),Всего - НДС);
			КонецЕсли;
			
			Если ОплатаДоговора = 2 Тогда
				Операция.Дебет.Счет = Сч76_22;
				Операция.Валюта = Валюта;
				Операция.ВалСумма = Сумма;
			КонецЕсли;
			Операция.Сумма = Стоимость;
		КонецЕсли;
		
		// Отражение списания материала в налоговом учете.
		Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда 

			ВсегоРасч 	= ?(ЦеныВДоговоре = 2,Окр(Всего*Курс/Кратность,2,1),Всего);
			НДСРасч 	= ?(ЦеныВДоговоре = 2,Окр(НДС*Курс/Кратность,2,1),НДС);
			НПРасч 		= ?(ЦеныВДоговоре = 2,Окр(НП*Курс/Кратность,2,1),НП);
				
			Если ВидОтпуска = 0 Тогда  //отгрузка
				СформироватьПроводкиДляЦелейНалоговогоУчета(ВсегоРасч-НДСРасч-НПРасч, ТаблицаОстатков)
				
			ИначеЕсли ВидОтпуска = 2 Тогда  //возврат поставщику
				СформироватьПроводкиДляЦелейНалоговогоУчета(ВсегоРасч-?(НДСвключатьВСтоимость = 1, 0, НДСРасч), ТаблицаОстатков)

			ИначеЕсли (ВидОтпуска=1) И (Константа.РаздельныйУчетМатериаловСкладПереработка.Получить(ДатаДок)=Да) Тогда //передача в переработку
				СформироватьПроводкиДляЦелейНалоговогоУчета (, ТаблицаОстатков);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;

	// Формируем проводку на общую сумму отпущенных материалов.
	Если ВидОтпуска = 0 Тогда
		СуммаЗачтенногоАванса = 0;
		СуммаЗачтенногоАвансаРуб = 0;
		КурсАванса = 0;
		
		ТаблицаРеализации = СоздатьОбъект("ТаблицаЗначений");
		ТаблицаРеализации.НоваяКолонка("Материал");
		ТаблицаРеализации.НоваяКолонка("ВидНоменклатуры");
		ТаблицаРеализации.НоваяКолонка("СтавкаНДС");
		ТаблицаРеализации.НоваяКолонка("СтавкаНП");
		ТаблицаРеализации.НоваяКолонка("Всего", "Число", 15, 2);
		ТаблицаРеализации.НоваяКолонка("ВалВсего", "Число", 15, 2);
		ТаблицаРеализации.НоваяКолонка("НП", "Число", 15, 2);
		ТаблицаРеализации.НоваяКолонка("НДС", "Число", 15, 2);
		ТаблицаРеализации.НоваяКолонка("Количество", "Число");
		ТаблицаРеализации.НоваяКолонка("НПВал", "Число", 15, 2);
		ТаблицаРеализации.НоваяКолонка("НДСВал", "Число", 15, 2);
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			ТаблицаРеализации.НоваяСтрока();
			ТаблицаРеализации.Материал = Материал;
			ТаблицаРеализации.СтавкаНДС = глСтавкаНалога(Контекст, "НДС");
			ТаблицаРеализации.СтавкаНП = глСтавкаНалога(Контекст, "НП");
			ТаблицаРеализации.Количество = КоличествоОтпущено;
			
			Если (ЦеныВДоговоре = 2) и (ВестиУчетРасчетовУЕ = 0) Тогда
				ТаблицаРеализации.Всего = Всего*Курс/Кратность;
				ТаблицаРеализации.НП = НП*Курс/Кратность;
				ТаблицаРеализации.НДС = НДС*Курс/Кратность;
				ТаблицаРеализации.НПВал = НП;
				ТаблицаРеализации.НДСВал = НДС;
				Если ОплатаДоговора =2 Тогда
				    ТаблицаРеализации.ВалВсего = Всего;
				КонецЕсли;
				
			Иначе
				ТаблицаРеализации.Всего = Всего;
				ТаблицаРеализации.НП = НП;
				ТаблицаРеализации.НДС = НДС;
			КонецЕсли;
		КонецЦикла;
		            
		Если ОплатаДоговора = 2 Тогда
		    ИтогВсего = ТаблицаРеализации.Итог("ВалВсего");
		
		Иначе
			ИтогВсего = ТаблицаРеализации.Итог("Всего");
		КонецЕсли;
		
		Если (ЗачитыватьАванс <> 1) и (Итог("Всего") <> 0) Тогда //Зачитывать аванс
			Если ВестиУчетРасчетовУЕ = 1 Тогда
				ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, ИтогВсего);
				ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, ИтогВсего - ЗачетАвансаПоДоговору);
				
				Аванс = АвансПоДоговору + АвансБезДоговора;
				ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
				
				Если ЗачетАванса = Аванс Тогда
				    ЗачетАвансаПоДоговоруРуб = АвансПоДоговоруРуб;
					ЗачетАвансаБезДоговораРуб = АвансБезДоговораРуб;
					
				ИначеЕсли ЗачетАванса > АвансПоДоговору Тогда
					ЗачетАвансаПоДоговоруРуб = АвансПоДоговоруРуб;
					ЗачетАвансаБезДоговораРуб = Окр(ЗачетАвансаБезДоговора * Окр(АвансБезДоговораРуб / ?(АвансБезДоговора = 0, 1, АвансБезДоговора), 4, 1), 2, 1);
					
				Иначе
					ЗачетАвансаПоДоговоруРуб = Окр(ЗачетАвансаПоДоговору * Окр(АвансПоДоговоруРуб / ?(АвансПоДоговору = 0, 1, АвансПоДоговору), 4, 1), 2, 1);
					ЗачетАвансаБезДоговораРуб = 0;
				КонецЕсли;
			
			Иначе
				ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, ИтогВсего);
				ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, ИтогВсего - ЗачетАвансаПоДоговору);
			КонецЕсли;
		    
			ЗачестьАванс(ЗачетАвансаПоДоговору, ЗачетАвансаПоДоговоруРуб, Договор);
			ЗачестьАванс(ЗачетАвансаБезДоговора, ЗачетАвансаБезДоговораРуб, БезДоговора);
			
			СуммаЗачтенногоАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
			
			Если ВестиУчетРасчетовУЕ = 1 Тогда
				СуммаЗачтенногоАвансаРуб = ЗачетАвансаПоДоговоруРуб + ЗачетАвансаБезДоговораРуб;
				
				ПечЗачетАванса = Окр(ЗачетАвансаПоДоговоруРуб + ЗачетАвансаБезДоговораРуб, 2, 1);
				ПечЗачетАвансаПоДоговору = Окр(ЗачетАвансаПоДоговоруРуб, 2, 1);
				ПечЗачетАвансаБезДоговора = Окр(ЗачетАвансаБезДоговораРуб, 2, 1);
				ПечАвансПоДоговору = Окр(АвансПоДоговоруРуб, 2, 1);
				ПечАвансБезДоговора = Окр(АвансБезДоговораРуб, 2, 1);
					
			Иначе 
				
				Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
					СуммаЗачтенногоАвансаРуб = ЗачетАвансаПоДоговоруРуб + ЗачетАвансаБезДоговораРуб;				
				КонецЕсли;
				
				ПечЗачетАванса = Окр(ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора, 2, 1);
				ПечЗачетАвансаПоДоговору = Окр(ЗачетАвансаПоДоговору, 2, 1);
				ПечЗачетАвансаБезДоговора = Окр(ЗачетАвансаБезДоговора, 2, 1);
				ПечАвансПоДоговору = Окр(АвансПоДоговору, 2, 1);
				ПечАвансБезДоговора = Окр(АвансБезДоговора, 2, 1);
			КонецЕсли;
			
			Если (АвансПоДоговору + АвансБезДоговора) <> 0 Тогда
			    ТекстСообщения = "=> Зачет аванса:" + РазделительСтрок
				+ "   отгружено на сумму: "+ИтогВсего+", зачтен аванс в размере: "+ПечЗачетАванса + РазделительСтрок
				+ "   - определен аванс по договору "+Договор+" в сумме: "+ПечАвансПоДоговору+", зачтен аванс в размере: "+ПечЗачетАвансаПоДоговору + РазделительСтрок;
				
				Если ЗачитыватьАванс = 0 Тогда
					ТекстСообщения = ТекстСообщения
					+ "   - определен аванс без указания договора в сумме: "+ПечАвансБезДоговора+", зачтен аванс в размере: "+ПечЗачетАвансаБезДоговора;
				Иначе
					ТекстСообщения = ТекстСообщения
					+ "   - аванс без указания договора не учтен";
				КонецЕсли;
				
				глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
			КонецЕсли;
		КонецЕсли;
		
		ТаблицаРеализации.НоваяКолонка("ВыручкаРуб", "Число", 15, 2);
		ТаблицаРеализации.НоваяКолонка("СуммоваяРазница", "Число", 15, 2);
		
		РаспределеноАванса = 0;
		РаспределеноАвансаРуб = 0;
		КолСтрокТабОтгрузки = ТаблицаРеализации.КоличествоСтрок();
		КурсАванса = ?(СуммаЗачтенногоАванса = 0, 0, Окр(СуммаЗачтенногоАвансаРуб / СуммаЗачтенногоАванса, 4, 1));
		К = ?(ИтогВсего = 0, 0, СуммаЗачтенногоАванса / ИтогВсего);
		
		ТаблицаРеализации.ВыбратьСтроки();
		Пока ТаблицаРеализации.ПолучитьСтроку() = 1 Цикл
			Если ((ЦеныВДоговоре = 2) и (ВестиУчетРасчетовУЕ = 1))
			или ((ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008')) Тогда
				Если ПустоеЗначение(ИтогВсего) = 0  Тогда
					Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
						ТаблицаВсего = ТаблицаРеализации.ВалВсего;
					Иначе
						ТаблицаВсего = ТаблицаРеализации.Всего;
					КонецЕсли;
					
					ЧастьСуммыЗачтенногоАванса = Окр(СуммаЗачтенногоАванса * ТаблицаВсего / ИтогВсего, 2, 1);
					ЧастьСуммыЗачтенногоАвансаРуб = Окр(СуммаЗачтенногоАвансаРуб * ТаблицаВсего / ИтогВсего, 2, 1);
					
				Иначе
					ЧастьСуммыЗачтенногоАванса = 0;
					ЧастьСуммыЗачтенногоАвансаРуб = 0;
				КонецЕсли;
				
				РаспределеноАванса = РаспределеноАванса + ЧастьСуммыЗачтенногоАванса;
				РаспределеноАвансаРуб = РаспределеноАвансаРуб + ЧастьСуммыЗачтенногоАвансаРуб;
				
				Если КолСтрокТабОтгрузки = ТаблицаРеализации.НомерСтроки Тогда
					Если РаспределеноАванса <> СуммаЗачтенногоАванса Тогда
						ЧастьСуммыЗачтенногоАванса = ЧастьСуммыЗачтенногоАванса + СуммаЗачтенногоАванса - РаспределеноАванса;
					КонецЕсли;
					
					Если РаспределеноАвансаРуб <> СуммаЗачтенногоАвансаРуб Тогда
						ЧастьСуммыЗачтенногоАвансаРуб = ЧастьСуммыЗачтенногоАвансаРуб + СуммаЗачтенногоАвансаРуб - РаспределеноАвансаРуб;
					КонецЕсли;
				КонецЕсли;
				
				Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
					ВсегоРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.ВалВсего * Курс / Кратность, 2, 1);
					НДСРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.НДС, 2, 1);                 
					НПРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.НП, 2, 1);
					НДСвЧастиЗачтенногоАвансаРуб = Окр(К * ТаблицаРеализации.НДСВал * Курс / Кратность, 2, 1);
					НПвЧастиЗачтенногоАвансаРуб = Окр(К * ТаблицаРеализации.НПВал * Курс / Кратность, 2, 1);
				Иначе
					ВсегоРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.Всего * Курс / Кратность, 2, 1);
					НДСРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.НДС * Курс / Кратность, 2, 1);
					НПРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.НП * Курс / Кратность, 2, 1);				
					НДСвЧастиЗачтенногоАвансаРуб = Окр(К * ТаблицаРеализации.НДС * КурсАванса, 2, 1);
					НПвЧастиЗачтенногоАвансаРуб = Окр(К * ТаблицаРеализации.НП * КурсАванса, 2, 1);
				КонецЕсли;
				
				ОплаченоПоКурсуОтгрузки = Окр(К * ВсегоРубПоКурсуОтгрузки, 2, 1);
				ТаблицаРеализации.ВыручкаРуб = ВсегоРубПоКурсуОтгрузки + ЧастьСуммыЗачтенногоАвансаРуб - ОплаченоПоКурсуОтгрузки;
				
				ОплаченоНДСПоКурсуОтгрузки = Окр(К * НДСРубПоКурсуОтгрузки, 2, 1);
				СуммаНДС = НДСРубПоКурсуОтгрузки + НДСвЧастиЗачтенногоАвансаРуб - ОплаченоНДСПоКурсуОтгрузки;
				
				Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
					НПРКурсоваяРазница = Окр(НПРКурсоваяРазница, 2) - Окр(СуммаНДС, 2);
				КонецЕсли;
				
				ОплаченоНППоКурсуОтгрузки = Окр(К * НПРубПоКурсуОтгрузки, 2, 1);
				СуммаНП = НПРубПоКурсуОтгрузки + НПвЧастиЗачтенногоАвансаРуб - ОплаченоНППоКурсуОтгрузки;
				
				ВыручкаБезНалогов = ТаблицаРеализации.ВыручкаРуб - СуммаНДС - СуммаНП;
				
				Если (Договор.ОтражатьСуммовуюРазницуПриЗачетеАванса = 1) и (ВестиУчетРасчетовУЕ = 1) Тогда
					ТаблицаРеализации.СуммоваяРазница = ЧастьСуммыЗачтенногоАвансаРуб - ОплаченоПоКурсуОтгрузки;
				КонецЕсли;
				
			Иначе
				ВыручкаБезНалогов = ТаблицаРеализации.Всего - ТаблицаРеализации.НДС - ТаблицаРеализации.НП;
			КонецЕсли;
			
			Если (Договор.ОтражатьСуммовуюРазницуПриЗачетеАванса = 1) и (ВестиУчетРасчетовУЕ = 1) Тогда
				ПоКурсуОтгрузки = ОплаченоПоКурсуОтгрузки - ОплаченоНДСПоКурсуОтгрузки - ОплаченоНППоКурсуОтгрузки;
				ПоКурсуОплаты = ЧастьСуммыЗачтенногоАвансаРуб - НДСвЧастиЗачтенногоАвансаРуб - НПвЧастиЗачтенногоАвансаРуб;
				СуммоваяРазница = ПоКурсуОплаты - ПоКурсуОтгрузки;
				
			Иначе
				СуммоваяРазница = 0; 
			КонецЕсли;
			
			Если (ВыручкаБезНалогов > 0) и (глНовыеПравилаВеденияНУ(ДатаДок) = 1) Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			    Операция.НомерЖурнала = "НУ";
				Операция.СодержаниеПроводки = "Выручка от реализации";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Кредит.Счет = СчетВыручкиНУ;
				Операция.Кредит.УсловияПоступленияИВыбытия = Перечисление.УсловияПоступленияИВыбытия.ЗаПлату;
				Операция.Кредит.Основание = Договор;
				Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
					Операция.Сумма = (ТаблицаРеализации.Всего - ТаблицаРеализации.НДС - ТаблицаРеализации.НП) - СуммоваяРазница;
					НПРКурсоваяРазница = Окр(НПРКурсоваяРазница, 2) - Окр(Операция.Сумма, 2);
				Иначе
					Операция.Сумма = ВыручкаБезНалогов - СуммоваяРазница;
				КонецЕсли;
				Операция.Количество = ТаблицаРеализации.Количество;
				
				глОтражениеСуммовыхРазницВНаловомУчете(Контекст, СуммоваяРазница, 0);
			КонецЕсли;
		КонецЦикла;
		
		СуммаРеализации = ТаблицаРеализации.Итог("Всего");
		ВсегоСуммоваяРазница = ТаблицаРеализации.Итог("СуммоваяРазница");
		Если СуммаРеализации > 0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "МТ";
			Операция.СодержаниеПроводки = "Реализ.по цене выбытия";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетРасчетовСПокупателем;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;
			Операция.Кредит.Счет = Сч91_1;
			Операция.Кредит.ПрочиеДоходыИРасходы = СтатьяПрочихДоходовИРасходов;
			Если ОплатаДоговора = 2 Тогда
				Операция.Валюта = Валюта;
				Операция.ВалСумма = ТаблицаРеализации.Итог("ВалВсего");
				Если ДатаДок >= '01.01.2008' Тогда	
					Операция.Сумма = ТаблицаРеализации.Итог("ВыручкаРуб");
					НПРКурсоваяРазница = Окр(НПРКурсоваяРазница, 2) + Окр(Операция.Сумма, 2);
				Иначе
					Операция.Сумма = СуммаРеализации;
				КонецЕсли;			
			КонецЕсли;
			
			Если ВестиУчетРасчетовУЕ = 1 Тогда
				Операция.Сумма = ТаблицаРеализации.Итог("ВыручкаРуб") - ВсегоСуммоваяРазница;
				Операция.Валюта = Валюта;
				Операция.ВалСумма = СуммаРеализации;  
				
				ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб + Операция.Сумма;
				ЗадолженностьПоРасчетамВУЕВал = ЗадолженностьПоРасчетамВУЕВал + Операция.ВалСумма;
				
			ИначеЕсли ОплатаДоговора <> 2 Тогда
				Операция.Сумма = СуммаРеализации;
			КонецЕсли;
			
			Если ВсегоСуммоваяРазница <> 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "МТ";
				Операция.СодержаниеПроводки = "Суммовая разница";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетРасчетовСПокупателем;
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.Договоры = Договор;
				Операция.Кредит.Счет = Сч91_1;
				Операция.Кредит.ПрочиеДоходыИРасходы = СтатьяПрочихДоходовИРасходов;
				Операция.Валюта = Валюта;
				Операция.Сумма = ВсегоСуммоваяРазница;
				
				ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб + Операция.Сумма;
				
			КонецЕсли;
		КонецЕсли;
		
		Если (СуммаРеализации > 0) и (ВерсияОбъекта >= "7.70.421") и (ТаблицаРеализации.Итог("НП") > 0) Тогда
			К = (ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора) / СуммаРеализации;
			НПкУплате = К*ТаблицаРеализации.Итог("НП");
			ОтложеноНП = ТаблицаРеализации.Итог("НП") - НПкУплате;
			Если НПкУплате > 0 Тогда // был зачет аванса
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ТВ";
				Операция.СодержаниеПроводки = "Начислен НП";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = Сч91_2;
				Операция.Дебет.ПрочиеДоходыИРасходы = СтатьяПрочихДоходовИРасходов;
				Операция.Кредит.Счет = Сч68_5;
				Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
				Если ВестиУчетРасчетовУЕ = 1 Тогда
					Операция.Сумма = НПкУплате*Курс/Кратность;
				Иначе
					Операция.Сумма = НПкУплате;
				КонецЕсли;
			КонецЕсли;
			
			Если ОтложеноНП > 0 Тогда
			    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ТВ";
				Операция.СодержаниеПроводки = "Начислен НП";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = Сч91_2;
				Операция.Дебет.ПрочиеДоходыИРасходы = СтатьяПрочихДоходовИРасходов;
				Операция.Кредит.Счет = Сч76_Н_4;
				Операция.Кредит.Контрагенты = Контрагент;
				Операция.Кредит.Договоры = Договор;
				Если ВестиУчетРасчетовУЕ = 1 Тогда
				    Операция.Сумма = ОтложеноНП*Курс/Кратность;
				Иначе
					Операция.Сумма = ОтложеноНП;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
		
		//********************************************************************
		//Проводки по расчетам в у.е. 
		Если ДатаДок >= '01.01.2007' Тогда
			Если ВестиУчетРасчетовУЕ = 1 Тогда
				Если (ЗадолженностьПоРасчетамВУЕРуб + ЗадолженностьПоРасчетамВУЕВал) <> 0 Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "УЕ";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = СчУЕ_62;
					Операция.Дебет.Субконто(1, Контрагент);
					Операция.Дебет.Субконто(2, Договор);			
					Операция.Валюта = Валюта;
					Операция.Сумма = ЗадолженностьПоРасчетамВУЕРуб;
					Операция.ВалСумма = ЗадолженностьПоРасчетамВУЕВал;
					Операция.СодержаниеПроводки = "Задолженность по реализации в у.е."; 
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;	
		//********************************************************************
		
		//********************************************************************
		//Проводки по расчетам в вал.
		Если (Константа.ПрименяетсяПБУ18.Получить(ДатаДок) = Да) Тогда
			Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
				Если НПРКурсоваяРазница <> 0 Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "НУ";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = СчетПоКоду("НПР.99");
					Операция.Сумма = -НПРКурсоваяРазница;
					Операция.СодержаниеПроводки = "Разница в оценке дохода"; 
				КонецЕсли;	
			КонецЕсли;	
		КонецЕсли;
		//********************************************************************
		
	ИначеЕсли ВидОтпуска = 2 Тогда // возврат поставщику
		
		Если ПустоеЗначение(ДокументПоступления) = 0 Тогда
			СФ=СоздатьОбъект("Документ");
			СФ.ВыбратьПодчиненныеДокументы(ДокументПоступления.ДатаДок,ДатаДок,ДокументПоступления);
			Пока СФ.ПолучитьДокумент()=1 Цикл
				Если СФ.Вид()="СчетФактураПолученный" Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если СФ.Вид()<>"СчетФактураПолученный" Тогда
				СФ.НайтиДокумент(ДокументПоступления);
			КонецЕсли;
		КонецЕсли;
		
		ИтогоНДС = Итог("НДС");
		Если (ИтогоНДС > 0) и (НДСвключатьВСтоимость = 0) и (ПоставщикуВыставляетсяСчетФактураНаВозврат = 0) Тогда 
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "МТ";
			Операция.СодержаниеПроводки = "Возврат НДС";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Кредит.Счет = Сч19_3;
			Операция.Кредит.Контрагенты = Контрагент;
			Если ПустоеЗначение(ДокументПоступления) = 0 Тогда
				Если (ПустоеЗначение(ДокументПоступления.ДатаНомерСчетаФактуры) = 0) или (СФ.Вид()="СчетФактураПолученный") Тогда
					Операция.Кредит.СчетаФактурыПолученные = СФ.ТекущийДокумент();
				КонецЕсли;    
			КонецЕсли;
			Операция.Дебет.Счет = Сч76_2;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;
			Если ЦеныВДоговоре = 2 Тогда
			    Операция.Сумма = ИтогоНДС*Курс/Кратность;
				Если ОплатаДоговора = 2 Тогда
					Операция.Дебет.Счет = Сч76_22;
				    Операция.Валюта = Валюта;
					Операция.ВалСумма = ИтогоНДС;
				КонецЕсли;
			Иначе
				Операция.Сумма = ИтогоНДС;
			КонецЕсли;
		КонецЕсли;
		
		Если (Договор.АвтоОбработкаНДС=1)и(НДСвключатьВСтоимость =0)и(ДокументПоступления.Выбран()=1) и (ПоставщикуВыставляетсяСчетФактураНаВозврат = 0) Тогда 
			ТаблицаДокумента = СоздатьОбъект("ТаблицаЗначений");
			ВыгрузитьТабличнуюЧасть(ТаблицаДокумента);
			ТаблицаДокумента.НоваяКолонка("СтавкаНДС");
			
			ТаблицаДокумента.ВыбратьСтроки();
			Пока ТаблицаДокумента.ПолучитьСтроку() = 1 Цикл
				Если ЦеныВДоговоре = 2 Тогда
					СуммаРуб = Окр(ТаблицаДокумента.Сумма*Курс/Кратность,2,1);
					НДСРуб = Окр(ТаблицаДокумента.НДС*Курс/Кратность,2,1);
				Иначе
					СуммаРуб = ТаблицаДокумента.Сумма;
					НДСРуб = ТаблицаДокумента.НДС;
				КонецЕсли;
				
				Если СуммаРуб > 0 Тогда
					СтавкаНДС = 100*НДСРуб/СуммаРуб;
				Иначе
					СтавкаНДС = 20;
				КонецЕсли;				
								
				Если СтавкаНДС>10.5 Тогда
					СтавкаНДС = 20;
				ИначеЕсли СтавкаНДС>0 Тогда
					СтавкаНДС = 10;
				Иначе
					СтавкаНДС = 0;
				КонецЕсли;

				ТаблицаДокумента.СтавкаНДС = СтавкаНДС
			КонецЦикла;

			ТаблицаДокумента.Свернуть("СтавкаНДС","НДС,Всего");
			ТаблицаДокумента.ВыбратьСтроки();
			Пока ТаблицаДокумента.ПолучитьСтроку()=1 Цикл
				
				Если ТаблицаДокумента.СтавкаНДС>10.5 Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "АВ";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = СчетПоКоду("ЗПК.20.Б");
					Операция.Дебет.Субконто(1,Контрагент);
					Операция.Дебет.Субконто(2,Договор);                    
					Операция.Дебет.Субконто(3,СФ.ТекущийДокумент());
					Если ЦеныВДоговоре = 2 Тогда
						Операция.Сумма = Окр(ТаблицаДокумента.Всего*Курс/Кратность,2,1) - Окр(ТаблицаДокумента.НДС*Курс/Кратность,2,1);
					Иначе
						Операция.Сумма = ТаблицаДокумента.Всего - ТаблицаДокумента.НДС;
					КонецЕсли;
					Операция.СодержаниеПроводки = "Данные для автоматического учета НДС";
					
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "АВ";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = СчетПоКоду("ЗПК.20.Н");
					Операция.Дебет.Субконто(1,Контрагент);
					Операция.Дебет.Субконто(2,Договор);
					Операция.Дебет.Субконто(3,СФ.ТекущийДокумент());
					Если ЦеныВДоговоре = 2 Тогда
						Операция.Сумма = (ТаблицаДокумента.НДС)*Курс/Кратность;
					Иначе
						Операция.Сумма = ТаблицаДокумента.НДС;
					КонецЕсли;
					Операция.СодержаниеПроводки = "Данные для автоматического учета НДС";
				ИначеЕсли ТаблицаДокумента.СтавкаНДС>0 Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "АВ";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = СчетПоКоду("ЗПК.10.Б");
					Операция.Дебет.Субконто(1,Контрагент);
					Операция.Дебет.Субконто(2,Договор);
					Операция.Дебет.Субконто(3,СФ.ТекущийДокумент());
					Если ЦеныВДоговоре = 2 Тогда
						Операция.Сумма = Окр(ТаблицаДокумента.Всего*Курс/Кратность,2,1) - Окр(ТаблицаДокумента.НДС*Курс/Кратность,2,1);
					Иначе
						Операция.Сумма = ТаблицаДокумента.Всего - ТаблицаДокумента.НДС;
					КонецЕсли;
					Операция.СодержаниеПроводки = "Данные для автоматического учета НДС";
					
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "АВ";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = СчетПоКоду("ЗПК.10.Н");
					Операция.Дебет.Субконто(1,Контрагент);
					Операция.Дебет.Субконто(2,Договор);
					Операция.Дебет.Субконто(3,СФ.ТекущийДокумент());
					Если ЦеныВДоговоре = 2 Тогда
						Операция.Сумма = (ТаблицаДокумента.НДС)*Курс/Кратность;
					Иначе
						Операция.Сумма = ТаблицаДокумента.НДС;
					КонецЕсли;
					Операция.СодержаниеПроводки = "Данные для автоматического учета НДС";
					
				Иначе 
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "АВ";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = СчетПоКоду("ЗПК.БН");
					Операция.Дебет.Субконто(1,Контрагент);
					Операция.Дебет.Субконто(2,Договор);
					Операция.Дебет.Субконто(3,СФ.ТекущийДокумент());
					Если ЦеныВДоговоре = 2 Тогда
						Операция.Сумма = (ТаблицаДокумента.Всего)*Курс/Кратность;
					Иначе
						Операция.Сумма = ТаблицаДокумента.Всего;
					КонецЕсли;
					Операция.СодержаниеПроводки = "Данные для автоматического учета НДС";
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры