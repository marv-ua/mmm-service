////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;
Перем НачальнаяДатаДокумента; 
Перем Новый;
Перем СписокДействий;
//_____________________________________________________________________________
Процедура ПриВыбореСчетаКонтрагента()
	Если СчетКонтрагента.Выбран() = 1 Тогда
		Если СчетКонтрагента.Владелец <> Контрагент Тогда
			Контрагент = СчетКонтрагента.Владелец;
		КонецЕсли;
		Если (ПустоеЗначение(Содержание) = 1) и (ПустоеЗначение(СчетКонтрагента.Назначение)=0) Тогда 
			Содержание=СчетКонтрагента.Назначение;
		КонецЕсли;               
	КонецЕсли;
КонецПроцедуры //ПриВыбореСчетаКонтрагента
//_____________________________________________________________________________
Процедура ПриВыбореКонтрагента()
	Если СчетКонтрагента.Владелец <> Контрагент Тогда
		СчетКонтрагента = "";
	КонецЕсли;
	Если ПустоеЗначение(СчетКонтрагента) = 1 Тогда
	    СчетКонтрагента = глРасчетныйСчетКонтрагента(Контрагент);
		ПриВыбореСчетаКонтрагента();
	КонецЕсли;
КонецПроцедуры //ПриВыбореКонтрагента
//_____________________________________________________________________________
Процедура ПриУстановкеАкцепта()
    Форма.СА.Доступность(Акцепт);
    Форма.СрокАкцепта.Доступность(Акцепт);
	Форма.УО.Доступность(?(Акцепт=1,0,1));
	Форма.УсловиеОплаты.Доступность(?(Акцепт=1,0,1));
	УсловиеОплаты = ?(Акцепт=1,"",УсловиеОплаты);
	СрокАкцепта   = ?(Акцепт=0, 0,СрокАкцепта);
КонецПроцедуры
//_____________________________________________________________________________
Процедура РасчетНДС()
	Если СтавкаНДС.Выбран() = 1 Тогда
	    СтНДС = СтавкаНДС.Ставка;
	Иначе
		СтНДС = 0;
	КонецЕсли;
	НДС = Сумма*СтНДС/(100+СтНДС);
КонецПроцедуры
//-----------------------------------------------
Процедура Печать()
  	Таб = СоздатьОбъект("Таблица");
  	
  	ИмяФайлаПечатнойФормы = КаталогИБ()+"ExtForms\PrnForms\1cbtr03.mxl";
	Если ФС.СуществуетФайл(ИмяФайлаПечатнойФормы) = 1 Тогда
		Таб.ИсходнаяТаблица(ИмяФайлаПечатнойФормы);
	Иначе
  		Таб.ИсходнаяТаблица("Таблица");
   	КонецЕсли;
	
	Если (Прав(Строка(НомерДок),3) = "000") Тогда
		Если Вопрос("Номер платежного требования не может оканчиваться на ""000"".
					|Вернуться к редактированию документа?", "Да+Нет") = "Да" Тогда
	    	Активизировать("НомерДок");
			Возврат;
	    КонецЕсли;
	КонецЕсли;
	
	Таб.Вывести();
	
	Заголовок = "ПЛАТЕЖНОЕ ТРЕБОВАНИЕ";
	
	Таб.Область("НаименованиеНомер").Текст = Заголовок+" № "+глПреобразоватьНомерДок(НомерДок, 0, 0);
	Таб.Область("ДатаДокумента").Текст = глВыводДатыПлатежныхДокументов(ДатаДок,РасчетныйСчет);	// Месяц даты числом или прописью
	Таб.Область("ОчередностьПлатежа").Текст = Очередность;
	Таб.Область("УсловиеОплаты").Текст = ?(Акцепт=1,"С акцептом","Без акцепта")+РазделительСтрок+СокрЛП(УсловиеОплаты);
	Таб.Область("СрокДляАкцепта").Текст = ?((Акцепт=1) И (СрокАкцепта>0), СрокАкцепта," ");
	Таб.Область("ВидПлатежа").Текст = ВидПлатежа;
	
	Таб.Область("СуммаЧислом").Текст = глВыводСуммыПлатежныхДокументов(Сумма,РасчетныйСчет);	// Сумма без 00 копеек (=) или (-00)
	Таб.Область("СуммаПрописью").Текст = глВыводСуммыПлатежныхДокументовПрописью(Сумма,РасчетныйСчет);	// Сумма без 00 копеек (=) или (-00)
	Таб.Область("ОчередностьПлатежа").Текст = Очередность;
	Таб.Область("ДатаОтсылки").Текст = ?(ПустоеЗначение(ДатаОтсылки)=1,"",глВыводДатыПлатежныхДокументов(ДатаОтсылки,РасчетныйСчет));
	
	Плательщик = "";БанкПлательщика = "";НомерСчетаПлательщика = "";БИКБанкаПлательщика = "";СчетБанкаПлательщика = "";ИННплательщика="";КППплательщика="";
	глПлатежныеРеквизиты(СчетКонтрагента, Плательщик, БанкПлательщика, НомерСчетаПлательщика, БИКБанкаПлательщика, СчетБанкаПлательщика, ИННплательщика, КППплательщика, 2);
	Таб.Область("Плательщик").Текст = Плательщик;
	Таб.Область("БанкПлательщика").Текст = БанкПлательщика;
	Таб.Область("НомерСчетаПлательщика").Текст = НомерСчетаПлательщика;
	Таб.Область("БИКБанкаПлательщика").Текст = БИКБанкаПлательщика;
	Таб.Область("СчетБанкаПлательщика").Текст = СчетБанкаПлательщика; 
	
		Таб.Область("ИННПлательщика").Текст = "ИНН " + ИННплательщика;
	
	Получатель = "";БанкПолучателя = "";НомерСчетаПолучателя = "";БИКБанкаПолучателя = "";СчетБанкаПолучателя = "";ИННполучателя="";КППполучателя="";
	глПлатежныеРеквизиты(РасчетныйСчет, Получатель, БанкПолучателя, НомерСчетаПолучателя, БИКБанкаПолучателя, СчетБанкаПолучателя, ИННполучателя, КППполучателя, 2);
	Таб.Область("Получатель").Текст = Получатель;
	Таб.Область("БанкПолучателя").Текст = БанкПолучателя;
	Таб.Область("НомерСчетаПолучателя").Текст = НомерСчетаПолучателя;
	Таб.Область("БИКБанкаПолучателя").Текст = БИКБанкаПолучателя;
	Таб.Область("СчетБанкаПолучателя").Текст = СчетБанкаПолучателя;
    
		Таб.Область("ИННПолучателя").Текст = "ИНН " + ИННполучателя; 
	
  	СтрСуммаНДС = ФормирСуммаНДС(АвтоПодстСуммыНДС,Сумма,НДС,СтавкаНДС);
	Таб.Область("НазначениеПлатежа").Текст = СокрЛП(Содержание)+" "+СокрЛП(СтрСуммаНДС);
	
  	Таб.Опции(0,0,0,0,"ОпцииПечатиПлТр","ПараметрыОкнаПлТр");
	Таб.ТолькоПросмотр(1);
	Таб.ПараметрыСтраницы(,,,,0,,0,,,);
	Таб.Показать("Печать платежного требования","");
КонецПроцедуры

//******************************************************************************
// ПоКнопкеПечать()
// 
// Вызывается из формул элементов диалога:
//  Кнопка "кнПечать".
//
// Описание:
//  Определяется соответствующая печатная форма.
// 	
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		Печать();
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(Контекст, "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Вызывается из формул элементов диалога:
//  Кнопка "кнВыбПечать".
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Документ." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()

//_____________________________________________________________________________
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	Если Копирование = 1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;

	РасчетныйСчет = глЗначениеПоУмолчанию("ОсновнойБанковскийСчет");
	СтавкаНДС = глЗначениеПоУмолчанию("ОсновнаяСтавкаНДС");
	ВидПлатежа = "";
	Очередность = 6;
	АвтоПодстСуммыНДС = Перечисление.ПодстановкаСуммыНДС.СуммаНДС;
	Акцепт=1;
КонецПроцедуры
//_____________________________________________________________________________
Процедура ПриОткрытии() //предопределенная
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.кнВыбораРасчетногоСчета.Доступность(0);
		Форма.кнОК.Доступность(0);
		Форма.КнопкаЗаписать.Доступность(0); 
		
		Форма.кнОчиститьСодержание.Доступность(0);
		Форма.кнОчиститьУсловие.Доступность(0);
		Форма.кнОчиститьВидПлатежа.Доступность(0);
		Форма.кнОчиститьСрокПлатежа.Доступность(0);
		Форма.кнОчиститьДатуОтсылки.Доступность(0);
		Форма.КнопкаПоУмолчанию("кнЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;

	Форма.СА.Доступность(Акцепт);
    Форма.СрокАкцепта.Доступность(Акцепт);
	Форма.УО.Доступность(?(Акцепт=1,0,1));
	Форма.УсловиеОплаты.Доступность(?(Акцепт=1,0,1));
	НачальнаяДатаДокумента = ДатаДок;
	
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Документ." + Вид(),ТаблицаПечФорм);
	
КонецПроцедуры //ПриОткрытии
//_____________________________________________________________________________
Процедура ПриЗаписи() //предопределенная
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	ИначеЕсли (ДатаДок >= '15.02.2000') и (Прав(Строка(НомерДок),3) = "000") Тогда
		Если Вопрос("Номер платежного требования не может оканчиваться на ""000"".
					|Вернуться к редактированию документа?", "Да+Нет") = "Да" Тогда
	    	Активизировать("НомерДок");
			СтатусВозврата(0);
			Возврат;
	    КонецЕсли;
	КонецЕсли;

	Если Константа.ВсеДокументыВЖурналОпераций = Да Тогда
	    СуществуетОперация(1);
		Операция.СуммаОперации = Сумма;
		Операция.Содержание = "Платежное требование";
	Иначе
	    СуществуетОперация(0);
	КонецЕсли;
КонецПроцедуры //ПриЗаписи

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//*****************************************************************************
Новый = 0;

ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");
	
// добавим информацию о встроенной форме
ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Платежное требование";
ТаблицаПечФорм.Кнопка       = "Печать"; 

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Ввести на основании");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");