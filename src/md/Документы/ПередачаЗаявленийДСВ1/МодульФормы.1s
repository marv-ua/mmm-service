Перем констРазмерПачки;
Перем СписокНередактируемыхКонтролов;

//******************************************************************************
//	ПриВыбореСотрудника()
//
//	Параметры:
// 
//	Описание:
//		Заполняет поля документа данными из выбранного сотрудника.
//
Процедура ПриВыбореСотрудника()  
	
	Если ПустоеЗначение(СтрЗаменить(Сотрудник.СтраховойНомерПФР, "-", "")) = 0 Тогда 
		СтраховойНомерПФР = Сотрудник.СтраховойНомерПФР;
	КонецЕсли; 
	
	ФИО = глРазложить(СОКРЛП(Сотрудник.Наименование)," ");
	
	Если ФИО.РазмерСписка() > 0 Тогда
		Фамилия = ФИО.ПолучитьЗначение(1);
		Если ФИО.РазмерСписка() > 1 Тогда
			Имя		= ФИО.ПолучитьЗначение(2);
			Если ФИО.РазмерСписка() > 2 Тогда 
				// а вот отчество бывает и не из одного слова				
				Для Сч = 3 По ФИО.РазмерСписка() Цикл
					Отчество1 = Отчество1 + ФИО.ПолучитьЗначение(Сч) + " ";
				КонецЦикла;
				Если ПустоеЗНачение(Отчество1) = 0 Тогда
					Отчество1 = Лев(Отчество1,СтрДлина(Отчество1)-1);
				КонецЕсли;
				Отчество = Отчество1;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
		
	ПропискаАдрес = Сотрудник.АдресПрописка;
	ФактическийАдрес = Сотрудник.АдресФакт;
	Если (ПустоеЗначение(СтрЗаменить(ФактическийАдрес,",",""))=0) и (ФактическийАдрес<>ПропискаАдрес) Тогда
		АдресДляИнформирования = ФактическийАдрес;
	ИначеЕсли ПустоеЗначение(СтрЗаменить(ПропискаАдрес,",",""))=0 Тогда 
		АдресДляИнформирования = ПропискаАдрес;
	КонецЕсли;
	
КонецПроцедуры //ПриВыбореСотрудника
 
 
 //******************************************************************************
// НайтиПервыйСуществующийФайл(СписокВозможныхМест)
//
// Параметры: 
//  СписокВозможныхМест
//
// Возвращаемое значение:
//
// Описание:
//
Функция НайтиПервыйСуществующийФайл(СписокВозможныхМест)
	
	Для Сч = 1 По СписокВозможныхМест.РазмерСписка() Цикл
		ПолноеИмя	=	СписокВозможныхМест.ПолучитьЗначение(Сч);
		Если Фс.СуществуетФайл(ПолноеИмя) = 1 Тогда	Возврат(ПолноеИмя)	КонецЕсли;
	КонецЦикла;

	Возврат("");

КонецФункции // НайтиПервыйСуществующийФайл()

//******************************************************************************
//СформироватьФайл()
//	ВФайлXML()
//
//	Параметры: нет
// 
//	Описание:
//		Формирование файла передачи в ПФР.
//
Процедура СформироватьФайл() 
	
	Если (Проведен() = 0) или (Модифицированность() = 1) Тогда
		Предупреждение("Перед формированием файла документ следует провести!");
		Возврат;
	КонецЕсли;
	
	Ктлг=СокрЛП(Каталог);
	Если Прав(Ктлг,1)="\" Тогда
		ПутьВывода = Лев(Ктлг,СтрДлина(Ктлг)-1);
	Иначе
		ПутьВывода = Ктлг;
	КонецЕсли;
	
	// Проверим, существует ли "Каталог".
	Если (Лев(ПутьВывода, 2) <> "\\") и ((Лев(ПутьВывода,2)="a:")или (Лев(ПутьВывода,2)="A:")) тогда
		Если ФС.СуществуетФайл(ПутьВывода+"\NUL")=0 Тогда
			Сообщить("Не возможен вывод файла данных для передачи в ПФР в каталог "+ПутьВывода+"!");
			Возврат;                     
		КонецЕсли;
	КонецЕсли;  
	
	// проверку правильности заполнения не проводим
	// все проверки при проведении документа

	// Получим ИНН.
	ИНН=СокрЛП(Константа.ИННОрганизации);
	ОдинадцатыйСимвол=Сред(ИНН,11,1);
	Если (Строка(Число(ОдинадцатыйСимвол))=ОдинадцатыйСимвол) и (ПустаяСтрока(ОдинадцатыйСимвол) = 0) Тогда 
		// оказалось физическое лицо
		ИНН=Лев(ИНН,12);
		КПП = "0";
	Иначе  // оказалась организация
		ИНН = Лев(Константа.ИННОрганизации,10);
		КПП = Прав(Константа.ИННОрганизации,9);
	КонецЕсли;
	Если ПустоеЗначение(КПП)=1 Тогда
		КПП = "0"
	КонецЕсли;
	
	Наименование = Константа.НазваниеОрганизации;         
	НаименованиеПолное = Константа.ОфициальноеНазваниеОрганизации;
	РегНомерПФР = СокрЛП(глПолучитьНалог("ПФР_страх").РегНомер);
	
	Наименование = ВРег(глЗаменитьСимволВСтроке(СокрЛП(Лев(СтрЗаменить(Наименование,"""","'"),100))));
	НаименованиеПолное = ВРег(глЗаменитьСимволВСтроке(СокрЛП(Лев(СтрЗаменить(НаименованиеПолное,"""","'"),100))));

	НомПачкаС = глДополнитьСтроку(СокрЛП(НомерДок),5,"0");                                                       
	
	
	//Загрузим внешнюю компоненту V7Plus.dll
	СписокВозможныхМест	=	СоздатьОбъект("СписокЗначений");
	СписокВозможныхМест.ДобавитьЗначение(КаталогИБ()		+	"v7plus.dll"	);
	СписокВозможныхМест.ДобавитьЗначение(КаталогПрограммы()	+	"v7plus.dll"	);
	СписокВозможныхМест.ДобавитьЗначение(КаталогИБ()		+	"ExtForms\"	+	"v7plus.dll"	);
	
	гПутьОбъекта_КомпонентаV7plus	=	НайтиПервыйСуществующийФайл(СписокВозможныхМест);
	
	Если ЗагрузитьВнешнююКомпоненту(гПутьОбъекта_КомпонентаV7plus) <> 1	Тогда
		Предупреждение("Компонента v7plus.dll не найдена! Файл выгрузки не сформирован");
		Возврат;
	КонецЕсли;
	
	XMLАнализатор	=	СоздатьОбъект("AddIn.XMLParser");
	ХМЛФайлДанных	=	XMLАнализатор.СоздатьДокумент();	         

	хмлКорень 		= ХМЛФайлДанных.СоздатьПодчиненныйЭлемент("ФайлПФР");
	
	хмлИмяФайла		= хмлКорень.СоздатьПодчиненныйЭлемент("ИмяФайла");
	
	ИмяФайла = "PFR-700-Y-" + ДатаГод(ДатаДок) + "-ORG-"+ Прав(РегНомерПФР,15) + "-DCK-"+Лев(НомПачкаС,5)+"-DPT-000000-DCK-00000.XML"; 
	хмлИмяФайла.Значение	= ИмяФайла;
	
	хмлЗаголовокФайла	 		= хмлКорень.СоздатьПодчиненныйЭлемент("ЗаголовокФайла");
	хмлВерсияФормата			= хмлЗаголовокФайла.СоздатьПодчиненныйЭлемент("ВерсияФормата");
	хмлВерсияФормата.Значение	= "07.00";
	
	хмлТипФайла				= хмлЗаголовокФайла.СоздатьПодчиненныйЭлемент("ТипФайла");
	хмлТипФайла.Значение	= "ВНЕШНИЙ";
	
	хмлПрограммаПодготовкиДанных	= хмлЗаголовокФайла.СоздатьПодчиненныйЭлемент("ПрограммаПодготовкиДанных");
	хмлНазваниеПрограммы			= хмлПрограммаПодготовкиДанных.СоздатьПодчиненныйЭлемент("НазваниеПрограммы");
	хмлНазваниеПрограммы.Значение	= "1С:ЗАРПЛАТА И КАДРЫ";
		
	хмлВерсия						= хмлПрограммаПодготовкиДанных.СоздатьПодчиненныйЭлемент("Версия");
	хмлВерсия.Значение				= Константа.НомерРелиза;
	
	хмлИсточникДанных				= хмлЗаголовокФайла.СоздатьПодчиненныйЭлемент("ИсточникДанных");
	хмлИсточникДанных.Значение		= "СТРАХОВАТЕЛЬ";
	
	хмлПачкаВходящихДокументов	 	= хмлКорень.СоздатьПодчиненныйЭлемент("ПачкаВходящихДокументов");	                          
	хмлПачкаВходящихДокументов.УстановитьАтрибут("Окружение","В составе файла");
	хмлПачкаВходящихДокументов.УстановитьАтрибут("Стадия","До обработки");
	хмлПачкаВходящихДокументов.УстановитьАтрибут("ДобровольныеПравоотношения","ДСВ");	
	
	хмлВходящаяОпись				= хмлПачкаВходящихДокументов.СоздатьПодчиненныйЭлемент("ВХОДЯЩАЯ_ОПИСЬ");		
	

	//ВХОДЯЩАЯ_ОПИСЬ
	хмлНомерВпачке = хмлВходящаяОпись.СоздатьПодчиненныйЭлемент("НомерВпачке");
	хмлНомерВпачке.Значение	= 1;
	
	хмлТипВходящейОписи	=  хмлВходящаяОпись.СоздатьПодчиненныйЭлемент("ТипВходящейОписи");
	хмлТипВходящейОписи.Значение = "ОПИСЬ ПАЧКИ";
	
	хмлСоставительПачки = хмлВходящаяОпись.СоздатьПодчиненныйЭлемент("СоставительПачки");
	
	глхмлСтрахователь(хмлСоставительПачки, ИНН, КПП, РегНомерПФР);
	
	хмлНомерПачки	= хмлВходящаяОпись.СоздатьПодчиненныйЭлемент("НомерПачки");
	хмлОсновной		= хмлНомерПачки.СоздатьПодчиненныйЭлемент("Основной");
	хмлОсновной.Значение = Число(НомПачкаС);
	
	хмлСоставДокументов	= хмлВходящаяОпись.СоздатьПодчиненныйЭлемент("СоставДокументов");
	
	хмлКоличество			= хмлСоставДокументов.СоздатьПодчиненныйЭлемент("Количество");
	хмлКоличество.Значение	= 1;
	
	хмлНаличиеДокументов	= хмлСоставДокументов.СоздатьПодчиненныйЭлемент("НаличиеДокументов");	
	
	хмлТипДокумента				= хмлНаличиеДокументов.СоздатьПодчиненныйЭлемент("ТипДокумента");
	хмлТипДокумента.Значение	= "ЗАЯВЛЕНИЕ_О_ДОБРОВОЛЬНОМ_ВСТУПЛЕНИИ_В_ПРАВООТНОШЕНИЯ_В_ЦЕЛЯХ_УПЛАТЫ_ДСВ";
	
	хмлКоличество			= хмлНаличиеДокументов.СоздатьПодчиненныйЭлемент("Количество");
	хмлКоличество.Значение	= КоличествоСтрок();		
        
	хмлДатаСоставления	=  хмлВходящаяОпись.СоздатьПодчиненныйЭлемент("ДатаСоставления");			
	хмлДатаСоставления.Значение = Формат(ДатаДок,"ДДДММГГГГ");			
                         

	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл  
		
		ТипАдреса = "РОССИЙСКИЙ";
		АдресСписком = "";
		Если глАдресСоответствуетТребованиямМНС(АдресДляИнформирования) = 0 Тогда
			ТипАдреса = "НЕСТРУКТУРИРОВАННЫЙ"
		Иначе
			АдресСписком = глРазложить(СокрЛП(АдресДляИнформирования));
		КонецЕсли;        
		
		// Реквизиты не проверяются на пустое значение, так как эта проверка есть при проведении.
		// Заполним общие реквизиты используемые в обоих документах.
		НомерДокументаВПачке = НомерСтроки;
		
		ФамилияВФайл  = ВРег(глЗаменитьСимволВСтроке(Фамилия));
		ИмяВФайл	  = ВРег(глЗаменитьСимволВСтроке(Имя));
		ОтчествоВФайл = ВРег(глЗаменитьСимволВСтроке(Отчество));
		
		ЗАЯВЛЕНИЕ = хмлПачкаВходящихДокументов.СоздатьПодчиненныйЭлемент("ЗАЯВЛЕНИЕ_О_ДОБРОВОЛЬНОМ_ВСТУПЛЕНИИ_В_ПРАВООТНОШЕНИЯ_В_ЦЕЛЯХ_УПЛАТЫ_ДСВ");	
		
		xmlНомерВпачке				= ЗАЯВЛЕНИЕ.СоздатьПодчиненныйЭлемент("НомерВпачке");
		xmlНомерВпачке.Значение		= НомерДокументаВПачке +1 ;	
		
		xmlНаименованиеТерриториальногоОрганаПФР = ЗАЯВЛЕНИЕ.СоздатьПодчиненныйЭлемент("НаименованиеТерриториальногоОрганаПФР");
		xmlНаименованиеТерриториальногоОрганаПФР.Значение = ВРег(глЗаменитьСимволВСтроке(СОКРЛП(НаименованиеПФР))); 
		
		
		xmlФИО = ЗАЯВЛЕНИЕ.СоздатьПодчиненныйЭлемент("ФИО");
		
		xmlФамилия	         = xmlФИО.СоздатьПодчиненныйЭлемент("Фамилия");
		xmlФамилия.Значение	 = ФамилияВФайл;  
		
		xmlИмя  	         = xmlФИО.СоздатьПодчиненныйЭлемент("Имя");
		xmlИмя.Значение		 = ИмяВФайл;
		
		xmlОтчество	         = xmlФИО.СоздатьПодчиненныйЭлемент("Отчество");
		xmlОтчество.Значение = ОтчествоВФайл;
		
		
		xmlСтраховойНомер			= ЗАЯВЛЕНИЕ.СоздатьПодчиненныйЭлемент("СтраховойНомер");
		xmlСтраховойНомер.Значение	= СтраховойНомерПФР; 
		
		
		xmlАдресМестаЖительства	= ЗАЯВЛЕНИЕ.СоздатьПодчиненныйЭлемент("АдресМестаЖительства");
		
		хмлТипАдреса			= xmlАдресМестаЖительства.СоздатьПодчиненныйЭлемент("ТипАдреса");
		хмлТипАдреса.Значение	= ТипАдреса;
		
		Если ТипАдреса = "НЕСТРУКТУРИРОВАННЫЙ" Тогда 
			
			хмлНеструктурированныйАдрес	= xmlАдресМестаЖительства.СоздатьПодчиненныйЭлемент("НеструктурированныйАдрес");
			хмлАдрес					= хмлНеструктурированныйАдрес.СоздатьПодчиненныйЭлемент("Адрес");
			хмлАдрес.Значение			= Врег(глЗаменитьСимволВСтроке(СОКРЛП(АдресДляИнформирования)));
			
		Иначе //российский адрес
			
			
			хмлИндекс = xmlАдресМестаЖительства.СоздатьПодчиненныйЭлемент("Индекс");						
			хмлИндекс.Значение	= АдресСписком.ПолучитьЗначение(2);
			
			хмлРоссийскийАдрес	= xmlАдресМестаЖительства.СоздатьПодчиненныйЭлемент("РоссийскийАдрес"); 						
			
			глxmlРоссийскийАдрес(хмлРоссийскийАдрес, АдресСписком);
			
		КонецЕсли;
		
		
		хмлДатаЗаполнения			= ЗАЯВЛЕНИЕ.СоздатьПодчиненныйЭлемент("ДатаЗаполнения");
		хмлДатаЗаполнения.Значение	= Формат(ДатаЗаполнения,"ДДДММГГГГ");
		
			
	КонецЦикла;
	
	глЗаписатьXMLФайлПФР(Ктлг + "\" + ИмяФайла,ХМЛФайлДанных);
	Если ФС.СуществуетФайл(Ктлг + "\" + ИмяФайла)=1 Тогда

		Предупреждение("Сформирован файл: " + ИмяФайла);
		
	КонецЕсли;

	
КонецПроцедуры // СформироватьФайл() 

//******************************************************************************
//	УстановитьДоступностьЭлементовФормыДляПроведенногоДокумента()
//
//	Параметры: нет
// 
//	Описание:
//		Устанавливает доступность для реквизитов документа.
//
Процедура УстановитьДоступностьЭлементовФормыДляПроведенногоДокумента()
	Для Счетчик = 1 По СписокНередактируемыхКонтролов.РазмерСписка() Цикл
		Форма.ПолучитьАтрибут(СписокНередактируемыхКонтролов.ПолучитьЗначение(Счетчик)).Доступность(0);
	КонецЦикла;
	Форма.кнВФайл.Доступность(1);	
КонецПроцедуры // УстановитьДоступностьЭлементовФормыДляПроведенногоДокумента   

//******************************************************************************
// ВводНового()
//
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	НаименованиеПФР = Константа.НаименованиеТерриториальногоОрганаПФР;
	
КонецПроцедуры //ВводНового

//******************************************************************************

Процедура ПриОткрытии() 
	
	
	// восстановим значение "Каталог".
	Каталог = ВосстановитьЗначение("ПутьДляСохранения");
	Если ПустоеЗначение(Каталог)=1 Тогда
		Каталог = КаталогПользователя();
	КонецЕсли;
	
	СтрокаНередактируемыхКонтролов = "НомерДок,ДатаДок,Сотрудник,Комментарий,НаименованиеПФР,"
	+"СтраховойНомерПФР,Фамилия,Имя,Отчество,ДатаЗаполнения";
	СписокНередактируемыхКонтролов = глРазложить(СтрокаНередактируемыхКонтролов);
	
	Если Проведен() = 1 Тогда
		УстановитьДоступностьЭлементовФормыДляПроведенногоДокумента();
	КонецЕсли;
	
	
КонецПроцедуры   

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	СохранитьЗначение("ПутьДляСохранения", Каталог);
	
КонецПроцедуры 

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриВводеСтроки()  
	
	Если Проведен() = 1 Тогда
		Сообщить("Редактирование проведенного документа запрещено!",10);
		СтатусВозврата(0); 
		Возврат;
	КонецЕсли; 
	
	// Размер пачки не может быть больше "констРазмерПачки".
	Если КоличествоСтрок() >= констРазмерПачки Тогда
		Предупреждение("В пачке уже содержится "+констРазмерПачки+" документов. "+
		РазделительСтрок+"Ввод нового документа в пачку невозможен!");
		СтатусВозврата(0); Возврат;
	КонецЕсли;
	
КонецПроцедуры	// ПриВводеСтроки   

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриУдаленииСтроки()
	Если Проведен() = 1 Тогда
		СтатусВозврата(0); Возврат;
	КонецЕсли;
КонецПроцедуры // ПриУдаленииСтроки

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриИзмененииПорядкаСтрок()
	Если Проведен() = 1 Тогда
		СтатусВозврата(0); Возврат;
	КонецЕсли;
КонецПроцедуры // ПриИзмененииПорядкаСтрок

//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаВыбораЗначения(ВыбЗнач, ИдентЭлемДиалога, ФлагСтандОбр)
	// внутренних совместителей включать в документ не будем и
	Если ИдентЭлемДиалога = "Сотрудник" Тогда
		// сотрудника два раза вводить нельзя.
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Если Сотрудник = ВыбЗнач Тогда
				Предупреждение("Данный сотрудник уже введен в документ.");
				ФлагСтандОбр = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры	// ОбработкаВыбораЗначения

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеРедактированияСтроки()
	// Вызов редактирования адреса через нажатие на текстовое поле.
	Если Проведен() = 0 Тогда
		Если Форма.АктивныйЭлемент() = "ПредставлениеАдреса" Тогда
			глВводАдреса(АдресДляИнформирования);
			Если СокрЛП(АдресДляИнформирования) = ",,,,,,,,," Тогда
				АдресДляИнформирования = "";
			КонецЕсли;
		КонецЕсли;	
	Иначе
		Сообщить("Редактирование проведенного документа запрещено!",10);
		СтатусВозврата(0);
	КонецЕсли;
КонецПроцедуры // ПриНачалеРедактированияСтроки

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, ФлагСтандОбр) 
	
	Если ИдентЭлемДиалога = "Каталог" Тогда  
		
		ФлагСтандОбр = 0;   
		Если ФС.СуществуетФайл(Каталог+"\NUL")=1 Тогда
			Кат = Каталог;
		Иначе
			Кат = "C:\";
		КонецЕсли;
		Если ФС.ВыбратьКаталог(Кат,"Выбор каталога для вывода файла данных")=1 Тогда
			Каталог = Кат;
		КонецЕсли;
   
	КонецЕсли;   
	
КонецПроцедуры // ПриНачалеВыбораЗначения()    

констРазмерПачки = 200;