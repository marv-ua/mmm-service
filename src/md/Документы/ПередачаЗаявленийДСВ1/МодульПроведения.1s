

//******************************************************************************
// СтраховойНомерПравильный(СтраховойНомер)
//
// Параметры:
//	СтраховойНомер - страховой номер ПФР для проверки
//
// Возвращаемое значение:
//		число 1, если номер введен верно, 0 - если номер неправильный
//
//	Описание:
//		вычисляет контрольное число и сравнивает его.
//
Функция СтраховойНомерПравильный(СтраховойНомер)
	КонтрольноеЧислоВСтраховомНомере = Число(Прав(СтраховойНомер,2));
	СтрНомерПФР = СтрЗаменить(Лев(СтраховойНомер,11),"-","");
	Если Число(СтрНомерПФР) > 001001998 Тогда 
		// Сумма произведений цифры номера на номер позиции. (позиции отсчитываются с конца).
		ВычисленноеКонтрольноеЧисло = Число(Сред(СтрНомерПФР,1,1))*9
		+Число(Сред(СтрНомерПФР,2,1))*8
		+Число(Сред(СтрНомерПФР,3,1))*7
		+Число(Сред(СтрНомерПФР,4,1))*6
		+Число(Сред(СтрНомерПФР,5,1))*5
		+Число(Сред(СтрНомерПФР,6,1))*4
		+Число(Сред(СтрНомерПФР,7,1))*3
		+Число(Сред(СтрНомерПФР,8,1))*2
		+Число(Сред(СтрНомерПФР,9,1))*1;
		// Получим остаток от деления на 101									 
		ВычисленноеКонтрольноеЧисло = ВычисленноеКонтрольноеЧисло - Цел(ВычисленноеКонтрольноеЧисло/101)*101;
		// и возьмем последние 2 цифры.
		ВычисленноеКонтрольноеЧисло = Число(Прав(ВычисленноеКонтрольноеЧисло,2));
		Если ВычисленноеКонтрольноеЧисло = КонтрольноеЧислоВСтраховомНомере Тогда
			Возврат 1;
		Иначе
			Возврат 0;
		КонецЕсли;
	Иначе         
		Возврат 1;
	КонецЕсли; 
КонецФункции // СтраховойНомерПравильный	

//******************************************************************************
// Проверка()
//
// Возвращаемое значение:
//		число 1, если провести документ можно, 0 - если введены неверные данные
//
//	Описание:
//		Проверяет возможность проведения документа. Данные проверяются на правильность заполнения.
//
Функция Проверка() 
	
	МожноПроводить = 1;
	ТекстСообщения = "";
	Если КоличествоСтрок() = 0 Тогда
		ТекстСообщения = "Список сотрудников пуст.";
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 1);
		МожноПроводить = 0;
	Иначе
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Если ПустоеЗначение(Сотрудник)=1 Тогда
				ТекстСообщения = "Не выбран сотрудник в строке "+НомерСтроки + ".";
				глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 1);
				МожноПроводить = 0;
			КонецЕсли;  
			
			// Проверим страховой номер на полноту.
			Если ПустоеЗначение(СтрЗаменить(СтраховойНомерПФР,"-","")) = 0 Тогда
				// страховой номер может быть не указан
				// проверку делаем если там что-то есть
				Если СтрДлина(СтрЗаменить(СтрЗаменить(СтраховойНомерПФР,"-","")," ",""))<>11 Тогда
					ТекстСообщения = "В строке №"+НомерСтроки+" страховой номер ПФР заполнен не полностью.";
					глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 1);
					МожноПроводить = 0;
				Иначе
					//Проверка контрольного числа Страхового номера
					Если СтраховойНомерПравильный(СтраховойНомерПФР)=0 Тогда
						ТекстСообщения = "В строке №"+НомерСтроки+" скорее всего страховой номер введен неправильно.";
						глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 1);
						МожноПроводить = 0;
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли;
			
			// Проверим Ф.И.О 
			Если ПустоеЗначение(Фамилия)=1 Тогда
				ТекстСообщения = "В строке №"+НомерСтроки+" не заполнена фамилия сотрудника.";
				глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 1);
				МожноПроводить = 0;
			КонецЕсли;
			Если ПустоеЗначение(Имя)=1 Тогда
				ТекстСообщения = "В строке №"+НомерСтроки+" не заполнено имя сотрудника.";
				глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 1);
				МожноПроводить = 0;
			КонецЕсли;
			Если ПустоеЗначение(Отчество)=1 Тогда
				ТекстСообщения = "В строке №"+НомерСтроки+" не заполнено отчество сотрудника.";
				глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 1);
				МожноПроводить = 0;
			КонецЕсли;                 
			
			Если глСтрокаНаписанаПоРусски(Фамилия+Имя+Отчество)=0 Тогда
				ТекстСообщения = "В строке №"+НомерСтроки+" ФИО сотрудника должны быть написаны русскими буквами.";
				глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 1);
				МожноПроводить = 0;
			КонецЕсли;    
			
			
			// Проверим адрес для информирования, он должен быть заполнен   как неструктурированный адрес
			Если ПустоеЗначение(СтрЗаменить(АдресДляИнформирования,",",""))=1 Тогда
				ТекстСообщения = "В строке №"+НомерСтроки+" не заполнен адрес для информирования.";
				глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 1);
				МожноПроводить = 0;  
			ИначеЕсли глАдресСоответствуетТребованиямМНС(АдресДляИнформирования) = 0 Тогда
				ТекстСообщения = "В строке №"+НомерСтроки+" адрес для информирования будет передаваться как неструктурированный адрес.";
				глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), -1);
			КонецЕсли;
		  
		КонецЦикла; 
	КонецЕсли;
	Возврат МожноПроводить;
КонецФункции  // Проверка

//******************************************************************************
//
Процедура ОбработкаПроведения()
	// если документ содержит ошибки, то не проводим
	Если Проверка() = 0 Тогда
		глСообщениеПроведения("Документ не проведен!", ТекущийДокумент(), 1);
		СтатусВозврата(0);
	Иначе
		ТекстСообщения = "Документ проведен.";
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	КонецЕсли;

глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения


