Перем РежимПроведения;
//******************************************************************************
// УстановитьЗначениеРеквизитовДляНМА()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура УстановитьЗначениеРеквизитовДляНМА(СпрНМА)
	
	Если РежимПроведения <> 1 Тогда
		СпрНМА.ПервоначальнаяСтоимость 		= БалансоваяСтоимость;	
		СпрНМА.ДатаПринятияКУчету 			= ДатаДок;
		СпрНМА.ПорядокНачисленияАмортизации = ПорядокНачисленияАмортизации;
		СпрНМА.ОбщийОбъемПродукцииРабот 	= ОбщийОбъемПродукцииРабот;
		СпрНМА.Коэффициент 	= Коэффициент;
		
		Если ПустоеЗначение(Группа) = 0 Тогда
			СпрНМА.Группа = Группа;    
		КонецЕсли;
		
		Если ПустоеЗначение(СпособНачисленияАмортизации) = 0 Тогда
			СпрНМА.СпособНачисленияАмортизации = СпособНачисленияАмортизации;
		КонецЕсли;
		
		Если ПустоеЗначение(МетодНачисленияАмортизации) = 0 Тогда
			СпрНМА.МетодНачисленияАмортизации = МетодНачисленияАмортизации;
		КонецЕсли;
		
		Если ПустоеЗначение(АмортизационнаяГруппа) = 0 Тогда
			СпрНМА.АмортизационнаяГруппа = АмортизационнаяГруппа;	
		КонецЕсли;
				    
		Если ПустоеЗначение(ПервоначальнаяСтоимостьН) = 0 Тогда
			СпрНМА.ПервоначальнаяСтоимостьН = ПервоначальнаяСтоимостьН;
		КонецЕсли;
		
		Если ПустоеЗначение(СрокПолезногоИспользования) = 0 Тогда
			СпрНМА.СрокПолезногоИспользования = СрокПолезногоИспользования;
		КонецЕсли;
		
		Если ПустоеЗначение(СрокПолезногоИспользованияН) = 0 Тогда
			СпрНМА.СрокПолезногоИспользованияН = СрокПолезногоИспользованияН;
		КонецЕсли;
					
		Если ПустоеЗначение(СпрНМА.ПрочиеСведения) = 1 Тогда
			СпрНМА.ПрочиеСведения			= ПрочиеСведения;
		КонецЕсли;
		СпрНМА.Записать();
	КонецЕсли;

	УстановитьРеквизитСправочника(НМА, "МОЛ", МОЛ, ДатаДок);
	УстановитьРеквизитСправочника(НМА, "СчетЗатрат", СчетЗатрат, ДатаДок);
	УстановитьРеквизитСправочника(НМА, "Субконто1", Субконто1, ДатаДок, СчетЗатрат.ВидСубконто(1));
	УстановитьРеквизитСправочника(НМА, "Субконто2", Субконто2, ДатаДок, СчетЗатрат.ВидСубконто(2));
	УстановитьРеквизитСправочника(НМА, "Субконто3", Субконто3, ДатаДок, СчетЗатрат.ВидСубконто(3));
	УстановитьРеквизитСправочника(НМА, "Состояние", Перечисление.СостоянияНМА.ПринятКУчету, ДатаДок);
	
	Если ВключитьВСоставРасходов = 1 Тогда    
		УстановитьРеквизитСправочника(НМА, "СпециальныйКоэффициент", СпециальныйКоэффициент, ДатаДок);
		УстановитьРеквизитСправочника(НМА, "ВидРасхода", ВидРасхода, ДатаДок);        
		УстановитьРеквизитСправочника(НМА, "Объект", Объект, ДатаДок);
		ИмяТипа = "";
		Если ПустоеЗначение(ЭлементРасхода) = 0 Тогда
			ИмяТипа = "Перечисление."+ ЭлементРасхода.Вид();
		КонецЕсли;
		УстановитьРеквизитСправочника(НМА, "ЭлементРасхода", ЭлементРасхода, ДатаДок, ИмяТипа);    
	КонецЕсли;
	
КонецПроцедуры // УстановитьЗначениеРеквизитовДляНМА()

//******************************************************************************
// УстановитьЗначениеРеквизитовДляНИОКР()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура УстановитьЗначениеРеквизитовДляНИОКР(СпрНМА)
	
	Если РежимПроведения <> 1 Тогда
		СпрНМА.ПервоначальнаяСтоимость 		= БалансоваяСтоимость;	
		СпрНМА.ДатаПринятияКУчету 			= ДатаДок;
		СпрНМА.ПорядокНачисленияАмортизации = ПорядокНачисленияАмортизации;
		СпрНМА.ОбщийОбъемПродукцииРабот 	= ОбщийОбъемПродукцииРабот;
		
		Если ПустоеЗначение(СпособНачисленияАмортизации) = 0 Тогда
			СпрНМА.СпособНачисленияАмортизации = СпособНачисленияАмортизации;
		КонецЕсли;
		
		Если ПустоеЗначение(СрокПолезногоИспользования) = 0 Тогда
			СпрНМА.СрокПолезногоИспользования = СрокПолезногоИспользования;
		КонецЕсли;
		
		Если ПустоеЗначение(СпрНМА.ПрочиеСведения) = 1 Тогда
			СпрНМА.ПрочиеСведения			= ПрочиеСведения;
		КонецЕсли;
		СпрНМА.Записать();
	КонецЕсли;

	УстановитьРеквизитСправочника(НМА, "СчетЗатрат", СчетЗатрат, ДатаДок);
	УстановитьРеквизитСправочника(НМА, "Субконто1", Субконто1, ДатаДок, СчетЗатрат.ВидСубконто(1));
	УстановитьРеквизитСправочника(НМА, "Субконто2", Субконто2, ДатаДок, СчетЗатрат.ВидСубконто(2));
	УстановитьРеквизитСправочника(НМА, "Субконто3", Субконто3, ДатаДок, СчетЗатрат.ВидСубконто(3));
	
КонецПроцедуры // УстановитьЗначениеРеквизитовДляНИОКР()

//******************************************************************************
// ПринятиеКНалоговомуУчетуНМА()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ПринятиеКНалоговомуУчетуНМА()
	
	// Отражение принятия к учету НМА в налоговом учете
	Если ДатаДок >= '01.01.2002' Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НА";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
			Операция.Кредит.Счет = СчетПоКоду("Н01.08");
			
		Иначе
			Операция.Кредит.Счет = СчетПоКоду("Н01.01");
		КонецЕсли;
		Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
		Операция.Сумма = ПервоначальнаяСтоимостьН;
		
		// Примем объеткт к налоговому учету, как амортизируемое имущество.
		Если ВключитьВСоставРасходов = 1 Тогда
			Операция.СодержаниеПроводки = "Принят к учету НМА";
			Операция.Дебет.Счет = СчетПоКоду("Н05.03");
			Операция.Дебет.НематериальныеАктивы = НМА;
			
		// Учтем стоимость в составе расходов.
		ИначеЕсли ВключитьВСоставРасходов = 2 Тогда
			СчетИАналитикаДляОтнесенияРасходов = глПолучитьСчетРасходовДляЦелейНалоговогоУчета(ВидРасхода, ЭлементРасхода, Объект, ДатаДок);
			СчетРасходов = СчетИАналитикаДляОтнесенияРасходов.Получить("Счет");
			Если ПустоеЗначение(СчетРасходов) = 1 Тогда
				ТекстСообщения = "На закладке ""Налоговый учет"" неверно указано направление отнесения расходов для целей налогового учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			СчетРасходов = глПолучитьСчетУчетаКосвенныхРасходовНУ(ДатаДок, СчетЗатрат, Субконто1, СчетРасходов);
			Если ПустоеЗначение(СчетРасходов) = 0 Тогда
				Операция.СодержаниеПроводки = "НМА включен в расходы";
				Операция.Дебет.Счет = СчетРасходов;
				Операция.Дебет.Субконто(1, СчетИАналитикаДляОтнесенияРасходов.Получить("Субконто1"));
				Операция.Дебет.Субконто(2, СчетИАналитикаДляОтнесенияРасходов.Получить("Субконто2"));
				Операция.Дебет.Субконто(3, СчетИАналитикаДляОтнесенияРасходов.Получить("Субконто3"));
			КонецЕсли;
			
		Иначе // не принимается к налоговому учету
			Операция.СодержаниеПроводки = "НМА не принят к налоговому учету";
			Операция.Кредит.УсловияПоступленияИВыбытия = Перечисление.УсловияПоступленияИВыбытия.Другие;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры // ПринятиеКНалоговомуУчетуНМА()

//******************************************************************************
// ПринятиеКНалоговомуУчетуНИОКР()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ПринятиеКНалоговомуУчетуНИОКР()
	
	// Отражение в для целей налогвого учета расходов по НИОКР
	// в виде расходов будущих периодов.
	
	СпрРБП = СоздатьОбъект("Справочник.РасходыБудущихПериодов");
	Если СпрРБП.НайтиЭлемент(РБП) = 0 Тогда
	    ТекстСообщения = "На закладке ""Налоговый учет"" не указана статья расходов будущих периодов.";
		глНеПроводить(Контекст, ТекстСообщения);
		Возврат;
		
	ИначеЕсли СпрРБП.НазначениеСтатьиРасходов <> 3 Тогда
		ТекстСообщения = "Назначение статьи расходов будущих периодов, указанной на закладке ""Налоговый учет"" должно быть - ""Прочие расходы"".";
		глНеПроводить(Контекст, ТекстСообщения);
		Возврат;
	КонецЕсли;
	     
	
	// Для целей налогового учета согласно п.2 ст. 262 НК в состав прочих
	// расходов равномерно включается 70% расходов на НИОКР, давшие отрицательный
	// результат, остальные расходы (30%) - не принимаются для налогообложения. 
	// Федеральным законом от 6.06.2005 № 58-ФЗ  с 2006 года 100% от расходов на НИОКР включается в состав прочих расходов.
	
	Если ПолученыПоложительныеРезультаты = 1 Тогда // положительный результат 
		СуммаПринимаемая = ПервоначальнаяСтоимостьН;
		
	Иначе  // не дали положительный результат
		Если Датадок < '01.01.2006' Тогда
			СуммаПринимаемая = ПервоначальнаяСтоимостьН*0.7; 
		Иначе
			СуммаПринимаемая = 0; 
		КонецЕсли;
	КонецЕсли;
	СуммаНеПринимаемая = ПервоначальнаяСтоимостьН - СуммаПринимаемая;
	
	// Заполним реквизиты статьи расходов будущих периодов.
	СпрРБП.Сумма = СуммаПринимаемая;
	СпрРБП.ДатаНачалаСписания    = КонМесяца(ДатаДок) + 1;
	СпрРБП.ДатаОкончанияСписания = КонМесяца(ДобавитьМесяц(ДатаДок, СрокПолезногоИспользованияН));
	СпрРБП.ВидРасхода = ВидРасхода;
	СпрРБП.ЭлементРасхода = ЭлементРасхода;
	СпрРБП.Объект = Объект;
	
	ИмяТипа = "";
	Если ПустоеЗначение(ЭлементРасхода) = 0 Тогда
		ИмяТипа = "Перечисление."+ ЭлементРасхода.Вид();
	КонецЕсли;             
	СпрРБП.НазначитьТип("ЭлементРасхода", ИмяТипа);
	СпрРБП.ЭлементРасхода = ЭлементРасхода;
	СпрРБП.Записать();
	
	Если СуммаПринимаемая > 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НУ";
		Операция.СодержаниеПроводки = "Принятие к учету результатов НИОКР";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Кредит.Счет = СчетПоКоду("Н01.03");
		Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
		Операция.Сумма = ПервоначальнаяСтоимостьН;
		Операция.Дебет.Счет = СчетПоКоду("Н04.03");
		Операция.Дебет.РасходыБудущихПериодов = РБП;    
		Операция.Сумма = СуммаПринимаемая;
	КонецЕсли;
	
	Если СуммаНеПринимаемая > 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НУ";
		Операция.СодержаниеПроводки = "Не принимаемые расходы по НИОКР (30%)";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Кредит.Счет = СчетПоКоду("Н01.03");
		Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
		Операция.Сумма = ПервоначальнаяСтоимостьН;
		Операция.Дебет.Счет = СчетПоКоду("Н09");
		Операция.Дебет.ВнереализационныеРасходы = Перечисление.ВнереализационныеРасходы.НеПринимаемые;    
		Операция.Сумма = СуммаНеПринимаемая;
	КонецЕсли;
	
КонецПроцедуры // ПринятиеКНалоговомуУчетуНИОКР()

//******************************************************************************
// ОтражениеПБУ18()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ОтражениеПБУ18()
	
	Если Константа.ПрименяетсяПБУ18.Получить(ДатаДок) = Да Тогда
			
		// Рассчитаем постоянные разницы
		БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИт.ИспользоватьСубконто(ВидыСубконто.ОбъектыСтроительства, ОбъектВнеоборотныхАктивов, 2);
		БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "НПР.08");
		ПостояннаяРазница = БухИт.СКД();
		
		Если ПостояннаяРазница <> 0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "ПР";
			
			Если (ВидОбъектаУчета = 1) и (ПолученыПоложительныеРезультаты = 0) Тогда // расходы по НИОКР не дали положительного результата
				Операция.СодержаниеПроводки = "Списание расходов по НИОКР";
				
				СчетНПР = глПолучитьСчетДебетаНПР(СчетЗатрат, Субконто1);
				Если ПустоеЗначение(СчетНПР) = 0 Тогда
					Операция.Дебет.Счет = СчетНПР;
					Операция.Дебет.Субконто(1, Субконто1);
					Операция.Дебет.Субконто(2, Субконто2);
					Операция.Дебет.Субконто(3, Субконто3);
				КонецЕсли;
				
			Иначе
				Операция.СодержаниеПроводки = ?(ВидОбъектаУчета = 1, "Принятие к учету результатов НИОКР",
																	 "Принят к учету НМА");
				Операция.Дебет.Счет = СчетПоКоду("НПР.04");
				Операция.Дебет.НематериальныеАктивы = НМА;
			КонецЕсли;
			Операция.Кредит.Счет = СчетПоКоду("НПР.08");
			Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
			Операция.Сумма = ПостояннаяРазница;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОтражениеПБУ18()

//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения(РежимПроведения=0)
	    
	СпрНМА = СоздатьОбъект("Справочник.НематериальныеАктивы");
	
	Если РежимПроведения = 1 Тогда
	    ОчиститьДвижения("Справочник");
	Иначе
		ОчиститьДвижения();
	
		Если БалансоваяСтоимость = 0 Тогда
			ТекстСообщения = ?(ВидОбъектаУчета = 1, "Сумма расходов равна нулю.", "Первоначальная стоимость равна нулю.");
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ВидОбъектаУчета = 1 Тогда // НИОКР
		Если ПолученыПоложительныеРезультаты = 1 Тогда
		    Если СпрНМА.НайтиЭлемент(НМА) = 0 Тогда
				ТекстСообщения = ?(ВидОбъектаУчета = 1, "Не задан объект НИОКР.", "Не задан нематериальный актив.");
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
		
		    УстановитьЗначениеРеквизитовДляНИОКР(СпрНМА);
		КонецЕсли;
		СчетУчетаОВА    = СчетПоКоду("08.8");
		СчетУчетаАктива = СчетПоКоду("04.2");
		
	Иначе // НМА
		Если СпрНМА.НайтиЭлемент(НМА) = 0 Тогда
			ТекстСообщения = ?(ВидОбъектаУчета = 1, "Не задан объект НИОКР.", "Не задан нематериальный актив.");
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		УстановитьЗначениеРеквизитовДляНМА(СпрНМА);
		СчетУчетаОВА    = СчетПоКоду("08.5");
		СчетУчетаАктива = СчетПоКоду("04.1");
	КонецЕсли;

	Если РежимПроведения <> 1 Тогда
		
		БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИт.ИспользоватьСубконто(ВидыСубконто.ОбъектыСтроительства, ОбъектВнеоборотныхАктивов, 2);
		БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), СчетУчетаОВА);
		
		Если БалансоваяСтоимость > БухИт.СКД() Тогда
			Если ВидОбъектаУчета = 1 Тогда // НИОКР
			    ТекстСообщения = "Сумма расходов по НИОКР превышает остаток по счету 08.8.";
				
			Иначе // НМА
				ТекстСообщения = "Первоначальная стоимость актива превышает остаток по счету 08.5.";
			КонецЕсли;
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
	    Если (ВидОбъектаУчета = 1) и (ПолученыПоложительныеРезультаты = 0) Тогда // НИОКР не дал положительного результата
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "НА";
			Операция.СодержаниеПроводки = "Списание расходов по НИОКР";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетЗатрат;
			Операция.Дебет.Субконто(1, Субконто1);
			Операция.Дебет.Субконто(2, Субконто2);
			Операция.Дебет.Субконто(3, Субконто3);
			Операция.Кредит.Счет = СчетУчетаОВА;
			Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
			Операция.Сумма = БалансоваяСтоимость;
		Иначе
			
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "НА";
			Операция.СодержаниеПроводки = ?(ВидОбъектаУчета = 1, "Принятие к учету результатов НИОКР",
																 "Принят к учету НМА");
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетУчетаАктива;
			Операция.Дебет.НематериальныеАктивы = НМА;
			Операция.Кредит.Счет = СчетУчетаОВА;
			Операция.Кредит.ОбъектыСтроительства = ОбъектВнеоборотныхАктивов;
			Операция.Сумма = БалансоваяСтоимость;
		КонецЕсли;
		
		//Отражение постоянных разниц.
		ОтражениеПБУ18();
		
		// Отражение в налоговом учете
		Если ВидОбъектаУчета = 1 Тогда // НИОКР
		    ПринятиеКНалоговомуУчетуНИОКР();
			ТекстСообщения = "Приняты к учету результаты НИОКР: "+НМА;
			
		Иначе // НМА
			ПринятиеКНалоговомуУчетуНМА();
			ТекстСообщения = "Нематериальный актив "+НМА+" принят к учету.";
		КонецЕсли;
		
		Если СтатусВозврата() = 0 Тогда
		    Возврат
		КонецЕсли;
		
		Операция.Записать();
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		
		ТекстСообщения = "Документ проведен.";
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		глПриПроведении(Контекст);
	
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроведения()