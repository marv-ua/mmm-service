////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;
Перем НачальнаяДатаДокумента;
Перем Новый;
Перем СписокДействий;

//*****************************************************************************
// Показать()
//
// Параметры:          
//		ЧтоИменно (необязательный) 
//
//	Вызывется из формул элементов диалога в табличной части:
// 	"Сумма док.","Счет плат.","БИК банка плат.".
//
// Описание
// 		Определяет из какой колонки таблицы был вызов
//		и возвращает значение реквизита
//		
Функция Показать(ЧтоИменно="")
	
	ТекстВозврата="";
	Если РасчетныйДокумент.Выбран()=1 Тогда
		
		Если ЧтоИменно="Сумма" Тогда
			ТекстВозврата=Формат(РасчетныйДокумент.Сумма, "Ч 18.2.,");
		ИначеЕсли ЧтоИменно="БИК" Тогда
			ТекстВозврата=РасчетныйДокумент.СчетКонтрагента.БанкОрганизации.Код;
		ИначеЕсли ЧтоИменно="Счет" Тогда
			ТекстВозврата=РасчетныйДокумент.СчетКонтрагента.Номер;
		КонецЕсли;
		
	КонецЕсли;
	Возврат ТекстВозврата;
			
КонецФункции // Показать()

//*****************************************************************************
// ДублированиеДокументов()
//
// Параметры:          
//		РасчДок - выбранный расчетный документ
//		ИзПодбора (необязательный) - число 1 - Процедура вызвана из подбора
//											0 - Процедура вызвана не из подбора
//
// Описание
// 		Проверяет если документ в Реестре
//		
Функция ДублированиеДокументов(РасчДок,ИзПодбора)
	Перем ВыбРасчДок;
	ВыбРасчДок=РасчДок;
	ТекСтрока=НомерСтроки;
	УжеЕсть=0;
	ВыбратьСтроки();    
	Пока (ПолучитьСтроку()=1) Цикл 
		Если ВыбРасчДок = РасчетныйДокумент Тогда 
			Если (ИзПодбора=0) и (НомерСтроки <> ТекСтрока) Тогда 
				УжеЕсть=1;
				Прервать;
				
			ИначеЕсли (ИзПодбора=1) Тогда 
				УжеЕсть=1;
				Прервать;
					
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;  
	
	Если (ИзПодбора=0) Тогда 
		
		ВыбратьСтроки();    
		Пока (ПолучитьСтроку()=1) Цикл 
			Если (НомерСтроки = ТекСтрока) Тогда 
				Прервать;
			КонецЕсли;
		КонецЦикла;  
		
	КонецЕсли;
		
	Возврат УжеЕсть ;
	
КонецФункции // ДублированиеДокументов()

//*****************************************************************************
// ПриВыбореДокумента()
// Параметры:          
//		РасчДок - выбранный расчетный документ
//		Предупредить (необязательный) - число 1 - выдать предупреждение о неправильном выборе
//												0 - не выдавать предупреждения
//		ИзПодбора (необязательный) - число 1 - Процедура вызвана из подбора
//											0 - Процедура вызвана не из подбора
//
// Описание
// 		Контролирует выбранный документ
//		
Процедура ПриВыбореДокумента(РасчДок,Предупредить=0,ИзПодбора=0)
	
	ТекстПредупреждения="";
	Если РасчДок.Выбран()=1 Тогда
		Если РасчетныйСчет<>РасчДок.РасчетныйСчет Тогда
			ТекстПредупреждения="Документ: "+глПредставлениеДокумента(РасчДок)+" выписан по другому счету!";
			РасчДок="";
		ИначеЕсли ДублированиеДокументов(РасчДок,ИзПодбора)=1 Тогда
			ТекстПредупреждения="Документ: "+глПредставлениеДокумента(РасчДок)+" уже есть в реестре!";
			РасчДок="";
		Иначе
			ДокумПодч=СоздатьОбъект("Документ");
			ДокумПодч.ВыбратьПодчиненныеДокументы(,,РасчДок);
			Пока ДокумПодч.ПолучитьДокумент()=1 Цикл
				Если (ДокумПодч.Вид()="РеестрДокументовНаИнкассо") и (ДокумПодч.ТекущийДокумент()<>ТекущийДокумент()) Тогда
					ТекстПредупреждения="Документ: "+глПредставлениеДокумента(РасчДок)+"
								 		|уже вошел в "+глПредставлениеДокумента(ДокумПодч)+"!";
					РасчДок="";
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Если (Предупредить=1) и (ПустаяСтрока(ТекстПредупреждения)=0) Тогда
		Предупреждение(ТекстПредупреждения);
	КонецЕсли;
		
КонецПроцедуры	//ПриВыбореДокумента()

//*****************************************************************************
// ОбработчикПоКнопкеПодбор()
// Параметры:          
//		Нет
//
// Описание
// 		Определяет какой документ подбирать ("Инкассовые поручения" 
//		или "Платежные требования"), при выборе 
//		включает непосредственно сам подбор
//		
Процедура ОбработчикПоКнопкеПодбор()
	
	Меню = СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("ИнкассовоеПоручение","Инкассовые поручения");
	Меню.ДобавитьЗначение("ПлатежноеТребование","Платежные требования");
	
	ВидДок = "";
	Если Меню.ВыбратьЗначение(ВидДок,,,,1) = 1 Тогда
		ОткрытьПодбор("Документ."+ВидДок,,Контекст,1);
	КонецЕсли;
	
КонецПроцедуры	//ОбработчикПоКнопкеПодбор()

//*****************************************************************************
// ОбработчикПоКнопкеЗаполнить()
// Параметры:          
//		Нет
//
// Описание
// 		Автоматически заполняет спецификацию реестра
//		документами которые не вошли ни в один реестр
//		выписанными за 10 дней до даты реестра и по дату реестра
//		
Процедура ОбработчикПоКнопкеЗаполнить()
	
	Если ВыбратьСтроки() = 1 Тогда
		Если Вопрос("Перед заполнением таблица будет очищена,"+РазделительСтрок+"а существующие строки удалены. Продолжать?","ОК+Отмена") = "ОК" Тогда
			ПолучитьСтроку();
			Пока КоличествоСтрок() <> 0 Цикл
				УдалитьСтроку();
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Если КоличествоСтрок() = 0 Тогда
		Докум=СоздатьОбъект("Документ");
		Докум.ВыбратьДокументы(ДатаДок-10,ДатаДок);
		Пока Докум.ПолучитьДокумент()=1 Цикл
			Если (Докум.Вид()="ИнкассовоеПоручение") или (Докум.Вид()="ПлатежноеТребование") Тогда
				Док=Докум.ТекущийДокумент();
				ПриВыбореДокумента(Док);
				Если ПустоеЗначение(Док)=0 Тогда
					НоваяСтрока();
					РасчетныйДокумент=Док;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры	//ОбработчикПоКнопкеЗаполнить()

//*****************************************************************************
// Печать()
// Параметры:          
//		Нет
//
// Описание
// 		Осуществляет вывод на печать
//		
Процедура Печать()

	Таб=СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	
	СуммаИтого=0;
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если РасчетныйДокумент.Выбран() = 1 Тогда
		    СуммаИтого=СуммаИтого+РасчетныйДокумент.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	// выводим шапку реестра
	СекцияШапка=Таб.ПолучитьСекцию("Шапка");
	
	СекцияШапка.НомерДокумента = глПреобразоватьНомерДок(НомерДок, 0, 0);
	СекцияШапка.ДатаДокумента = глВыводДатыПлатежныхДокументов(ДатаДок,РасчетныйСчет);	// Месяц даты числом или прописью
	
	СекцияШапка.Представление = "Представляем на инкассо платежные документы в количестве "+
								СокрЛП(Строка(КоличествоСтрок()))+" на сумму "+
								глВыводСуммыПлатежныхДокументов(СуммаИтого,РасчетныйСчет);	// Сумма без 00 копеек (=) или (-00)
	
	СекцияШапка.СуммаПрописью = глВыводСуммыПлатежныхДокументовПрописью(СуммаИтого,РасчетныйСчет);	// Сумма без 00 копеек (=) или (-00)
	
	СекцияШапка.Поставщик = Константа.ОфициальноеНазваниеОрганизации;
	СекцияШапка.БанкПоставщика = РасчетныйСчет.БанкОрганизации;
	
	Таб.ВывестиСекцию(СекцияШапка);

	// выводим таблицу реестра
	// формирование строк
	СекцияСтрока = Таб.ПолучитьСекцию("Строка");
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если РасчетныйДокумент.Выбран() = 1 Тогда
			СекцияСтрока.НомерСтроки	 = НомерСтроки;
			Если (РасчетныйДокумент.Вид()="ИнкассовоеПоручение") Тогда
				СекцияСтрока.ВидОперации = "06";
			ИначеЕсли (РасчетныйДокумент.Вид()="ПлатежноеТребование") Тогда
				СекцияСтрока.ВидОперации = "02";
			КонецЕсли;
			СекцияСтрока.НомерДатаДокумента = "№ "+глПреобразоватьНомерДок(РасчетныйДокумент.НомерДок, 0, 0)+" от "+глВыводДатыПлатежныхДокументов(РасчетныйДокумент.ДатаДок,РасчетныйСчет);
			СекцияСтрока.СчетПлательщика = РасчетныйДокумент.СчетКонтрагента.Номер;
			СекцияСтрока.БИКБанка		 = РасчетныйДокумент.СчетКонтрагента.БанкОрганизации.Код;
			СекцияСтрока.СуммаДокумента	 = СокрЛ(Формат(РасчетныйДокумент.Сумма, "Ч 15.2-"));
			Таб.ВывестиСекцию(СекцияСтрока);
		КонецЕсли;
	КонецЦикла;
	
	// выводим подвал реестра
	Таб.ВывестиСекцию("Подвал");

	Таб.Опции(0,0,0,0,"ОпцииПечатиРеестраДокументов","ПараметрыОкнаРеестраДокументов");
	Таб.ТолькоПросмотр(1);
	Таб.ПараметрыСтраницы(,,,,0,,0,,,);
	Таб.Показать("Печать реестра документов на инкассо","");   
	
КонецПроцедуры //Печать()

//******************************************************************************
// ПоКнопкеПечать()
// 
// Вызывается из формул элементов диалога:
//  Кнопка "кнПечать".
//
// Описание:
//  Определяется соответствующая печатная форма.
// 	
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		Печать();
		
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(Контекст, "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Вызывается из формул элементов диалога:
//  Кнопка "кнВыбПечать".
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Документ." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()

//*****************************************************************************
Процедура ВводНового(Копирование) //предопределенная
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	Если Копирование = 1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;
	РасчетныйСчет = глЗначениеПоУмолчанию("ОсновнойБанковскийСчет");

КонецПроцедуры //ВводНового()

//*****************************************************************************
Процедура ПриОткрытии() //предопределенная
	
	НачальнаяДатаДокумента=ДатаДок;
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.кнОК.Доступность(0);
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.кнПодбор.Доступность(0);
		Форма.кнЗаполнить.Доступность(0);
		Форма.КнопкаПоУмолчанию("кнЗакрыть");
	Иначе
		Форма.КнопкаПоУмолчанию("кнОК");
	КонецЕсли;
	Форма.Кн_Справка.Видимость(?(ТипЗначения(Норм_Компонента) = 0, 0, 1));
	
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Документ." + Вид(),ТаблицаПечФорм);
	
КонецПроцедуры //ПриОткрытии

//*****************************************************************************
Процедура ПриЗаписи() //предопределенная

	Если глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента)=1 Тогда
       СтатусВозврата(0);Возврат;
	КонецЕсли;
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

КонецПроцедуры //ПриЗаписи

//*****************************************************************************
Процедура ПриРедактированииНовойСтроки() //предопределенная
	
	РасчетныйДокумент.ВидыДляВыбора("ИнкассовоеПоручение,ПлатежноеТребование");
	
КонецПроцедуры //ПриРедактированииНовойСтроки()

//*****************************************************************************
Процедура ПриНачалеРедактированияСтроки() //предопределенная
	
	РасчетныйДокумент.ВидыДляВыбора("ИнкассовоеПоручение,ПлатежноеТребование");
	
КонецПроцедуры //ПриНачалеРедактированияСтроки()

//*****************************************************************************
Процедура ОбработкаПодбора(Док,Конт)  //предопределенная
	
	ПриВыбореДокумента(Док,1,1);
	Если ПустоеЗначение(Док)=0 Тогда
		НоваяСтрока();
		РасчетныйДокумент=Док;
	КонецЕсли;
	
КонецПроцедуры //ОбработкаПодбора

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Новый = 0;

ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");
	
// добавим информацию о встроенной форме
ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Реестр документов на инкассо";
ТаблицаПечФорм.Кнопка       = "Печать";

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Ввести на основании");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");