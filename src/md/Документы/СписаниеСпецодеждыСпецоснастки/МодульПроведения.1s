Перем Сч10_11;

//******************************************************************************
// ПогашениеСтоимости()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ПогашениеСтоимости(НепогашеннаяСтоимость, ПервоначальнаяСтоимость)
	
	Если НепогашеннаяСтоимость <= 0 Тогда
	    Возврат;
	КонецЕсли;
	
	НезаполненныйРеквизит      = "";
	ДатаВводаВЭксплуатацию     = НазначениеИспользования.ДатаВводаВЭксплуатацию;
	СпособПогашенияСтоимости   = НазначениеИспользования.СпособПогашенияСтоимости;
	СрокПолезногоИспользования = НазначениеИспользования.СрокПолезногоИспользования;
	
	Если ПустоеЗначение(НазначениеИспользования) = 1 Тогда
		Возврат;
		
	ИначеЕсли ПустоеЗначение(ДатаВводаВЭксплуатацию) = 1 Тогда
		НезаполненныйРеквизит = "Дата ввода в эксплуатацию";
		
	ИначеЕсли ПустоеЗначение(СпособПогашенияСтоимости) = 1 Тогда
		НезаполненныйРеквизит = "Способ погашения стоимости";
		
	ИначеЕсли ПустоеЗначение(СрокПолезногоИспользования) = 1 Тогда
		НезаполненныйРеквизит = "Срок полезного использования \ предполагаемый объем выпуска продукции";
		
	ИначеЕсли ПустоеЗначение(НазначениеИспользования.СчетЗатрат) = 1 Тогда
		НезаполненныйРеквизит = "Счет отнесения погашенной стоимости";
	КонецЕсли;
	
	Если ПустоеЗначение(НезаполненныйРеквизит) = 0 Тогда
		ТекстСообщения = "Для материала " + Материал + " в назначении использования 
					 	|" + НазначениеИспользования + " не заполнен реквизит """ + НезаполненныйРеквизит + """.
					 	|Стоимость погашения материала для этого назначения использования не будет рассчитана.";
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), -1,, НазначениеИспользования.ТекущийЭлемент());
	    Возврат;
	КонецЕсли;
	
	// Списывать начинаем с начала следующего месяца,
	// т.к. единица учета - месяц, то списывать необходимо
	// начиная с полного месяца.
	Если КонМесяца(ДатаВводаВЭксплуатацию) >= КонМесяца(ДатаДок) Тогда
	    Возврат;
	КонецЕсли;                                                                                
	
	Если ПервоначальнаяСтоимость <= 0 Тогда
	    Возврат;
	КонецЕсли;
	
	СуммаПогашения = 0;
    
	Если СпособПогашенияСтоимости = Перечисление.СпособПогашенияСтоимости.ПропорциональноОбъемуПродукцииРабот Тогда
		ОбщийОбъемПродукции   = СрокПолезногоИспользования;
		ОбъемПродукцииЗаМесяц = НазначениеИспользования.ФактическийОбъемПродукцииРаботЗаМесяц.Получить(КонМесяца(ДатаДок));
		
		СуммаПогашения = Окр((ПервоначальнаяСтоимость / ОбщийОбъемПродукции) * ОбъемПродукцииЗаМесяц, 2);
		СуммаПогашения = Мин(СуммаПогашения, НепогашеннаяСтоимость);
		
	ИначеЕсли СпособПогашенияСтоимости = Перечисление.СпособПогашенияСтоимости.Линейный Тогда
		СуммаПогашения = Окр(ПервоначальнаяСтоимость / СрокПолезногоИспользования, 2);
		СуммаПогашения = Мин(СуммаПогашения, НепогашеннаяСтоимость);
		
	КонецЕсли;
		
	Если СуммаПогашения > 0 Тогда
	    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "МТ";
		Операция.СодержаниеПроводки = ?(ВидМатериалов = 1, "Погашение стоимости спецодежды",
		                                                   "Погашение стоимости спецоснастки");
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = НазначениеИспользования.СчетЗатрат;
		Операция.Дебет.Субконто(1, НазначениеИспользования.Субконто1);
		Операция.Дебет.Субконто(2, НазначениеИспользования.Субконто2);
		Операция.Дебет.Субконто(3, НазначениеИспользования.Субконто3);
		Операция.Кредит.Счет = Сч10_11;
		Операция.Кредит.Субконто(1, Материал);
		Операция.Кредит.Субконто(2, НазначениеИспользования);
		                                 
		Если ВидМатериалов = 1 Тогда // списание спецодежды
			Операция.Кредит.Субконто(3, Сотрудник);
			
		Иначе // списание спецоснастки
			Операция.Кредит.Субконто(3, Подразделение);
		КонецЕсли;
		
		Операция.Сумма = СуммаПогашения;
		                          
		СуммаПогашеннойСтоимости = Операция.Сумма;
		НепогашеннаяСтоимость = НепогашеннаяСтоимость - СуммаПогашеннойСтоимости;
		
		СуммаОперации = СуммаОперации + СуммаПогашеннойСтоимости;
	КонецЕсли;
	
КонецПроцедуры // ПогашениеСтоимости()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаПроведения()   

	Операция.СуммаОперации = 0;
	
	СписокМатериалов = СоздатьОбъект("СписокЗначений");
	ВыгрузитьТабличнуюЧасть(СписокМатериалов, "Материал");
	
	СписокНазначенийИспользования = СоздатьОбъект("СписокЗначений");
	ВыгрузитьТабличнуюЧасть(СписокНазначенийИспользования, "НазначениеИспользования");
	
	ТаблицаОстатков = СоздатьОбъект("ТаблицаЗначений");
	
	Если ВидМатериалов = 1 Тогда // спецодежда
		Сч10_11 = СчетПоКоду("10.11.1");
		СчМЦ    = СчетПоКоду("МЦ.02");
		
		СписокСотрудников = СоздатьОбъект("СписокЗначений");
		ВыгрузитьТабличнуюЧасть(СписокСотрудников, "Сотрудник");
		
		БухИтоги = СоздатьОбъект("БухгалтерскиеИтоги"); БухИтоги.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИтоги.ИспользоватьСубконто(ВидыСубконто.Материалы, СписокМатериалов, 2);
		БухИтоги.ИспользоватьСубконто(ВидыСубконто.НазначенияИспользования, СписокНазначенийИспользования);
		БухИтоги.ИспользоватьСубконто(ВидыСубконто.Сотрудники, СписокСотрудников, 2);
		БухИтоги.ВыполнитьЗапрос(,ТекущийДокумент(), "10.11.1, МЦ.02",,,,, "СК");
		
		ВыгрузитьТабличнуюЧасть(ТаблицаОстатков, "Материал, Сотрудник, НазначениеИспользования, Количество");
		ТаблицаОстатков.Свернуть("Материал, Сотрудник, НазначениеИспользования", "Количество");
	
	ИначеЕсли ВидМатериалов=2 Тогда// спецоснастка
		Сч10_11 = СчетПоКоду("10.11.2");
		СчМЦ    = СчетПоКоду("МЦ.03");
		
		Если ПустоеЗначение(Подразделение) = 1 Тогда
			ТекстСообщения = "Не указано подразделение";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		БухИтоги = СоздатьОбъект("БухгалтерскиеИтоги"); БухИтоги.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИтоги.ИспользоватьСубконто(ВидыСубконто.Материалы, СписокМатериалов, 2);
		БухИтоги.ИспользоватьСубконто(ВидыСубконто.НазначенияИспользования, СписокНазначенийИспользования);
		БухИтоги.ИспользоватьСубконто(ВидыСубконто.Подразделения, Подразделение);
		БухИтоги.ВыполнитьЗапрос(,ТекущийДокумент(), "10.11.2, МЦ.03",,,,, "СК");
		
		ВыгрузитьТабличнуюЧасть(ТаблицаОстатков, "Материал, НазначениеИспользования, Количество");
		ТаблицаОстатков.Свернуть("Материал, НазначениеИспользования", "Количество"); 
		
	ИначеЕсли ВидМатериалов=3 Тогда //Инвентарь
		
		СчМЦ    = СчетПоКоду("МЦ.04");
		БухИтоги = СоздатьОбъект("БухгалтерскиеИтоги"); БухИтоги.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИтоги.ИспользоватьСубконто(ВидыСубконто.Материалы, СписокМатериалов, 2);
		БухИтоги.ИспользоватьСубконто(ВидыСубконто.Сотрудники, СписокСотрудников);
		БухИтоги.ВыполнитьЗапрос(,ТекущийДокумент(), "МЦ.04",,,,, "СК");
		
		ВыгрузитьТабличнуюЧасть(ТаблицаОстатков, "Материал, Сотрудник,НазначениеИспользования,Количество");
		ТаблицаОстатков.Свернуть("Материал, Сотрудник,НазначениеИспользования", "Количество"); 
		
	КонецЕсли;
	
	ТаблицаОстатков.НоваяКолонка("КоличествоВыдано", "Число");
	ТаблицаОстатков.НоваяКолонка("СуммаВыдано", "Число");
	ТаблицаОстатков.НоваяКолонка("СредняяСтоимость", "Число");
	ТаблицаОстатков.НоваяКолонка("СуммаПервоначальная", "Число");
	ТаблицаОстатков.НоваяКолонка("СредняяСтоимостьПервоначальная", "Число");
	
	ТаблицаОстатков.ВыбратьСтроки();
	Пока ТаблицаОстатков.ПолучитьСтроку() = 1 Цикл
			
		Если БухИтоги.ПолучитьСубконто(1,, ТаблицаОстатков.Материал) = 1 Тогда
			Если БухИтоги.ПолучитьСубконто(2,, ТаблицаОстатков.НазначениеИспользования) = 1 Тогда
			    Если ВидМатериалов = 1 Тогда // спецодежда
					Если БухИтоги.ПолучитьСубконто(3,, ТаблицаОстатков.Сотрудник ) = 1 Тогда
						БухИтоги.ВыбратьСчета();
						Пока БухИтоги.ПолучитьСчет() = 1 Цикл
							Если Найти(БухИтоги.Счет, "МЦ") <> 0 Тогда
								ТаблицаОстатков.СуммаПервоначальная = Макс(БухИтоги.СКД("С"), 0);
								
							Иначе // счет 10.11
						        ТаблицаОстатков.СуммаВыдано = Макс(БухИтоги.СКД("С"), 0);
								ТаблицаОстатков.КоличествоВыдано = БухИтоги.СКД("К");
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
					
				Иначе
					БухИтоги.ВыбратьСчета();
					Пока БухИтоги.ПолучитьСчет() = 1 Цикл
						Если Найти(БухИтоги.Счет, "МЦ") <> 0 Тогда
							ТаблицаОстатков.СуммаПервоначальная = Макс(БухИтоги.СКД("С"), 0);
							
						Иначе // счет 10.11
					        ТаблицаОстатков.СуммаВыдано = Макс(БухИтоги.СКД("С"), 0);
							ТаблицаОстатков.КоличествоВыдано = БухИтоги.СКД("К");
						КонецЕсли;
					КонецЦикла;
			    КонецЕсли;
			ИначеЕсли БухИтоги.ПолучитьСубконто(2,, ТаблицаОстатков.Сотрудник) = 1 Тогда //Инвентарь 
				Если ВидМатериалов=3 Тогда
					БухИтоги.ВыбратьСчета();
					Пока БухИтоги.ПолучитьСчет() = 1 Цикл
						Если Найти(БухИтоги.Счет, "МЦ") <> 0 Тогда
							ТаблицаОстатков.СуммаПервоначальная = Макс(БухИтоги.СКД("С"), 0);
							ТаблицаОстатков.КоличествоВыдано = БухИтоги.СКД("К"); 
							
							ТекстСообщенияДляИнвентаря  = "Сотруднику " + ТаблицаОстатков.Сотрудник + 
							" выдано "+ТаблицаОстатков.КоличествоВыдано +" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+ 
							" из списываемых "+ТаблицаОстатков.Количество+" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+" "+
							ТаблицаОстатков.Материал + " (МЦ.04)";			
							
							СписокОс=СоздатьОбъект("Справочник.ОсновныеСредства");
							Если СписокОс.ВыбратьЭлементыПоРеквизиту("МатериалДляОтраженияВСоставеМПЗ",ТаблицаОстатков.Материал,0,0)>0 Тогда
								ТекстПереченьОС="";
								Пока СписокОс.ПолучитьЭлемент()=1 Цикл									
									Если (ПустоеЗначение(СписокОс.ДатаВводаВЭксплуатацию)=0)
									и (СписокОс.Состояние.Получить(ДатаДок)=Перечисление.СостоянияОС.В_Эксплуатации) Тогда
									ТекстПереченьОС=ТекстПереченьОС + "Инв.№ "+СписокОс.Код+" "+СписокОс.Наименование+"
									|"; 
									КонецЕсли;
								КонецЦикла;
								Если ПустоеЗначение(ТекстПереченьОС)=0 Тогда
									ТекстСообщенияДляИнвентаря =ТекстСообщенияДляИнвентаря +"
									|Для списания инвентаря по регистрам налогового учета используйте документ Списание ОС!
									|Список ОС, отраженных в бухгалтерском учете в качестве материала:"+ТаблицаОстатков.Материал+"
									|"+ТекстПереченьОС;
								КонецЕсли;
							КонецЕсли;	
							
						КонецЕсли;
					КонецЦикла
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ТаблицаОстатков.КоличествоВыдано > 0 Тогда
			ТаблицаОстатков.СредняяСтоимость = ТаблицаОстатков.СуммаВыдано/ТаблицаОстатков.КоличествоВыдано;
			ТаблицаОстатков.СредняяСтоимостьПервоначальная = ТаблицаОстатков.СуммаПервоначальная/ТаблицаОстатков.КоличествоВыдано;
		КонецЕсли;
		
		
		Если ВидМатериалов = 1 Тогда // спецодежда
			ТекстСообщения = "Сотруднику " + ТаблицаОстатков.Сотрудник +
			" по назначению использования " + ТаблицаОстатков.НазначениеИспользования + 
			" выдано "+ТаблицаОстатков.КоличествоВыдано +" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+ 
			" из списываемых "+ТаблицаОстатков.Количество+" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+" "+
			ТаблицаОстатков.Материал + " (10.11.1)";
			
		ИначеЕсли ВидМатериалов = 2 Тогда // спецоснастка
			ТекстСообщения = "В подразделение " + Подразделение + 
			" по назначению использования " + ТаблицаОстатков.НазначениеИспользования + 
			" выдано "+ТаблицаОстатков.КоличествоВыдано +" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+ 
			" из списываемых "+ТаблицаОстатков.Количество+" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+" "+
			ТаблицаОстатков.Материал + " (10.11.2)";
		ИначеЕсли ВидМатериалов = 3 Тогда //инвентарь
			ТекстСообщения="Сотруднику " + ТаблицаОстатков.Сотрудник + 
			" выдано "+ТаблицаОстатков.КоличествоВыдано +" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+ 
			" из списываемых "+ТаблицаОстатков.Количество+" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+" "+
			ТаблицаОстатков.Материал + " (МЦ.04)";
		КонецЕсли;                                                              
		
		ПараметрКомментария = 0;
		Если ТаблицаОстатков.КоличествоВыдано < ТаблицаОстатков.Количество Тогда
			ПараметрКомментария = -1;
			Если Константа.КонтрольОтрицательныхОстатков = Да Тогда
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если ВидМатериалов<>3 Тогда
			глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), ПараметрКомментария);
		Иначе
			глСообщениеПроведения(ТекстСообщенияДляИнвентаря, ТекущийДокумент(), ПараметрКомментария);
		КонецЕсли;				
		
	КонецЦикла;
	    
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Если Количество=0 Тогда
			// Если не указано количество, выдаем сообщение и проводку не формируем.
			ТекстСообщения = "Строка " + НомерСтроки + ", не указано количество""" + Материал;
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
			
		ИначеЕсли Материал.Выбран() = 0 Тогда
			ТекстСообщения = "Строка " + НомерСтроки + ", не указан объект учета.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат; 
			
			
		ИначеЕсли НазначениеИспользования.Выбран() = 0 Тогда
			Если ВидМатериалов<>3 Тогда
				ТекстСообщения = "Строка " + НомерСтроки + ", не указано назначение использования.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат; 
			КонецЕсли;
		КонецЕсли;
		
		
		// Поиск соответствующей строки в таблице "ТаблицаОстатков"
		ТаблицаОстатков.ВыбратьСтроки();
		Пока ТаблицаОстатков.ПолучитьСтроку() = 1 Цикл
		    Если ВидМатериалов = 1 Тогда // спецодежда
		    	Если (ТаблицаОстатков.Материал = Материал)
				   и (ТаблицаОстатков.Сотрудник = Сотрудник)
				   и (ТаблицаОстатков.НазначениеИспользования = НазначениеИспользования) Тогда
		    	    Прервать;
				КонецЕсли;
				
			ИначеЕсли ВидМатериалов = 2 Тогда // спецоснастка
				Если (ТаблицаОстатков.Материал = Материал)
				   и (ТаблицаОстатков.НазначениеИспользования = НазначениеИспользования) Тогда
		    	    Прервать;
				КонецЕсли;
				
			ИначеЕсли ВидМатериалов = 3 Тогда //инвентарь
				Если (ТаблицаОстатков.Материал = Материал)
				и (ТаблицаОстатков.Сотрудник = Сотрудник) Тогда
					Прервать;
				КонецЕсли; 
				
			КонецЕсли;
		КонецЦикла;
		
		Если Количество >= ТаблицаОстатков.КоличествоВыдано Тогда
			НепогашеннаяСтоимость = ТаблицаОстатков.СуммаВыдано;
			СписываемаяПервоначальнаяСтоимость = ТаблицаОстатков.СуммаПервоначальная;
			
		Иначе
			НепогашеннаяСтоимость = Окр(Количество*ТаблицаОстатков.СредняяСтоимость, 2, 1);
			СписываемаяПервоначальнаяСтоимость = Окр(Количество*ТаблицаОстатков.СредняяСтоимостьПервоначальная, 2, 1);
		КонецЕсли;
		
		// Уменьшим остаток материала на списанное количество.
		ТаблицаОстатков.СуммаВыдано = ТаблицаОстатков.СуммаВыдано - НепогашеннаяСтоимость;
		ТаблицаОстатков.КоличествоВыдано = ТаблицаОстатков.КоличествоВыдано - Количество;
		
		ПогашениеСтоимости(НепогашеннаяСтоимость, СписываемаяПервоначальнаяСтоимость);
	    
		Если ВидМатериалов<>3 Тогда //не инвентарь
			
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.СодержаниеПроводки = ?(ВидМатериалов = 1, "Списание спецодежды ",
			"Списание спецоснастки");
			Операция.НомерЖурнала = "МТ";
			Операция.Кредит.Счет = Сч10_11;
			Операция.Кредит.Субконто(1, Материал);
			Операция.Кредит.Субконто(2, НазначениеИспользования);
			
			Если ВидМатериалов = 1 Тогда // списание спецодежды
				Операция.Кредит.Субконто(3, Сотрудник);
				
			Иначе // списание спецоснастки
				Операция.Кредит.Субконто(3, Подразделение);
			КонецЕсли;
			
			Если ВидСписания = 1 Тогда // списание по назначению использования
				Операция.Дебет.Счет = НазначениеИспользования.СчетЗатрат;
				Операция.Дебет.Субконто(1, НазначениеИспользования.Субконто1);
				Операция.Дебет.Субконто(2, НазначениеИспользования.Субконто2);
				Операция.Дебет.Субконто(3, НазначениеИспользования.Субконто3);
				
			ИначеЕсли ВидСписания = 2 Тогда // списать на прочие расходы (91.2);
				Операция.Дебет.Счет = СчетПоКоду("91.2");
				Операция.Дебет.Субконто(1, СтатьяРасходов);
				
			Иначе // списать на чрезвычайные расходы
				Операция.Дебет.Счет = СчетПоКоду("99.1");
				Операция.Дебет.Субконто(1, Перечисление.ПрибылиИУбытки.ЧрезвычайныеРасходы);
			КонецЕсли;
			
			Операция.Количество = Количество;
			Операция.Сумма      = НепогашеннаяСтоимость;
			
			Операция.СуммаОперации = Операция.СуммаОперации + Операция.Сумма;
			
		КонецЕсли;
		// Списание первоначальной стоимости
		Если СписываемаяПервоначальнаяСтоимость > 0 Тогда
			ТаблицаОстатков.СуммаПервоначальная = ТаблицаОстатков.СуммаПервоначальная - СписываемаяПервоначальнаяСтоимость;
			
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.СодержаниеПроводки = "Списание первоначальной стоимости";
			Операция.НомерЖурнала = "МТ";
			Операция.Кредит.Счет = СчМЦ;
			Операция.Кредит.Субконто(1, Материал);
			Операция.Кредит.Субконто(2, НазначениеИспользования);
			Если ВидМатериалов = 1 Тогда // списание спецодежды
				Операция.Кредит.Субконто(3, Сотрудник);
				
			Иначе // списание спецоснастки
				Операция.Кредит.Субконто(3, Подразделение);
			КонецЕсли;
			Операция.Сумма      = СписываемаяПервоначальнаяСтоимость;
			Операция.Количество = Количество;
			
			СуммаПервоначальнойСтоимости = СписываемаяПервоначальнаяСтоимость;
			СуммаПогашеннойСтоимости     = СписываемаяПервоначальнаяСтоимость - НепогашеннаяСтоимость;
		Иначе 
			Если ВидМатериалов=3 Тогда  //инвентарь только по количеству
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.СодержаниеПроводки = "Списание первоначальной стоимости";
				Операция.НомерЖурнала = "МТ";
				Операция.Кредит.Счет = СчМЦ;
				Операция.Кредит.Субконто(1, Материал);
				Операция.Кредит.Субконто(2, Сотрудник); 
				Операция.Количество = Количество;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаУдаленияПроведения()
	
	Если ВыбратьСтроки() = 1 Тогда
		Пока ПолучитьСтроку() = 1 Цикл
			СуммаПогашеннойСтоимости = 0;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаУдаленияПроведения()