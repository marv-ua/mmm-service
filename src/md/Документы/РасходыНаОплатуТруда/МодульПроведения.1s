//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()
    
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(ВидРасхода) = 1 Тогда
			ТекстСообщения = "В строке №" + НомерСтроки + " не указан вид расхода.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		ПривязыватьСтроку(НомерСтроки);
		
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.СодержаниеПроводки = "" + ВидНачисления;
		Операция.НомерЖурнала = "НУ";
		Операция.Сумма = Сумма;
		                                         
		Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
			Если ВидРасхода <> Перечисление.ВидыРасходов.РасходыБудущихПериодов Тогда
			    
				// Расходы на оплату труда для работников аппарата
				// управления отражаются по дебету счета Н14.01,
				// а прочих сотрудников - на счете Н14.02
				Если ВидПерсонала = "+" Тогда
				    Сч14 = счетПоКоду("Н14.01");
					
				Иначе
					Сч14 = счетПоКоду("Н14.02");
				КонецЕсли;
				Операция.Кредит.Счет = Сч14;
				Операция.Кредит.Сотрудники = Сотрудник;
				Операция.Кредит.ВидыНачисленийПоОплатеТруда = ВидНачисления;
			КонецЕсли;
		КонецЕсли;

		Если ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеТоваров Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
					
		    ИначеЕсли Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Товар Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н02.02.1");
				Операция.Дебет.Номенклатура = Объект;
				
			Иначе
				ТекстСообщения = "В строке №" + НомерСтроки + " указан объект учета, не являющийся товаром.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеМатериалов Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			Операция.Дебет.Счет = СчетПоКоду("Н02.01");
			Операция.Дебет.Материалы = Объект;
		    
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеОсновныхСредств Тогда
			Если Объект.Вид() = "ОбъектыСтроительства" Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н01.01");
				Операция.Дебет.ОбъектыСтроительства = Объект;
				
			ИначеЕсли Объект.Вид() = "Оборудование" Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н01.02");
				Операция.Дебет.Оборудование = Объект;
				
			Иначе
				ТекстСообщения = "В строке №" + НомерСтроки + " указан объекта учета не являющийся внеоборотным активом или оборудованием.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.СтроительствоОсновныхСредств Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект налогового учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			Операция.Дебет.Счет = СчетПоКоду("Н01.09");
			Операция.Дебет.ОбъектыСтроительства = Объект;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПриобретениеНематериальныхАктивов Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект налогового учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н01.08");
				
			Иначе
				Операция.Дебет.Счет = СчетПоКоду("Н01.01");
			КонецЕсли;
			Операция.Дебет.ОбъектыСтроительства = Объект;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РасходыБудущихПериодов Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект налогового учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			Операция.Дебет.Счет = СчетПоКоду("Н04.10");
			Операция.Дебет.РасходыБудущихПериодов = Объект;
			Операция.Дебет.Сотрудники = Сотрудник;
			
			// Заполним реквизиты элемента справочника РБП на основании данных документа.
			СтатьяРасходов = СоздатьОбъект("Справочник.РасходыБудущихПериодов");
			Если СтатьяРасходов.НайтиЭлемент(Объект) = 1 Тогда
				СтатьяРасходов.НазначениеСтатьиРасходов = 1; // расходы на оплату труда
				СтатьяРасходов.Сумма     = Сумма;
				СтатьяРасходов.ВидНачисления = ВидНачисления;
				СтатьяРасходов.Записать();
			КонецЕсли;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.НИОКР Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н01.03");
			Операция.Дебет.ОбъектыСтроительства = Объект;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ОсвоениеПриродныхРесурсов Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н01.04");
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ПрямыеРасходы Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н01.06");
				Если ВидНачисления <> Перечисление.ВидыНачисленийПоОплатеТруда.пп481ст264 Тогда
					Операция.Дебет.ГруппыВидыРасходов = Перечисление.ГруппыВидыРасходов.ОплатаТруда;
					
				Иначе
					Операция.Дебет.ГруппыВидыРасходов = Перечисление.ГруппыВидыРасходов.ДругиеРасходы;
				КонецЕсли;
			
			ИначеЕсли (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.УслугаЕНВД) Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " указаны расходы, не принимаемые к налоговоому учету!";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
				
			ИначеЕсли (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Продукция)
				или (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Полуфабрикат)
				или (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Услуга)
				или (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Работа) Тогда
				Операция.Дебет.Счет = СчетПоКоду("Н01.05");
				Операция.Дебет.ВидыНоменклатуры = Объект;
				
				Если ВидНачисления <> Перечисление.ВидыНачисленийПоОплатеТруда.пп481ст264 Тогда
					Операция.Дебет.ГруппыВидыРасходов = Перечисление.ГруппыВидыРасходов.ОплатаТруда;
					
				Иначе
					Операция.Дебет.ГруппыВидыРасходов = Перечисление.ГруппыВидыРасходов.ДругиеРасходы;
				КонецЕсли;
				
			Иначе
				ТекстСообщения = "В строке №" + НомерСтроки + " указан объект учета, не являющийся видом номенклатуры, относящимся к продукции, работам, услугам.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.КосвенныеРасходы Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н07.04.1");
			Если ВидНачисления <> Перечисление.ВидыНачисленийПоОплатеТруда.пп481ст264 Тогда
				Операция.Дебет.ГруппыВидыРасходов = Перечисление.ГруппыВидыРасходов.ОплатаТруда;
				
			Иначе
				Операция.Дебет.ГруппыВидыРасходов = Перечисление.ГруппыВидыРасходов.ДругиеРасходы;
			КонецЕсли;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РеализацияОсновныхСредств Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект налогового учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			Операция.Дебет.Счет = СчетПоКоду("Н07.07");
			Операция.Дебет.ОсновныеСредства = Объект;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.РеализацияНематериальныхАктивов Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект налогового учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
			Операция.Дебет.Счет = СчетПоКоду("Н07.08");
			Операция.Дебет.НематериальныеАктивы = Объект;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.ВнереализационныеРасходы Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н09");
			Операция.Дебет.ВнереализационныеРасходы = Перечисление.ВнереализационныеРасходы.ПрочиеВнереализационныеРасходы;
			
		ИначеЕсли ВидРасхода = Перечисление.ВидыРасходов.НеПринимаемые Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н07.04.1");
			Операция.Дебет.ГруппыВидыРасходов = Перечисление.ГруппыВидыРасходов.НеПринимаемые;
		КонецЕсли;
		
		Если ВидНачисления = Перечисление.ВидыНачисленийПоОплатеТруда.пп481ст264 Тогда
			Операция.Кредит.Счет = "";   
		КонецЕсли;
		
		
	КонецЦикла;

	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);

КонецПроцедуры // ОбработкаПроведения()
