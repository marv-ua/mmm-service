////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем Сч62_1, Сч62_2, Сч62_6, Сч62_7, Сч62_11, Сч62_22;
Перем СчетРасчетовСПокупателем, СчетАвансовПолученных;
Перем ЦеныВДоговоре;
Перем ОплатаДоговора;
Перем Валюта;
Перем КурсВалюты;
Перем Кратность;
Перем ВестиУчетРасчетовУЕ;
Перем АвансПоДоговору;
Перем АвансБезДоговора;
Перем АвансПоДоговоруРуб;
Перем АвансБезДоговораРуб;
Перем БезДоговора; 
Перем НПРКурсоваяРазница;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//******************************************************************************
// РассчитатьСуммуАванса(Дог)
//
Процедура РассчитатьСуммуАванса(Дог)
	
	АвансБезДоговора = 0;
	АвансБезДоговораРуб = 0;
	АвансПоДоговору = 0;
	АвансПоДоговоруРуб = 0;
	
	Если ЗачитыватьАванс <> 1 Тогда
		СписокДоговоров = СоздатьОбъект("СписокЗначений");
		СписокДоговоров.ДобавитьЗначение(Дог);
		БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
		Если (ПустоеЗначение(БезДоговора) = 0) и (ЗачитыватьАванс = 0) Тогда //работы были оплачены без указания договора
			СписокДоговоров.ДобавитьЗначение(БезДоговора);
		КонецЕсли;

		БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Контрагенты, Контрагент);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Договоры, СписокДоговоров, 2);
		Если ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда	
				БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "62.22, ВР.11",, Валюта,,, "СВ");
			Иначе                                                                          
				БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "62.22, ВР.11",, Валюта,,, "В");
			КонецЕсли;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "62.7, ВР.6",, Валюта,,, "СВ");
			
		Иначе
			БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "62.2, ВР.1",,,,, "С");
		КонецЕсли;
		
		ЗачтенныйАвансПоДоговору = 0;
		ЗачтенныйАвансПоДоговоруРуб = 0;
		ЗачтенныйАвансБезДоговора = 0;
		ЗачтенныйАвансБезДоговораРуб = 0;
		
		Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, Дог) = 1 Тогда
			Если ОплатаДоговора = 2 Тогда
				Если БухИт.ПолучитьСчет(, "62.22") = 1 Тогда
					АвансПоДоговору = БухИт.СКК("В");
					Если ДатаДок >= '01.01.2008' Тогда	
						АвансПоДоговоруРуб = БухИт.СКК("С");
					КонецЕсли;
				КонецЕсли;
				Если БухИт.ПолучитьСчет(, "ВР.11") = 1 Тогда
					ЗачтенныйАвансПоДоговору = БухИт.СКД("В");
				КонецЕсли;
			
			ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
				Если БухИт.ПолучитьСчет(, "62.7") = 1 Тогда
					АвансПоДоговору = БухИт.СКК("В");
					АвансПоДоговоруРуб = БухИт.СКК("С");
				КонецЕсли;
				Если БухИт.ПолучитьСчет(, "ВР.6") = 1 Тогда
					ЗачтенныйАвансПоДоговору = БухИт.СКД("В");
					ЗачтенныйАвансПоДоговоруРуб = БухИт.СКД("С");
				КонецЕсли;
			
			Иначе
				Если БухИт.ПолучитьСчет(, "62.2") = 1 Тогда
					АвансПоДоговору = БухИт.СКК("С");
				КонецЕсли;
				Если БухИт.ПолучитьСчет(, "ВР.1") = 1 Тогда
					ЗачтенныйАвансПоДоговору = БухИт.СКД("С");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, БезДоговора) = 1 Тогда
			Если ОплатаДоговора = 2 Тогда
				Если БухИт.ПолучитьСчет(, "62.22") = 1 Тогда
					АвансБезДоговора = БухИт.СКК("В");  
					Если ДатаДок >= '01.01.2008' Тогда	
						АвансБезДоговораРуб = БухИт.СКК("С");
					КонецЕсли;
				КонецЕсли;
				Если БухИт.ПолучитьСчет(, "ВР.11") = 1 Тогда
					ЗачтенныйАвансБезДоговора = БухИт.СКД("В");
				КонецЕсли;
				
			ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
				Если БухИт.ПолучитьСчет(, "62.7") = 1 Тогда
					АвансБезДоговора = БухИт.СКК("В");
					АвансБезДоговораРуб = БухИт.СКК("С");
				КонецЕсли;
				Если БухИт.ПолучитьСчет(, "ВР.6") = 1 Тогда
					ЗачтенныйАвансБезДоговора = БухИт.СКД("В");
					ЗачтенныйАвансБезДоговораРуб = БухИт.СКД("С");
				КонецЕсли;
				
			Иначе
				Если БухИт.ПолучитьСчет(, "62.2") = 1 Тогда
					АвансБезДоговора = БухИт.СКК("С");
				КонецЕсли;
				Если БухИт.ПолучитьСчет(, "ВР.1") = 1 Тогда
					ЗачтенныйАвансБезДоговора = БухИт.СКД("С");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		АвансПоДоговору = АвансПоДоговору - ЗачтенныйАвансПоДоговору;
		АвансБезДоговора = АвансБезДоговора - ЗачтенныйАвансБезДоговора;
		АвансПоДоговоруРуб = АвансПоДоговоруРуб - ЗачтенныйАвансПоДоговоруРуб;
		АвансБезДоговораРуб = АвансБезДоговораРуб - ЗачтенныйАвансБезДоговораРуб;
	КонецЕсли;
	
КонецПроцедуры // РассчитатьСуммуАванса()

//*****************************************************************************
// ЭтапОплачен(ДокВыполнения, ДокОплаты)
//
//
Функция ЭтапОплачен(ДокВыполнения, ДокОплаты)
	Перем ЭтапРаботОплачен;
	
    Если ДокВыполнения.ЗавершитьРаботы = 2 Тогда
		ЭтапРаботОплачен = 1;
		
	Иначе //Если Док.ЗавершитьРаботы = 1
		ДокОплатыЭтапаРабот = СоздатьОбъект("Документ");
		ДокОплатыЭтапаРабот.ВыбратьПодчиненныеДокументы(, ДатаДок, ДокВыполнения);
		ЭтапРаботОплачен = 0;
		Пока (ДокОплатыЭтапаРабот.ПолучитьДокумент() = 1) и (ЭтапРаботОплачен = 0) Цикл
			Если ДокОплатыЭтапаРабот.Вид() = "ОплатаЭтапаРабот" Тогда
				ЭтапРаботОплачен = 2;
				ДокОплаты = ДокОплатыЭтапаРабот.ТекущийДокумент();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ЭтапРаботОплачен;
КонецФункции // ЭтапОплачен()

//*****************************************************************************
// УстановитьВалюту(Дог)
//
// Параметры:
//  Дог       - Справочник.Договоры. Договор с контрагентом.
//
// Описание:
//  Устанавливает значения переменных контекста ЦеныВДоговоре, ОплатаДоговора,
// Валюта, КурсВалюты и Кратность.
//
Процедура УстановитьВалюту(Дог)
	        
	ОплатаДоговора = 1;
	ВестиУчетРасчетовУЕ = 0;
	СчетРасчетовСПокупателем = Сч62_1;
	СчетАвансовПолученных = Сч62_2;
	
	Если Дог.ВалютаДоговора.Выбран() = 1 Тогда
	    ЦеныВДоговоре = 2;
		ВестиУчетРасчетовУЕ = Дог.ВестиУчетРасчетовУЕ;
		Валюта = Дог.ВалютаДоговора;
		КурсВалюты = Валюта.Курс.Получить(ДатаДок);
		Кратность = Валюта.Кратность.Получить(ДатаДок);
		Кратность = ?(Кратность = 0, 1, Кратность);
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			Кратность = Кратность * 100 / (100 + Дог.ПроцентКорректировкиКурсаУЕ);
			СчетРасчетовСПокупателем = Сч62_6;
			СчетАвансовПолученных = Сч62_7;
		КонецЕсли;
		
		Если Дог.ОплатаДоговора = 2 Тогда
		    ОплатаДоговора = 2;
			СчетРасчетовСПокупателем = Сч62_11;
			СчетАвансовПолученных = Сч62_22;
		КонецЕсли;
	Иначе
		ЦеныВДоговоре = 1;
	КонецЕсли;
	
КонецПроцедуры // УстановитьВалюту()

//*****************************************************************************
// ЗачестьАванс(ЗачетАванса, ЗачетАвансаРуб, ДоговорАванса, ДоговорЭтапа)
//
Процедура ЗачестьАванс(ЗачетАванса, ЗачетАвансаРуб, ДоговорАванса, ДоговорЭтапа)
	
	Если ЗачетАванса > 0 Тогда
		УстановитьВалюту(ДоговорЭтапа);
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "ТВ";
		Операция.СодержаниеПроводки = "Зачтена предоплата";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);

		Если ОплатаДоговора = 2 Тогда
			Операция.Дебет.Счет = Сч62_22;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = ДоговорАванса;
			Операция.Кредит.Счет = Сч62_11;
			Операция.Кредит.Контрагенты = Контрагент;
			Операция.Кредит.Договоры = ДоговорЭтапа;
			Если ДатаДок >= '01.01.2008' Тогда
				Если ПустоеЗначение(АвансПоДоговору) = 1 Тогда
					ЗачетАвансаРуб = ЗачетАвансаРуб;
				ИначеЕсли (АвансПоДоговору + АвансБезДоговора) = ЗачетАванса Тогда
					ЗачетАвансаРуб = АвансПоДоговоруРуб + АвансБезДоговораРуб;
				Иначе
					КурсЗачетаАванса = Окр((АвансПоДоговоруРуб + АвансБезДоговораРуб)/(АвансПоДоговору + АвансБезДоговора), 4, 1);
					ЗачетАвансаРуб = ЗачетАванса*КурсЗачетаАванса;
				КонецЕсли;
			Иначе
				ЗачетАвансаРуб = ЗачетАванса*КурсВалюты/Кратность;
			КонецЕсли;
			Операция.Сумма = ЗачетАвансаРуб;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			Операция.Дебет.Счет = Сч62_7;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = ДоговорАванса;
			Операция.Кредит.Счет = Сч62_6;
			Операция.Кредит.Контрагенты = Контрагент;
			Операция.Кредит.Договоры = ДоговорЭтапа;
			
			Операция.Сумма = ЗачетАвансаРуб;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса;
			
		Иначе
			Операция.Дебет.Счет = Сч62_2;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = ДоговорАванса;
			Операция.Кредит.Счет = Сч62_1;
			Операция.Кредит.Контрагенты = Контрагент;
			Операция.Кредит.Договоры = ДоговорЭтапа;
			Операция.Сумма = ЗачетАванса;
		КонецЕсли;  
		
		Если ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ВЛ";
				Операция.СодержаниеПроводки = "Зачтена предоплата";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетПоКоду("ВАЛ.62");
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.Договоры = ДоговорАванса;
				Операция.Сумма = ЗачетАванса*КурсВалюты/Кратность;
				Операция.Валюта = Валюта;
				Операция.ВалСумма = ЗачетАванса;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ЗачетАванса()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()
	
	Сч46 = СчетПоКоду("46");
	Сч62_1 = СчетПоКоду("62.1");
	Сч62_6 = СчетПоКоду("62.6");
	Сч62_11 = СчетПоКоду("62.11");
	Сч62_2 = СчетПоКоду("62.2");
	Сч62_7 = СчетПоКоду("62.7");
	Сч62_22 = СчетПоКоду("62.22");
	Сч90_1_1 = СчетПоКоду("90.1.1");
	СчВР_1 = СчетПоКоду("ВР.1");
	СчВР_6 = СчетПоКоду("ВР.6");
	СчВР_11 = СчетПоКоду("ВР.11");
	СчН06_01 = СчетПоКоду("Н06.01");
	
	Сч68_5 = СчетПоКоду("68.5");
	Сч76_Н_4 = СчетПоКоду("76.Н.4");
	Сч90_6 = СчетПоКоду("90.6");
	
	СчВАЛ_62 = СчетПоКоду("ВАЛ.62");
	НПРКурсоваяРазница = 0;
	
	глТаблицаСчетов.УдалитьСтроки();
	Опер = СоздатьОбъект("Операция");
	
	ТаблицаОплаченныхРабот = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаОплаченныхРабот.НоваяКолонка("Договор");
	ТаблицаОплаченныхРабот.НоваяКолонка("Работа");
	ТаблицаОплаченныхРабот.НоваяКолонка("ВидНоменклатуры");
	ТаблицаОплаченныхРабот.НоваяКолонка("Всего", "Число", 15, 2);
	ТаблицаОплаченныхРабот.НоваяКолонка("ВсегоРуб", "Число", 15, 2);

	ТаблицаЗачтенныхАвансов = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаЗачтенныхАвансов.НоваяКолонка("ДоговорЭтапа");
	ТаблицаЗачтенныхАвансов.НоваяКолонка("ДоговорАванса");
	ТаблицаЗачтенныхАвансов.НоваяКолонка("Сумма", "Число", 15, 2);
	ТаблицаЗачтенныхАвансов.НоваяКолонка("СуммаРуб", "Число", 15, 2);

	ТаблицаНеОплаченныхРабот = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаНеОплаченныхРабот.НоваяКолонка("Договор");
	ТаблицаНеОплаченныхРабот.НоваяКолонка("ТаблицаРеализации");
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ДокументВыполненияЭтапаРабот.Проведен() = 0 Тогда
			ТекстСообщения = "Акт о выполнении этапа работ "+ДокументВыполненияЭтапаРабот+" не проведен."
					       + РазделительСтрок + "Завершение работ "+ТекущийДокумент()+" не может быть оформлено.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
			
		ИначеЕсли ДокументВыполненияЭтапаРабот.ЗавершитьРаботы = 3 Тогда
			ТекстСообщения = "По Акту о выполнении этапа работ "+ДокументВыполненияЭтапаРабот+" работы уже завершены."
					       + РазделительСтрок + "Завершение работ "+ТекущийДокумент()+" не может быть оформлено.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;

		Иначе
			ДокЗавершенияРабот = СоздатьОбъект("Документ");
			ДокЗавершенияРабот.ВыбратьПодчиненныеДокументы(,ДатаДок,ДокументВыполненияЭтапаРабот);
			Пока ДокЗавершенияРабот.ПолучитьДокумент() = 1 Цикл
				Если ДокЗавершенияРабот.Вид() <> "ЗавершениеРабот" Тогда
					Продолжить;
				КонецЕсли;
				
				Если ДокЗавершенияРабот.ТекущийДокумент() <> ТекущийДокумент() Тогда
					ТекстСообщения = "По Акту о выполнении этапа работ "+ДокументВыполненияЭтапаРабот+" работы завершены документом "+ДокЗавершенияРабот.ТекущийДокумент()
							       + РазделительСтрок + "Завершение работ "+ТекущийДокумент()+" не может быть оформлено.";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ДокументОплатыЭтапаРабот = "";		
		ЭтапРаботОплачен = ЭтапОплачен(ДокументВыполненияЭтапаРабот, ДокументОплатыЭтапаРабот);
		
		УстановитьВалюту(ДокументВыполненияЭтапаРабот.Договор);
		Если ОплатаДоговора = 2 Тогда
		    глТаблицаСчетов.НоваяСтрока();
			глТаблицаСчетов.Счет = Сч62_11;
			глТаблицаСчетов.Субконто1 = Контрагент;
			глТаблицаСчетов.Субконто2 = ДокументВыполненияЭтапаРабот.Договор;
			глТаблицаСчетов.Субконто3 = "";
			глТаблицаСчетов.Валюта = Валюта;
			глТаблицаСчетов.Курс = КурсВалюты;
		КонецЕсли;
		         
		Опер.НайтиОперацию(ДокументВыполненияЭтапаРабот);
		ДокументВыполненияЭтапаРабот.ВыбратьСтроки();
		Пока ДокументВыполненияЭтапаРабот.ПолучитьСтроку() = 1 Цикл
			Работа = ДокументВыполненияЭтапаРабот.Работа;
			ДатаЭтапа = ДокументВыполненияЭтапаРабот.ДатаДок;

			Если ЭтапРаботОплачен > 0 Тогда
				ТаблицаОплаченныхРабот.НоваяСтрока();
				ТаблицаОплаченныхРабот.Договор = ДокументВыполненияЭтапаРабот.Договор;
				ТаблицаОплаченныхРабот.Работа = Работа;
				ТаблицаОплаченныхРабот.ВидНоменклатуры = Работа.ВидНоменклатуры;
				Если (ЦеныВДоговоре = 2) и (ВестиУчетРасчетовУЕ = 0) и (ОплатаДоговора = 1) Тогда
					ТаблицаОплаченныхРабот.Всего = Окр(ДокументВыполненияЭтапаРабот.Всего * КурсВалюты / Кратность, 2, 1);
					
				Иначе
					ТаблицаОплаченныхРабот.Всего = ДокументВыполненияЭтапаРабот.Всего;
				КонецЕсли;
				
			Иначе
				НомСтроки = 0;
				Если ТаблицаНеОплаченныхРабот.НайтиЗначение(ДокументВыполненияЭтапаРабот.Договор, НомСтроки, "Договор") = 1 Тогда
				    ТаблицаНеОплаченныхРабот.ПолучитьСтрокуПоНомеру(НомСтроки);
					ТаблицаРеализации = ТаблицаНеОплаченныхРабот.ТаблицаРеализации;
					
				Иначе
					ТаблицаНеОплаченныхРабот.НоваяСтрока();
					ТаблицаНеОплаченныхРабот.Договор = ДокументВыполненияЭтапаРабот.Договор;
					ТаблицаНеОплаченныхРабот.ТаблицаРеализации = СоздатьОбъект("ТаблицаЗначений");
					ТаблицаРеализации = ТаблицаНеОплаченныхРабот.ТаблицаРеализации;
					
					ТаблицаРеализации.НоваяКолонка("ВидНоменклатуры");
					ТаблицаРеализации.НоваяКолонка("СтавкаНДС");
					ТаблицаРеализации.НоваяКолонка("СтавкаНП");
					ТаблицаРеализации.НоваяКолонка("Всего", "Число", 15, 2);
					ТаблицаРеализации.НоваяКолонка("ВалВсего", "Число", 15, 2);
					ТаблицаРеализации.НоваяКолонка("НП", "Число", 15, 2);
					ТаблицаРеализации.НоваяКолонка("НДС", "Число", 15, 2);
					ТаблицаРеализации.НоваяКолонка("ВыручкаНУ", "Число", 15, 2);
					ТаблицаРеализации.НоваяКолонка("НПВал", "Число", 15, 2);
					ТаблицаРеализации.НоваяКолонка("НДСВал", "Число", 15, 2);
				КонецЕсли;
				
				ТаблицаРеализации.НоваяСтрока();
				ТаблицаРеализации.ВидНоменклатуры = Работа.ВидНоменклатуры;
				ТаблицаРеализации.СтавкаНДС = глСтавкаНалога(ДокументВыполненияЭтапаРабот, "НДС");
				ТаблицаРеализации.СтавкаНП = глСтавкаНалога(ДокументВыполненияЭтапаРабот, "НП");
				
				Если (ЦеныВДоговоре = 2) и (ВестиУчетРасчетовУЕ = 0) Тогда
					ТаблицаРеализации.Всего = Окр(ДокументВыполненияЭтапаРабот.Всего*КурсВалюты/Кратность, 2, 1);
					ТаблицаРеализации.НП = Окр(ДокументВыполненияЭтапаРабот.НП*КурсВалюты/Кратность, 2, 1);
					ТаблицаРеализации.НДС = Окр(ДокументВыполненияЭтапаРабот.НДС*КурсВалюты/Кратность, 2, 1);
					Если ОплатаДоговора =2 Тогда
					    ТаблицаРеализации.ВалВсего = ДокументВыполненияЭтапаРабот.Всего;
					КонецЕсли;
					
				Иначе
					ТаблицаРеализации.Всего = ДокументВыполненияЭтапаРабот.Всего;
					ТаблицаРеализации.НП = ДокументВыполненияЭтапаРабот.НП;
					ТаблицаРеализации.НДС = ДокументВыполненияЭтапаРабот.НДС;
				КонецЕсли;
				
				Опер.ВыбратьПроводки();
				Пока Опер.ПолучитьПроводку() = 1 Цикл
				    Если Опер.Кредит.Счет = СчН06_01 Тогда
						Если Опер.Кредит.Субконто(ВидыСубконто.Номенклатура) = Работа Тогда
						    ТаблицаРеализации.ВыручкаНУ = ТаблицаРеализации.ВыручкаНУ + Опер.Сумма;
						КонецЕсли;
				    КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		Если ЭтапРаботОплачен > 0 Тогда
			Если ЭтапРаботОплачен = 1 Тогда
				ОперДок = ДокументВыполненияЭтапаРабот.Операция;
			Иначе
				ОперДок = ДокументОплатыЭтапаРабот.Операция;
			КонецЕсли;
			
			ОперДок.ВыбратьПроводки();
			Пока ОперДок.ПолучитьПроводку() = 1 Цикл
				Если (ОперДок.Дебет.Счет = СчВР_1) или (ОперДок.Дебет.Счет = СчВР_6) или (ОперДок.Дебет.Счет = СчВР_11) Тогда
					
					ТаблицаЗачтенныхАвансов.НоваяСтрока();
					Если ЭтапРаботОплачен = 1 Тогда
						ТаблицаЗачтенныхАвансов.ДоговорЭтапа = ДокументВыполненияЭтапаРабот.Договор;
					Иначе
						ТаблицаЗачтенныхАвансов.ДоговорЭтапа = ДокументОплатыЭтапаРабот.ДокументВыполненияЭтапаРабот.Договор;
					КонецЕсли;
					ТаблицаЗачтенныхАвансов.ДоговорАванса = ОперДок.Дебет.Договоры; 
					Если ОперДок.Дебет.Счет = СчВР_1 Тогда
						ТаблицаЗачтенныхАвансов.Сумма = ОперДок.Сумма;
					
					ИначеЕсли ОперДок.Дебет.Счет = СчВР_6 Тогда
						ТаблицаЗачтенныхАвансов.Сумма = ОперДок.ВалСумма;
						ТаблицаЗачтенныхАвансов.СуммаРуб = ОперДок.Сумма;
						
					Иначе
						ТаблицаЗачтенныхАвансов.Сумма = ОперДок.ВалСумма;
						
						Если ДатаДок >= '01.01.2008' Тогда
							СчетАвансовПолученныхПереоценка = СчВАЛ_62; 
							ТаблицаЗачтенныхАвансов.СуммаРуб = ОперДок.Сумма;
						Иначе
							СчетАвансовПолученныхПереоценка = Сч62_22;
						КонецЕсли;
						
						глТаблицаСчетов.НоваяСтрока();
						глТаблицаСчетов.Счет = СчетАвансовПолученныхПереоценка;
						глТаблицаСчетов.Субконто1 = Контрагент;
						глТаблицаСчетов.Субконто2 = ТаблицаЗачтенныхАвансов.ДоговорАванса;
						глТаблицаСчетов.Субконто3 = "";
						глТаблицаСчетов.Валюта = ОперДок.Валюта;
						глТаблицаСчетов.Курс = КурсВалюты;
						
						глТаблицаСчетов.НоваяСтрока();
						глТаблицаСчетов.Счет = СчВР_11;
						глТаблицаСчетов.Субконто1 = Контрагент;
						глТаблицаСчетов.Субконто2 = ТаблицаЗачтенныхАвансов.ДоговорАванса;
						глТаблицаСчетов.Субконто3 = "";
						глТаблицаСчетов.Валюта = ОперДок.Валюта;
						глТаблицаСчетов.Курс = КурсВалюты;
					КонецЕсли;
					
				ИначеЕсли ОперДок.Дебет.Счет = Сч46 Тогда
					ТаблицаОплаченныхРабот.НоваяСтрока();
					ТаблицаОплаченныхРабот.Договор = ДокументВыполненияЭтапаРабот.Договор;
					ТаблицаОплаченныхРабот.Работа = ОперДок.Дебет.Номенклатура;
					ТаблицаОплаченныхРабот.ВидНоменклатуры = ОперДок.Кредит.ВидыНоменклатуры;
					ТаблицаОплаченныхРабот.ВсегоРуб = ОперДок.Сумма;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

    глПереоценкаСчетов(Контекст, глТаблицаСчетов, , 0);
	
	ТаблицаОплаченныхРабот.Свернуть("Договор, ВидНоменклатуры, Работа", "Всего, ВсегоРуб");
	ТаблицаОплаченныхРабот.Сортировать("Договор, ВидНоменклатуры, Работа");
	ТаблицаОплаченныхРабот.ВыбратьСтроки();
	Пока ТаблицаОплаченныхРабот.ПолучитьСтроку() = 1 Цикл
		УстановитьВалюту(ТаблицаОплаченныхРабот.Договор);
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "ТВ";
		Операция.СодержаниеПроводки = "Учтена выручка";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = СчетРасчетовСПокупателем;
		Операция.Дебет.Контрагенты = Контрагент;
		Операция.Дебет.Договоры = ТаблицаОплаченныхРабот.Договор;

		Операция.Кредит.Счет = Сч46;
		Операция.Кредит.Контрагенты = Контрагент;
		Операция.Кредит.Договоры = ТаблицаОплаченныхРабот.Договор;
		Операция.Кредит.Номенклатура = ТаблицаОплаченныхРабот.Работа;
		
		Если ОплатаДоговора = 2 Тогда
			Операция.Валюта = Валюта;
			Операция.ВалСумма = ТаблицаОплаченныхРабот.Всего;
			
			Если ДатаДок >= '01.01.2008' Тогда	
				Операция.Сумма = ТаблицаОплаченныхРабот.ВсегоРуб;
				НПРКурсоваяРазница = Окр(Операция.Сумма, 2) - Окр(ТаблицаОплаченныхРабот.Всего*КурсВалюты/Кратность, 2);
			Иначе
				Операция.Сумма = ТаблицаОплаченныхРабот.Всего*КурсВалюты/Кратность;
			КонецЕсли;			
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			Операция.Сумма = ТаблицаОплаченныхРабот.ВсегоРуб;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ТаблицаОплаченныхРабот.Всего;
		
		Иначе
			Операция.Сумма = ТаблицаОплаченныхРабот.Всего;
		КонецЕсли;
	КонецЦикла;

	ТаблицаЗачтенныхАвансов.Свернуть("ДоговорЭтапа, ДоговорАванса", "Сумма,СуммаРуб");
	ТаблицаЗачтенныхАвансов.Сортировать("ДоговорЭтапа, ДоговорАванса");
	ТаблицаЗачтенныхАвансов.ВыбратьСтроки();
	Пока ТаблицаЗачтенныхАвансов.ПолучитьСтроку() = 1 Цикл
		ЗачестьАванс(ТаблицаЗачтенныхАвансов.Сумма, ТаблицаЗачтенныхАвансов.СуммаРуб, ТаблицаЗачтенныхАвансов.ДоговорАванса, ТаблицаЗачтенныхАвансов.ДоговорЭтапа);
		
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "ТВ";
		Операция.СодержаниеПроводки = "Зачтена предоплата";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		
		Если ОплатаДоговора = 2 Тогда
			Операция.Кредит.Счет = СчВР_11;
			Операция.Кредит.Контрагенты = Контрагент;
			Операция.Кредит.Договоры = ТаблицаЗачтенныхАвансов.ДоговорАванса;
			
			Операция.Сумма = ТаблицаЗачтенныхАвансов.Сумма*КурсВалюты/Кратность;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ТаблицаЗачтенныхАвансов.Сумма;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			Операция.Кредит.Счет = СчВР_6;
			Операция.Кредит.Контрагенты = Контрагент;
			Операция.Кредит.Договоры = ТаблицаЗачтенныхАвансов.ДоговорАванса;
			
			Операция.Сумма = ТаблицаЗачтенныхАвансов.СуммаРуб;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ТаблицаЗачтенныхАвансов.Сумма;
			
		Иначе
			Операция.Кредит.Счет = СчВР_1;
			Операция.Кредит.Контрагенты = Контрагент;
			Операция.Кредит.Договоры = ТаблицаЗачтенныхАвансов.ДоговорАванса;
			Операция.Сумма = ТаблицаЗачтенныхАвансов.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаНеОплаченныхРабот.КоличествоСтрок() > 0 Тогда
	    Операция.ЗаписатьПроводки();
	КонецЕсли;

	ТаблицаНеОплаченныхРабот.Сортировать("Договор");
	ТаблицаНеОплаченныхРабот.ВыбратьСтроки();
	Пока ТаблицаНеОплаченныхРабот.ПолучитьСтроку() = 1 Цикл
		ТаблицаРеализации = ТаблицаНеОплаченныхРабот.ТаблицаРеализации;
		Договор = ТаблицаНеОплаченныхРабот.Договор;
		
		ЗачетАвансаПоДоговору = 0;
		ЗачетАвансаБезДоговора = 0;
		ЗачетАвансаПоДоговоруРуб = 0;
	    ЗачетАвансаБезДоговораРуб = 0;
		РассчитатьСуммуАванса(Договор);
		
		// Расчет суммы аванса.
		СуммаЗачтенногоАванса = 0;
		СуммаЗачтенногоАвансаРуб = 0;
		КурсАванса = 0;
		
		Если ОплатаДоговора = 2 Тогда
		    ВсегоОтгружено = ТаблицаРеализации.Итог("ВалВсего");
			
		Иначе
			ВсегоОтгружено = ТаблицаРеализации.Итог("Всего");
		КонецЕсли;
		
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, ВсегоОтгружено);
			ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, ВсегоОтгружено - ЗачетАвансаПоДоговору);
			
			Аванс = АвансПоДоговору + АвансБезДоговора;
			ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
			
			Если ЗачетАванса = Аванс Тогда
			    ЗачетАвансаПоДоговоруРуб = АвансПоДоговоруРуб;
				ЗачетАвансаБезДоговораРуб = АвансБезДоговораРуб;
				
			ИначеЕсли ЗачетАванса > АвансПоДоговору Тогда
				ЗачетАвансаПоДоговоруРуб = АвансПоДоговоруРуб;
				ЗачетАвансаБезДоговораРуб = Окр(ЗачетАвансаБезДоговора * Окр(АвансБезДоговораРуб / ?(АвансБезДоговора = 0, 1, АвансБезДоговора), 4, 1), 2, 1);
				
			Иначе
				ЗачетАвансаПоДоговоруРуб = Окр(ЗачетАвансаПоДоговору * Окр(АвансПоДоговоруРуб / ?(АвансПоДоговору = 0, 1, АвансПоДоговору), 4, 1), 2, 1);
				ЗачетАвансаБезДоговораРуб = 0;
			КонецЕсли;
		
		Иначе
			ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, ВсегоОтгружено);
			ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, ВсегоОтгружено - ЗачетАвансаПоДоговору);
		КонецЕсли;
		
		СуммаЗачтенногоАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			СуммаЗачтенногоАвансаРуб = ЗачетАвансаПоДоговоруРуб + ЗачетАвансаБезДоговораРуб;
		Иначе
			Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
				СуммаЗачтенногоАвансаРуб = ЗачетАвансаПоДоговоруРуб + ЗачетАвансаБезДоговораРуб;				
			КонецЕсли;
		КонецЕсли;
		
		ТаблицаРеализации.Свернуть("Работа, ВидНоменклатуры, СтавкаНДС, СтавкаНП","ВалВсего, Всего, НДС, НП, Количество, НДСВал, НПВал");
	
		Если ОплатаДоговора = 2 Тогда
		    ИтогВсего = ТаблицаРеализации.Итог("ВалВсего");
		
		Иначе
			ИтогВсего = ТаблицаРеализации.Итог("Всего");
		КонецЕсли;
		
		ТаблицаРеализации.НоваяКолонка("ВыручкаРуб", "Число", 15, 2);
		ТаблицаРеализации.НоваяКолонка("СуммоваяРазница", "Число", 15, 2);
		
		РаспределеноАванса = 0;
		РаспределеноАвансаРуб = 0;
		КолСтрокТабОтгрузки = ТаблицаРеализации.КоличествоСтрок();
		КурсАванса = ?(СуммаЗачтенногоАванса = 0, 0, Окр(СуммаЗачтенногоАвансаРуб / СуммаЗачтенногоАванса, 4, 1));
		К = ?(ИтогВсего = 0, 0, СуммаЗачтенногоАванса / ИтогВсего);
		
		ТаблицаРеализации.ВыбратьСтроки();
		Пока ТаблицаРеализации.ПолучитьСтроку() = 1 Цикл
			Если ((ЦеныВДоговоре = 2) и (ВестиУчетРасчетовУЕ = 1))
			или ((ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008')) Тогда
				Если ПустоеЗначение(ИтогВсего) = 0  Тогда        
					Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
						ТаблицаВсего = ТаблицаРеализации.ВалВсего;
					Иначе
						ТаблицаВсего = ТаблицаРеализации.Всего;
					КонецЕсли;
					
					ЧастьСуммыЗачтенногоАванса = Окр(СуммаЗачтенногоАванса * ТаблицаВсего / ИтогВсего, 2, 1);
					ЧастьСуммыЗачтенногоАвансаРуб = Окр(СуммаЗачтенногоАвансаРуб * ТаблицаВсего / ИтогВсего, 2, 1);
					
				Иначе
					ЧастьСуммыЗачтенногоАванса = 0;
					ЧастьСуммыЗачтенногоАвансаРуб = 0;
				КонецЕсли;
				
				РаспределеноАванса = РаспределеноАванса + ЧастьСуммыЗачтенногоАванса;
				РаспределеноАвансаРуб = РаспределеноАвансаРуб + ЧастьСуммыЗачтенногоАвансаРуб;
				
				Если КолСтрокТабОтгрузки = ТаблицаРеализации.НомерСтроки Тогда
					Если РаспределеноАванса <> СуммаЗачтенногоАванса Тогда
						ЧастьСуммыЗачтенногоАванса = ЧастьСуммыЗачтенногоАванса + СуммаЗачтенногоАванса - РаспределеноАванса;
					КонецЕсли;
					
					Если РаспределеноАвансаРуб <> СуммаЗачтенногоАвансаРуб Тогда
						ЧастьСуммыЗачтенногоАвансаРуб = ЧастьСуммыЗачтенногоАвансаРуб + СуммаЗачтенногоАвансаРуб - РаспределеноАвансаРуб;
					КонецЕсли;
				КонецЕсли; 
				
				Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
					ВсегоРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.ВалВсего * КурсВалюты / Кратность, 2, 1);
					НДСРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.НДС, 2, 1);                 
					НПРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.НП, 2, 1);
					НДСвЧастиЗачтенногоАвансаРуб = Окр(К * ТаблицаРеализации.НДСВал * КурсВалюты / Кратность, 2, 1);
					НПвЧастиЗачтенногоАвансаРуб = Окр(К * ТаблицаРеализации.НПВал * КурсВалюты / Кратность, 2, 1);
				Иначе
					ВсегоРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.Всего * КурсВалюты / Кратность, 2, 1);
					НДСРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.НДС * КурсВалюты / Кратность, 2, 1);
					НПРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.НП * КурсВалюты / Кратность, 2, 1);
					НДСвЧастиЗачтенногоАвансаРуб = Окр(К * ТаблицаРеализации.НДС * КурсАванса, 2, 1);
					НПвЧастиЗачтенногоАвансаРуб = Окр(К * ТаблицаРеализации.НП * КурсАванса, 2, 1);
				КонецЕсли;
				
				
				ОплаченоПоКурсуОтгрузки = Окр(К * ВсегоРубПоКурсуОтгрузки, 2, 1);
			    ТаблицаРеализации.ВыручкаРуб = ВсегоРубПоКурсуОтгрузки + ЧастьСуммыЗачтенногоАвансаРуб - ОплаченоПоКурсуОтгрузки;
				
				ОплаченоНДСПоКурсуОтгрузки = Окр(К * НДСРубПоКурсуОтгрузки, 2, 1);
				СуммаНДС = НДСРубПоКурсуОтгрузки + НДСвЧастиЗачтенногоАвансаРуб - ОплаченоНДСПоКурсуОтгрузки;
				
				Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
					НПРКурсоваяРазница = Окр(НПРКурсоваяРазница, 2) - Окр(СуммаНДС, 2);
				КонецЕсли;
				
				ОплаченоНППоКурсуОтгрузки = Окр(К * НПРубПоКурсуОтгрузки, 2, 1);
				СуммаНП = НПРубПоКурсуОтгрузки + НПвЧастиЗачтенногоАвансаРуб - ОплаченоНППоКурсуОтгрузки;
				
				ВыручкаБезНалогов = ТаблицаРеализации.ВыручкаРуб - СуммаНДС - СуммаНП;
				
				Если (Договор.ОтражатьСуммовуюРазницуПриЗачетеАванса = 1) и (ВестиУчетРасчетовУЕ = 1) Тогда
					ТаблицаРеализации.СуммоваяРазница = ЧастьСуммыЗачтенногоАвансаРуб - ОплаченоПоКурсуОтгрузки;
				КонецЕсли;
				
			Иначе
				ВыручкаБезНалогов = ТаблицаРеализации.Всего - ТаблицаРеализации.НДС - ТаблицаРеализации.НП;
			КонецЕсли;
			
			Если (Договор.ОтражатьСуммовуюРазницуПриЗачетеАванса = 1) и (ВестиУчетРасчетовУЕ = 1) и (глНовыеПравилаВеденияНУ(ДатаДок) = 1) Тогда
				ПоКурсуОтгрузки = ОплаченоПоКурсуОтгрузки - ОплаченоНДСПоКурсуОтгрузки - ОплаченоНППоКурсуОтгрузки;
				ПоКурсуОплаты = ЧастьСуммыЗачтенногоАвансаРуб - НДСвЧастиЗачтенногоАвансаРуб - НПвЧастиЗачтенногоАвансаРуб;
				СуммоваяРазница = ПоКурсуОплаты - ПоКурсуОтгрузки;
				глОтражениеСуммовыхРазницВНаловомУчете(Контекст, СуммоваяРазница, 0);
			КонецЕсли;
		КонецЦикла;
		
		ТаблицаРеализации.Свернуть("ВидНоменклатуры, СтавкаНДС, СтавкаНП","Всего, ВалВсего, ВыручкаРуб, СуммоваяРазница, НП");
		ТаблицаРеализации.ВыбратьСтроки();
		Пока ТаблицаРеализации.ПолучитьСтроку() = 1 Цикл
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "ТВ";
			Операция.СодержаниеПроводки = "Выполнен этап работ";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетРасчетовСПокупателем;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;
			Операция.Кредит.Счет = Сч90_1_1;
			Операция.Кредит.ВидыНоменклатуры = ТаблицаРеализации.ВидНоменклатуры;
			Если ВерсияОбъекта >= "7.70.421" Тогда
				Операция.Кредит.СтавкиНДС = ТаблицаРеализации.СтавкаНДС;
				Операция.Кредит.СтавкиНП = ТаблицаРеализации.СтавкаНП;
			КонецЕсли;

			Если ОплатаДоговора = 2 Тогда
				Операция.ВалСумма = ТаблицаРеализации.ВалВсего;
				Операция.Валюта = Валюта;
			КонецЕсли;
			
			Если ВестиУчетРасчетовУЕ = 1 Тогда
				Операция.ВалСумма = ТаблицаРеализации.Всего;
				Операция.Валюта = Валюта;
				Операция.Сумма = ТаблицаРеализации.ВыручкаРуб - ТаблицаРеализации.СуммоваяРазница;
				
			Иначе
				Операция.Сумма = ТаблицаРеализации.Всего;
			КонецЕсли;
			
			Если ТаблицаРеализации.СуммоваяРазница <> 0 Тогда
			    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ТВ";
				Операция.СодержаниеПроводки = "Суммовая разница";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетРасчетовСПокупателем;
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.Договоры = Договор;
				Операция.Кредит.Счет = Сч90_1_1;
				Операция.Кредит.ВидыНоменклатуры = ТаблицаРеализации.ВидНоменклатуры;
				Операция.Кредит.СтавкиНДС = ТаблицаРеализации.СтавкаНДС;
				Операция.Кредит.СтавкиНП = ТаблицаРеализации.СтавкаНП;
				Операция.Сумма = ТаблицаРеализации.СуммоваяРазница;
			    Операция.Валюта = Валюта;
			КонецЕсли;
		КонецЦикла;
		
		Если ОплатаДоговора = 2 Тогда
		    ИтогВсего = ТаблицаРеализации.Итог("ВалВсего");
			
		Иначе
			ИтогВсего = ТаблицаРеализации.Итог("Всего");
		КонецЕсли;
		
		// Отразим зачет аванса.
		ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
		Если ЗачетАванса <> 0 Тогда
			ЗачестьАванс(ЗачетАвансаПоДоговору, ЗачетАвансаПоДоговоруРуб, Договор, Договор);
			ЗачестьАванс(ЗачетАвансаБезДоговора, ЗачетАвансаБезДоговораРуб, БезДоговора, Договор);
			
			ТекстСообщения = "=> Зачет аванса." + РазделительСтрок
			               + "   выполнены работы на сумму: "+ИтогВсего+", зачтен аванс в размере: "+ЗачетАванса + РазделительСтрок
						   + "   - определен аванс по договору "+Договор+" в сумме: "+АвансПоДоговору+", зачтен аванс в размере: "+ЗачетАвансаПоДоговору + РазделительСтрок;
			
			Если ЗачитыватьАванс = 0 Тогда
				ТекстСообщения = ТекстСообщения
							   + "   - определен аванс без указания договора в сумме: "+АвансБезДоговора+", зачтен аванс в размере: "+ЗачетАвансаБезДоговора;
			Иначе
				ТекстСообщения = ТекстСообщения
				               + "   - аванс без указания договора не учтен";
			КонецЕсли;
			глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		КонецЕсли;
		
		ТаблицаРеализации.Свернуть("ВидНоменклатуры, СтавкаНП","Всего, НП");
		Если (ИтогВсего > 0) и (ВерсияОбъекта >= "7.70.421") Тогда
			К = ЗачетАванса / ИтогВсего;
			ТаблицаРеализации.ВыбратьСтроки();
			Пока ТаблицаРеализации.ПолучитьСтроку() = 1 Цикл
				Если ТаблицаРеализации.НП = 0 Тогда
				    Продолжить;
				КонецЕсли;
			    
				НПкУплате = К*ТаблицаРеализации.НП;
				ОтложеноНП = ТаблицаРеализации.НП - НПкУплате;
				Если НПкУплате > 0 Тогда // был зачет аванса
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ТВ";
					Операция.СодержаниеПроводки = "Начислен НП";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = Сч90_6;
					Операция.Дебет.ВидыНоменклатуры = ТаблицаРеализации.ВидНоменклатуры;
					Операция.Дебет.СтавкиНП = ТаблицаРеализации.СтавкаНП;
					Операция.Кредит.Счет = Сч68_5;
					Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
					Если ВестиУчетРасчетовУЕ = 1 Тогда
						Операция.Сумма = НПкУплате*КурсВалюты/Кратность;
					Иначе
						Операция.Сумма = НПкУплате;
					КонецЕсли;
				КонецЕсли;
				
				Если ОтложеноНП > 0 Тогда
				    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ТВ";
					Операция.СодержаниеПроводки = "Начислен НП";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = Сч90_6;
					Операция.Дебет.ВидыНоменклатуры = ТаблицаРеализации.ВидНоменклатуры;
					Операция.Дебет.СтавкиНП = ТаблицаРеализации.СтавкаНП;
					Операция.Кредит.Счет = Сч76_Н_4;
					Операция.Кредит.Контрагенты = Контрагент;
					Операция.Кредит.Договоры = Договор;
					Если ВестиУчетРасчетовУЕ = 1 Тогда
					    Операция.Сумма = ОтложеноНП*КурсВалюты/Кратность;
					Иначе
						Операция.Сумма = ОтложеноНП;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;  
	
	
	//********************************************************************
	//Проводки по расчетам в вал. 
	Если (Константа.ПрименяетсяПБУ18.Получить(ДатаДок) = Да) Тогда
		Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
			Если НПРКурсоваяРазница <> 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "НУ";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетПоКоду("НПР.99");
				Операция.Сумма = -НПРКурсоваяРазница;
				Операция.СодержаниеПроводки = "Разница в оценке дохода"; 
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
	//********************************************************************

	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры