////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем СписокСписанныхОбъектов;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//******************************************************************************
// СформироватьПроводкиВыбытияОбъекта()
//
Процедура СформироватьПроводкиВыбытияОбъекта(Счет07=0, Счет09=0)
	
	Если СписокСписанныхОбъектов.НайтиЗначение(Объект) > 0 Тогда
        Возврат;
		
	ИначеЕсли Объект.Вид() = "ОсновныеСредства" Тогда
	    СчетУчетаСтоимости = СчетПоКоду("Н05.01");
		СчетНачисленияАмортизации = СчетПоКоду("Н05.02");
		СчетЗатрат = СчетПоКоду("Н07.07");
		СведенияОбОбъекте = глРасчетАмортизацииОС(Объект, ДатаДок);
		
	ИначеЕсли Объект.Вид() = "НематериальныеАктивы" Тогда
		СчетУчетаСтоимости = СчетПоКоду("Н05.03");
		СчетНачисленияАмортизации = СчетПоКоду("Н05.04");
		СчетЗатрат = СчетПоКоду("Н07.08");
		СведенияОбОбъекте = глРасчетАмортизацииНМА(Объект, ДатаДок);
		
	Иначе
		Возврат;
	КонецЕсли;
	
	РассчитаннаяАмортизация = СведенияОбОбъекте.Получить("РассчитаннаяАмортизацияНалог");
	НачисленнаяАмортизация = РассчитаннаяАмортизация + СведенияОбОбъекте.Получить("НачисленнаяАмортизацияНалог");
	ОстаточнаяСтоимость = СведенияОбОбъекте.Получить("БалансоваяСтоимостьНалогКон") - НачисленнаяАмортизация;
	
	Если РассчитаннаяАмортизация <> 0  Тогда
		СчетИАналитикаДляОтнесенияРасходов = глПолучитьСчетРасходовДляЦелейНалоговогоУчета(Объект.ВидРасхода.Получить(ДатаДок), Объект.ЭлементРасхода.Получить(ДатаДок), Объект.Объект.Получить(ДатаДок));
		СчетРасходов = СчетИАналитикаДляОтнесенияРасходов.Получить("Счет");
		Если ПустоеЗначение(СчетРасходов) = 1 Тогда
			ТекстСообщения = "На закладке ""Налоговый учет"" неверно указано направление отнесения расходов по начисленной амортизации для объекта: "
						+ Объект.Наименование + ", инв.№" + Объект.Код + ".";
			глНеПроводить(Контекст, ТекстСообщения, Объект.ТекущийЭлемент());
			Возврат;
		КонецЕсли;
		
	    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.СодержаниеПроводки = "Доначислена амортизация";
		Операция.НомерЖурнала = "НУ";
	    Операция.Дебет.Счет = СчетРасходов;
		Операция.Дебет.Субконто(1, СчетИАналитикаДляОтнесенияРасходов.Получить("Субконто1"));
		Операция.Дебет.Субконто(2, СчетИАналитикаДляОтнесенияРасходов.Получить("Субконто2"));
		Операция.Дебет.Субконто(3, СчетИАналитикаДляОтнесенияРасходов.Получить("Субконто3"));
		Операция.Кредит.Счет = СчетНачисленияАмортизации;
		Операция.Кредит.Субконто(1, Объект);
		Операция.Сумма = РассчитаннаяАмортизация;
	КонецЕсли;
	
	Если НачисленнаяАмортизация <> 0  Тогда
	    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.СодержаниеПроводки = "Списана амортизация";
		Операция.НомерЖурнала = "НУ";
		Операция.Дебет.Счет = СчетНачисленияАмортизации;
		Операция.Дебет.Субконто(1, Объект);
		Операция.Кредит.Счет = СчетУчетаСтоимости;
		Операция.Кредит.Субконто(1, Объект);
		Операция.Сумма = НачисленнаяАмортизация;
	КонецЕсли;
	
	Если ОстаточнаяСтоимость <> 0  Тогда
	    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.СодержаниеПроводки = "Остаточная стоимость";
		Операция.НомерЖурнала = "НУ";
		Если Счет07 = 1 Тогда
			Операция.Дебет.Счет = СчетЗатрат;
			Операция.Дебет.Субконто(1, Объект);
		
		ИначеЕсли Счет09 = 1 Тогда
			Операция.Дебет.Счет = СчетПоКоду("Н09");
			Операция.Дебет.ВнереализационныеРасходы = Перечисление.ВнереализационныеРасходы.ПрочиеВнереализационныеРасходы;
		КонецЕсли;
		Операция.Кредит.Счет = СчетУчетаСтоимости;
		Операция.Кредит.Субконто(1, Объект);
		Операция.Сумма = ОстаточнаяСтоимость;
	КонецЕсли;
	
	СписокСписанныхОбъектов.ДобавитьЗначение(Объект);
	
КонецПроцедуры // СформироватьПроводкиВыбытияОбъекта()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//******************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()
    
	Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
		Предупреждение("Документ не формирует проводок, т.к. установлен способ ведения
					   |налогового учета - ""Одновременно с бухгалтерским учетом"" !");
		Возврат;
	КонецЕсли;
	
	СписокСписанныхОбъектов = СоздатьОбъект("СписокЗначений");
	СписокДоговоров = СоздатьОбъект("СписокЗначений");
	СписокЦенныхБумаг = СоздатьОбъект("СписокЗначений");
	СписокКонтрагентов = СоздатьОбъект("СписокЗначений");
	СписокДоговоровКонтрагентов = СоздатьОбъект("СписокЗначений");
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
	    Если Объект.Вид() = "Договоры" Тогда
	        СписокДоговоров.ДобавитьЗначение(Объект);
			
		ИначеЕсли Объект.Вид() = "ЦенныеБумаги" Тогда
			СписокЦенныхБумаг.ДобавитьЗначение(Объект);
		КонецЕсли;
		
		Если (УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВозвратТоваровПоставщику)
	      или (УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВозвратМатериаловПоставщику ) Тогда
			СписокКонтрагентов.ДобавитьЗначение(Контрагент);
			СписокДоговоровКонтрагентов.ДобавитьЗначение(Договор);
		КонецЕсли;

	КонецЦикла;
	
	Если СписокЦенныхБумаг.РазмерСписка() > 0 Тогда
		БИН0205 = СоздатьОбъект("БухгалтерскиеИтоги"); БИН0205.ИспользоватьРазделительУчета(ЮрЛицо);
		БИН0205.ИспользоватьСубконто(ВидыСубконто.ЦенныеБумаги, СписокЦенныхБумаг, 2);
		БИН0205.ВыполнитьЗапрос(ДатаДок, ДатаДок, "Н02.05");
	КонецЕсли;
	
	Если СписокДоговоров.РазмерСписка() > 0 Тогда
		БИН0206 = СоздатьОбъект("БухгалтерскиеИтоги"); БИН0206.ИспользоватьРазделительУчета(ЮрЛицо);
		БИН0206.ИспользоватьСубконто(ВидыСубконто.Договоры, СписокДоговоров, 2);
		БИН0206.ВыполнитьЗапрос(ДатаДок, ДатаДок, "Н02.06");
	КонецЕсли;
	
	Если СписокКонтрагентов.РазмерСписка() > 0 Тогда
		БИН13 = СоздатьОбъект("БухгалтерскиеИтоги"); БИН13.ИспользоватьРазделительУчета(ЮрЛицо);
		БИН13.ИспользоватьСубконто(ВидыСубконто.Контрагенты, СписокКонтрагентов, 2);
		БИН13.ИспользоватьСубконто(ВидыСубконто.Договоры, СписокДоговоровКонтрагентов, 2);
		БИН13.ВыполнитьЗапрос(ДатаДок, ДатаДок, "Н13.02, Н13.03");
	КонецЕсли;
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(УсловиеВыбытия) = 1 Тогда
			ТекстСообщения = "В строке №" + НомерСтроки + " не указано условие выбытия.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		ПривязыватьСтроку(НомерСтроки);

		Если УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.СПоследующейОплатой Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.СодержаниеПроводки = НаименованиеОперации;
			Операция.НомерЖурнала = "НУ";
			Операция.Сумма = Задолженность;
			Операция.Дебет.Счет = СчетПоКоду("Н13.01");
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;

		ИначеЕсли УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВСчетРанееПолученныхАвансов Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.СодержаниеПроводки = НаименованиеОперации;
			Операция.НомерЖурнала = "НУ";
			Операция.Сумма = Задолженность;
			Операция.Дебет.Счет = СчетПоКоду("Н13.02");
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;
			
			Если НДС <> 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = НаименованиеОперации;
				Операция.НомерЖурнала = "НУ";
				Операция.Сумма = НДС;
				Операция.Дебет.Счет = СчетПоКоду("Н13.03");
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.Договоры = Договор;
			КонецЕсли;

		ИначеЕсли (УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВозвратТоваровПоставщику)
		      или (УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВозвратМатериаловПоставщику) Тогда
			                                                                                    
			СуммаКредитроскойЗадолженности    = 0;
			СуммаНДСКредитроскойЗадолженности = 0;
			Если (БИН13.ПолучитьСубконто(ВидыСубконто.Контрагенты,, Контрагент) = 1) Тогда
				Если (БИН13.ПолучитьСубконто(ВидыСубконто.Договоры,, Договор) = 1) Тогда
					БИН13.ВыбратьСчета();
					Пока БИН13.ПолучитьСчет() = 1 Цикл
					    Если БИН13.Счет = СчетПоКоду("Н13.02") Тогда
							СуммаКредитроскойЗадолженности = БИН13.СКК();	
							
						ИначеЕсли БИН13.Счет = СчетПоКоду("Н13.03") Тогда
					        СуммаНДСКредитроскойЗадолженности = БИН13.СКК();
					    КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
			СуммаСписываемойКредиторскойЗадолженности     = Мин(СуммаКредитроскойЗадолженности, Задолженность);
			СуммаСписываемогоНДСКредиторскойЗадолженности = Мин(СуммаНДСКредитроскойЗадолженности, НДС);
			СуммаДебиторскойЗадолженности = Задолженность - СуммаСписываемойКредиторскойЗадолженности;
			
			Если СуммаСписываемойКредиторскойЗадолженности <> 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = НаименованиеОперации;
				Операция.НомерЖурнала = "НУ";
				Операция.Сумма = -СуммаСписываемойКредиторскойЗадолженности;
				Операция.Кредит.Счет = СчетПоКоду("Н13.02");
				Операция.Кредит.Контрагенты = Контрагент;
				Операция.Кредит.Договоры = Договор;
				
				Если НДС <> 0 Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.СодержаниеПроводки = НаименованиеОперации;
					Операция.НомерЖурнала = "НУ";
					Операция.Сумма = -СуммаСписываемогоНДСКредиторскойЗадолженности;
					Операция.Кредит.Счет = СчетПоКоду("Н13.03");
					Операция.Кредит.Контрагенты = Контрагент;
					Операция.Кредит.Договоры = Договор;
				КонецЕсли;    
			КонецЕсли;
			
			Если СуммаДебиторскойЗадолженности <> 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = НаименованиеОперации;
				Операция.НомерЖурнала = "НУ";
				Операция.Сумма = СуммаДебиторскойЗадолженности;
				Операция.Дебет.Счет = СчетПоКоду("Н13.01");
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.Договоры = Договор;    
			КонецЕсли;
			
		ИначеЕсли УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ПоДоговоруЗайма Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.СодержаниеПроводки = НаименованиеОперации;
			Операция.НомерЖурнала = "НУ";
			Операция.Сумма = Задолженность;
			Операция.Дебет.Счет = СчетПоКоду("Н13.01");
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;

		ИначеЕсли УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВРамкахЦелевогоФинансирования Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.СодержаниеПроводки = НаименованиеОперации;
			Операция.НомерЖурнала = "НУ";
			Операция.Сумма = Задолженность;
			Операция.Дебет.Счет = СчетПоКоду("Н12");
		КонецЕсли;

		Если УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВозвратТоваровПоставщику Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
				
			ИначеЕсли Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Товар Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = НаименованиеОперации;
				Операция.НомерЖурнала = "НУ";
				Операция.Сумма = Сумма;
				Операция.Кредит.Счет = СчетПоКоду("Н02.02.1");
				Операция.Кредит.Номенклатура = Объект;

			Иначе
				ТекстСообщения = "В строке №" + НомерСтроки + " указан объект учета, не являющийся товаром.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;

		ИначеЕсли УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВозвратМатериаловПоставщику Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
				
			Иначе
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = НаименованиеОперации;
				Операция.НомерЖурнала = "НУ";
				Операция.Сумма = Сумма;
				Операция.Кредит.Счет = СчетПоКоду("Н02.01");
				Операция.Кредит.Материалы = Объект;
			КонецЕсли;
			
		ИначеЕсли УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.СписаниеИмущества Тогда
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
		
			ИначеЕсли Объект.Вид() = "Номенклатура" Тогда
				Если (Объект.ТипНоменклатуры <> Перечисление.ТипыНоменклатуры.Товар) и
					 (Объект.ТипНоменклатуры <> Перечисление.ТипыНоменклатуры.Продукция) Тогда
					ТекстСообщения = "В строке №" + НомерСтроки + " указан объект учета, не являющийся товаром или готовой продукцией.";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				КонецЕсли;

			ИначеЕсли Объект.Вид() = "Материалы" Тогда
				
			ИначеЕсли Объект.Вид() = "Оборудование" Тогда
				
			ИначеЕсли Объект.Вид() = "ОсновныеСредства" Тогда // списание ОС
				СформироватьПроводкиВыбытияОбъекта(0, 1);
				
			ИначеЕсли Объект.Вид() = "НематериальныеАктивы" Тогда // списание НМА
				СформироватьПроводкиВыбытияОбъекта(0, 1);
			КонецЕсли;
			
		ИначеЕсли (УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВРамкахЦелевогоФинансирования)
			или (УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВРамкахБлаготворительнойДеятельности)
			или (УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.Безвозмездно) Тогда
				
			Если ПустоеЗначение(Объект) = 1 Тогда
				ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			
			ИначеЕсли Объект.Вид() = "Номенклатура" Тогда
				Если (Объект.ТипНоменклатуры <> Перечисление.ТипыНоменклатуры.Товар) и
					 (Объект.ТипНоменклатуры <> Перечисление.ТипыНоменклатуры.Продукция) Тогда
					ТекстСообщения = "В строке №" + НомерСтроки + " указан объект учета, не являющийся товаром или готовой продукцией.";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				КонецЕсли;

			ИначеЕсли Объект.Вид() = "Материалы" Тогда
				
			ИначеЕсли Объект.Вид() = "Оборудование" Тогда

			ИначеЕсли Объект.Вид() = "ЦенныеБумаги" Тогда
				Если (БИН0205.ПолучитьСубконто(ВидыСубконто.ЦенныеБумаги,, Объект) = 1) и
					 (СписокСписанныхОбъектов.НайтиЗначение(Объект) = 0) Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.СодержаниеПроводки = НаименованиеОперации;
					Операция.НомерЖурнала = "НУ";
					Операция.Сумма = БИН0205.СКД();
					Операция.Кредит.Счет = СчетПоКоду("Н02.05");
					Операция.Кредит.ЦенныеБумаги = Объект;
					СписокСписанныхОбъектов.ДобавитьЗначение(Объект);
				КонецЕсли;
				
			ИначеЕсли Объект.Вид() = "ОсновныеСредства" Тогда // безвозмездная передача ОС
				СформироватьПроводкиВыбытияОбъекта(0, 0);
				
			ИначеЕсли Объект.Вид() = "НематериальныеАктивы" Тогда // безвозмездная передача НМА
				СформироватьПроводкиВыбытияОбъекта(0, 0);
			КонецЕсли;
		ИначеЕсли (УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.Другие)
			    и (ПустоеЗначение(ВидДохода) = 1) Тогда
			   	
			Если Объект.Вид() = "Номенклатура" Тогда
				
			ИначеЕсли Объект.Вид() = "Материалы" Тогда
				
			ИначеЕсли Объект.Вид() = "Оборудование" Тогда

			ИначеЕсли Объект.Вид() = "ЦенныеБумаги" Тогда
				Если (БИН0205.ПолучитьСубконто(ВидыСубконто.ЦенныеБумаги,, Объект) = 1) и
					 (СписокСписанныхОбъектов.НайтиЗначение(Объект) = 0) Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.СодержаниеПроводки = НаименованиеОперации;
					Операция.НомерЖурнала = "НУ";
					Операция.Сумма = БИН0205.СКД();
					Операция.Кредит.Счет = СчетПоКоду("Н02.05");
					Операция.Кредит.ЦенныеБумаги = Объект;
					СписокСписанныхОбъектов.ДобавитьЗначение(Объект);
				КонецЕсли;
				
			ИначеЕсли Объект.Вид() = "ОсновныеСредства" Тогда
				СформироватьПроводкиВыбытияОбъекта(0, 0);
				
			ИначеЕсли Объект.Вид() = "НематериальныеАктивы" Тогда
				СформироватьПроводкиВыбытияОбъекта(0, 0);
			КонецЕсли;
			
		Иначе
			Если (ВидДохода = Перечисление.ВидыДоходов.РеализацияТоваров)
				или (ВидДохода = Перечисление.ВидыДоходов.РеализацияТоваровРанееОтгруженных) Тогда
				Если ПустоеЗначение(Объект) = 1 Тогда
					ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
					
				ИначеЕсли Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Товар Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.СодержаниеПроводки = НаименованиеОперации;
					Операция.НомерЖурнала = "НУ";
					Операция.Сумма = Сумма;
					Операция.Кредит.Счет = СчетПоКоду("Н06.04");
					
				Иначе
					ТекстСообщения = "В строке №" + НомерСтроки + " указан объект учета, не являющийся товаром.";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				КонецЕсли;

			ИначеЕсли (ВидДохода = Перечисление.ВидыДоходов.РеализацияПродукции)
				или (ВидДохода = Перечисление.ВидыДоходов.РеализацияПродукцииРанееОтгруженной)Тогда
				Если ПустоеЗначение(Объект) = 1 Тогда
					ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
					
				ИначеЕсли (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Продукция)
					  или (((Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Услуга) 
					  или (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.ПосредническаяУслуга)
					  или (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Работа))
					    и (ВидДохода = Перечисление.ВидыДоходов.РеализацияПродукции)) Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.СодержаниеПроводки = НаименованиеОперации;
					Операция.НомерЖурнала = "НУ";
					Операция.Сумма = Сумма;
					Операция.Кредит.Счет = СчетПоКоду("Н06.01");
		
				Иначе	
					ТекстСообщения = "В строке №" + НомерСтроки + " указан объект учета, не являющийся готовой продукцией  (услугой, работой).";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				КонецЕсли;
				
			ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияПродукцииОбслуживающихПроизводств Тогда
				Если ПустоеЗначение(Объект) = 1 Тогда
					ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
					
				ИначеЕсли (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Продукция)
					  или (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Услуга) 
					  или (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.ПосредническаяУслуга)
					  или (Объект.ТипНоменклатуры = Перечисление.ТипыНоменклатуры.Работа) Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.СодержаниеПроводки = НаименованиеОперации;
					Операция.НомерЖурнала = "НУ";
					Операция.Сумма = Сумма;
					Операция.Кредит.Счет = СчетПоКоду("Н06.10");
	
				Иначе	
					ТекстСообщения = "В строке №" + НомерСтроки + " указан объект учета, не являющийся готовой продукцией (услугой, работой).";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				КонецЕсли;

			ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияОсновныхСредств Тогда // передача ОС
				Если ПустоеЗначение(Объект) = 1 Тогда
					ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект ОС.";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				КонецЕсли;
				
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = НаименованиеОперации;
				Операция.НомерЖурнала = "НУ";
				Операция.Сумма = Сумма;
				Операция.Кредит.Счет = СчетПоКоду("Н06.05");
				Операция.Кредит.ОсновныеСредства = Объект;
								
				СформироватьПроводкиВыбытияОбъекта(?(Сумма = 0, 0, 1), 0);

			ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияНематериальныхАктивов Тогда // передача НМА
				Если ПустоеЗначение(Объект) = 1 Тогда
					ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект НМА.";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				КонецЕсли;
				
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = НаименованиеОперации;
				Операция.НомерЖурнала = "НУ";
				Операция.Сумма = Сумма;
				Операция.Кредит.Счет = СчетПоКоду("Н06.06");
				Операция.Кредит.НематериальныеАктивы = Объект;
				
				СформироватьПроводкиВыбытияОбъекта(?(Сумма = 0, 0, 1), 0);

			ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияПрочегоИмущества Тогда
				Если ПустоеЗначение(Объект) = 1 Тогда
					ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				КонецЕсли;
				
				Если Объект.Вид() = "ЦенныеБумаги" Тогда
				    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.СодержаниеПроводки = НаименованиеОперации;
					Операция.НомерЖурнала = "НУ";
					Операция.Сумма = Сумма;
					Операция.Кредит.Счет = СчетПоКоду("Н06.11");
					Операция.Кредит.ЦенныеБумаги = Объект;
					
					Если (БИН0205.ПолучитьСубконто(ВидыСубконто.ЦенныеБумаги,, Объект) = 1) и
						 (СписокСписанныхОбъектов.НайтиЗначение(Объект) = 0) Тогда
						Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
						Операция.СодержаниеПроводки = НаименованиеОперации;
						Операция.НомерЖурнала = "НУ";
						Операция.Сумма = БИН0205.СКД();
						Операция.Дебет.Счет = СчетПоКоду("Н07.14");
						Операция.Дебет.ЦенныеБумаги = Объект;
						Операция.Кредит.Счет = СчетПоКоду("Н02.05");
						Операция.Кредит.ЦенныеБумаги = Объект;
						СписокСписанныхОбъектов.ДобавитьЗначение(Объект);
					КонецЕсли;
				
				Иначе
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.СодержаниеПроводки = НаименованиеОперации;
					Операция.НомерЖурнала = "НУ";
					Операция.Сумма = Сумма;
					Операция.Кредит.Счет = СчетПоКоду("Н06.03");
				КонецЕсли;

			ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияПраваТребованияВРамкахФинансовыхУслуг Тогда
				Если ПустоеЗначение(Объект) = 1 Тогда
					ТекстСообщения = "В строке №" + НомерСтроки + " не указан объект учета.";
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				КонецЕсли;
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = НаименованиеОперации;
				Операция.НомерЖурнала = "НУ";
				Операция.Кредит.Счет = СчетПоКоду("Н06.07");
				Операция.Кредит.Договоры = Объект;
				Операция.Сумма = Сумма;
				
				Если УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.Другие Тогда // возврат долга должником
					СтоимостьЗадолженности = Задолженность;
					
				Иначе // уступка права требования третьему лицу
				    Если (БИН0206.ПолучитьСубконто(ВидыСубконто.Договоры,, Объект) = 1) и
						 (СписокСписанныхОбъектов.НайтиЗначение(Объект) = 0) Тогда
						СтоимостьЗадолженности = БИН0206.СКД();
						СписокСписанныхОбъектов.ДобавитьЗначение(Объект);
						
					Иначе
						СтоимостьЗадолженности = 0;
					КонецЕсли;
				КонецЕсли;
					
				Если СтоимостьЗадолженности <> 0 Тогда
				    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.СодержаниеПроводки = НаименованиеОперации;
					Операция.НомерЖурнала = "НУ";
					Операция.Дебет.Счет = СчетПоКоду("Н07.09");
					Операция.Дебет.Контрагенты = Объект.Владелец;
					Операция.Дебет.Договоры = Объект;
					Операция.Кредит.Счет = СчетПоКоду("Н02.06");
					Операция.Кредит.Договоры = Объект;
					Операция.Сумма = СтоимостьЗадолженности;
				
					ПривязыватьСтроку(НомерСтроки);
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.СодержаниеПроводки = НаименованиеОперации;
					Операция.НомерЖурнала = "НУ";
					Операция.Кредит.Счет = СчетПоКоду("Н13.01");
					Операция.Кредит.Контрагенты = Объект.Владелец;
					Операция.Кредит.Договоры = Объект;
					Операция.Сумма = СтоимостьЗадолженности;
				КонецЕсли;

			ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияУступкиПраваТребованияДоНаступленияСрокаПлатежа Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = НаименованиеОперации;
				Операция.НомерЖурнала = "НУ";
				Операция.Кредит.Счет = СчетПоКоду("Н06.08");
				Операция.Кредит.Договоры = Договор;
				Операция.Сумма = Сумма;

			ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияУступкиПраваТребованияПослеНаступленияСрокаПлатежа Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = НаименованиеОперации;
				Операция.НомерЖурнала = "НУ";
				Операция.Кредит.Счет = СчетПоКоду("Н06.09");
				Операция.Кредит.Договоры = Договор;
				Операция.Сумма = Сумма;
	
			ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияПрочихИмущественныхПрав Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.СодержаниеПроводки = НаименованиеОперации;
				Операция.НомерЖурнала = "НУ";
				Операция.Кредит.Счет = СчетПоКоду("Н06.02");
				Операция.Сумма = Сумма;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);

КонецПроцедуры // ОбработкаПроведения()
