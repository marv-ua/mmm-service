////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем НачальнаяДатаДокумента; 
Перем Новый;
Перем СписокДействий;
Перем СписокДокументов;
Перем СписокВидовДоходов;
Перем СписокВидовДокументовДляВыбора;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//*****************************************************************************
//
// ИтоговаяСтрока()
// 
// Вызывается из формул элементов диалога:
//  Текст ИтоговаяСтрока.
//
// Описание:
//  Формирует строку, содержащую итоговые суммы колонок табличной части.
//
Функция ИтоговаяСтрока()
	
	Возврат "Итого на сумму: " + СокрЛ(Формат(Итог("Сумма"),"Ч20.2-,"));
	
КонецФункции // ИтоговаяСтрока()

//******************************************************************************
//
// ПриИзмененииВидаДохода()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Графа "Вид дохода".
//
// Описание:
//  Очищает поле объекта, если вид дохода изменился.
//
Процедура ПриИзмененииВидаДохода()
	
	ВидыОбъекта = "";
	Если ВидДохода = Перечисление.ВидыДоходов.РеализацияТоваров Тогда
		ВидыОбъекта = "Номенклатура";
	    
	ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияПродукции Тогда
		ВидыОбъекта = "Номенклатура";

	ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияПродукцииОбслуживающихПроизводств Тогда
		ВидыОбъекта = "Номенклатура";

	ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияОсновныхСредств Тогда
		ВидыОбъекта = "ОсновныеСредства";
		
	ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияНематериальныхАктивов Тогда
		ВидыОбъекта = "НематериальныеАктивы";
		
	ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияПрочегоИмущества Тогда
		ВидыОбъекта = "Материалы,Оборудование,ОбъектыСтроительства,ЦенныеБумаги";
		
	ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияПраваТребованияВРамкахФинансовыхУслуг Тогда
		ВидыОбъекта = "Договоры";
		
	ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияУступкиПраваТребованияДоНаступленияСрокаПлатежа Тогда
		Объект = "";
		Форма.Объект.Доступность(0);
		
	ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияУступкиПраваТребованияПослеНаступленияСрокаПлатежа Тогда
		Объект = "";
		Форма.Объект.Доступность(0);

	ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияПрочихИмущественныхПрав Тогда
		Объект = "";
		Форма.Объект.Доступность(0);

	ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.ВнереализационныеДоходы Тогда
		Объект = "";
		Форма.Объект.Доступность(0); 
		
	ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияТоваровРанееОтгруженных Тогда
		ВидыОбъекта = "Номенклатура";	
		
	ИначеЕсли ВидДохода = Перечисление.ВидыДоходов.РеализацияПродукцииРанееОтгруженной Тогда
		ВидыОбъекта = "Номенклатура";
		 
	ИначеЕсли (ПустоеЗначение(ВидДохода) = 1) 
	        и (УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.Другие) Тогда
		ВидыОбъекта = "Номенклатура,Материалы,Оборудование,ЦенныеБумаги,ОсновныеСредства,НематериальныеАктивы";
		
	Иначе
		Объект = "";
		Форма.Объект.Доступность(0); 
	КонецЕсли;
	
	Если ПустоеЗначение(ВидыОбъекта) = 0 Тогда
		Если Найти("," + ВидыОбъекта + ",", "," + Объект.Вид() + ",") = 0 Тогда
		    Объект = "";
		КонецЕсли;
		Объект.ВидыДляВыбора(ВидыОбъекта);
		Форма.Объект.Доступность(1);
	КонецЕсли;

КонецПроцедуры // ПриИзмененииВидаДохода()

//******************************************************************************
//
// ПриИзмененииУсловияВыбытия()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура управления реквозитами.
//
Процедура ПриИзмененииУсловияВыбытия()
	
	ВидыОбъекта = "";
	Если УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВозвратТоваровПоставщику Тогда
		ВидДохода = "";
		Форма.ВидДохода.Доступность(0);
		ВидыОбъекта = "Номенклатура";
		
	ИначеЕсли УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВозвратМатериаловПоставщику Тогда
		ВидДохода = "";
		Форма.ВидДохода.Доступность(0);
		ВидыОбъекта = "Материалы";
		
	ИначеЕсли УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.СписаниеИмущества Тогда
		ВидДохода = "";
		Форма.ВидДохода.Доступность(0);
		ВидыОбъекта = "Номенклатура,Материалы,Оборудование,ОсновныеСредства,НематериальныеАктивы";
		
	ИначеЕсли УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВРамкахЦелевогоФинансирования Тогда
		ВидДохода = "";
		Форма.ВидДохода.Доступность(0);
		ВидыОбъекта = "Номенклатура,Материалы,Оборудование,ЦенныеБумаги";
	    
	ИначеЕсли УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.ВРамкахБлаготворительнойДеятельности Тогда
		ВидДохода = "";
		Форма.ВидДохода.Доступность(0);
		ВидыОбъекта = "Номенклатура,Материалы,Оборудование,ЦенныеБумаги";

	ИначеЕсли УсловиеВыбытия = Перечисление.УсловияВыбытияИмущества.Безвозмездно Тогда
		ВидДохода = "";
		Форма.ВидДохода.Доступность(0);
		ВидыОбъекта = "Номенклатура,Материалы,Оборудование,ЦенныеБумаги,ОсновныеСредства,НематериальныеАктивы";
		
	Иначе
		Форма.ВидДохода.Доступность(1);
		Форма.Объект.Доступность(0);
		ПриИзмененииВидаДохода();
	КонецЕсли;
	
	Если ПустоеЗначение(ВидыОбъекта) = 0 Тогда
		Если Найти("," + ВидыОбъекта + ",", "," + Объект.Вид() + ",") = 0 Тогда
		    Объект = "";
		КонецЕсли;
		Объект.ВидыДляВыбора(ВидыОбъекта);
		Форма.Объект.Доступность(1);
	КонецЕсли;

КонецПроцедуры // ПриИзмененииУсловияВыбытия()

//******************************************************************************
// Заполнить()
//
// Параметры: Нет.
//
// Вызывается из формул элементов диалога: Кнопка "Заполнить".
//
// Описание:
//  Заполняет табличную часть документа по данным учета.
//
Процедура Заполнить()
	
	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.Установить("КонтекстДокумента", Контекст);
	ОткрытьФормуМодально("Обработка.ФормированиеНалоговыхДокументов", СписокПараметров);
	
КонецПроцедуры // Заполнить()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	Если Копирование = 1 Тогда //копирование документа
	    Возврат;
	КонецЕсли;

КонецПроцедуры // ВводНового()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	                   
	Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
		Предупреждение("Данные, введенные в документ не будут влиять на
		               |налоговый учет, т.к. установлен способ ведения
					   |налогового учета - ""Одновременно с бухгалтерским учетом"" !");
	КонецЕсли;
				
	НачальнаяДатаДокумента = ДатаДок;
	ПриЗаписиПерепроводить(1);
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаЗаполнить.Доступность(0);
	КонецЕсли;

	Форма.УсловиеВыбытия.ВыполнятьФормулуТолькоПриИзменении(1);
	
	СписокВидовДоходов = СоздатьОбъект("СписокЗначений");
	СписокВидовДоходов.ДобавитьЗначение(Перечисление.ВидыДоходов.РеализацияТоваров);
	СписокВидовДоходов.ДобавитьЗначение(Перечисление.ВидыДоходов.РеализацияТоваровРанееОтгруженных);
	СписокВидовДоходов.ДобавитьЗначение(Перечисление.ВидыДоходов.РеализацияПродукции);
	СписокВидовДоходов.ДобавитьЗначение(Перечисление.ВидыДоходов.РеализацияПродукцииРанееОтгруженной);
	СписокВидовДоходов.ДобавитьЗначение(Перечисление.ВидыДоходов.РеализацияПродукцииОбслуживающихПроизводств);
	СписокВидовДоходов.ДобавитьЗначение(Перечисление.ВидыДоходов.РеализацияОсновныхСредств);
	СписокВидовДоходов.ДобавитьЗначение(Перечисление.ВидыДоходов.РеализацияНематериальныхАктивов);
	СписокВидовДоходов.ДобавитьЗначение(Перечисление.ВидыДоходов.РеализацияПрочегоИмущества);
	СписокВидовДоходов.ДобавитьЗначение(Перечисление.ВидыДоходов.РеализацияПраваТребованияВРамкахФинансовыхУслуг);
	СписокВидовДоходов.ДобавитьЗначение(Перечисление.ВидыДоходов.РеализацияУступкиПраваТребованияДоНаступленияСрокаПлатежа);
	СписокВидовДоходов.ДобавитьЗначение(Перечисление.ВидыДоходов.РеализацияУступкиПраваТребованияПослеНаступленияСрокаПлатежа);
	СписокВидовДоходов.ДобавитьЗначение(Перечисление.ВидыДоходов.РеализацияПрочихИмущественныхПрав);
	
КонецПроцедуры // ПриОткрытии()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриРедактированииНовойСтроки()

	ПриИзмененииУсловияВыбытия();
	ДокументБУ.ВидыДляВыбора(СписокВидовДокументовДляВыбора);

КонецПроцедуры // ПриРедактированииНовойСтроки()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеРедактированияСтроки()
                                
	Если (Форма.ТекущаяКолонка() = "ОткрытьДокумент") Тогда
		Если ДокументБУ.Выбран()=0 Тогда
			Предупреждение("Не могу открыть документ, отражающий данную операцию
			                |в бухгалтеском учете: документ не указан!",60);
		Иначе     
			ОткрытьФорму(ДокументБУ);
		КонецЕсли;
		
	Иначе
		ПриИзмененииУсловияВыбытия();
		ДокументБУ.ВидыДляВыбора(СписокВидовДокументовДляВыбора);
	КонецЕсли;

КонецПроцедуры // ПриНачалеРедактированияСтроки()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, Флаг)
	
	Если ЭлементДиалога = "ВидДохода" Тогда
		Флаг = 0;                                                            
		Позиция = СписокВидовДоходов.НайтиЗначение(ВидДохода);
		СписокВидовДоходов.ВыбратьЗначение(ВидДохода,, Позиция,, 2);
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи() //предопределенная
	
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
		
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;

	КонецЕсли;
	
	Операция.СуммаОперации = Итог("Сумма");
	Операция.Содержание = "Операции выбытия имущества";
	
КонецПроцедуры // ПриЗаписи()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Новый = 0;

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности"); 
СписокДействий.ДобавитьЗначение("Перейти  в журнал");

СписокВидовДокументовДляВыбора = "ВыполнениеЭтапаРабот,ОказаниеУслуг,РасходнаяНакладная,РеализацияОтгруженнойПродукции,ОтпускМатериаловНаСторону,ПродажаВРозницу,ПрекращениеСрокаДействияНМА,СписаниеОС,ПередачаНМА,ПередачаОС,Выписка,Операция";