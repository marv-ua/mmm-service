////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;
Перем ПодписьСтатьиОтчисленийНаСоцНужды;
Перем ПодписьСтатьиПрочихОтчисленийНаСоцНужды;
Перем НачальнаяДатаДокумента; 
Перем Новый;
Перем СписокДействий;
Перем СписокХарактеровРаботы;


//******************************************************************************
Процедура ПриИзмененииФлагаВзносов()
	
	НеВзиматьВзносыФОМС = НеВзиматьВзносыПФРиФСС;
	
КонецПроцедуры

//******************************************************************************
// МожноВыбиратьСтатьюОтчислений()
//
Функция МожноВыбиратьСтатьюОтчислений()
	
	Если СчетЗатрат.Выбран() = 1 Тогда
		Для А = 1 По 3 Цикл
			Если (СчетЗатрат.ВидСубконто(А) = ВидыСубконто.СтатьиЗатратНаПроизводство) или
				 (СчетЗатрат.ВидСубконто(А) = ВидыСубконто.СтатьиЗатратОбслуживающихПроизводств) или
				 (СчетЗатрат.ВидСубконто(А) = ВидыСубконто.СтатьиОбщепроизводственныхЗатрат) или
				 (СчетЗатрат.ВидСубконто(А) = ВидыСубконто.СтатьиОбщехозяйственныхЗатрат) или
				 (СчетЗатрат.ВидСубконто(А) = ВидыСубконто.СтатьиИздержекОбращения) или
				 (СчетЗатрат.ВидСубконто(А) = ВидыСубконто.СтатьиКоммерческихРасходов) или
				 (СчетЗатрат.ВидСубконто(А) = ВидыСубконто.СтатьиРасходовНаПродажу) Тогда
				Возврат А;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции // МожноВыбиратьСтатьюОтчислений()

//*****************************************************************************
// ПриВыбореСчета()
//
// Вызывается из формул элементов диалога:
//  Поле ввода СчетЗатрат.
//
// Описание:
//  Устанавливает тип объектов аналитического учета по счету затрат.
//
Процедура ПриВыбореСчета(НеПоказаватьСтатьюОтчислений=0)

	Для А = 1 По 3 Цикл
		НазначитьТип("Субконто"+А,СчетЗатрат.ВидСубконто(А));
	КонецЦикла;
        
	НомерСубконто = МожноВыбиратьСтатьюОтчислений();
	Если НомерСубконто > 0 Тогда 
		
		Если ДатаГод(ДатаДок) < 2010 Тогда
			ПодписьСтатьиОтчисленийНаСоцНужды = "Статья затрат по начисленному ЕСН";
			ПодписьСтатьиПрочихОтчисленийНаСоцНужды = "Статья затрат по прочим отчисл. с ФОТ";
			Если НеПоказаватьСтатьюОтчислений = 0 Тогда
				Форма.СтатьяПрочихОтчисленийНаСоцНужды.Видимость(1);
			КонецЕсли;
		Иначе
			ПодписьСтатьиОтчисленийНаСоцНужды = "Статья затрат по страховым взносам";
			Форма.СтатьяПрочихОтчисленийНаСоцНужды.Видимость(0);
			ПодписьСтатьиПрочихОтчисленийНаСоцНужды = "";
		КонецЕсли;			
		
		// Если счет устанавливается при выборе сотрудника, то реквизиты остаются невидимы.
		Если НеПоказаватьСтатьюОтчислений = 0 Тогда			
			Форма.СтатьяОтчисленийНаСоцНужды.Видимость(1);
		КонецЕсли;
		НазначитьВид(СтатьяОтчисленийНаСоцНужды, СчетЗатрат.ВидСубконто(НомерСубконто));
		НазначитьВид(СтатьяПрочихОтчисленийНаСоцНужды, СчетЗатрат.ВидСубконто(НомерСубконто));
		
	Иначе
		ПодписьСтатьиОтчисленийНаСоцНужды = "";
		ПодписьСтатьиПрочихОтчисленийНаСоцНужды = "";
		Форма.СтатьяОтчисленийНаСоцНужды.Видимость(0);
		Форма.СтатьяПрочихОтчисленийНаСоцНужды.Видимость(0);
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореСчета()

//_____________________________________________________________________________
Процедура ОбновитьДанные()
	Если Сотрудник.Выбран()=0 Тогда
	ИначеЕсли Вопрос("Заполнить Приказ данными по сотруднику на дату документа?","Да+Нет")="Да" Тогда
		Подразделение = Сотрудник.Подразделение.Получить(ДатаДок);
		Оклад = Сотрудник.Оклад.Получить(ДатаДок);
		ПроцентСевернойНадбавки = Сотрудник.ПроцентСевернойНадбавки.Получить(ДатаДок);
		Дети = Сотрудник.Дети.Получить(ДатаДок);
		ДетиИнвалиды = Сотрудник.ДетиИнвалиды.Получить(ДатаДок);
		РазмерВычетаНаРебенка = Сотрудник.РазмерВычетаНаРебенка.Получить(ДатаДок);
		РазмерВычетаНаРебенкаИнвалида = Сотрудник.РазмерВычетаНаРебенкаИнвалида.Получить(ДатаДок);
		ТипВычета = Сотрудник.ТипВычета.Получить(ДатаДок);
		СчетЗатрат = Сотрудник.СчетЗатрат.Получить(ДатаДок);
		ПриВыбореСчета(1);
		Субконто1 = Сотрудник.Субконто1.Получить(ДатаДок);
		Субконто2 = Сотрудник.Субконто2.Получить(ДатаДок);
		Субконто3 = Сотрудник.Субконто3.Получить(ДатаДок);
		СтатьяОтчисленийНаСоцНужды = Сотрудник.СтатьяОтчисленийНаСоцНужды.Получить(ДатаДок);
		СтатьяПрочихОтчисленийНаСоцНужды = Сотрудник.СтатьяПрочихОтчисленийНаСоцНужды.Получить(ДатаДок);
		Инвалид = Сотрудник.Инвалид.Получить(ДатаДок);
		
		ХарактерРаботы = Сотрудник.ХарактерРаботы.Получить(ДатаДок);
		Статус = Сотрудник.Статус.Получить(ДатаДок);
		
		НачислятьНалогиСФОТ = Сотрудник.НачислятьНалогиСФОТ.Получить(ДатаДок);
		НеВзиматьВзносыПФРиФСС = Сотрудник.НеВзиматьВзносыПФРиФСС.Получить(ДатаДок);
		НеВзиматьВзносыФОМС = Сотрудник.НеВзиматьВзносыФОМС.Получить(ДатаДок);		
		
	КонецЕсли;
	
	Если ДатаГод(ДатаДок) < 2010 Тогда
		Форма.ИспользоватьСлой("Основной, Общий, НачислятьНалогиСФОТ",2);
	Иначе
		Форма.ИспользоватьСлой("Основной, Общий, НачислятьВзносыСФОТ",2);
	КонецЕсли;
	
КонецПроцедуры //ОбновитьДанные
//_____________________________________________________________________________
Процедура Печать()
	Таб = СоздатьОбъект("Таблица");   
	РубКоп = Строка(Цел(Оклад))+" руб. "+Строка(Формат((Оклад-Цел(Оклад))*100,"Ч(0)2"))+" коп.";
	Надбав = " руб.     коп.";
	Если ДатаГод(ДатаПриказа) > 2000 Тогда
		Таб.ИсходнаяТаблица("Т-5");
	Иначе
		ПредставлениеГода = Лев(ДатаГод(ДатаДок),2);
	КонецЕсли;
	Таб.Вывести();
	Таб.Опции(0,0,0,0, "ОпцииПечатиТ5", "ОкноТ5");
	Таб.ПараметрыСтраницы(1,,,0,0,0,0,0,0);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Приказ",)
КонецПроцедуры

//******************************************************************************
// ПоКнопкеПечать()
// 
// Вызывается из формул элементов диалога:
//  Кнопка "кнПечать".
//
// Описание:
//  Определяется соответствующая печатная форма.
// 	
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		Печать();
		
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(Контекст, "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Вызывается из формул элементов диалога:
//  Кнопка "кнВыбПечать".
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Документ." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()

//_____________________________________________________________________________
Процедура ВыборЗакладки(Номер,Значение)
	Если Значение = 1 Тогда
		
		Если ДатаГод(ДатаДок) < 2010 Тогда
			Форма.ИспользоватьСлой("Основной, Общий, НачислятьНалогиСФОТ",2);
		Иначе
			Форма.ИспользоватьСлой("Основной, Общий, НачислятьВзносыСФОТ",2);
		КонецЕсли;
		
	ИначеЕсли Значение = 2 Тогда
		Форма.ИспользоватьСлой("Основной, ЗП",2);

		Если МожноВыбиратьСтатьюОтчислений() > 0 Тогда
			
			Если ДатаГод(ДатаДок) < 2010 Тогда
				ПодписьСтатьиОтчисленийНаСоцНужды = "Статья затрат по начисленному ЕСН";
				ПодписьСтатьиПрочихОтчисленийНаСоцНужды = "Статья затрат по прочим отчисл. с ФОТ";
				Форма.СтатьяПрочихОтчисленийНаСоцНужды.Видимость(1);
			Иначе
				ПодписьСтатьиОтчисленийНаСоцНужды = "Статья затрат по страховым взносам";
				Форма.СтатьяПрочихОтчисленийНаСоцНужды.Видимость(0);
				ПодписьСтатьиПрочихОтчисленийНаСоцНужды = "";
			КонецЕсли;			
			
			Форма.СтатьяОтчисленийНаСоцНужды.Видимость(1);
			
		Иначе
			ПодписьСтатьиОтчисленийНаСоцНужды = "";
			ПодписьСтатьиПрочихОтчисленийНаСоцНужды = "";
			Форма.СтатьяОтчисленийНаСоцНужды.Видимость(0);
			Форма.СтатьяПрочихОтчисленийНаСоцНужды.Видимость(0);
		КонецЕсли;

	ИначеЕсли Значение = 3 Тогда
		Форма.ИспользоватьСлой("Основной, Дополнительно",2);
	КонецЕсли;
КонецПроцедуры // ВыборЗакладки()
//_____________________________________________________________________________
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Если Константа.УчетЗарплатыВоВнешнейПрограмме = Да Тогда
		Предупреждение("Документ ""Приказ о кадровых изменениях"" не используется в режиме
		               |расчета зарплаты во внешней программе");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Новый = 1;
	Если Копирование = 1 Тогда //копирование документа
	    Возврат;
	КонецЕсли;

	Если глФлагРасшифровки = 1 Тогда
	    Сотрудник = глРасшифровка.Получить("Сотрудник");
	    ОбновитьДанные();
	Иначе
		Подразделение = глЗначениеПоУмолчанию("ОсновноеПодразделение");
	КонецЕсли;

	ДатаПриказа = РабочаяДата();
КонецПроцедуры //ВводНового
//_____________________________________________________________________________
Процедура ПриОткрытии()
	ПриЗаписиПерепроводить(1);
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаЗаписать.Доступность(0);
	КонецЕсли;
	
	Форма.СтатьяОтчисленийНаСоцНужды.НеИзменятьВид(1);
	Форма.СтатьяПрочихОтчисленийНаСоцНужды.НеИзменятьВид(1);
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение(1,"Общие сведения");
	Форма.Закладки.ДобавитьЗначение(2,"Начисление з/пл");
	//Форма.Закладки.ДобавитьЗначение(3,"Налоги и отчисления");
	ВыборЗакладки(,1);
	
	НачальнаяДатаДокумента = ДатаДок;
	
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Документ." + Вид(),ТаблицаПечФорм);
	
КонецПроцедуры //ПриОткрытии
//_____________________________________________________________________________
Процедура ПриВыбореЗакладки(Номер,Значение)
	ВыборЗакладки(Номер,Значение);
КонецПроцедуры //ПриВыбореЗакладки

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, Флаг)
	
	Если (ЭлементДиалога = "СчетЗатрат") и (Константа.ИспользоватьСписокКорректныхПроводок = Да) Тогда
		СписокКорректныхПроводок = СоздатьОбъект("СписокЗначений");
		СписокКорректныхПроводок.Установить("Счет", СчетПоКоду("70"));
		глЗначениеОтбора = СписокКорректныхПроводок;
		
	ИначеЕсли ЭлементДиалога = "СтатьяОтчисленийНаСоцНужды" Тогда
		Если МожноВыбиратьСтатьюОтчислений() > 0 Тогда
			ПриВыбореСчета();
			ОткрытьФорму("Справочник."+СтатьяОтчисленийНаСоцНужды.Вид(), Перечисление.ГруппыВидыРасходов.ЕСН);
			Флаг = 0;
		КонецЕсли;
		
	ИначеЕсли ЭлементДиалога = "СтатьяПрочихОтчисленийНаСоцНужды" Тогда
		Если МожноВыбиратьСтатьюОтчислений() > 0 Тогда
			ПриВыбореСчета();
			Если  ПустоеЗначение(СтатьяПрочихОтчисленийНаСоцНужды) = 1 Тогда
				ОткрытьФорму("Справочник."+СтатьяПрочихОтчисленийНаСоцНужды.Вид(), Перечисление.ГруппыВидыРасходов.ДругиеРасходы);
			Иначе
				Если (СтатьяПрочихОтчисленийНаСоцНужды.ВидРасходовДляНалоговогоУчета = Перечисление.ГруппыВидыРасходов.НалогиИСборы) Тогда
					ОткрытьФорму("Справочник."+СтатьяПрочихОтчисленийНаСоцНужды.Вид());
				Иначе
					ОткрытьФорму("Справочник."+СтатьяПрочихОтчисленийНаСоцНужды.Вид(), Перечисление.ГруппыВидыРасходов.ДругиеРасходы);
				КонецЕсли;					
			КонецЕсли;
			Флаг = 0;
		КонецЕсли;
		
	ИначеЕсли ЭлементДиалога = "РазмерВычетаНаРебенка" Тогда
		ВычетыНаРебенка = СоздатьОбъект("СписокЗначений");
		Если ДатаГод(ДатаДок) < 2005 Тогда
		    ВычетыНаРебенка.ДобавитьЗначение(300);
			ВычетыНаРебенка.ДобавитьЗначение(600);
			
		Иначе
			Если ДатаГод(ДатаДок) < 2009 Тогда
				ВычетыНаРебенка.ДобавитьЗначение(600);
				ВычетыНаРебенка.ДобавитьЗначение(1200); 
			Иначе
				ВычетыНаРебенка.ДобавитьЗначение(1000);
				ВычетыНаРебенка.ДобавитьЗначение(2000); 
			КонецЕсли;
		КонецЕсли;
		
		ВычетыНаРебенка.ВыбратьЗначение(РазмерВычетаНаРебенка,,,, 2);
		Флаг = 0;
	
	ИначеЕсли ЭлементДиалога = "РазмерВычетаНаРебенкаИнвалида" Тогда
		ВычетыНаРебенка = СоздатьОбъект("СписокЗначений");
		Если ДатаГод(ДатаДок) < 2009 Тогда
			ВычетыНаРебенка.ДобавитьЗначение(1200);
			ВычетыНаРебенка.ДобавитьЗначение(2400);
		Иначе                                      
			ВычетыНаРебенка.ДобавитьЗначение(2000);
			ВычетыНаРебенка.ДобавитьЗначение(4000);
		КонецЕсли;
		ВычетыНаРебенка.ВыбратьЗначение(РазмерВычетаНаРебенкаИнвалида,,,, 2);
		Флаг = 0;
		
	ИначеЕсли ЭлементДиалога = "ХарактерРаботы" Тогда
		СписокХарактеровРаботы.ВыбратьЗначение(ХарактерРаботы,,,, 2);
		Флаг = 0;
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//_____________________________________________________________________________
Процедура ПриЗаписи()
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	Если Сотрудник.Выбран() = 0 Тогда
		Предупреждение("Не указан сотрудник."+РазделительСтрок+
		"Документ не может быть записан.");
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	Если Константа.ВсеДокументыВЖурналОпераций = Да Тогда
	    СуществуетОперация(1);
		Операция.СуммаОперации = 0;
		Операция.Содержание = "Приказ о кадр.изм.по сотр.: "+Сотрудник;
	Иначе
	    СуществуетОперация(0);
	КонецЕсли;
КонецПроцедуры //ПриЗаписи

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Новый = 0;

ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");
	
// добавим информацию о встроенной форме
ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Приказ о переводе работника на другую работу Т-5";
ТаблицаПечФорм.Кнопка       = "Т-5";

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Ввести на основании");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");

СписокХарактеровРаботы = СоздатьОбъект("СписокЗначений");
СписокХарактеровРаботы.ДобавитьЗначение(Перечисление.ХарактерРаботы.ТрудовойДоговор);
СписокХарактеровРаботы.ДобавитьЗначение(Перечисление.ХарактерРаботы.ТрудовыеОтношенияПрекращены);