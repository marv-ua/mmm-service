Перем БухИт;
//_____________________________________________________________________________
Процедура ДобавитьСтрокуВТаблицу()
	РублевыйОстаток = БухИт.СКД("С") - БухИт.СКК("С");
	ВалютныйОстаток = БухИт.СКД("В") - БухИт.СКК("В");
	Если (РублевыйОстаток <> 0) или (ВалютныйОстаток <> 0) Тогда
		глТаблицаСчетов.НоваяСтрока();
		глТаблицаСчетов.Счет = БухИт.Счет;
		глТаблицаСчетов.Валюта = БухИт.Валюта;
		Смещение = 0;
		Для Инд = 1 По БухИт.Счет.КоличествоСубконто() Цикл
			Если БухИт.Счет.ТолькоОбороты(Инд,) = 0 Тогда
				глТаблицаСчетов.УстановитьЗначение(глТаблицаСчетов.НомерСтроки, "Субконто"+Инд, БухИт.Субконто(Инд - Смещение));
			Иначе
				глТаблицаСчетов.УстановитьЗначение(глТаблицаСчетов.НомерСтроки, "Субконто"+Инд, ПолучитьПустоеЗначение());
				Смещение = Смещение + 1;
			КонецЕсли;
		КонецЦикла;
		глТаблицаСчетов.УстановитьЗначение(глТаблицаСчетов.НомерСтроки, "РублевыйОстаток", РублевыйОстаток);
		глТаблицаСчетов.УстановитьЗначение(глТаблицаСчетов.НомерСтроки, "ВалютныйОстаток", ВалютныйОстаток);
	КонецЕсли;
КонецПроцедуры //ДобавитьСтрокуВТаблицу
//_____________________________________________________________________________
Процедура ОбработкаПроведения()

	БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
	глТаблицаСчетов.УдалитьСтроки();
	Сч = СоздатьОбъект("Счет.Основной");
	Сч.ВыбратьСчета();
	Пока Сч.ПолучитьСчет()=1 Цикл
		Если (Сч.ЭтоГруппа() = 0) и (Сч.Валютный = 1) Тогда
			КоличествоСубконто = Сч.КоличествоСубконто();
			КоличествоОборотныхСубконто = 0;
			Для Инд = 1 По КоличествоСубконто Цикл
				Если Сч.ТолькоОбороты(Инд,) = 0 Тогда
					БухИт.ИспользоватьСубконто(Сч.ВидСубконто(Инд));
				Иначе
					КоличествоОборотныхСубконто = КоличествоОборотныхСубконто + 1;
				КонецЕсли;
			КонецЦикла;
			КоличествоСубконто = КоличествоСубконто - КоличествоОборотныхСубконто;
			БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), Сч,,,,,);
			БухИт.ВыбратьВалюты(,,,,);
			Пока БухИт.ПолучитьВалюту(,) = 1 Цикл
				Если КоличествоСубконто > 0 Тогда
					БухИт.ВыбратьСубконто(1);
					Пока БухИт.ПолучитьСубконто(1) = 1 Цикл
						Если КоличествоСубконто > 1 Тогда
							БухИт.ВыбратьСубконто(2);
							Пока БухИт.ПолучитьСубконто(2) = 1 Цикл
								Если КоличествоСубконто > 2 Тогда
									БухИт.ВыбратьСубконто(3);
									Пока БухИт.ПолучитьСубконто(3) = 1 Цикл
										ДобавитьСтрокуВТаблицу();
									КонецЦикла;
								Иначе
									ДобавитьСтрокуВТаблицу();
								КонецЕсли;
							КонецЦикла;
						Иначе
							ДобавитьСтрокуВТаблицу();
						КонецЕсли;
					КонецЦикла;
				Иначе
					ДобавитьСтрокуВТаблицу();
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

	глПереоценкаСчетов(Контекст, глТаблицаСчетов, 0);

	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры