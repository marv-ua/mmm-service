////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем СчетРасчетовСПокупателем;
Перем СчетАвансовПолученных;
Перем СчВР_1, СчВР_6, СчВР_11;
Перем ОплатаДоговора;
Перем ЦеныВдоговоре;
Перем ВестиУчетРасчетовУЕ;
Перем Валюта;
Перем Кратность;
Перем ИтогВсего;
Перем АвансПоДоговору;
Перем АвансБезДоговора;
Перем АвансПоДоговоруРуб;
Перем АвансБезДоговораРуб;
Перем БезДоговора;
Перем ЗадолженностьПоРасчетамВУЕРуб, ЗадолженностьПоРасчетамВУЕВал;
Перем НПРКурсоваяРазница;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// РассчитатьСуммуАванса()
//
Процедура РассчитатьСуммуАванса()
	
	АвансБезДоговора = 0;
	АвансБезДоговораРуб = 0;
	АвансПоДоговору = 0;
	АвансПоДоговоруРуб = 0;
	
	Если ЗавершитьРаботы = 1 Тогда // завершение этапа неоплаченных работ.
	ИначеЕсли (ЗавершитьРаботы = 3) и (ЗачитыватьАванс = 1) Тогда
	Иначе
		СписокДоговоров = СоздатьОбъект("СписокЗначений");
		СписокДоговоров.ДобавитьЗначение(Договор);
		БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
		Если ПустоеЗначение(БезДоговора) = 0 Тогда
			Если ((ВариантОплатыРабот = 0) и (ЗавершитьРаботы = 2)) или ((ЗачитыватьАванс = 0) и ((ЗавершитьРаботы = 3))) Тогда //работы были оплачены без указания договора
				СписокДоговоров.ДобавитьЗначение(БезДоговора);
			КонецЕсли;
		КонецЕсли;

		БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Контрагенты, Контрагент, 2);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Договоры, СписокДоговоров, 2);
		Если ОплатаДоговора = 2 Тогда 
			Если ДатаДок >= '01.01.2008' Тогда	
				БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "62.22, ВР.11",, Валюта,,, "СВ");
			Иначе                                                                          
				БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "62.22, ВР.11",, Валюта,,, "В");
			КонецЕсли;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "62.7, ВР.6",, Валюта,,, "СВ");
			
		Иначе
			БухИт.ВыполнитьЗапрос(, ТекущийДокумент(), "62.2, ВР.1",,,,, "С");
		КонецЕсли;
		
		ЗачтенныйАвансПоДоговору = 0;
		ЗачтенныйАвансПоДоговоруРуб = 0;
		ЗачтенныйАвансБезДоговора = 0;
		ЗачтенныйАвансБезДоговораРуб = 0;
		
		Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, Договор) = 1 Тогда
			Если ОплатаДоговора = 2 Тогда
				Если БухИт.ПолучитьСчет(, "62.22") = 1 Тогда
					АвансПоДоговору = БухИт.СКК("В");
					Если ДатаДок >= '01.01.2008' Тогда	
						АвансПоДоговоруРуб = БухИт.СКК("С");
					КонецЕсли;
				КонецЕсли;
				Если БухИт.ПолучитьСчет(, "ВР.11") = 1 Тогда
					ЗачтенныйАвансПоДоговору = БухИт.СКД("В");
				КонецЕсли;
			
			ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
				Если БухИт.ПолучитьСчет(, "62.7") = 1 Тогда
					АвансПоДоговору = БухИт.СКК("В");
					АвансПоДоговоруРуб = БухИт.СКК("С");
				КонецЕсли;
				Если БухИт.ПолучитьСчет(, "ВР.6") = 1 Тогда
					ЗачтенныйАвансПоДоговору = БухИт.СКД("В");
					ЗачтенныйАвансПоДоговоруРуб = БухИт.СКД("С");
				КонецЕсли;
			
			Иначе
				Если БухИт.ПолучитьСчет(, "62.2") = 1 Тогда
					АвансПоДоговору = БухИт.СКК("С");
				КонецЕсли;
				Если БухИт.ПолучитьСчет(, "ВР.1") = 1 Тогда
					ЗачтенныйАвансПоДоговору = БухИт.СКД("С");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, БезДоговора) = 1 Тогда
			Если ОплатаДоговора = 2 Тогда
				Если БухИт.ПолучитьСчет(, "62.22") = 1 Тогда
					АвансБезДоговора = БухИт.СКК("В");
					Если ДатаДок >= '01.01.2008' Тогда	
						АвансБезДоговораРуб = БухИт.СКК("С");
					КонецЕсли;
				КонецЕсли;
				Если БухИт.ПолучитьСчет(, "ВР.11") = 1 Тогда
					ЗачтенныйАвансБезДоговора = БухИт.СКД("В");
				КонецЕсли;
				
			ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
				Если БухИт.ПолучитьСчет(, "62.7") = 1 Тогда
					АвансБезДоговора = БухИт.СКК("В");
					АвансБезДоговораРуб = БухИт.СКК("С");
				КонецЕсли;
				Если БухИт.ПолучитьСчет(, "ВР.6") = 1 Тогда
					ЗачтенныйАвансБезДоговора = БухИт.СКД("В");
					ЗачтенныйАвансБезДоговораРуб = БухИт.СКД("С");
				КонецЕсли;
				
			Иначе
				Если БухИт.ПолучитьСчет(, "62.2") = 1 Тогда
					АвансБезДоговора = БухИт.СКК("С");
				КонецЕсли;
				Если БухИт.ПолучитьСчет(, "ВР.1") = 1 Тогда
					ЗачтенныйАвансБезДоговора = БухИт.СКД("С");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		АвансПоДоговору = АвансПоДоговору - ЗачтенныйАвансПоДоговору;
		АвансБезДоговора = АвансБезДоговора - ЗачтенныйАвансБезДоговора;
		АвансПоДоговоруРуб = АвансПоДоговоруРуб - ЗачтенныйАвансПоДоговоруРуб;
		АвансБезДоговораРуб = АвансБезДоговораРуб - ЗачтенныйАвансБезДоговораРуб;
	КонецЕсли;
	
КонецПроцедуры // РассчитатьСуммуАванса()

//*****************************************************************************
// ЗачетАвансаПоСчетуВР(ЗачетАванса, ЗачетАвансаРуб, ДоговорАванса)
//
Процедура ЗачетАвансаПоСчетуВР(ЗачетАванса, ЗачетАвансаРуб, ДоговорАванса)
	
	Если ЗачетАванса > 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "ТВ";
		Операция.СодержаниеПроводки = "Зачтена предоплата";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		
		Если ОплатаДоговора = 2 Тогда
			Операция.Дебет.Счет = СчВР_11;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = ДоговорАванса;
			Если ДатаДок >= '01.01.2008' Тогда
				Если (АвансПоДоговору + АвансБезДоговора) = ЗачетАванса Тогда
					ЗачетАвансаРуб = АвансПоДоговоруРуб + АвансБезДоговораРуб;
				Иначе
					КурсЗачетаАванса = Окр((АвансПоДоговоруРуб + АвансБезДоговораРуб)/(АвансПоДоговору + АвансБезДоговора), 4, 1);
					ЗачетАвансаРуб = ЗачетАванса*КурсЗачетаАванса;
				КонецЕсли;
			Иначе
				ЗачетАвансаРуб = ЗачетАванса*Курс/Кратность;
			КонецЕсли;
			Операция.Сумма = ЗачетАвансаРуб;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			Операция.Дебет.Счет = СчВР_6;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = ДоговорАванса;
		    Операция.Сумма = ЗачетАвансаРуб;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса;
			
		Иначе
			Операция.Дебет.Счет = СчВР_1;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = ДоговорАванса;
			Операция.Сумма = ЗачетАванса;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры // ЗачетАвансаПоСчетуВР()

//*****************************************************************************
// ЗачестьАванс(ЗачетАванса, ДоговорАванса)
//
Процедура ЗачестьАванс(ЗачетАванса, ЗачетАвансаРуб, ДоговорАванса)
	
	Если ЗачетАванса > 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "ТВ";
		Операция.СодержаниеПроводки = "Зачтена предоплата";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = СчетАвансовПолученных;
		Операция.Дебет.Контрагенты = Контрагент;
		Операция.Дебет.Договоры = ДоговорАванса;
		Операция.Кредит.Счет = СчетРасчетовСПокупателем;
		Операция.Кредит.Контрагенты = Контрагент;
		Операция.Кредит.Договоры = Договор;

		Если ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Если (АвансПоДоговору + АвансБезДоговора) = ЗачетАванса Тогда
					ЗачетАвансаРуб = АвансПоДоговоруРуб + АвансБезДоговораРуб;
				Иначе
					КурсЗачетаАванса = Окр((АвансПоДоговоруРуб + АвансБезДоговораРуб)/(АвансПоДоговору + АвансБезДоговора), 4, 1);
					ЗачетАвансаРуб = ЗачетАванса*КурсЗачетаАванса;
				КонецЕсли;
			Иначе
				ЗачетАвансаРуб = ЗачетАванса*Курс/Кратность;
			КонецЕсли;
			Операция.Сумма = ЗачетАвансаРуб;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			Операция.Сумма = ЗачетАвансаРуб;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса;
			
		Иначе
			Операция.Сумма = ЗачетАванса;
		КонецЕсли; 
		
		ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб - Операция.Сумма;
		ЗадолженностьПоРасчетамВУЕВал = ЗадолженностьПоРасчетамВУЕВал - Операция.ВалСумма;
		
		Если ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ВЛ";
				Операция.СодержаниеПроводки = "Зачтена предоплата";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетПоКоду("ВАЛ.62");
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.Договоры = ДоговорАванса;
				Операция.Сумма = ЗачетАванса*Курс/Кратность;
				Операция.Валюта = Валюта;
				Операция.ВалСумма = ЗачетАванса;
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры // ЗачетАванса()

//******************************************************************************
// СформироватьПроводкиДляЦелейНалоговогоУчета()
//
Процедура СформироватьПроводкиДляЦелейНалоговогоУчета(Работа, Знач ВыручкаБезНалогов)
	
	Если ВыручкаБезНалогов > 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
	    Операция.НомерЖурнала = "НУ";
		Операция.СодержаниеПроводки = "Выполнен этап работ";
        Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Кредит.Счет = СчетПоКоду("Н06.01");
		Операция.Кредит.Номенклатура = Работа;
		Операция.Кредит.УсловияПоступленияИВыбытия = Перечисление.УсловияПоступленияИВыбытия.ЗаПлату;
		Операция.Кредит.Основание = Договор;
		Операция.Сумма = ВыручкаБезНалогов;
	КонецЕсли;
	
КонецПроцедуры // СформироватьПроводкиДляЦелейНалоговогоУчета()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()

	Сч46 = СчетПоКоду("46");
	Сч62_1 = СчетПоКоду("62.1");
	Сч62_6 = СчетПоКоду("62.6");
	Сч62_11 = СчетПоКоду("62.11");
	Сч62_2 = СчетПоКоду("62.2");
	Сч62_7 = СчетПоКоду("62.7");
	Сч62_22 = СчетПоКоду("62.22");
	Сч90_1_1 = СчетПоКоду("90.1.1");
	СчВР_1 = СчетПоКоду("ВР.1");
	СчВР_6 = СчетПоКоду("ВР.6");
	СчВР_11 = СчетПоКоду("ВР.11");
	
	СчУЕ_62 = СчетПоКоду("УЕ.62");
	СчВАЛ_62 = СчетПоКоду("ВАЛ.62");
	
	Сч68_5 = СчетПоКоду("68.5");
	Сч76_Н_4 = СчетПоКоду("76.Н.4");
	Сч90_6 = СчетПоКоду("90.6");
	СчН06_01 = СчетПоКоду("Н06.01");
	                                
	ЗадолженностьПоРасчетамВУЕРуб = 0;
	ЗадолженностьПоРасчетамВУЕВал = 0;
	НПРКурсоваяРазница = 0;
	
	ЦеныВДоговоре = 1; // в рублях
	ВестиУчетРасчетовУЕ = 0;
	
	Если Договор.Выбран() = 1 Тогда
	    Если ПустоеЗначение(Договор.ВалютаДоговора) = 0 Тогда
			ЦеныВДоговоре = 2; // в валюте
		КонецЕсли;
		ОплатаДоговора = Договор.ОплатаДоговора; // 1 - врублях, 2 - в валюте
		ВестиУчетРасчетовУЕ = Договор.ВестиУчетРасчетовУЕ;
	КонецЕсли;
	
	Если ЦеныВДоговоре = 2 Тогда
		Валюта = Договор.ВалютаДоговора;
		Кратность = Валюта.Кратность.Получить(ДатаДок);
		Кратность = ?(Кратность=0, 1, Кратность);
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			Кратность = Кратность * 100 / (100 + Договор.ПроцентКорректировкиКурсаУЕ);
		КонецЕсли;
		
		Если Курс = 0 Тогда
			ТекстСообщения = "Не указан курс валюты.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СчетРасчетовСПокупателем = Сч62_1;
	СчетАвансовПолученных = Сч62_2;
	
	Если ЗавершитьРаботы <> 1 Тогда
		Если ОплатаДоговора = 2 Тогда
			СчетРасчетовСПокупателем = Сч62_11;
			СчетАвансовПолученных = Сч62_22;
			Если ДатаДок >= '01.01.2008' Тогда
				СчетАвансовПолученныхПереоценка = СчВАЛ_62;
			Иначе
				СчетАвансовПолученныхПереоценка = Сч62_22;
			КонецЕсли;
			
			глТаблицаСчетов.УдалитьСтроки();
	
			Если ЗавершитьРаботы = 3 Тогда
				глТаблицаСчетов.НоваяСтрока();
				глТаблицаСчетов.Счет = Сч62_11;
				глТаблицаСчетов.Субконто1 = Контрагент;
				глТаблицаСчетов.Субконто2 = Договор;
				глТаблицаСчетов.Субконто3 = "";
				глТаблицаСчетов.Валюта = Валюта;
				глТаблицаСчетов.Курс = Курс;
				
				Если ЗачитыватьАванс = 1 Тогда
				Иначе
					глТаблицаСчетов.НоваяСтрока();
					глТаблицаСчетов.Счет = СчетАвансовПолученныхПереоценка;
					глТаблицаСчетов.Субконто1 = Контрагент;
					глТаблицаСчетов.Субконто2 = Договор;
					глТаблицаСчетов.Субконто3 = "";
					глТаблицаСчетов.Валюта = Валюта;
					глТаблицаСчетов.Курс = Курс;
					Если ЗачитыватьАванс = 0 Тогда
						БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
						Если ПустоеЗначение(БезДоговора) = 0 Тогда
							глТаблицаСчетов.НоваяСтрока();
							глТаблицаСчетов.Счет = СчетАвансовПолученныхПереоценка;
							глТаблицаСчетов.Субконто1 = Контрагент;
							глТаблицаСчетов.Субконто2 = БезДоговора;
							глТаблицаСчетов.Субконто3 = "";
							глТаблицаСчетов.Валюта = Валюта;
							глТаблицаСчетов.Курс = Курс;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
	
			ИначеЕсли ВариантОплатыРабот = 2 Тогда
				глТаблицаСчетов.НоваяСтрока();
				глТаблицаСчетов.Счет = СчВР_11;
				глТаблицаСчетов.Субконто1 = Контрагент;
				глТаблицаСчетов.Субконто2 = Договор;
				глТаблицаСчетов.Субконто3 = "";
				глТаблицаСчетов.Валюта = Валюта;
				глТаблицаСчетов.Курс = Курс;
				
			КонецЕсли;
	
			глПереоценкаСчетов(Контекст, глТаблицаСчетов,, 0);
		
		ИначеЕсли ВестиУчетРасчетовУЕ = 0 Тогда
		    СчетРасчетовСПокупателем = Сч62_1;
			СчетАвансовПолученных = Сч62_2;
			
		Иначе
			СчетРасчетовСПокупателем = Сч62_6;
			СчетАвансовПолученных = Сч62_7;
		КонецЕсли;
	КонецЕсли;
	
	ЗачетАвансаПоДоговору = 0;
	ЗачетАвансаБезДоговора = 0;
	ЗачетАвансаПоДоговоруРуб = 0;
    ЗачетАвансаБезДоговораРуб = 0;
	РассчитатьСуммуАванса();
	
	ТаблицаРеализации = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаРеализации.НоваяКолонка("Работа");
	ТаблицаРеализации.НоваяКолонка("ВидНоменклатуры");
	ТаблицаРеализации.НоваяКолонка("СтавкаНДС");
	ТаблицаРеализации.НоваяКолонка("СтавкаНП");
	ТаблицаРеализации.НоваяКолонка("Всего", "Число", 15, 2);
	ТаблицаРеализации.НоваяКолонка("ВалВсего", "Число", 15, 2);
	ТаблицаРеализации.НоваяКолонка("НП", "Число", 15, 2);
	ТаблицаРеализации.НоваяКолонка("НДС", "Число", 15, 2);
	ТаблицаРеализации.НоваяКолонка("Количество", "Число", 15, 2);
	ТаблицаРеализации.НоваяКолонка("НПВал", "Число", 15, 2);
	ТаблицаРеализации.НоваяКолонка("НДСВал", "Число", 15, 2);

	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		ТаблицаРеализации.НоваяСтрока();
		ТаблицаРеализации.Работа = Работа;
		ТаблицаРеализации.ВидНоменклатуры = Работа.ВидНоменклатуры;
		ТаблицаРеализации.СтавкаНДС = глСтавкаНалога(Контекст, "НДС");
		ТаблицаРеализации.СтавкаНП = глСтавкаНалога(Контекст, "НП");
		ТаблицаРеализации.Количество = Количество;
		
		Если (ЦеныВДоговоре = 2) и (ВестиУчетРасчетовУЕ = 0) Тогда
			ТаблицаРеализации.Всего = Всего*Курс/Кратность;
			ТаблицаРеализации.НП = НП*Курс/Кратность;
			ТаблицаРеализации.НДС = НДС*Курс/Кратность;
			ТаблицаРеализации.НПВал = НП;
			ТаблицаРеализации.НДСВал = НДС;
			Если ОплатаДоговора =2 Тогда
			    ТаблицаРеализации.ВалВсего = Всего;
			КонецЕсли;
			
		Иначе
			ТаблицаРеализации.Всего = Всего;
			ТаблицаРеализации.НП = НП;
			ТаблицаРеализации.НДС = НДС;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗавершитьРаботы = 1 Тогда //завершение не оплаченного этапа многоэтапных работ
		
		// Отражение в налоговом учете дохода, полученного в результате выполнения этапа работ.
		Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
			ТаблицаРеализации.ВыбратьСтроки();
			Пока ТаблицаРеализации.Получитьстроку() = 1 Цикл
				Если ВестиУчетРасчетовУЕ = 1 Тогда
					ВыручкаБезНалогов = Окр((ТаблицаРеализации.Всего - ТаблицаРеализации.НДС - ТаблицаРеализации.НП) * Курс / Кратность, 2, 1);
					
					
				Иначе
					ВыручкаБезНалогов = ТаблицаРеализации.Всего - ТаблицаРеализации.НДС - ТаблицаРеализации.НП;
				КонецЕсли;
				
				СформироватьПроводкиДляЦелейНалоговогоУчета(ТаблицаРеализации.Работа, ВыручкаБезНалогов);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// Расчет суммы аванса.
	СуммаЗачтенногоАванса = 0;
	СуммаЗачтенногоАвансаРуб = 0;
	КурсАванса = 0;
	
	Если (ЗавершитьРаботы = 2) или (ЗавершитьРаботы = 3) Тогда
		Если ОплатаДоговора = 2 Тогда
		    ВсегоОтгружено = ТаблицаРеализации.Итог("ВалВсего");
			
		Иначе
			ВсегоОтгружено = ТаблицаРеализации.Итог("Всего");
		КонецЕсли;
		
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, ВсегоОтгружено);
			ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, ВсегоОтгружено - ЗачетАвансаПоДоговору);
			
			Аванс = АвансПоДоговору + АвансБезДоговора;
			ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
			
			Если ЗачетАванса = Аванс Тогда
			    ЗачетАвансаПоДоговоруРуб = АвансПоДоговоруРуб;
				ЗачетАвансаБезДоговораРуб = АвансБезДоговораРуб;
				
			ИначеЕсли ЗачетАванса > АвансПоДоговору Тогда
				ЗачетАвансаПоДоговоруРуб = АвансПоДоговоруРуб;
				ЗачетАвансаБезДоговораРуб = Окр(ЗачетАвансаБезДоговора * Окр(АвансБезДоговораРуб / ?(АвансБезДоговора = 0, 1, АвансБезДоговора), 4, 1), 2, 1);
				
			Иначе
				ЗачетАвансаПоДоговоруРуб = Окр(ЗачетАвансаПоДоговору * Окр(АвансПоДоговоруРуб / ?(АвансПоДоговору = 0, 1, АвансПоДоговору), 4, 1), 2, 1);
				ЗачетАвансаБезДоговораРуб = 0;
			КонецЕсли;
		
		Иначе
			ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, ВсегоОтгружено);
			ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, ВсегоОтгружено - ЗачетАвансаПоДоговору);
		КонецЕсли;
		
		СуммаЗачтенногоАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			СуммаЗачтенногоАвансаРуб = ЗачетАвансаПоДоговоруРуб + ЗачетАвансаБезДоговораРуб;
		Иначе
			Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда 
				Если (ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора) <> 0 Тогда
					Если (АвансПоДоговору + АвансБезДоговора) = (ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора) Тогда
						СуммаЗачтенногоАвансаРуб = АвансПоДоговоруРуб + АвансБезДоговораРуб;
					Иначе                          
						СуммаЗачтенногоАвансаРуб = (ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора)*ОКР((АвансПоДоговоруРуб + АвансБезДоговораРуб)/(АвансПоДоговору + АвансБезДоговора), 4); 
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ТаблицаРеализации.Свернуть("Работа, ВидНоменклатуры, СтавкаНДС, СтавкаНП","ВалВсего, Всего, НДС, НП, Количество, НДСВал, НПВал");
	
		Если ОплатаДоговора = 2 Тогда
		    ИтогВсего = ТаблицаРеализации.Итог("ВалВсего");
		
		Иначе
			ИтогВсего = ТаблицаРеализации.Итог("Всего");
		КонецЕсли;
		
		ТаблицаРеализации.НоваяКолонка("ВыручкаРуб", "Число", 15, 2);
		ТаблицаРеализации.НоваяКолонка("СуммоваяРазница", "Число", 15, 2);
		
		РаспределеноАванса = 0;
		РаспределеноАвансаРуб = 0;
		КолСтрокТабОтгрузки = ТаблицаРеализации.КоличествоСтрок();
		КурсАванса = ?(СуммаЗачтенногоАванса = 0, 0, Окр(СуммаЗачтенногоАвансаРуб / СуммаЗачтенногоАванса, 4, 1));
		К = ?(ИтогВсего = 0, 0, СуммаЗачтенногоАванса / ИтогВсего);
		
		ТаблицаРеализации.ВыбратьСтроки();
		Пока ТаблицаРеализации.ПолучитьСтроку() = 1 Цикл
			Если ((ЦеныВДоговоре = 2) и (ВестиУчетРасчетовУЕ = 1))
			или ((ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008')) Тогда
				Если ПустоеЗначение(ИтогВсего) = 0  Тогда        
					Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
						ТаблицаВсего = ТаблицаРеализации.ВалВсего;
					Иначе
						ТаблицаВсего = ТаблицаРеализации.Всего;
					КонецЕсли;
					
					ЧастьСуммыЗачтенногоАванса = Окр(СуммаЗачтенногоАванса * ТаблицаВсего / ИтогВсего, 2, 1);
					ЧастьСуммыЗачтенногоАвансаРуб = Окр(СуммаЗачтенногоАвансаРуб * ТаблицаВсего / ИтогВсего, 2, 1);
					
				Иначе
					ЧастьСуммыЗачтенногоАванса = 0;
					ЧастьСуммыЗачтенногоАвансаРуб = 0;
				КонецЕсли;
				
				РаспределеноАванса = РаспределеноАванса + ЧастьСуммыЗачтенногоАванса;
				РаспределеноАвансаРуб = РаспределеноАвансаРуб + ЧастьСуммыЗачтенногоАвансаРуб;
				
				Если КолСтрокТабОтгрузки = ТаблицаРеализации.НомерСтроки Тогда
					Если РаспределеноАванса <> СуммаЗачтенногоАванса Тогда
						ЧастьСуммыЗачтенногоАванса = ЧастьСуммыЗачтенногоАванса + СуммаЗачтенногоАванса - РаспределеноАванса;
					КонецЕсли;
					
					Если РаспределеноАвансаРуб <> СуммаЗачтенногоАвансаРуб Тогда
						ЧастьСуммыЗачтенногоАвансаРуб = ЧастьСуммыЗачтенногоАвансаРуб + СуммаЗачтенногоАвансаРуб - РаспределеноАвансаРуб;
					КонецЕсли;
				КонецЕсли; 
				
				Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
					ВсегоРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.ВалВсего * Курс / Кратность, 2, 1);
					НДСРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.НДС, 2, 1);                 
					НПРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.НП, 2, 1);
					НДСвЧастиЗачтенногоАвансаРуб = Окр(К * ТаблицаРеализации.НДСВал * Курс / Кратность, 2, 1);
					НПвЧастиЗачтенногоАвансаРуб = Окр(К * ТаблицаРеализации.НПВал * Курс / Кратность, 2, 1);
				Иначе
					ВсегоРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.Всего * Курс / Кратность, 2, 1);    
					НДСРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.НДС * Курс / Кратность, 2, 1);
					НПРубПоКурсуОтгрузки = Окр(ТаблицаРеализации.НП * Курс / Кратность, 2, 1);
					НДСвЧастиЗачтенногоАвансаРуб = Окр(К * ТаблицаРеализации.НДС * КурсАванса, 2, 1);
					НПвЧастиЗачтенногоАвансаРуб = Окр(К * ТаблицаРеализации.НП * КурсАванса, 2, 1);
				КонецЕсли;
				    
				ОплаченоПоКурсуОтгрузки = Окр(К * ВсегоРубПоКурсуОтгрузки, 2, 1);
			    ТаблицаРеализации.ВыручкаРуб = ВсегоРубПоКурсуОтгрузки + ЧастьСуммыЗачтенногоАвансаРуб - ОплаченоПоКурсуОтгрузки;
				
				ОплаченоНДСПоКурсуОтгрузки = Окр(К * НДСРубПоКурсуОтгрузки, 2, 1);
				СуммаНДС = НДСРубПоКурсуОтгрузки + НДСвЧастиЗачтенногоАвансаРуб - ОплаченоНДСПоКурсуОтгрузки;
				
				Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
					НПРКурсоваяРазница = Окр(НПРКурсоваяРазница, 2) - Окр(СуммаНДС, 2);
				КонецЕсли;
				
				ОплаченоНППоКурсуОтгрузки = Окр(К * НПРубПоКурсуОтгрузки, 2, 1);
				СуммаНП = НПРубПоКурсуОтгрузки + НПвЧастиЗачтенногоАвансаРуб - ОплаченоНППоКурсуОтгрузки;
				
				ВыручкаБезНалогов = ТаблицаРеализации.ВыручкаРуб - СуммаНДС - СуммаНП;
				
				Если (Договор.ОтражатьСуммовуюРазницуПриЗачетеАванса = 1) и (ВестиУчетРасчетовУЕ = 1) Тогда
					ТаблицаРеализации.СуммоваяРазница = ЧастьСуммыЗачтенногоАвансаРуб - ОплаченоПоКурсуОтгрузки;
				КонецЕсли;
				
			Иначе
				ВыручкаБезНалогов = ТаблицаРеализации.Всего - ТаблицаРеализации.НДС - ТаблицаРеализации.НП;
			КонецЕсли;
			
			Если (Договор.ОтражатьСуммовуюРазницуПриЗачетеАванса = 1) и (ВестиУчетРасчетовУЕ = 1) Тогда
				ПоКурсуОтгрузки = ОплаченоПоКурсуОтгрузки - ОплаченоНДСПоКурсуОтгрузки - ОплаченоНППоКурсуОтгрузки;
				ПоКурсуОплаты = ЧастьСуммыЗачтенногоАвансаРуб - НДСвЧастиЗачтенногоАвансаРуб - НПвЧастиЗачтенногоАвансаРуб;
				СуммоваяРазница = ПоКурсуОплаты - ПоКурсуОтгрузки;
				
			Иначе
				СуммоваяРазница = 0; 
			КонецЕсли;
			
			Если (ВыручкаБезНалогов > 0) и (глНовыеПравилаВеденияНУ(ДатаДок) = 1) Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			    Операция.НомерЖурнала = "НУ";
				Операция.СодержаниеПроводки = "Выручка от реализации";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Кредит.Счет = СчН06_01;
				Операция.Кредит.Номенклатура = ТаблицаРеализации.Работа;
				Операция.Кредит.УсловияПоступленияИВыбытия = Перечисление.УсловияПоступленияИВыбытия.ЗаПлату;
				Операция.Кредит.Основание = Договор;
				Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
					Операция.Сумма = (ТаблицаРеализации.Всего - ТаблицаРеализации.НДС - ТаблицаРеализации.НП) - СуммоваяРазница;
					НПРКурсоваяРазница = Окр(НПРКурсоваяРазница, 2) - Окр(Операция.Сумма, 2);
				Иначе
					Операция.Сумма = ВыручкаБезНалогов - СуммоваяРазница;
				КонецЕсли;
				
				глОтражениеСуммовыхРазницВНаловомУчете(Контекст, СуммоваяРазница, 0);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗавершитьРаботы = 2 Тогда //завершение оплаченного этапа многоэтапных работ
		
		//Проверяем - достаточно ли аванса для закрытия всего этапа
		ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
		Если ЗачетАванса < Итог("Всего") Тогда
			РезультатПроверки = 0;
			ТекстСообщения = "Для отражения выполнения этапа работ недостаточно аванса.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
        
		ЗачетАвансаПоСчетуВР(ЗачетАвансаПоДоговору, ЗачетАвансаПоДоговоруРуб, Договор);
		ЗачетАвансаПоСчетуВР(ЗачетАвансаБезДоговора, ЗачетАвансаБезДоговораРуб, БезДоговора);
		
		ТекстСообщения = "=> Зачет аванса:" + РазделительСтрок
		               + "   выполнены работы на сумму: "+ВсегоОтгружено+", зачтен аванс в размере: "+ЗачетАванса + РазделительСтрок
					   + "   - определен аванс по договору "+Договор+" в сумме: "+АвансПоДоговору+", зачтен аванс в размере: "+ЗачетАвансаПоДоговору + РазделительСтрок;
		
		Если ЗачитыватьАванс = 0 Тогда
			ТекстСообщения = ТекстСообщения
						   + "   - определен аванс без указания договора в сумме: "+АвансБезДоговора+", зачтен аванс в размере: "+ЗачетАвансаБезДоговора;
		Иначе
			ТекстСообщения = ТекстСообщения
			               + "   - аванс без указания договора не учтен";
		КонецЕсли;
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		
		ТаблицаРеализации.ВыбратьСтроки();
		Пока ТаблицаРеализации.ПолучитьСтроку() = 1 Цикл
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "ТВ";
			Операция.СодержаниеПроводки = "Выполнен этап работ";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = Сч46;
			Операция.Дебет.Номенклатура = ТаблицаРеализации.Работа;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;
			Операция.Кредит.Счет = Сч90_1_1;
			Операция.Кредит.ВидыНоменклатуры = ТаблицаРеализации.ВидНоменклатуры;
			Если ВерсияОбъекта >= "7.70.421" Тогда
				Операция.Кредит.СтавкиНДС = ТаблицаРеализации.СтавкаНДС;
				Операция.Кредит.СтавкиНП = ТаблицаРеализации.СтавкаНП;
			КонецЕсли;
			
			Если ОплатаДоговора = 2 Тогда
				Если ДатаДок >= '01.01.2008' Тогда	
					Операция.Сумма = ТаблицаРеализации.ВыручкаРуб;
				Иначе
					Операция.Сумма = ТаблицаРеализации.Всего;
				КонецЕсли;			
			КонецЕсли;

			
			Если ВестиУчетРасчетовУЕ = 1 Тогда
				Операция.Сумма = ТаблицаРеализации.ВыручкаРуб - ТаблицаРеализации.СуммоваяРазница;
			ИначеЕсли ОплатаДоговора <> 2 Тогда
				Операция.Сумма = ТаблицаРеализации.Всего;
			КонецЕсли;
			
			Если ТаблицаРеализации.СуммоваяРазница <> 0 Тогда
			    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ТВ";
				Операция.СодержаниеПроводки = "Суммовая разница";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = Сч46;
				Операция.Дебет.Номенклатура = ТаблицаРеализации.Работа;
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.Договоры = Договор;
				Операция.Кредит.Счет = Сч90_1_1;
				Операция.Кредит.ВидыНоменклатуры = ТаблицаРеализации.ВидНоменклатуры;
				Операция.Кредит.СтавкиНДС = ТаблицаРеализации.СтавкаНДС;
				Операция.Кредит.СтавкиНП = ТаблицаРеализации.СтавкаНП;
				Операция.Сумма = ТаблицаРеализации.СуммоваяРазница;				
			КонецЕсли;
		КонецЦикла;
		
		Если (ВерсияОбъекта >= "7.70.421") и (ТаблицаРеализации.Итог("НП") > 0) Тогда
			ТаблицаРеализации.Свернуть("ВидНоменклатуры, СтавкаНП","НП");
			ТаблицаРеализации.ВыбратьСтроки();
			Пока ТаблицаРеализации.ПолучитьСтроку() = 1 Цикл
				Если ТаблицаРеализации.НП > 0 Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ТВ";
					Операция.СодержаниеПроводки = "Начислен НП";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = Сч90_6;
					Операция.Дебет.ВидыНоменклатуры = ТаблицаРеализации.ВидНоменклатуры;
					Операция.Дебет.СтавкиНП = ТаблицаРеализации.СтавкаНП;
					Операция.Кредит.Счет = Сч68_5;
					Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
					Операция.Сумма = ТаблицаРеализации.НП;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
			
	ИначеЕсли ЗавершитьРаботы = 3 Тогда // завершение одноэтапной работы
		ТаблицаРеализации.Свернуть("ВидНоменклатуры, СтавкаНДС, СтавкаНП","Всего, ВалВсего, ВыручкаРуб, СуммоваяРазница, НП");
		ТаблицаРеализации.ВыбратьСтроки();
		Пока ТаблицаРеализации.ПолучитьСтроку() = 1 Цикл
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "ТВ";
			Операция.СодержаниеПроводки = "Выполнен этап работ";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетРасчетовСПокупателем;
			Операция.Дебет.Контрагенты = Контрагент;
			Операция.Дебет.Договоры = Договор;
			Операция.Кредит.Счет = Сч90_1_1;
			Операция.Кредит.ВидыНоменклатуры = ТаблицаРеализации.ВидНоменклатуры;
			Если ВерсияОбъекта >= "7.70.421" Тогда
				Операция.Кредит.СтавкиНДС = ТаблицаРеализации.СтавкаНДС;
				Операция.Кредит.СтавкиНП = ТаблицаРеализации.СтавкаНП;
			КонецЕсли;

			Если ОплатаДоговора = 2 Тогда
				Операция.ВалСумма = ТаблицаРеализации.ВалВсего;
				Операция.Валюта = Валюта;
				Если ДатаДок >= '01.01.2008' Тогда	
					Операция.Сумма = ТаблицаРеализации.ВыручкаРуб;
					НПРКурсоваяРазница = Окр(НПРКурсоваяРазница, 2) + Окр(Операция.Сумма, 2);
				Иначе
					Операция.Сумма = ТаблицаРеализации.Всего;
				КонецЕсли;			
			КонецЕсли;
			
			Если ВестиУчетРасчетовУЕ = 1 Тогда
				Операция.ВалСумма = ТаблицаРеализации.Всего;
				Операция.Валюта = Валюта;
				Операция.Сумма = ТаблицаРеализации.ВыручкаРуб - ТаблицаРеализации.СуммоваяРазница;
				
				ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб + Операция.Сумма;
				ЗадолженностьПоРасчетамВУЕВал = ЗадолженностьПоРасчетамВУЕВал + Операция.ВалСумма;
				
			ИначеЕсли ОплатаДоговора <> 2 Тогда
				Операция.Сумма = ТаблицаРеализации.Всего;
			КонецЕсли; 
				
			Если ТаблицаРеализации.СуммоваяРазница <> 0 Тогда
			    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ТВ";
				Операция.СодержаниеПроводки = "Суммовая разница";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетРасчетовСПокупателем;
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.Договоры = Договор;
				Операция.Кредит.Счет = Сч90_1_1;
				Операция.Кредит.ВидыНоменклатуры = ТаблицаРеализации.ВидНоменклатуры;
				Операция.Кредит.СтавкиНДС = ТаблицаРеализации.СтавкаНДС;
				Операция.Кредит.СтавкиНП = ТаблицаРеализации.СтавкаНП;
				Операция.Сумма = ТаблицаРеализации.СуммоваяРазница;
			    Операция.Валюта = Валюта;  
				
				ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб + Операция.Сумма;
				
			КонецЕсли;
		КонецЦикла;
		
		Если ОплатаДоговора = 2 Тогда
		    ИтогВсего = ТаблицаРеализации.Итог("ВалВсего");
			
		Иначе
			ИтогВсего = ТаблицаРеализации.Итог("Всего");
		КонецЕсли;
		
		// Отразим зачет аванса.
		ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
		Если ЗачетАванса <> 0 Тогда
			ЗачестьАванс(ЗачетАвансаПоДоговору, ЗачетАвансаПоДоговоруРуб, Договор);
			ЗачестьАванс(ЗачетАвансаБезДоговора, ЗачетАвансаБезДоговораРуб, БезДоговора);
			
			ТекстСообщения = "=> Зачет аванса." + РазделительСтрок
			               + "   выполнены работы на сумму: "+ИтогВсего+", зачтен аванс в размере: "+ЗачетАванса + РазделительСтрок
						   + "   - определен аванс по договору "+Договор+" в сумме: "+АвансПоДоговору+", зачтен аванс в размере: "+ЗачетАвансаПоДоговору + РазделительСтрок;
			
			Если ЗачитыватьАванс = 0 Тогда
				ТекстСообщения = ТекстСообщения
							   + "   - определен аванс без указания договора в сумме: "+АвансБезДоговора+", зачтен аванс в размере: "+ЗачетАвансаБезДоговора;
			Иначе
				ТекстСообщения = ТекстСообщения
				               + "   - аванс без указания договора не учтен";
			КонецЕсли;
			глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		КонецЕсли;
		
		ТаблицаРеализации.Свернуть("ВидНоменклатуры, СтавкаНП","Всего, НП");
		Если (ИтогВсего > 0) и (ВерсияОбъекта >= "7.70.421") Тогда
			К = ЗачетАванса / ИтогВсего;
			ТаблицаРеализации.ВыбратьСтроки();
			Пока ТаблицаРеализации.ПолучитьСтроку() = 1 Цикл
				Если ТаблицаРеализации.НП = 0 Тогда
				    Продолжить;
				КонецЕсли;
			    
				НПкУплате = К*ТаблицаРеализации.НП;
				ОтложеноНП = ТаблицаРеализации.НП - НПкУплате;
				Если НПкУплате > 0 Тогда // был зачет аванса
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ТВ";
					Операция.СодержаниеПроводки = "Начислен НП";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = Сч90_6;
					Операция.Дебет.ВидыНоменклатуры = ТаблицаРеализации.ВидНоменклатуры;
					Операция.Дебет.СтавкиНП = ТаблицаРеализации.СтавкаНП;
					Операция.Кредит.Счет = Сч68_5;
					Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
					Если ВестиУчетРасчетовУЕ = 1 Тогда
						Операция.Сумма = НПкУплате*Курс/Кратность;
					Иначе
						Операция.Сумма = НПкУплате;
					КонецЕсли;
				КонецЕсли;
				
				Если ОтложеноНП > 0 Тогда
				    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ТВ";
					Операция.СодержаниеПроводки = "Начислен НП";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Дебет.Счет = Сч90_6;
					Операция.Дебет.ВидыНоменклатуры = ТаблицаРеализации.ВидНоменклатуры;
					Операция.Дебет.СтавкиНП = ТаблицаРеализации.СтавкаНП;
					Операция.Кредит.Счет = Сч76_Н_4;
					Операция.Кредит.Контрагенты = Контрагент;
					Операция.Кредит.Договоры = Договор;
					Если ВестиУчетРасчетовУЕ = 1 Тогда
					    Операция.Сумма = ОтложеноНП*Курс/Кратность;
					Иначе
						Операция.Сумма = ОтложеноНП;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;  
	
	//********************************************************************
	//Проводки по расчетам в у.е.
	Если ДатаДок >= '01.01.2007' Тогда
		Если (ВестиУчетРасчетовУЕ = 1) и (ЗавершитьРаботы = 3) Тогда
			Если (ЗадолженностьПоРасчетамВУЕРуб + ЗадолженностьПоРасчетамВУЕВал) <> 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "УЕ";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчУЕ_62;
				Операция.Дебет.Субконто(1, Контрагент);
				Операция.Дебет.Субконто(2, Договор);			
				Операция.Валюта = Валюта;
				Операция.Сумма = ЗадолженностьПоРасчетамВУЕРуб;
				Операция.ВалСумма = ЗадолженностьПоРасчетамВУЕВал;
				Операция.СодержаниеПроводки = "Задолженность по реализации в у.е."; 
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	//********************************************************************
	
	//********************************************************************
	//Проводки по расчетам в вал. 
	Если (Константа.ПрименяетсяПБУ18.Получить(ДатаДок) = Да) Тогда
		Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') и (ЗавершитьРаботы = 3) Тогда
			Если НПРКурсоваяРазница <> 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "НУ";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетПоКоду("НПР.99");
				Операция.Сумма = -НПРКурсоваяРазница;
				Операция.СодержаниеПроводки = "Разница в оценке дохода"; 
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
	//********************************************************************


	Операция.Записать();  
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения()