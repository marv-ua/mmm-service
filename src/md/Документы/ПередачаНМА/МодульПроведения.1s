////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем СчетРасчетовСПокупателем;
Перем СчетАвансовПолученных;
Перем ОплатаДоговора;
Перем ЦеныВДоговоре;
Перем ВестиУчетРасчетовУЕ;
Перем Валюта;
Перем Кратность;
Перем АвансПоДоговору;
Перем АвансБезДоговора;
Перем АвансПоДоговоруРуб;
Перем АвансБезДоговораРуб;
Перем БезДоговора;
Перем ЗадолженностьПоРасчетамВУЕРуб, ЗадолженностьПоРасчетамВУЕВал;
Перем НПРКурсоваяРазница;


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//******************************************************************************
// РассчитатьСуммуАванса()
//
// Описание:
//  Выполняет расчет бухгалтерских итогов и определяет текущую сумму аванса.
//
Процедура РассчитатьСуммуАванса()
	
	АвансБезДоговора = 0;
	АвансБезДоговораРуб = 0;
	АвансПоДоговору = 0;
	АвансПоДоговоруРуб = 0;
	
	Если ЗачитыватьАванс <> 1 Тогда //Зачитывать аванс
		СписокДоговоров = СоздатьОбъект("СписокЗначений");
		СписокДоговоров.ДобавитьЗначение(Договор);
		БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
		Если (ЗачитыватьАванс = 0) и (ПустоеЗначение(БезДоговора) = 0) Тогда //Зачитывать аванс без договора
			СписокДоговоров.ДобавитьЗначение(БезДоговора);
		КонецЕсли;                                  
		
		БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Контрагенты, Контрагент, 2);
		БухИт.ИспользоватьСубконто(ВидыСубконто.Договоры, СписокДоговоров, 2);
		Если ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда	
				БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"62.22",,Валюта,,,"СВ");
			Иначе
				БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"62.22",,Валюта,,,"В");
			КонецЕсли;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
		    БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"62.7",,Валюта,,,"СВ");
			
		Иначе
			БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"62.2",,,,,"С");
		КонецЕсли;

		Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, БезДоговора) = 1 Тогда
			Если ОплатаДоговора = 2 Тогда
				АвансБезДоговора = БухИт.СКК("В");
				Если ДатаДок >= '01.01.2008' Тогда	
					АвансБезДоговораРуб = БухИт.СКК("С");
				КонецЕсли;

			ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
				АвансБезДоговора = БухИт.СКК("В");
				АвансБезДоговораРуб = БухИт.СКК("С");
				
			Иначе
				АвансБезДоговора = БухИт.СКК("С");
			КонецЕсли;
		КонецЕсли;
		
		Если БухИт.ПолучитьСубконто(ВидыСубконто.Договоры,, Договор) = 1 Тогда
			Если ОплатаДоговора = 2 Тогда
			    АвансПоДоговору = БухИт.СКК("В");
				Если ДатаДок >= '01.01.2008' Тогда	
					АвансПоДоговоруРуб = БухИт.СКК("С");
				КонецЕсли;
				
			ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
				АвансПоДоговору = БухИт.СКК("В");
				АвансПоДоговоруРуб = БухИт.СКК("С");
				
			Иначе
				АвансПоДоговору = БухИт.СКК("С");
			КонецЕсли;
		КонецЕсли;
		
		АвансБезДоговора = Макс(АвансБезДоговора, 0);
		АвансБезДоговораРуб = Макс(АвансБезДоговораРуб, 0);
		АвансПоДоговору = Макс(АвансПоДоговору, 0);
		АвансПоДоговоруРуб = Макс(АвансПоДоговоруРуб, 0);
	КонецЕсли;
	
КонецПроцедуры // РассчитатьСуммуАванса()

//*****************************************************************************
// ЗачестьАванс(ЗачетАванса, ЗачетАвансаРуб, ДоговорАванса)
//
Процедура ЗачестьАванс(ЗачетАванса, ЗачетАвансаРуб, ДоговорАванса)
	
	Если ЗачетАванса > 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НА";
		Операция.СодержаниеПроводки = "Зачтена предоплата";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = СчетАвансовПолученных;
		Операция.Дебет.Контрагенты = Контрагент;
		Операция.Дебет.Договоры = ДоговорАванса;
		Операция.Кредит.Счет = СчетРасчетовСПокупателем;
		Операция.Кредит.Контрагенты = Контрагент;
		Операция.Кредит.Договоры = Договор;
		
		Если ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Если (АвансПоДоговору + АвансБезДоговора) = ЗачетАванса Тогда
					ЗачетАвансаРуб = АвансПоДоговоруРуб + АвансБезДоговораРуб;
				Иначе
					КурсЗачетаАванса = Окр((АвансПоДоговоруРуб + АвансБезДоговораРуб)/(АвансПоДоговору + АвансБезДоговора), 4, 1);
					ЗачетАвансаРуб = ЗачетАванса*КурсЗачетаАванса;
				КонецЕсли;
			Иначе
				ЗачетАвансаРуб = ЗачетАванса*Курс/Кратность;
			КонецЕсли;
			Операция.Сумма = ЗачетАвансаРуб;
		    Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса;
			
		ИначеЕсли ВестиУчетРасчетовУЕ = 1 Тогда
			Операция.Сумма = ЗачетАвансаРуб;
			Операция.Валюта = Валюта;
			Операция.ВалСумма = ЗачетАванса; 
			
			ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб - Операция.Сумма;
			ЗадолженностьПоРасчетамВУЕВал = ЗадолженностьПоРасчетамВУЕВал - Операция.ВалСумма;
			
		Иначе
			Операция.Сумма = ЗачетАванса;
		КонецЕсли; 
		
		Если ОплатаДоговора = 2 Тогда
			Если ДатаДок >= '01.01.2008' Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ВЛ";
				Операция.СодержаниеПроводки = "Зачтена предоплата";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетПоКоду("ВАЛ.62");
				Операция.Дебет.Контрагенты = Контрагент;
				Операция.Дебет.Договоры = ДоговорАванса;
				Операция.Сумма = ЗачетАванса*Курс/Кратность;
				Операция.Валюта = Валюта;
				Операция.ВалСумма = ЗачетАванса;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ЗачетАванса()

//******************************************************************************
// ДоначислениеАмортизацииДляЦелейНалоговогоУчета()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ДоначислениеАмортизацииДляЦелейНалоговогоУчета(СведенияОбОбъекте)
	
	РассчитаннаяАмортизация = СведенияОбОбъекте.Получить("РассчитаннаяАмортизацияНалог");
	
	Если РассчитаннаяАмортизация <> 0  Тогда
		СчетИАналитикаДляОтнесенияРасходов = глПолучитьСчетРасходовДляЦелейНалоговогоУчета(НМА.ВидРасхода.Получить(ДатаДок), НМА.ЭлементРасхода.Получить(ДатаДок), НМА.Объект.Получить(ДатаДок), ДатаДок);
		СчетРасходов = СчетИАналитикаДляОтнесенияРасходов.Получить("Счет");
		Если ПустоеЗначение(СчетРасходов) = 1 Тогда
			ТекстСообщения = "На закладке ""Налоговый учет"" неверно указано направление отнесения расходов по начисленной амортизации для объекта: "
							+ НМА.Наименование + ", инв.№" + НМА.Код + ".";
			глНеПроводить(Контекст, ТекстСообщения, НМА.ТекущийЭлемент());
			Возврат;
		КонецЕсли;
		СчетРасходов = глПолучитьСчетУчетаКосвенныхРасходовНУ(ДатаДок, НМА.СчетЗатрат.Получить(ДатаДок), НМА.Субконто1.Получить(ДатаДок), СчетРасходов);
	    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НУ";
		Операция.СодержаниеПроводки = "Аморт.за "+Формат(ДатаДок,"Д ММММГГГГ");
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Если ПустоеЗначение(СчетРасходов) = 0 Тогда
			Операция.Дебет.Счет = СчетРасходов;
			Операция.Дебет.Субконто(1, СчетИАналитикаДляОтнесенияРасходов.Получить("Субконто1"));
			Операция.Дебет.Субконто(2, СчетИАналитикаДляОтнесенияРасходов.Получить("Субконто2"));
			Операция.Дебет.Субконто(3, СчетИАналитикаДляОтнесенияРасходов.Получить("Субконто3"));
		КонецЕсли;
		Операция.Кредит.Счет = СчетПоКоду("Н05.04");
		Операция.Кредит.Субконто(1, НМА);
		Операция.Сумма = РассчитаннаяАмортизация;
	КонецЕсли;
	
КонецПроцедуры // ДоначислениеАмортизацииДляЦелейНалоговогоУчета()

//******************************************************************************
// СписаниеАмортизацииДляЦелейНалоговогоУчета()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура СписаниеАмортизацииДляЦелейНалоговогоУчета(СведенияОбОбъекте)
	                                                                                     
	РассчитаннаяАмортизация = СведенияОбОбъекте.Получить("РассчитаннаяАмортизацияНалог");
	НачисленнаяАмортизация = РассчитаннаяАмортизация + СведенияОбОбъекте.Получить("НачисленнаяАмортизацияНалог");
	
	Если НачисленнаяАмортизация <> 0  Тогда
	    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НУ";
		Операция.СодержаниеПроводки = "Списана амортизация";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = СчетПоКоду("Н05.04");
		Операция.Дебет.Субконто(1, НМА);
		Операция.Кредит.Счет = СчетПоКоду("Н05.03");
		Операция.Кредит.Субконто(1, НМА);
		Операция.Сумма = НачисленнаяАмортизация;
	КонецЕсли;
	
КонецПроцедуры // СписаниеАмортизацииДляЦелейНалоговогоУчета()

//******************************************************************************
// ВыбытиеНМАДляЦелейНалоговогоУчета()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ВыбытиеНМАДляЦелейНалоговогоУчета(СведенияОбОбъекте, Знач ВыручкаБезНалогов)
	
	// Отразим списание остаточной стоимости объекта НМА.
	РассчитаннаяАмортизация = СведенияОбОбъекте.Получить("РассчитаннаяАмортизацияНалог");
	НачисленнаяАмортизация = РассчитаннаяАмортизация + СведенияОбОбъекте.Получить("НачисленнаяАмортизацияНалог");
	ОстаточнаяСтоимость = СведенияОбОбъекте.Получить("БалансоваяСтоимостьНалогКон") - НачисленнаяАмортизация;
	
	Если ОстаточнаяСтоимость <> 0 Тогда
	    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НУ";
		Операция.СодержаниеПроводки = "Списание остаточной стоимости";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = СчетПоКоду("Н07.08");
		Операция.Дебет.НематериальныеАктивы = НМА;
		Операция.Кредит.Счет = СчетПоКоду("Н05.03");
		Операция.Кредит.НематериальныеАктивы = НМА;
		Операция.Кредит.УсловияПоступленияИВыбытия = Перечисление.УсловияПоступленияИВыбытия.ЗаПлату;
		Операция.Кредит.Основание = Договор;
		Операция.Сумма = ОстаточнаяСтоимость;
	КонецЕсли;
	
КонецПроцедуры // ВыбытиеНМАДляЦелейНалоговогоУчета()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ОбработкаПроведения()
	
	Сч04 = СчетПоКоду("04.1");
	Сч05 = СчетПоКоду("05");
	Сч62_1 = СчетПоКоду("62.1");
	Сч62_2 = СчетПоКоду("62.2");
	Сч62_6 = СчетПоКоду("62.6");
	Сч62_7 = СчетПоКоду("62.7");
	Сч62_11 = СчетПоКоду("62.11");
	Сч62_22 = СчетПоКоду("62.22");
	Сч91_1 = СчетПоКоду("91.1");
	Сч91_2 = СчетПоКоду("91.2");
	
	Сч68_5 = СчетПоКоду("68.5");
	Сч76_Н_4 = СчетПоКоду("76.Н.4"); 
	
	СчУЕ_62 = СчетПоКоду("УЕ.62");
	СчВАЛ_62 = СчетПоКоду("ВАЛ.62");
	                               
	ЗадолженностьПоРасчетамВУЕРуб = 0;
	ЗадолженностьПоРасчетамВУЕВал = 0;
	НПРКурсоваяРазница = 0;
	
	БухИтНПР = СоздатьОбъект("БухгалтерскиеИтоги"); БухИтНПР.ИспользоватьРазделительУчета(ЮрЛицо);
	БухИтНПР.ПериодМ(ДатаДок);
	СписаноПостоянныхРазниц = 0;
	
	ЦеныВДоговоре = 1; // в рублях
	ВестиУчетРасчетовУЕ = 0;
	
	Если Договор.Выбран() = 1 Тогда
	    Если ПустоеЗначение(Договор.ВалютаДоговора) = 0 Тогда
			ЦеныВДоговоре = 2; // в валюте
		КонецЕсли;
		ОплатаДоговора = Договор.ОплатаДоговора; // 1 - врублях, 2 - в валюте
		ВестиУчетРасчетовУЕ = Договор.ВестиУчетРасчетовУЕ;
	КонецЕсли;
	
	Если ЦеныВДоговоре = 2 Тогда
		Валюта = Договор.ВалютаДоговора;
		Кратность = Валюта.Кратность.Получить(ДатаДок);
		Кратность = ?(Кратность=0, 1, Кратность);
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			Кратность = Кратность * 100 / (100 + Договор.ПроцентКорректировкиКурсаУЕ);
		КонецЕсли;
			
		Если Курс = 0 Тогда
			ТекстСообщения = "Не указан курс валюты";
			глНеПроводить(Контекст, ТекстСообщения, НМА.ТекущийЭлемент());
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ОплатаДоговора = 2 Тогда
		глТаблицаСчетов.УдалитьСтроки();
		
		СчетРасчетовСПокупателем = Сч62_11;
		СчетАвансовПолученных = Сч62_22;
		Если ДатаДок >= '01.01.2008' Тогда
			СчетАвансовПолученныхПереоценка = СчВАЛ_62;
		Иначе
			СчетАвансовПолученныхПереоценка = Сч62_22;
		КонецЕсли;

		глТаблицаСчетов.НоваяСтрока();
		глТаблицаСчетов.Счет = Сч62_11;
		глТаблицаСчетов.Субконто1 = Контрагент;
		глТаблицаСчетов.Субконто2 = Договор;
		глТаблицаСчетов.Субконто3 = "";
		глТаблицаСчетов.Валюта = Валюта;
		глТаблицаСчетов.Курс = Курс;
		
		Если ЗачитыватьАванс <> 1 Тогда
			глТаблицаСчетов.НоваяСтрока();
			глТаблицаСчетов.Счет = СчетАвансовПолученныхПереоценка;
			глТаблицаСчетов.Субконто1 = Контрагент;
			глТаблицаСчетов.Субконто2 = Договор;
			глТаблицаСчетов.Субконто3 = "";
			глТаблицаСчетов.Валюта = Валюта;
			глТаблицаСчетов.Курс = Курс;
			Если ЗачитыватьАванс = 0 Тогда
				БезДоговора = глДоговор(глИмяДоговораДляПлатежейБезДоговора, Контрагент);
				Если ПустоеЗначение(БезДоговора) = 0 Тогда
					глТаблицаСчетов.НоваяСтрока();
					глТаблицаСчетов.Счет = СчетАвансовПолученныхПереоценка;
					глТаблицаСчетов.Субконто1 = Контрагент;
					глТаблицаСчетов.Субконто2 = БезДоговора;
					глТаблицаСчетов.Субконто3 = "";
					глТаблицаСчетов.Валюта = Валюта;
					глТаблицаСчетов.Курс = Курс;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	
		глПереоценкаСчетов(Контекст, глТаблицаСчетов,, 0);
		
	ИначеЕсли ВестиУчетРасчетовУЕ = 0 Тогда
	    СчетРасчетовСПокупателем = Сч62_1;
		СчетАвансовПолученных = Сч62_2;
		
	Иначе
		СчетРасчетовСПокупателем = Сч62_6;
		СчетАвансовПолученных = Сч62_7;
	КонецЕсли;
    
	СведенияО_НМА = глРасчетАмортизацииНМА(НМА, ДатаДок);
	БалансоваяСтоимость = СведенияО_НМА.Получить("БалансоваяСтоимостьКон");
	НачисленнаяАмортизация = СведенияО_НМА.Получить("НачисленнаяАмортизацияНач"); 
	РассчитаннаяАмортизация = СведенияО_НМА.Получить("РассчитаннаяАмортизацияБух");
		
	Если РассчитаннаяАмортизация > 0 Тогда
		Если НМА.СчетЗатрат.Получить(ДатаДок).Выбран() = 0 Тогда
			ТекстСообщения = "Не указан счет отнесения затрат по начисленной амортизации для НМА: "+НМА.Наименование+", инв.№"+НМА.Код+".";
			глНеПроводить(Контекст, ТекстСообщения, НМА.ТекущийЭлемент());
			Возврат;
		КонецЕсли;
		
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НА";
		Операция.СодержаниеПроводки = "Аморт.за "+Формат(ДатаДок,"Д ММММГГГГ");
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = НМА.СчетЗатрат.Получить(ДатаДок);
		Операция.Дебет.Субконто(1, НМА.Субконто1.Получить(ДатаДок));
		Операция.Дебет.Субконто(2, НМА.Субконто2.Получить(ДатаДок));
		Операция.Дебет.Субконто(3, НМА.Субконто3.Получить(ДатаДок));
		Операция.Кредит.Счет = СведенияО_НМА.Получить("СчетНачисленияАмортизации");
		Операция.Кредит.НематериальныеАктивы = НМА;
		Операция.Сумма = РассчитаннаяАмортизация;
	КонецЕсли;
	
	//Отражение постоянных разниц
	Если Константа.ПрименяетсяПБУ18.Получить(ДатаДок) = Да Тогда
		ВсегоПостоянныхРазниц = БухИтНПР.СНД("НПР.04",,, НМА);
		Если ВсегоПостоянныхРазниц <> 0 Тогда
            БалансоваяСтоимость			= СведенияО_НМА.Получить("БалансоваяСтоимостьНач");
			ОстаточнаяСтоимостьНаНачало = БалансоваяСтоимость - НачисленнаяАмортизация;
			Если ОстаточнаяСтоимостьНаНачало <> 0  Тогда
			    СписатьПостоянныхРазниц = Окр(ВсегоПостоянныхРазниц * (РассчитаннаяАмортизация / ОстаточнаяСтоимостьНаНачало), 2);
				Если СписатьПостоянныхРазниц <> 0 Тогда
					СчетДт = НМА.СчетЗатрат.Получить(ДатаДок);
					СчетНПР = глПолучитьСчетДебетаНПР(СчетДт, НМА.Субконто1.Получить(ДатаДок));
					
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "ПР";
					Операция.СодержаниеПроводки = "Списание постоянных разниц";
					Если ПустоеЗначение(СчетНПР) = 0 Тогда
						Операция.Дебет.Счет = СчетНПР;
						Операция.Дебет.Субконто(1, НМА.Субконто1.Получить(ДатаДок));
						Операция.Дебет.Субконто(2, НМА.Субконто2.Получить(ДатаДок));
						Операция.Дебет.Субконто(3, НМА.Субконто3.Получить(ДатаДок));
					КонецЕсли;
					Операция.Кредит.Счет = СчетПоКоду("НПР.04");
					Операция.Кредит.НематериальныеАктивы = НМА;
					Операция.Сумма = СписатьПостоянныхРазниц;
					
					СписаноПостоянныхРазниц = СписатьПостоянныхРазниц;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Доначислим амортизацию для целей налогового учета.
	Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
		ДоначислениеАмортизацииДляЦелейНалоговогоУчета(СведенияО_НМА);
		
		// Если невозможно определить счет отнесения амортизации для целей налогового учета.
		Если СтатусВозврата() = 0 Тогда 
		    Возврат;
		КонецЕсли;
	КонецЕсли;

	НачисленнаяАмортизацияВсего = НачисленнаяАмортизация + РассчитаннаяАмортизация;
	Если (НМА.ПорядокНачисленияАмортизации = 2) и (НачисленнаяАмортизацияВсего <> 0) Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НА";
		Операция.СодержаниеПроводки = "Списана начисл.аморт.";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Сумма = НачисленнаяАмортизацияВсего;
		Операция.Дебет.Счет = Сч05;
		Операция.Дебет.НематериальныеАктивы = НМА;
		Операция.Кредит.Счет = Сч04;
		Операция.Кредит.НематериальныеАктивы = НМА;
	КонецЕсли;
	
	// Списание амортизации для целей налогового учета.
	Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
		СписаниеАмортизацииДляЦелейНалоговогоУчета(СведенияО_НМА);
	КонецЕсли;
	
	ОстаточнаяСтоимость = БалансоваяСтоимость - НачисленнаяАмортизацияВсего;
	Если ОстаточнаяСтоимость <> 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НА";
		Операция.СодержаниеПроводки = "Списана остаточн.ст-ть";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = Сч91_2; 
		Операция.Дебет.ПрочиеДоходыИРасходы = СтатьяПрочихДоходовИРасходов;
		Операция.Кредит.Счет = Сч04;
		Операция.Кредит.НематериальныеАктивы = НМА;
		Операция.Сумма = ОстаточнаяСтоимость;
	КонецЕсли;
	
	//Списание постоянных разниц
	Если Константа.ПрименяетсяПБУ18.Получить(ДатаДок) = Да Тогда
		СписатьПостоянныхРазниц = БухИтНПР.СКД("НПР.04",,, НМА) - СписаноПостоянныхРазниц;
		Если СписатьПостоянныхРазниц <> 0 Тогда
			СчетНПР = глПолучитьСчетДебетаНПР(Сч91_2, СтатьяПрочихДоходовИРасходов);
			
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "ПР";
			Операция.СодержаниеПроводки = "Списание постоянных разниц";
			Если ПустоеЗначение(СчетНПР) = 0 Тогда
				Операция.Дебет.Счет = СчетНПР;
			КонецЕсли;
			Операция.Кредит.Счет = СчетПоКоду("НПР.04");
			Операция.Кредит.НематериальныеАктивы = НМА;
			Операция.Сумма = СписатьПостоянныхРазниц;
		КонецЕсли;
	КонецЕсли;
	
	// Списание остаточной стоимости НМА для целей налогообложения.
	Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
		ВыбытиеНМАДляЦелейНалоговогоУчета(СведенияО_НМА, Стоимость - НДС - НП);
	КонецЕсли;
	
	ЗачетАвансаПоДоговору = 0;
	ЗачетАвансаБезДоговора = 0;
	ЗачетАвансаПоДоговоруРуб = 0;
	ЗачетАвансаБезДоговораРуб = 0;
	РассчитатьСуммуАванса(); // расчет итогов по авансам
	
	СуммаЗачтенногоАванса = 0;
	СуммаЗачтенногоАвансаРуб = 0;
	КурсАванса = 0;
	
	Если (ЗачитыватьАванс <> 1) и (Стоимость <> 0) Тогда //Зачитывать аванс
		Если (ЦеныВДоговоре = 2) и (ВестиУчетРасчетовУЕ = 0) Тогда
			Если ОплатаДоговора = 2 Тогда
			    ВсегоОтгружено = Стоимость;
			
			Иначе
				ВсегоОтгружено = Окр(Стоимость * Курс / Кратность, 2, 1);
			КонецЕсли;
			
		Иначе
			ВсегоОтгружено = Стоимость;
		КонецЕсли;
		
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, ВсегоОтгружено);
			ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, ВсегоОтгружено - ЗачетАвансаПоДоговору);
			
			Аванс = АвансПоДоговору + АвансБезДоговора;
			ЗачетАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
			
			Если ЗачетАванса = Аванс Тогда
			    ЗачетАвансаПоДоговоруРуб = АвансПоДоговоруРуб;
				ЗачетАвансаБезДоговораРуб = АвансБезДоговораРуб;
				
			ИначеЕсли ЗачетАванса > АвансПоДоговору Тогда
				ЗачетАвансаПоДоговоруРуб = АвансПоДоговоруРуб;
				ЗачетАвансаБезДоговораРуб = Окр(ЗачетАвансаБезДоговора * Окр(АвансБезДоговораРуб / ?(АвансБезДоговора = 0, 1, АвансБезДоговора), 4, 1), 2, 1);
				
			Иначе
				ЗачетАвансаПоДоговоруРуб = Окр(ЗачетАвансаПоДоговору * Окр(АвансПоДоговоруРуб / ?(АвансПоДоговору = 0, 1, АвансПоДоговору), 4, 1), 2, 1);
				ЗачетАвансаБезДоговораРуб = 0;
			КонецЕсли;
		
		Иначе
			ЗачетАвансаПоДоговору = Мин(АвансПоДоговору, ВсегоОтгружено);
			ЗачетАвансаБезДоговора = Мин(АвансБезДоговора, ВсегоОтгружено - ЗачетАвансаПоДоговору);
		КонецЕсли;
	    
		ЗачестьАванс(ЗачетАвансаПоДоговору, ЗачетАвансаПоДоговоруРуб, Договор);
		ЗачестьАванс(ЗачетАвансаБезДоговора, ЗачетАвансаБезДоговораРуб, БезДоговора);
		
		СуммаЗачтенногоАванса = ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора;
		
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			СуммаЗачтенногоАвансаРуб = ЗачетАвансаПоДоговоруРуб + ЗачетАвансаБезДоговораРуб;
			
			ПечЗачетАванса = Окр(ЗачетАвансаПоДоговоруРуб + ЗачетАвансаБезДоговораРуб, 2, 1);
			ПечЗачетАвансаПоДоговору = Окр(ЗачетАвансаПоДоговоруРуб, 2, 1);
			ПечЗачетАвансаБезДоговора = Окр(ЗачетАвансаБезДоговораРуб, 2, 1);
			ПечАвансПоДоговору = Окр(АвансПоДоговоруРуб, 2, 1);
			ПечАвансБезДоговора = Окр(АвансБезДоговораРуб, 2, 1);
			
		Иначе
			Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
				СуммаЗачтенногоАвансаРуб = ЗачетАвансаПоДоговоруРуб + ЗачетАвансаБезДоговораРуб;				
			КонецЕсли;			
			ПечЗачетАванса = Окр(ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора, 2, 1);
			ПечЗачетАвансаПоДоговору = Окр(ЗачетАвансаПоДоговору, 2, 1);
			ПечЗачетАвансаБезДоговора = Окр(ЗачетАвансаБезДоговора, 2, 1);
			ПечАвансПоДоговору = Окр(АвансПоДоговору, 2, 1);			
			ПечАвансБезДоговора = Окр(АвансБезДоговора, 2, 1);
		КонецЕсли;
		
		Если (АвансПоДоговору + АвансБезДоговора) <> 0 Тогда
		    ТекстСообщения = "=> Зачет аванса:" + РазделительСтрок
			+ "   отгружено на сумму: "+ВсегоОтгружено+", зачтен аванс в размере: "+ПечЗачетАванса + РазделительСтрок
			+ "   - определен аванс по договору "+Договор+" в сумме: "+ПечАвансПоДоговору+", зачтен аванс в размере: "+ПечЗачетАвансаПоДоговору + РазделительСтрок;
			
			Если ЗачитыватьАванс = 0 Тогда
				ТекстСообщения = ТекстСообщения
				+ "   - определен аванс без указания договора в сумме: "+ПечАвансБезДоговора+", зачтен аванс в размере: "+ПечЗачетАвансаБезДоговора;
			Иначе
				ТекстСообщения = ТекстСообщения
				+ "   - аванс без указания договора не учтен";
			КонецЕсли;
			
			глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		КонецЕсли;
	КонецЕсли;
	
	ВсегоВыручкаРуб = 0;
	ВсегоСуммоваяРазница = 0;
	КурсАванса = ?(СуммаЗачтенногоАванса = 0, 0, Окр(СуммаЗачтенногоАвансаРуб / СуммаЗачтенногоАванса, 4, 1));
	
	Если ((ЦеныВДоговоре = 2) и (ВестиУчетРасчетовУЕ = 1))
	или ((ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008')) Тогда
		К = СуммаЗачтенногоАванса / Стоимость; 
		
		Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
			НДСвЧастиЗачтенногоАвансаРуб = Окр(К * НДС * Курс / Кратность, 2, 1);
			НПвЧастиЗачтенногоАвансаРуб = Окр(К * НП * Курс / Кратность, 2, 1);
		Иначе
			НДСвЧастиЗачтенногоАвансаРуб = Окр(К * НДС * КурсАванса, 2, 1);
			НПвЧастиЗачтенногоАвансаРуб = Окр(К * НП * КурсАванса, 2, 1);
		КонецЕсли;
		
		ВсегоРубПоКурсуОтгрузки = Окр(Стоимость * Курс / Кратность, 2, 1);
		ОплаченоПоКурсуОтгрузки = Окр(К * ВсегоРубПоКурсуОтгрузки, 2, 1);
		ВыручкаРуб = ВсегоРубПоКурсуОтгрузки + СуммаЗачтенногоАвансаРуб - ОплаченоПоКурсуОтгрузки;
		
		НДСРубПоКурсуОтгрузки = Окр(НДС * Курс / Кратность, 2, 1);
		ОплаченоНДСПоКурсуОтгрузки = Окр(К * НДСРубПоКурсуОтгрузки, 2, 1);
		СуммаНДС = НДСРубПоКурсуОтгрузки + НДСвЧастиЗачтенногоАвансаРуб - ОплаченоНДСПоКурсуОтгрузки;
		
		НПРубПоКурсуОтгрузки = Окр(НП * Курс / Кратность, 2, 1);
		ОплаченоНППоКурсуОтгрузки = Окр(К * НПРубПоКурсуОтгрузки, 2, 1);
		СуммаНП = НПРубПоКурсуОтгрузки + НПвЧастиЗачтенногоАвансаРуб - ОплаченоНППоКурсуОтгрузки;
		
		ВыручкаБезНалогов = ВыручкаРуб - СуммаНДС - СуммаНП;
		
		Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
			СуммоваяРазница = 0;
			НПРКурсоваяРазница = Окр(НПРКурсоваяРазница, 2) - Окр(СуммаНДС, 2);
		Иначе
			Если (Договор.ОтражатьСуммовуюРазницуПриЗачетеАванса = 1) и (ВестиУчетРасчетовУЕ = 1) Тогда
				СуммоваяРазница = СуммаЗачтенногоАвансаРуб - ОплаченоПоКурсуОтгрузки;
			КонецЕсли;
		КонецЕсли;
		
		ВсегоВыручкаРуб = ВсегоВыручкаРуб + ВыручкаРуб;
		ВсегоСуммоваяРазница = ВсегоСуммоваяРазница + СуммоваяРазница;
		
	ИначеЕсли ЦеныВДоговоре = 2 Тогда
	    ВыручкаБезНалогов = Окр((Стоимость - НДС - НП)*Курс/Кратность, 2, 1);
		
	Иначе
		ВыручкаБезНалогов = Стоимость - НДС - НП;
	КонецЕсли;
	
	// Отразим в налоговом учете выручку (минус налоги), при этом учитывается аванс.
	// Если отгрузка без перехода права собственности,то не отражается выручка по товарам и продукции
	Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
		Если (Договор.ОтражатьСуммовуюРазницуПриЗачетеАванса = 1) и (ВестиУчетРасчетовУЕ = 1) Тогда
			ПоКурсуОтгрузки = ОплаченоПоКурсуОтгрузки - ОплаченоНДСПоКурсуОтгрузки - ОплаченоНППоКурсуОтгрузки;
			ПоКурсуОплаты = СуммаЗачтенногоАвансаРуб - НДСвЧастиЗачтенногоАвансаРуб - НПвЧастиЗачтенногоАвансаРуб;
			СуммоваяРазница = ПоКурсуОплаты - ПоКурсуОтгрузки;
			
		Иначе
			СуммоваяРазница = 0; 
		КонецЕсли;
			
		Если ВыручкаБезНалогов > 0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		    Операция.НомерЖурнала = "НУ";
			Операция.СодержаниеПроводки = "Учтена выручка";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Кредит.Счет = СчетПоКоду("Н06.06");
			Операция.Кредит.НематериальныеАктивы = НМА;
			Операция.Кредит.УсловияПоступленияИВыбытия = Перечисление.УсловияПоступленияИВыбытия.ЗаПлату;
			Операция.Кредит.Основание = Договор;
			Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
				Операция.Сумма = Окр((Стоимость - НДС - НП)*Курс/Кратность, 2, 1);
				НПРКурсоваяРазница = Окр(НПРКурсоваяРазница, 2) - Окр(Операция.Сумма, 2);
			Иначе
				Операция.Сумма = ВыручкаБезНалогов - СуммоваяРазница;
			КонецЕсли;
			
			глОтражениеСуммовыхРазницВНаловомУчете(Контекст, СуммоваяРазница, 0);
		КонецЕсли;
	КонецЕсли;

	Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
	Операция.НомерЖурнала = "НА";
	Операция.СодержаниеПроводки = "Учтена выручка";
	Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
	Операция.Дебет.Счет = СчетРасчетовСПокупателем;
	Операция.Дебет.Контрагенты = Контрагент;
	Операция.Дебет.Договоры = Договор;
	Операция.Кредит.Счет = Сч91_1;
	Операция.Кредит.ПрочиеДоходыИРасходы = СтатьяПрочихДоходовИРасходов;
	Если ЦеныВДоговоре = 2 Тогда
		Если ОплатаДОговора = 2 Тогда
			Операция.Валюта = Валюта;
			Операция.ВалСумма = Стоимость; 
			Если ДатаДок >= '01.01.2008' Тогда
				Операция.Сумма = ВсегоВыручкаРуб;
				НПРКурсоваяРазница = Окр(НПРКурсоваяРазница, 2) + Окр(Операция.Сумма, 2);
			Иначе
				Операция.Сумма = Стоимость*Курс/Кратность;
			КонецЕсли;
		КонецЕсли;
		
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			Операция.Сумма = ВсегоВыручкаРуб - ВсегоСуммоваяРазница;
			Операция.Валюта = Валюта;
			Операция.ВалСумма = Стоимость; 
			
			ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб + Операция.Сумма;
			ЗадолженностьПоРасчетамВУЕВал = ЗадолженностьПоРасчетамВУЕВал + Операция.ВалСумма;
			
		ИначеЕсли ОплатаДОговора <> 2 Тогда
			Операция.Сумма = Стоимость*Курс/Кратность;
		КонецЕсли;
			
	Иначе
		Операция.Сумма = Стоимость;
	КонецЕсли;
	
	Если ВсегоСуммоваяРазница <> 0 Тогда
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "НА";
		Операция.СодержаниеПроводки = "Суммовая разница";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = СчетРасчетовСПокупателем;
		Операция.Дебет.Контрагенты = Контрагент;
		Операция.Дебет.Договоры = Договор;
		Операция.Кредит.Счет = Сч91_1;
		Операция.Кредит.ПрочиеДоходыИРасходы = СтатьяПрочихДоходовИРасходов;
		Операция.Валюта = Валюта;
		Операция.Сумма = ВсегоСуммоваяРазница;   
		
		ЗадолженностьПоРасчетамВУЕРуб = ЗадолженностьПоРасчетамВУЕРуб + Операция.Сумма;
		
	КонецЕсли;
	
	СуммаРеализации = Стоимость;
	
	Если (СуммаРеализации > 0) и (ВерсияОбъекта >= "7.70.421") и (НП > 0) Тогда
		К = (ЗачетАвансаПоДоговору + ЗачетАвансаБезДоговора) / СуммаРеализации;
		НПкУплате = К*НП;
		ОтложеноНП = НП - НПкУплате;
		Если НПкУплате > 0 Тогда // был зачет аванса
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "ТВ";
			Операция.СодержаниеПроводки = "Начислен НП";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = Сч91_2;
			Операция.Дебет.ПрочиеДоходыИРасходы = СтатьяПрочихДоходовИРасходов;
			Операция.Кредит.Счет = Сч68_5;
			Операция.Кредит.ВидыПлатежейВБюджет = Перечисление.ВидыПлатежейВБюджет.Налог;
			Если ЦеныВДоговоре = 2 Тогда
				Операция.Сумма = НПкУплате*Курс/Кратность;
			Иначе
				Операция.Сумма = НПкУплате;
			КонецЕсли;
		КонецЕсли;
		
		Если ОтложеноНП > 0 Тогда
		    Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "ТВ";
			Операция.СодержаниеПроводки = "Начислен НП";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = Сч91_2;
			Операция.Дебет.ПрочиеДоходыИРасходы = СтатьяПрочихДоходовИРасходов;
			Операция.Кредит.Счет = Сч76_Н_4;
			Операция.Кредит.Контрагенты = Контрагент;
			Операция.Кредит.Договоры = Договор;
			Если ЦеныВДоговоре = 2 Тогда
			    Операция.Сумма = ОтложеноНП*Курс/Кратность;
			Иначе
				Операция.Сумма = ОтложеноНП;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;  
	
	//********************************************************************
	//Проводки по расчетам в у.е. 
	Если ДатаДок >= '01.01.2007' Тогда
		Если ВестиУчетРасчетовУЕ = 1 Тогда
			Если (ЗадолженностьПоРасчетамВУЕРуб + ЗадолженностьПоРасчетамВУЕВал) <> 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "УЕ";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчУЕ_62;
				Операция.Дебет.Субконто(1, Контрагент);
				Операция.Дебет.Субконто(2, Договор);			
				Операция.Валюта = Валюта;
				Операция.Сумма = ЗадолженностьПоРасчетамВУЕРуб;
				Операция.ВалСумма = ЗадолженностьПоРасчетамВУЕВал;
				Операция.СодержаниеПроводки = "Задолженность по реализации в у.е."; 
			КонецЕсли;	
		КонецЕсли; 
		
	КонецЕсли;	
	//********************************************************************
	
	//********************************************************************
	//Проводки по расчетам в вал.
	Если (Константа.ПрименяетсяПБУ18.Получить(ДатаДок) = Да) Тогда
		Если (ОплатаДоговора = 2) и (ДатаДок >= '01.01.2008') Тогда
			Если НПРКурсоваяРазница <> 0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "НУ";
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Дебет.Счет = СчетПоКоду("НПР.99");
				Операция.Сумма = -НПРКурсоваяРазница;
				Операция.СодержаниеПроводки = "Разница в оценке дохода"; 
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
	//********************************************************************
	
	Операция.Записать();
    
	СпрНМА = СоздатьОбъект("Справочник.НематериальныеАктивы");
	Если СпрНМА.НайтиЭлемент(НМА) = 0 Тогда
	    СтатусВозврата(0); Возврат;
	КонецЕсли;
	СпрНМА.ДатаСписания = ДатаДок;
	СпрНМА.Записать();
	УстановитьРеквизитСправочника(НМА, "Состояние", Перечисление.СостоянияНМА.Списан, ДатаДок);
	
	ТекстСообщения = "В справочнике ""Нематериальные активы"" отражено выбытие неметериального актива "+НМА;
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0,, НМА.ТекущийЭлемент());
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения()