////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;
Перем НачальнаяДатаДокумента; 
Перем Новый;
Перем СписокДействий;

Процедура ПриВыбореВариантаОплаты()
	
	Флаг = ?(ВариантОплаты=1,0,1);
	Форма.ИспользоватьСлой("Плательщик", Флаг);
	
КонецПроцедуры // ПриВыбореВариантаОплаты()
//_____________________________________________________________________________
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	ДатаДействия = ДатаДок + 10;
	Если Копирование = 1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;
	
	ВариантОплаты = 1;
	РасчетныйСчет = глЗначениеПоУмолчанию("ОсновнойБанковскийСчет");
КонецПроцедуры
//_____________________________________________________________________________
Процедура ПриОткрытии()
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаПодбор.Доступность(0);
		Форма.КнопкаВыбораРасчетногоСчета.Доступность(0);
		Форма.КнопкаЗаписать.Доступность(0);
	КонецЕсли;

	ПриВыбореВариантаОплаты();
	НачальнаяДатаДокумента = ДатаДок;
	
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Документ." + Вид(),ТаблицаПечФорм);
	
КонецПроцедуры
//_____________________________________________________________________________
Процедура УстСотрудник()
	Если Сотрудник.Выбран() = 1 Тогда
		Выдана = СокрП(Сотрудник.Должность) + " " + СокрП(Сотрудник.Наименование);
	КонецЕсли;
КонецПроцедуры
//_____________________________________________________________________________
Процедура УстПоставщик()
	Если Поставщик.Выбран() = 1 Тогда
	    НаПолучениеОт = СокрП(Поставщик.ПолнНаименование);
	КонецЕсли;
КонецПроцедуры
//_____________________________________________________________________________
Процедура Подбор()
	Меню = СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("Номенклатура");
	Меню.ДобавитьЗначение("Материалы");
	Меню.ДобавитьЗначение("ОсновныеСредства","Основные cредства");
	Меню.ДобавитьЗначение("НематериальныеАктивы","Нематериальные активы");
	ИмяСпр="";
	Если Меню.ВыбратьЗначение(ИмяСпр,"Выберите справочник",,,1)=1 Тогда
		ОткрытьПодбор("Справочник."+ИмяСпр);
	КонецЕсли;
КонецПроцедуры
//_____________________________________________________________________________
Процедура ОбработкаПодбора(ВыбрТовар)
	Кол = 1;
	Если ВвестиЧисло(Кол, "Введите количество", 10, 0) = 0 Тогда
		Возврат;
	ИначеЕсли Кол = 0 Тогда
		Возврат;
	КонецЕсли;

	НоваяСтрока();
	Товар = ВыбрТовар.Наименование;
	Если Найти("Номенклатура,Материалы",ВыбрТовар.Вид()) > 0 Тогда
		ЕдиницаИзмерения = ВыбрТовар.ЕдиницаИзмерения.Наименование;
	Иначе
		ЕдиницаИзмерения = "шт.";
	КонецЕсли;
	Количество = Кол;
	АктивизироватьСтроку();
КонецПроцедуры

//******************************************************************************
// ЧислоПрописью()
//
// Параметры:
//  ЗначЧисло - число, которое нужно представить прописью
//
// Возвращаемое значение:
//  строка, число представленное прописью.
//
// Описание:
//  Преобразует число в его представление пропись. В отличии от стандартной
//  функции формат умеет преобразовывать дробные числа с точностью до 3-х
//  знаков после запятой.
Функция ЧислоПрописью(ЗначЧисло) Экспорт
	
	Перем Возвр;
	Перем ЦелЧасть, ДробЧасть;
	Перем Дробная;
	Перем Окончание;
	Перем ПоследнееСлово;

	ЦелЧасть	= Цел(ЗначЧисло);
	ДробЧасть	= Окр(ЗначЧисло - ЦелЧасть, 3);
	                          
	Если ДробЧасть=Окр(ДробЧасть,0) Тогда
		ДробЧасть	= 0;
		Дробная		= "";
		
	ИначеЕсли ДробЧасть = Окр(ДробЧасть, 1) Тогда
		ДробЧасть	= Окр(ДробЧасть, 1) * 10;
		Дробная		= "десят";
		
	ИначеЕсли ДробЧасть = Окр(ДробЧасть, 2) Тогда
		ДробЧасть	= Окр(ДробЧасть, 2) * 100;
		Дробная		= "сот";
	Иначе
		ДробЧасть	= ДробЧасть * 1000;
		Дробная		= "тысячн";
	КонецЕсли;

	Возвр = "";

	Возвр = Возвр + ?(ПустоеЗначение(ЦелЧасть) = 1, "Ноль", СокрЛП(Формат(ЦелЧасть, "ЧП")));

	Если Дробная <> "" Тогда	// есть дробная часть

		// теперь займемся окончанием последнего слова целой части
		ПоследнееСлово = "";
		Окончание = "";

		Пока (Возвр <> "") И (Возвр = СокрП(Возвр)) Цикл	// последний символ в строке не пробел
			ПоследнееСлово	= Прав(Возвр, 1) + ПоследнееСлово;	// добавляем последнюю букву
			Возвр			= Лев(Возвр, СтрДлина(Возвр) - 1);    // отрезаем последнюю букву
		КонецЦикла;
		
		Если Врег(ПоследнееСлово) = "ОДИН" Тогда
			// первую букву слова оставляем
			ПоследнееСлово	= Лев(ПоследнееСлово, 1) + "дна";
			Окончание		= "ая";
		Иначе
			Если (Врег(ПоследнееСлово) = "ДВА") Тогда
				ПоследнееСлово = Лев(ПоследнееСлово, 1) + "ве";
			КонецЕсли;
			Окончание = "ых";
		КонецЕсли;

		Возвр = Возвр + ПоследнееСлово + " цел" + Окончание + " " + НРег(СокрЛП(Формат(ДробЧасть, "ЧП")));

		// теперь займемся окончанием последнего слова дробной части
		ПоследнееСлово	= "";
		Окончание		= "";
		
		Пока Возвр = СокрП(Возвр) Цикл	// последний символ в строке не пробел
			ПоследнееСлово	= Прав(Возвр, 1) + ПоследнееСлово;	// добавляем последнюю букву
			Возвр			= Лев(Возвр, СтрДлина(Возвр) - 1);    // отрезаем последнюю букву
		КонецЦикла;
		
		Если Врег(ПоследнееСлово) = "ОДИН" Тогда
			// первую букву слова оставляем
			ПоследнееСлово	= Лев(ПоследнееСлово, 1) + "дна";
			Окончание		= "ая";
		Иначе
			Если (Врег(ПоследнееСлово) = "ДВА") Тогда
				ПоследнееСлово = Лев(ПоследнееСлово, 1) + "ве";
			КонецЕсли;
			
			Окончание = "ых";
		КонецЕсли;
		
		Возвр = Возвр + ПоследнееСлово + " " + Дробная + Окончание;
	КонецЕсли;

	Возврат Возвр;
	
КонецФункции // ЧислоПрописью()

//_____________________________________________________________________________
Процедура Печать(Режим)

	Если Режим = 1 Тогда
		НомерФормы = "Типовая межотраслевая форма № М-2";
		ОКУД = "0315001";
	Иначе
		НомерФормы = "Типовая межотраслевая форма № М-2а";
		ОКУД = "0315002";
	КонецЕсли;
	Потребитель="";
	глДобавитьРеквизит(Потребитель, "", Константа.НазваниеОрганизации);
	глДобавитьРеквизит(Потребитель, ", ИНН ", Константа.ИННОрганизации);
	глДобавитьРеквизит(Потребитель, ", ", глПредставлениеАдреса(Константа.АдресОрганизации));

	Организация = Потребитель;
	
	ОргПлательщик = "";
	БанкРекв="";
	Если ВариантОплаты = 1 Тогда
		глДобавитьРеквизит(ОргПлательщик, "", Константа.НазваниеОрганизации);
		глДобавитьРеквизит(ОргПлательщик, ", ИНН ", Константа.ИННОрганизации);
		глДобавитьРеквизит(ОргПлательщик, ", ", глПредставлениеАдреса(Константа.АдресОрганизации));
        РС = РасчетныйСчет;

	Иначе
		глДобавитьРеквизит(ОргПлательщик, "", Плательщик.ПолнНаименование);
		глДобавитьРеквизит(ОргПлательщик, ", ", Плательщик.ЮридическийАдрес);
        РС = СчетПлательщика;
	КонецЕсли;

	глДобавитьРеквизит(БанкРекв, "Счет № ", РС.Номер);
	глДобавитьРеквизит(БанкРекв," в ",РС.БанкОрганизации.Наименование);
	глДобавитьРеквизит(БанкРекв," в ",РС.БанкОрганизации.Местонахождение);
	глДобавитьРеквизит(БанкРекв,", БИК ",РС.БанкОрганизации.Код);
	глДобавитьРеквизит(БанкРекв,", корр.сч. ",РС.БанкОрганизации.КоррСчет);
	Если РС.БанкДляРасчетов.Выбран() = 1 Тогда
		глДобавитьРеквизит(БанкРекв," в ",РС.БанкДляРасчетов.Наименование);
		глДобавитьРеквизит(БанкРекв," в ",РС.БанкДляРасчетов.Местонахождение);
		глДобавитьРеквизит(БанкРекв,", БИК ",РС.БанкДляРасчетов.Код);
		глДобавитьРеквизит(БанкРекв,", корр.сч. ",РС.БанкДляРасчетов.КоррСчет);
	КонецЕсли;

	//~~~~~~~~~~~~~~~~~~~~
	Таб=СоздатьОбъект("Таблица");
	ИмяФайлаПечатнойФормы = КаталогИБ()+"ExtForms\PrnForms\1cbd.mxl";
	Если ФС.СуществуетФайл(ИмяФайлаПечатнойФормы) = 1 Тогда
		Таб.ИсходнаяТаблица(ИмяФайлаПечатнойФормы);
	Иначе
		Таб.ИсходнаяТаблица("Таблица");
	КонецЕсли;
	Если Режим = 1 Тогда
	    Таб.ВывестиСекцию("ШапкаМ2");
	КонецЕсли;
	Таб.ВывестиСекцию("Шапка");
	ВыбратьСтроки();
	Ном=0;
	Пока ПолучитьСтроку() = 1 Цикл
		Ном=Ном+1;             
		ПечКоличество = ?(Количество > 0, Формат(Количество,"Ч015.3")+"   ("+ЧислоПрописью(Количество)+")", "---");
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;
	Таб.ВывестиСекцию("Подвал");
	Таб.Опции(0,0,0,0,"ОпцииПечатиДоверенности");
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Печать доверенности","");
КонецПроцедуры

//******************************************************************************
// ПоКнопкеПечать()
// 
// Вызывается из формул элементов диалога:
//  Кнопка "кнПечать".
//
// Описание:
//  Определяется соответствующая печатная форма.
// 	
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		Печать(1);
		
	ИначеЕсли НомерТекущейФормы = 2  Тогда
		Печать(2);
		
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(Контекст, "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Вызывается из формул элементов диалога:
//  Кнопка "кнВыбПечать".
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Документ." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()

//_____________________________________________________________________________
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога,ФлагСтандОбр)
	Если ИдентЭлемДиалога = "ЕдиницаИзмерения" Тогда
		СпрЕдиницИзмерений = СоздатьОбъект("Справочник.ЕдиницыИзмерений");
		СпрЕдиницИзмерений.НайтиПоНаименованию(ЕдиницаИзмерения);
		Если СпрЕдиницИзмерений.Выбрать("Выберите единицу измерения",) = 1 Тогда
			ЕдиницаИзмерения = СпрЕдиницИзмерений.Наименование;
		КонецЕсли;
		ФлагСтандОбр = 0;
	КонецЕсли;
КонецПроцедуры
//_____________________________________________________________________________
Процедура ПриЗаписи() //предопределенная
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	Если Константа.ВсеДокументыВЖурналОпераций = Да Тогда
	    СуществуетОперация(1);
		Операция.СуммаОперации = 0;
		Операция.Содержание = "Доверенность: "+Сотрудник;
	Иначе
	    СуществуетОперация(0);
	КонецЕсли;
КонецПроцедуры //ПриЗаписи

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()                                                 

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//

Новый = 0;

ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");
	
// добавим информацию о встроенной форме
ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Доверенность по форме М-2";
ТаблицаПечФорм.Кнопка       = "Печ. М-2";

ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Доверенность по форме М-2а";
ТаблицаПечФорм.Кнопка       = "Печ. М-2а";

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Ввести на основании");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");