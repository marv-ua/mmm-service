////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем НачальнаяДатаДокумента; 
Перем Новый;
Перем СписокДействий;
Перем СписокПризнаковЗадолженности;
Перем СписокВидовДокументовДляВыбора;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//******************************************************************************
// ПриИзмененииПризнакаЗадолженности()
// 
// Вызывается из формул элементов диалога:
//  Колонка "Признак задолженности".
// 
// Описание:
//  Устанавливает доступность реквизита "НДС".
//
Процедура ПриИзмененииПризнакаЗадолженности()
	
	Если ПризнакЗадолженности = Перечисление.ПризнакЗадолженности.Дебиторская Тогда
		НДС = 0;
		Форма.НДС.Доступность(0);
	
	Иначе
	    Форма.НДС.Доступность(1)
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииПризнакаЗадолженности()
	
//******************************************************************************
// Заполнить()
//
// Вызывается из формул элементов диалога:
//  Кнопка "Заполнить".
//
// Описание:
//  Заполняет табличную часть документа по данным учета.
//
Процедура Заполнить()

	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.Установить("КонтекстДокумента", Контекст);
	ОткрытьФормуМодально("Обработка.ФормированиеНалоговыхДокументов", СписокПараметров);

КонецПроцедуры // Заполнить()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;

	Если Копирование = 1 Тогда //копирование документа
	    Возврат;
	КонецЕсли;

КонецПроцедуры // ВводНового()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
		Предупреждение("Данные, введенные в документ не будут влиять на
		               |налоговый учет, т.к. установлен способ ведения
					   |налогового учета - ""Одновременно с бухгалтерским учетом"" !");
	КонецЕсли;
	
	НачальнаяДатаДокумента = ДатаДок;
	ПриЗаписиПерепроводить(1);
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаЗаполнить.Доступность(0);
	КонецЕсли;
	
	Форма.ПризнакЗадолженности.ВыполнятьФормулуТолькоПриИзменении(1);
	
	СписокПризнаковЗадолженности = СоздатьОбъект("СписокЗначений");
	СписокПризнаковЗадолженности.ДобавитьЗначение(Перечисление.ПризнакЗадолженности.Дебиторская);
	СписокПризнаковЗадолженности.ДобавитьЗначение(Перечисление.ПризнакЗадолженности.Кредиторская);
	
КонецПроцедуры // ПриОткрытии()
                                                 
//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриРедактированииНовойСтроки()

	ДокументБУ.ВидыДляВыбора(СписокВидовДокументовДляВыбора);

КонецПроцедуры // ПриРедактированииНовойСтроки()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеРедактированияСтроки()
    
	Если (Форма.ТекущаяКолонка() = "ОткрытьДокумент") Тогда
		Если ДокументБУ.Выбран()=0 Тогда
			Предупреждение("Не могу открыть документ, отражающий данную операцию
			                |в бухгалтеском учете: документ не указан!",60);
		Иначе     
			ОткрытьФорму(ДокументБУ);
		КонецЕсли;
		
	Иначе
		ПриИзмененииПризнакаЗадолженности();
		ДокументБУ.ВидыДляВыбора(СписокВидовДокументовДляВыбора);
	КонецЕсли;

КонецПроцедуры // ПриНачалеРедактированияСтроки()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ЭлементДиалога, Флаг)
	
	Если ЭлементДиалога = "ПризнакЗадолженности" Тогда
		Флаг = 0;
		Позиция = СписокПризнаковЗадолженности.НайтиЗначение(ПризнакЗадолженности);
		СписокПризнаковЗадолженности.ВыбратьЗначение(ПризнакЗадолженности,, Позиция,, 2);
		ПриИзмененииПризнакаЗадолженности();
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи() //предопределенная
	
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
		
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;

	Операция.Содержание = "Корректировка задолженности";
	
КонецПроцедуры // ПриЗаписи()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Новый = 0;

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");

СписокВидовДокументовДляВыбора = "ОказаниеУслуг,ВыполнениеЭтапаРабот,ПередачаНМА,ЗавершениеРабот,"+
                               "ОплатаЭтапаРабот, ЗачетАвансаПоставщику, ПоступлениеНМА, ПоступлениеОС,"+
							   "ПоступлениеОборудования, ПоступлениеМатериалов, РасходнаяНакладная,"+
							   "ОтпускМатериаловНаСторону, ПоступлениеТоваров, УслугиСтороннихОрганизаций,"+
							   "ПередачаОС, ЗачетАвансаПокупателя, РеализацияОтгруженнойПродукции, Операция";