////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;
Перем НачальнаяДатаДокумента;
Перем Новый;
Перем СписокДействий;
Перем БИ;


//_____________________________________________________________________________
Процедура Подбор()
	ОткрытьПодбор("Справочник.Оборудование");
КонецПроцедуры

//_____________________________________________________________________________
Процедура ОбработкаПодбора(ВыбрОборудование)
	Кол = 1;
	Если ВвестиЧисло(Кол, "Введите количество", 14, 3) = 0 Тогда
		Возврат;
	ИначеЕсли Кол = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока();
	Оборудование = ВыбрОборудование;
	Количество = Кол;
	АктивизироватьСтроку();
КонецПроцедуры

Процедура Печать()                       
	
	Если ДатаДок<Дата("23.03.2003") Тогда
		// инициализация таблицы
		Таб = СоздатьОбъект("Таблица");
		Таб.ИсходнаяТаблица("ОС-15_97");
		// формирование шапки
		Если Проведен() = 0 Тогда
			ТекстПредупреждения = "Для непроведенного документа графы ""Цена"" и ""Сумма"" не заполняются.";
			Таб.ВывестиСекцию("Предупреждение");
		ИначеЕсли Модифицированность() = 1 Тогда
			Предупреждение("Для печати документ необходимо перепровести.");
			Возврат;
		КонецЕсли;   
		
		СекцияШапка = Таб.ПолучитьСекцию("Шапка");
		СекцияШапка.Номер                    = глПреобразоватьНомерДок(НомерДок, 0, 0);
		СекцияШапка.ДатаСоставления          = Формат(ДатаДок, "Д (0)ДДММГГГГ");
		ОрганизацияАдрес = СокрЛП(Константа.НазваниеОрганизации);
		глДобавитьРеквизит(ОрганизацияАдрес, ", ", глПредставлениеАдреса(Константа.АдресОрганизации));
		глДобавитьРеквизит(ОрганизацияАдрес, ", тел. : ", Константа.ТелефоныОрганизации);
		СекцияШапка.Организация              = "     " + СокрЛП(ОрганизацияАдрес);
		СекцияШапка.КодОКПО                  = СокрЛП(Константа.КодОКПО);
		СекцияШапка.СтруктурноеПодразделение = "";
		СекцияШапка.МестоХранения            = МестоХранения;
		СекцияШапка.КоррСчет                  = "";
		Таб.ВывестиСекцию(СекцияШапка);
		// формирование строк
		СекцияСтрока = Таб.ПолучитьСекцию("Строка");
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			СекцияСтрока.Наименование = Оборудование.Наименование;
			СекцияСтрока.НоменклатурныйНомер = Оборудование.Код;
			СекцияСтрока.Количество = СокрЛ(Формат(Количество, "Ч 15.3."));
			Если Проведен() = 1 Тогда
				СекцияСтрока.Сумма = СокрЛ(Формат(Сумма,"Ч 15.2.,"));
				СекцияСтрока.Цена = СокрЛ(Формат(Окр(Сумма/Количество,2,1),"Ч 15.2.,"));
			КонецЕсли;
			Таб.ВывестиСекцию(СекцияСтрока);
			Состояние("Выведено строк: " + НомерСтроки);
		КонецЦикла;
		Таб.ПовторятьПриПечатиСтроки(20,24);
		Таб.ВывестиСекцию("Подвал");
		Таб.Опции(0,0,0,0,"ОпцииПечатиОС15","ОкноОС15");
		// вывод результирующей таблицы на экран
		Таб.ОбластьПечати(1, 2,Таб.ВысотаТаблицы(),);
		Таб.ТолькоПросмотр(1);
		
		ТабОб = СоздатьОбъект("Таблица");
		ТабОб.ИсходнаяТаблица("ОС-15_97");
		//ТабОб.ИсходнаяТаблица(КаталогИБ()+"ExtForms\"+"ОС-15"+".mxl");
		// формирование обратной стороны акта
		ТабОб.ВывестиСекцию("ОборотнаяСторона");
		// вывод результирующей таблицы на экран
		ТабОб.Опции(0,0,0,0,"ОпцииПечатиОС15об","ОкноОС15об");
		ТабОб.ОбластьПечати(1, 2,,12);
		ТабОб.ТолькоПросмотр(1);
		// сначала оборотную сторону
		ТабОб.Показать("Печать акта приемки-передачи форма ОС-15 (оборотная сторона)","");
		// потом лицевую
		Таб.Показать("Печать акта приемки-передачи форма ОС-15","");
	Иначе
		Таб = СоздатьОбъект("Таблица");
		Таб.ИсходнаяТаблица("ОС-15");
		Таб.ВывестиСекцию("Оборот");
		Таб.ТолькоПросмотр(1);
		Таб.ПараметрыСтраницы(1);
		Таб.Опции(0,0,0,0,"ОпцииПечатиОС_15_об","ОкноОС_15_об");
		Таб.Показать("Печать акта приемки-передачи форма ОС-15 (оборотная сторона)");
		
		Таб = СоздатьОбъект("Таблица");
		Таб.ИсходнаяТаблица("ОС-15");
		Если Проведен() = 0 Тогда
			ТекстПредупреждения = "Для непроведенного документа графа ""Стоимость"" не заполняются.";
			Таб.ВывестиСекцию("Предупреждение");
		ИначеЕсли Модифицированность() = 1 Тогда
			Предупреждение("Для печати документ необходимо перепровести.");
			Возврат;
		КонецЕсли;   
		
		Организация = СокрЛП(Константа.НазваниеОрганизации);
		Таб.ВывестиСекцию("Шапка");
		ВыбратьСтроки();
		Пока ПолучитьСтроку()=1 Цикл
			Наим = Оборудование.Наименование;
			Кол = Формат(Количество,"Ч015.3");
			Если Проведен() = 1 Тогда
				ЦенаЗаЕдиницу = Формат(?(Количество>0, Окр(Сумма/Количество,2,0),Сумма),"Ч015.2");
				Всего = Формат(Сумма,"Ч015.2");
			КонецЕсли;
			Таб.ВывестиСекцию("Строка");
		КонецЦикла;
		Таб.Опции(0,0,0,0,"ОпцииПечатиОС_15","ОкноОС_15");
		Таб.ТолькоПросмотр(1);
		Таб.ПараметрыСтраницы(2);
		
		Таб.Показать("Печать акта приемки-передачи форма ОС-15");
		
	КонецЕсли;
    	
КонецПроцедуры // Печать

//******************************************************************************
// ПоКнопкеПечать()
// 
// Вызывается из формул элементов диалога:
//  Кнопка "кнПечать".
//
// Описание:
//  Определяется соответствующая печатная форма.
// 	
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		Печать();
		
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(Контекст, "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Вызывается из формул элементов диалога:
//  Кнопка "кнВыбПечать".
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Документ." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()
//_____________________________________________________________________________
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	Если Копирование = 1 Тогда	//копирование документа
		Возврат;
	КонецЕсли;
	МестоХранения = глЗначениеПоУмолчанию("ОсновнойСклад");
КонецПроцедуры // ВводНового

//_____________________________________________________________________________
Процедура ПриОткрытии()
	ПриЗаписиПерепроводить(1);

	глПроверкаРазрешенияРедактирования(Контекст);

	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаПодбор.Доступность(0);
		Форма.КнопкаОчистить.Доступность(0);
	КонецЕсли;

	НачальнаяДатаДокумента = ДатаДок;
	
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Документ." + Вид(),ТаблицаПечФорм);
	
КонецПроцедуры //ПриОткрытии

//_____________________________________________________________________________
Процедура ПриЗаписи()
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	Операция.Содержание = "Передача оборудования в монтаж";
КонецПроцедуры // ПриЗаписи

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//*****************************************************************************
Новый = 0;

ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");
	
// добавим информацию о встроенной форме
ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Акт приемки-передачи оборудования в монтаж";
ТаблицаПечФорм.Кнопка       = "Акт ОС-15";

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Ввести на основании"); 
СписокДействий.ДобавитьЗначение("Перейти  в журнал");
