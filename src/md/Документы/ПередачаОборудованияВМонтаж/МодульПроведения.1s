////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаПроведения()
    
	Сч07 = СчетПоКоду("07","Основной");
	Сч08_3 = СчетПоКоду("08.3","Основной");
	СчН01_01 = СчетПоКоду("Н01.01","Основной");
	СчН01_02 = СчетПоКоду("Н01.02","Основной");
	СчН01_09 = СчетПоКоду("Н01.09","Основной");
	
	Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
		СчетДебетНУ = СчН01_09;
		
	Иначе
		СчетДебетНУ = СчН01_01;
	КонецЕсли;
	
	СписокОборудования = СоздатьОбъект("СписокЗначений");
	ВыгрузитьТабличнуюЧасть(СписокОборудования,"Оборудование");
	
	БухИт = СоздатьОбъект("БухгалтерскиеИтоги"); БухИт.ИспользоватьРазделительУчета(ЮрЛицо);
	БухИт.ИспользоватьСубконто(ВидыСубконто.Оборудование,СписокОборудования,2);
	БухИт.ИспользоватьСубконто();
	БухИт.ВыполнитьЗапрос(,ТекущийДокумент(),"07, Н01.02, НПР.07",,,,,"СК");
	
	ТаблицаОстатков = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(ТаблицаОстатков, "Оборудование, Количество");
	ТаблицаОстатков.Свернуть("Оборудование", "Количество");
	ТаблицаОстатков.НоваяКолонка("КоличествоНаСкладах", "Число");
	ТаблицаОстатков.НоваяКолонка("СуммаНаСкладах", "Число");
	ТаблицаОстатков.НоваяКолонка("СредняяСтоимость", "Число");
	ТаблицаОстатков.НоваяКолонка("СуммаПР", "Число");
	ТаблицаОстатков.НоваяКолонка("КоличествоНаСкладахН", "Число");
	ТаблицаОстатков.НоваяКолонка("СуммаНаСкладахН", "Число");
	ТаблицаОстатков.ВыбратьСтроки();
	Пока ТаблицаОстатков.ПолучитьСтроку() = 1 Цикл
		Если ТаблицаОстатков.Количество = 0 Тогда
			ТекстСообщения = "Не указано количество оборудования: "+ТаблицаОстатков.Оборудование;
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
		КоличествоНаСкладе = 0;
		Если БухИт.ПолучитьСубконто(ВидыСубконто.Оборудование,, ТаблицаОстатков.Оборудование) = 1 Тогда
			БухИт.ВыбратьСчета();
			Пока БухИт.ПолучитьСчет() = 1 Цикл
			    Если БухИт.Счет.Код = "07" Тогда
			        ТаблицаОстатков.СуммаНаСкладах = БухИт.СКД("С"); // Суммовой остаток, потребуется для расчета средней стоимости
					ТаблицаОстатков.КоличествоНаСкладах = БухИт.СКД("К");
					Если БухИт.ПолучитьСубконто(2,, МестоХранения) = 1 Тогда
						КоличествоНаСкладе = БухИт.СКД("К");
					КонецЕсли;
				
				ИначеЕсли БухИт.Счет.Код = "НПР.07" Тогда
					ТаблицаОстатков.СуммаПР = БухИт.СКД("С");
					
				Иначе // счет = Н01.02
					ТаблицаОстатков.СуммаНаСкладахН = БухИт.СКД("С");
					ТаблицаОстатков.КоличествоНаСкладахН = БухИт.СКД("К");
			    КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если КоличествоНаСкладе < ТаблицаОстатков.Количество Тогда
			ТекстСообщения = "На складе "+КоличествоНаСкладе+" единиц "+
							" из необходимых "+ТаблицаОстатков.Количество+" единиц оборудования "+ТаблицаОстатков.Оборудование;
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		ТаблицаОстатков.СредняяСтоимость = ТаблицаОстатков.СуммаНаСкладах/ТаблицаОстатков.КоличествоНаСкладах;
		Стр = ""+ТаблицаОстатков.НомерСтроки+". "+ТаблицаОстатков.Оборудование+" ";
	   	Стр = Стр+": на складе "+МестоХранения+" "+КоличествоНаСкладе+" единиц,";
	   	Стр = Стр+" всего на складах "+ТаблицаОстатков.КоличествоНаСкладах+" единиц на сумму "+ТаблицаОстатков.СуммаНаСкладах+",";
	   	Стр = Стр+" средняя стоимость "+Окр(ТаблицаОстатков.СредняяСтоимость, 2, 1);
	   	ТекстСообщения = Стр;
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
		
		Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
			Если ТаблицаОстатков.КоличествоНаСкладахН < ТаблицаОстатков.Количество Тогда
				ТекстСообщения = "По нанным налогового учета "+ТаблицаОстатков.КоличествоНаСкладахН+" единиц "+
								" из необходимых "+ТаблицаОстатков.Количество+" единиц оборудования "+ТаблицаОстатков.Оборудование;
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если Количество = 0 Тогда
			ТекстСообщения = "Строка " + НомерСтроки + ", не указано количество оборудования """ + Оборудование + ".";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
			
		ИначеЕсли Оборудование.Выбран() = 0 Тогда
			ТекстСообщения = "Строка " + НомерСтроки + ", не указано оборудование.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		// Проверяем остаток оборудования.
		НомСтроки = 0;
		ТаблицаОстатков.НайтиЗначение(Оборудование, НомСтроки, "Оборудование");
		ТаблицаОстатков.ПолучитьСтрокуПоНомеру(НомСтроки); 
		
		Если Количество = ТаблицаОстатков.КоличествоНаСкладах Тогда
			// Если оборудование отпускается полностью, полностью списываем сумму.
			СуммаОтгрузки         = ТаблицаОстатков.СуммаНаСкладах;
			СуммаОтгрузкиН        = ТаблицаОстатков.СуммаНаСкладахН;
			СуммаПостоянныхРазниц = ТаблицаОстатков.СуммаПР;
			
		Иначе
			СуммаОтгрузки = Окр(Количество*ТаблицаОстатков.СредняяСтоимость,2,1);
			СуммаПостоянныхРазниц = Окр(Количество*(ТаблицаОстатков.СуммаПР/ТаблицаОстатков.КоличествоНаСкладах), 2);
			
			// Рассчитаем сумму передаваемого оборудования для целей налогового учета.
			Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
				СуммаОтгрузкиН = Окр(Количество*ТаблицаОстатков.СуммаНаСкладахН/ТаблицаОстатков.КоличествоНаСкладахН,2,1);
				
			Иначе // количественный учет на счете Н01.02 еще не велся,
				  // поэтому количество оборудования берется по данным бух. учета. 
				СуммаОтгрузкиН = Окр(Количество*ТаблицаОстатков.СуммаНаСкладахН/ТаблицаОстатков.КоличествоНаСкладах,2,1);
			КонецЕсли;
		КонецЕсли;
		
		// Уменьшим остаток оборудования на списанное количество.
		ТаблицаОстатков.СуммаНаСкладах = ТаблицаОстатков.СуммаНаСкладах - СуммаОтгрузки;
		ТаблицаОстатков.КоличествоНаСкладах = ТаблицаОстатков.КоличествоНаСкладах - Количество;
		              
		ТаблицаОстатков.СуммаПР = ТаблицаОстатков.СуммаПР - СуммаПостоянныхРазниц;
		
		ТаблицаОстатков.СуммаНаСкладахН = ТаблицаОстатков.СуммаНаСкладахН - СуммаОтгрузки;
		ТаблицаОстатков.КоличествоНаСкладахН = ТаблицаОстатков.КоличествоНаСкладахН - Количество;
		
		// Сформируем проводку по передаче оборудования.
		Операция.НоваяПроводка();
		Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "ОС";
		Операция.СодержаниеПроводки = "Передано в монтаж";
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.Дебет.Счет = Сч08_3;                         
		Операция.Дебет.Субконто(1,ОбъектВнеоборотныхАктивов);
		Операция.Дебет.Субконто(2,СтатьяЗатратНаСтроительство);
		Операция.Кредит.Счет = Сч07;                         
		Операция.Кредит.Субконто(1,Оборудование);
		Операция.Кредит.Субконто(2,МестоХранения);
		Операция.Сумма = СуммаОтгрузки;
		Операция.Количество = Количество;
		
		// Запишем скрытые реквизиты документа.
		Сумма = Операция.Сумма;
		
		// Постоянные разницы
		Если Константа.ПрименяетсяПБУ18.Получить(ДатаДок) = Да Тогда
			Если СуммаПостоянныхРазниц <> 0 Тогда
			    Операция.НоваяПроводка();
				Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "ПР";
				Операция.СодержаниеПроводки = "Передача оборудования в монтаж";
				Операция.Дебет.Счет = СчетПоКоду("НПР.08");
				Операция.Дебет.Субконто(1, ОбъектВнеоборотныхАктивов);
				Операция.Кредит.Счет = СчетПоКоду("НПР.07");
				Операция.Кредит.Субконто(1, Оборудование);
				Операция.Сумма = СуммаПостоянныхРазниц;
			КонецЕсли;
		КонецЕсли;
		
		// Налоговый учет.
		Если СуммаОтгрузкиН <> 0 Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.НомерЖурнала = "ОС";
			Операция.СодержаниеПроводки = "Передано в монтаж";
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.Дебет.Счет = СчетДебетНУ;
			Операция.Дебет.Субконто(1, ОбъектВнеоборотныхАктивов);
			Операция.Кредит.Счет = СчН01_02;                         
			Операция.Кредит.Субконто(1, Оборудование);
			Операция.Сумма = СуммаОтгрузкиН;
			Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
				Операция.Количество = Количество;
			КонецЕсли;
		КонецЕсли;
						
	КонецЦикла;

	Операция.СуммаОперации = Итог("Сумма");
	Операция.Записать();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);

КонецПроцедуры // ОбработкаПроведения()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаУдаленияПроведения()
	
	Если ВыбратьСтроки() = 1 Тогда
		Пока ПолучитьСтроку() = 1 Цикл
			Сумма = 0;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаУдаленияПроведения()


