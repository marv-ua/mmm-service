
//******************************************************************************
// ПометитьНаУдалениеНеиспользуемыеНазначенияИспоьзования()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ПометитьНаУдалениеНеиспользуемыеНазначенияИспоьзования()
	
	ТаблицаСНазначениямиИспользования = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(ТаблицаСНазначениямиИспользования, "НазначениеИспользования");
	
	СпрНазначениеИспользования = СоздатьОбъект("Справочник.НазначенияИспользования");
	СпрНазначениеИспользования.ВыбратьЭлементыПоРеквизиту("ДокументПередачаВПроизводство", ТекущийДокумент(), 0);
	Пока СпрНазначениеИспользования.ПолучитьЭлемент() = 1 Цикл
		Если глПолучитьСтрокуПоЗначению(ТаблицаСНазначениямиИспользования, СпрНазначениеИспользования.ТекущийЭлемент(), "НазначениеИспользования") = 0 Тогда
			СпрНазначениеИспользования.Удалить(0);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПометитьНаУдалениеНеиспользуемыеНазначенияИспоьзования()

//******************************************************************************
// ЗаполнитьНазначенияИспользования()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ЗаполнитьНазначенияИспользования()
	
	СпрНазначениеИспользования = СоздатьОбъект("Справочник.НазначенияИспользования");
	
	ТаблицаСНазначениямиИспользования = СоздатьОбъект("ТаблицаЗначений");
	ВыгрузитьТабличнуюЧасть(ТаблицаСНазначениямиИспользования, "НазначениеИспользования");
	ТаблицаСНазначениямиИспользования.Свернуть("НазначениеИспользования",);
	ТаблицаСНазначениямиИспользования.НоваяКолонка("Используется", "Число", 1, 0);
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		
		Если Материал.Выбран() = 0 Тогда
		    Продолжить;
		КонецЕсли;
		
		НайденоНазначениеИспользования = 0;
		
		// Найдем подходящее назначение использования в списке тех назначений
		// использования, которые уже указаны в документе.
		ТаблицаСНазначениямиИспользования.ВыбратьСтроки();
		Пока ТаблицаСНазначениямиИспользования.ПолучитьСтроку() = 1 Цикл
			НазИспользования = ТаблицаСНазначениямиИспользования.НазначениеИспользования;
		    Если ПустоеЗначение(НазИспользования) = 1 Тогда
		        Продолжить;
			КонецЕсли;
			
			Если (Материал = НазИспользования.Владелец) 
			   и (СпособПогашенияСтоимости = НазИспользования.СпособПогашенияСтоимости)
			   и (СрокПолезногоИспользования = НазИспользования.СрокПолезногоИспользования) Тогда
				НазначениеИспользования = ТаблицаСНазначениямиИспользования.НазначениеИспользования;
				ТаблицаСНазначениямиИспользования.Используется = 1;
				НайденоНазначениеИспользования = 1;
				Прервать;
			КонецЕсли;
			   
		КонецЦикла;
		
		// Если назначение использования не найдено, то создадим новое и поместим
		// в таблицу назначений использования.
		Если НайденоНазначениеИспользования = 0 Тогда
			СпрНазначениеИспользования.ИспользоватьВладельца(Материал);
			СпрНазначениеИспользования.Новый();
			СпрНазначениеИспользования.СпособПогашенияСтоимости   = СпособПогашенияСтоимости;
			СпрНазначениеИспользования.СрокПолезногоИспользования = СрокПолезногоИспользования;
			СпрНазначениеИспользования.Записать();
			
			НазначениеИспользования = СпрНазначениеИспользования.ТекущийЭлемент();
			
			ТаблицаСНазначениямиИспользования.НоваяСтрока();
			ТаблицаСНазначениямиИспользования.НазначениеИспользования = НазначениеИспользования;
			ТаблицаСНазначениямиИспользования.Используется = 1;
		КонецЕсли;
	КонецЦикла;
	
	// Те назначение использования, которые ранее использовались документом и сейчас
	// больше не используются необходимо пометить на удаление, а тем, которые используются
	// пропишем значения реквизитов.
	ТаблицаСНазначениямиИспользования.ВыбратьСтроки();
	Пока ТаблицаСНазначениямиИспользования.ПолучитьСтроку() = 1 Цикл
		Если ПустоеЗначение(ТаблицаСНазначениямиИспользования.НазначениеИспользования) = 1 Тогда
			
		ИначеЕсли ТаблицаСНазначениямиИспользования.Используется = 1 Тогда
			СпрНазначениеИспользования.НайтиЭлемент(ТаблицаСНазначениямиИспользования.НазначениеИспользования);
			
			СпрНазначениеИспользования.ДатаВводаВЭксплуатацию = ДатаДок;
		   	СпрНазначениеИспользования.Наименование = НаименованиеНазначенияИспользования;
		   	СпрНазначениеИспользования.СчетЗатрат   = СчетЗатрат;
		   	Для А = 1 По 3 Цикл
				СпрНазначениеИспользования.НазначитьТип("Субконто"+А,СчетЗатрат.ВидСубконто(А));
			КонецЦикла;
		   	СпрНазначениеИспользования.Субконто1 = Субконто1;
		   	СпрНазначениеИспользования.Субконто2 = Субконто2;
		   	СпрНазначениеИспользования.Субконто3 = Субконто3;
		   	СпрНазначениеИспользования.ДокументПередачаВПроизводство = ТекущийДокумент();
		   	СпрНазначениеИспользования.Записать();
		   	
		   	Если СпрНазначениеИспользования.ПометкаУдаления() = 1 Тогда
		   	    СпрНазначениеИспользования.СнятьПометкуУдаления();
		   	КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПометитьНаУдалениеНеиспользуемыеНазначенияИспоьзования();
	
КонецПроцедуры // ЗаполнитьНазначенияИспользования()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаПроведения()
    		
	Сч10_10   = СчетПоКоду("10.10");
	Сч10_11_1 = СчетПоКоду("10.11.1");
	Сч10_11_2 = СчетПоКоду("10.11.2");
	СчН02_01  = СчетПоКоду("Н02.01");
	СчМЦ_02     = СчетПоКоду("МЦ.02");
	СчМЦ_03     = СчетПоКоду("МЦ.03");
	Сч10_9	=	СчетПоКоду("10.9");   
	СчМЦ_04	=	СчетПоКоду("МЦ.04");
	СчН02_МЦ	=	СчетПоКоду("Н02.МЦ");
		
	Если ПустоеЗначение(СчетЗатрат) = 1 Тогда
		ТекстСообщения = "Не указан счет отнесения погашенной стоимости.";
		глНеПроводить(Контекст, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Операция.СуммаОперации = 0;
	
	СписокМатериалов = СоздатьОбъект("СписокЗначений");
	ВыгрузитьТабличнуюЧасть(СписокМатериалов, "Материал");
	
	ТаблицаОстатков = СоздатьОбъект("ТаблицаЗначений");

	БухИтоги = СоздатьОбъект("БухгалтерскиеИтоги"); БухИтоги.ИспользоватьРазделительУчета(ЮрЛицо);
	БухИтоги.ИспользоватьСубконто(ВидыСубконто.Материалы, СписокМатериалов, 2);
	БухИтоги.ИспользоватьСубконто(ВидыСубконто.МестаХранения);
	Если ВидМатериалов=3 Тогда
		БухИтоги.ВыполнитьЗапрос(,ТекущийДокумент(), Сч10_9,,,,, "СК");
		//_____________________________________________________________________________
		БИН_Н02_МЦ = СоздатьОбъект("БухгалтерскиеИтоги"); БИН_Н02_МЦ.ИспользоватьРазделительУчета(ЮрЛицо);
		БИН_Н02_МЦ.ИспользоватьСубконто(ВидыСубконто.Материалы, СписокМатериалов, 1);
		БИН_Н02_МЦ.ВыполнитьЗапрос(,ТекущийДокумент(), СчН02_МЦ,,,,, "СК"); 
		СтрокаСчет="(10.9)";
		//_____________________________________________________________________________
	Иначе
		БухИтоги.ВыполнитьЗапрос(,ТекущийДокумент(), Сч10_10,,,,, "СК");
		СтрокаСчет="(10.10)"
	КонецЕсли;
		
	БИН02_01 = СоздатьОбъект("БухгалтерскиеИтоги"); БИН02_01.ИспользоватьРазделительУчета(ЮрЛицо);
	БИН02_01.ИспользоватьСубконто(ВидыСубконто.Материалы, СписокМатериалов, 2);
	БИН02_01.ВыполнитьЗапрос(,ТекущийДокумент(), СчН02_01,,,,, "СК");
    	
	ВыгрузитьТабличнуюЧасть(ТаблицаОстатков, "Материал, Количество");
	ТаблицаОстатков.Свернуть("Материал", "Количество");
	
	ТаблицаОстатков.НоваяКолонка("КоличествоНаСкладах", "Число");
	ТаблицаОстатков.НоваяКолонка("СуммаНаСкладах", "Число");
	ТаблицаОстатков.НоваяКолонка("СредняяСтоимость", "Число");
	ТаблицаОстатков.НоваяКолонка("КоличествоНаСкладахНУ", "Число");
	ТаблицаОстатков.НоваяКолонка("СуммаНаСкладахНУ", "Число");
	ТаблицаОстатков.НоваяКолонка("СредняяСтоимостьНУ", "Число");   
	ТаблицаОстатков.НоваяКолонка("КоличествоИнвентаряНаСкладахНУ", "Число");
	ТаблицаОстатков.ВыбратьСтроки();
	Пока ТаблицаОстатков.ПолучитьСтроку() = 1 Цикл
		КоличествоНаСкладе = 0;
			
		Если БухИтоги.ПолучитьСубконто(1,, ТаблицаОстатков.Материал) = 1 Тогда
			ТаблицаОстатков.СуммаНаСкладах = Макс(БухИтоги.СКД("С"), 0);
			ТаблицаОстатков.КоличествоНаСкладах = БухИтоги.СКД("К");
			Если БухИтоги.ПолучитьСубконто(ВидыСубконто.МестаХранения,, МестоХранения) = 1 Тогда
				КоличествоНаСкладе = БухИтоги.СКД("К");
			КонецЕсли;
		КонецЕсли;
		
		Стр = ""+ТаблицаОстатков.НомерСтроки+". "+ТаблицаОстатков.Материал+": на складе "+МестоХранения+" "+КоличествоНаСкладе+" "+
			  ТаблицаОстатков.Материал.ЕдиницаИзмерения+", всего на складах "+ТаблицаОстатков.КоличествоНаСкладах+" "+
			  ТаблицаОстатков.Материал.ЕдиницаИзмерения+" на сумму "+ТаблицаОстатков.СуммаНаСкладах;
		
		Если ТаблицаОстатков.КоличествоНаСкладах > 0 Тогда
			ТаблицаОстатков.СредняяСтоимость = ТаблицаОстатков.СуммаНаСкладах/ТаблицаОстатков.КоличествоНаСкладах;
			Стр = Стр + ",  средняя стоимость " + Окр(ТаблицаОстатков.СредняяСтоимость, 2, 1);
		КонецЕсли;
		ТекстСообщения = Стр;
		глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);

		Если КоличествоНаСкладе < ТаблицаОстатков.Количество Тогда
			ТекстСообщения = "На складе "+КоличествоНаСкладе+" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+
							" из необходимых "+ТаблицаОстатков.Количество+" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+" "+
							ТаблицаОстатков.Материал + " "+СтрокаСчет;
			Если Константа.КонтрольОтрицательныхОстатков = Да Тогда
				глНеПроводить(Контекст, ТекстСообщения);
				Возврат;
			
			Иначе
				глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), -1);
			КонецЕсли;
		КонецЕсли;    
		
		// Предварительные операции по налоговому учету.
		Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
			//_____________________________________________________________________________           
			Если ВидМатериалов=3 Тогда				
				Если БИН_Н02_МЦ.ПолучитьСубконто(,, ТаблицаОстатков.Материал) = 1 Тогда
					ТаблицаОстатков.КоличествоИнвентаряНаСкладахНУ= БИН_Н02_МЦ.СКД("К");
					ТекстСписокОС=глПереченьМПЗВСоставеОС(ТаблицаОстатков.Материал,ТекущийДокумент());
					СчетУчетаИнвентаря=" и Н02.МЦ"
				КонецЕсли;
			Иначе
				ТаблицаОстатков.КоличествоИнвентаряНаСкладахНУ=0;
			КонецЕсли;
			//_____________________________________________________________________________

			Если БИН02_01.ПолучитьСубконто(1,, ТаблицаОстатков.Материал) = 1 Тогда
				ТаблицаОстатков.СуммаНаСкладахНУ = Макс(БИН02_01.СКД("С"), 0);
				ТаблицаОстатков.КоличествоНаСкладахНУ = БИН02_01.СКД("К");				
			КонецЕсли;
			
			Если ТаблицаОстатков.КоличествоНаСкладахНУ > 0 Тогда
				ТаблицаОстатков.СредняяСтоимостьНУ = ТаблицаОстатков.СуммаНаСкладахНУ/ТаблицаОстатков.КоличествоНаСкладахНУ;
			КонецЕсли;
			
			Если (ТаблицаОстатков.КоличествоНаСкладахНУ + ТаблицаОстатков.КоличествоИнвентаряНаСкладахНУ) < ТаблицаОстатков.Количество Тогда
				
					ТекстСообщения = "По данным налогового учета на складах "+(ТаблицаОстатков.КоличествоНаСкладахНУ+ТаблицаОстатков.КоличествоИнвентаряНаСкладахНУ)+" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+
					" из необходимых "+ТаблицаОстатков.Количество+" "+ТаблицаОстатков.Материал.ЕдиницаИзмерения+" "+
					ТаблицаОстатков.Материал + " (Н02.01"+СчетУчетаИнвентаря+")";	

				Если Константа.КонтрольОтрицательныхОстатков = Да Тогда
					глНеПроводить(Контекст, ТекстСообщения);
					Возврат;
				Иначе
					глСообщениеПроведения(ТекстСообщения + ?(Пустоезначение(ТекстСписокОС) = 0, "
										|"+ТекстСписокОС,""), ТекущийДокумент(), -1);
				КонецЕсли; 
			Иначе
				Если Пустоезначение(ТекстСписокОС) = 0 Тогда
					глСообщениеПроведения(ТекстСписокОС, ТекущийДокумент(), -1);				
				КонецЕсли;				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
		// Определим по данным бухгалтерского учета направление списания МПЗ в налоговом учете (счет и аналитику).
		Если СчетЗатрат.Код = "20" Тогда    
			ПереченьСтатейЗатрат = глПолучитьПереченьСтатейЗатрат(ДатаДок); 
			
			Если ВидМатериалов = 3 Тогда
				ОбъектыАналитикиНУ = глСчетИАналитикаРасходовНУ(Контекст, СчетЗатрат, Субконто1, Субконто2, Субконто3, ПереченьСтатейЗатрат, СчетПоКоду("10.9"));
			Иначе
				ОбъектыАналитикиНУ = глСчетИАналитикаРасходовНУ(Контекст, СчетЗатрат, Субконто1, Субконто2, Субконто3, ПереченьСтатейЗатрат, СчетПоКоду("10.11." + ВидМатериалов));
			КонецЕсли;
			
		Иначе
			ОбъектыАналитикиНУ = глСчетИАналитикаРасходовНУ(Контекст, СчетЗатрат, Субконто1, Субконто2, Субконто3);
		КонецЕсли;
		СчетНУ = ОбъектыАналитикиНУ.Получить("Счет");
	КонецЕсли;
	
	НачатьТранзакцию();
	
	// заполнение справочника назначения использования
	ЗаполнитьНазначенияИспользования();
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку()=1 Цикл
		Если Количество=0 Тогда
			// Если не указано количество отпускаемного материала, выдаем сообщение и проводку не формируем.
			ТекстСообщения = "Строка " + НомерСтроки + ", не указано отпущенное количество""" + Материал;
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат;
			
		ИначеЕсли Материал.Выбран() = 0 Тогда
			ТекстСообщения = "Строка " + НомерСтроки + ", не указан объект учета.";
			глНеПроводить(Контекст, ТекстСообщения);
			Возврат; 
		КонецЕсли;
		
		НомСтроки = 0;
		ТаблицаОстатков.НайтиЗначение(Материал, НомСтроки, "Материал");
		ТаблицаОстатков.ПолучитьСтрокуПоНомеру(НомСтроки);
		
		Если Количество >= ТаблицаОстатков.КоличествоНаСкладах Тогда
			СуммаОтгрузки = ТаблицаОстатков.СуммаНаСкладах;
			
		Иначе
			СуммаОтгрузки = Окр(Количество*ТаблицаОстатков.СредняяСтоимость, 2, 1);
		КонецЕсли;
		
		// Уменьшим остаток материала на списанное количество.
		ТаблицаОстатков.СуммаНаСкладах = ТаблицаОстатков.СуммаНаСкладах - СуммаОтгрузки;
		ТаблицаОстатков.КоличествоНаСкладах = ТаблицаОстатков.КоличествоНаСкладах - Количество;

		Если ВидМатериалов<>3 Тогда              
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
		Операция.СодержаниеПроводки = ?(ВидМатериалов = 1, "Спецодежда передана в производство (экспл.)",
		                                                   "Спецоснастка передана в производство (экспл.)");
		Операция.НомерЖурнала = "МТ";
			Если ВидМатериалов = 2 Тогда // спецоснастка
				Операция.Дебет.Счет = Сч10_11_2;
				Операция.Дебет.Субконто(1,Материал);
				Операция.Дебет.Субконто(2,НазначениеИспользования);
				Операция.Дебет.Субконто(3,Подразделение);
				
			ИначеЕсли ВидМатериалов=1 Тогда// спецодежда
				Операция.Дебет.Счет = Сч10_11_1;
				Операция.Дебет.Субконто(1,Материал);
				Операция.Дебет.Субконто(2,НазначениеИспользования);
				Операция.Дебет.Субконто(3,Сотрудник);
			КонецЕсли;
			
			Операция.Кредит.Счет = Сч10_10;
			Операция.Кредит.Субконто(1,Материал);
			Операция.Кредит.Субконто(2, МестоХранения);
			Операция.Количество = Количество;
			Операция.Сумма = СуммаОтгрузки;
			
			Операция.СуммаОперации = Операция.СуммаОперации + Операция.Сумма;
			
			// Сумму отпущенного материала записываем в скрытую колонку табличной части.
			Сумма = Операция.Сумма;
		КонецЕсли;                       
	// Если стоимость материалов погашается сразу,
		// то необходимо погасить стоимость.
		Если НазначениеИспользования.СпособПогашенияСтоимости =
		     Перечисление.СпособПогашенияСтоимости.ПогашатьСтоимостьВМоментПередачиВПроизводство Тогда
			Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
			Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			Операция.СодержаниеПроводки = "Погашение стоимости при вводе в эскплуатацию";
			Операция.НомерЖурнала = "МТ";
			Операция.Дебет.Счет = СчетЗатрат;
			Операция.Дебет.Субконто(1,Субконто1);
			Операция.Дебет.Субконто(2,Субконто2);
			Операция.Дебет.Субконто(3,Субконто3);
			
			Если ВидМатериалов <> 3 Тогда 
				
				Если ВидМатериалов = 2 Тогда // спецоснастка
					Операция.Кредит.Счет = Сч10_11_2;
					Операция.Кредит.Субконто(1,Материал);
					Операция.Кредит.Субконто(2,НазначениеИспользования);
					Операция.Кредит.Субконто(3,Подразделение);
					
				ИначеЕсли ВидМатериалов=1 Тогда// спецодежда
					Операция.Кредит.Счет = Сч10_11_1;
					Операция.Кредит.Субконто(1,Материал);
					Операция.Кредит.Субконто(2,НазначениеИспользования);
					Операция.Кредит.Субконто(3,Сотрудник);
				КонецЕсли;
				
				Операция.Сумма = СуммаОтгрузки;
				
			Иначе   //Инвентарь 
				
				Операция.Кредит.Счет = Сч10_9;
				Операция.Кредит.Субконто(1,Материал);
				Операция.Кредит.Субконто(2, МестоХранения);
				Операция.Количество = Количество;
				Операция.Сумма = СуммаОтгрузки;
				// Сумму отпущенного материала записываем в скрытую колонку табличной части.
				Операция.СуммаОперации = Операция.СуммаОперации + Операция.Сумма;
				Сумма = Операция.Сумма;
				
			КонецЕсли;			
			
		КонецЕсли;
		
		// Отразим стоимсть материалов на вспомогательном забалансовом счете.
		Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
		Операция.НомерЖурнала = "МТ";
		Операция.СодержаниеПроводки = ?(ВидМатериалов = 1, "Учтена ст-ть спецодежды, перед.в пр-во (экспл.)",
		?(ВидМатериалов=2,"Учтена ст-ть спецоснастки, перед.в пр-во (экспл.)","Учтена ст-ть инвентаря, перед.в пр-во (экспл.)"));
		Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
			
		Если ВидМатериалов=3 Тогда //Инвентарь 
			Операция.Дебет.Счет = СчМЦ_04;
			Операция.Дебет.Субконто(1,Материал);
			Операция.Дебет.Субконто(2,Сотрудник); 
		Иначе
			Если ВидМатериалов = 2 Тогда // спецоснастка
				Операция.Дебет.Счет = СчМЦ_03;
				Операция.Дебет.Субконто(1,Материал);
				Операция.Дебет.Субконто(2,НазначениеИспользования);
				Операция.Дебет.Субконто(3,Подразделение);
			ИначеЕсли ВидМатериалов=1 Тогда // спецодежда
				Операция.Дебет.Счет = СчМЦ_02;
				Операция.Дебет.Субконто(1,Материал);
				Операция.Дебет.Субконто(2,НазначениеИспользования);
				Операция.Дебет.Субконто(3,Сотрудник);
			КонецЕсли;
			Операция.Сумма = СуммаОтгрузки;
		КонецЕсли;
		Операция.Количество = Количество;
		
	
		// Передача материалов в налоговом учете.
		Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
			
			КоличествоОтгрузкиНУ=Количество;
			
			Если Количество >= ТаблицаОстатков.КоличествоИнвентаряНаСкладахНУ Тогда
				КоличествоОтгрузкиИнвентаряНУ = ТаблицаОстатков.КоличествоИнвентаряНаСкладахНУ;
				ТаблицаОстатков.КоличествоИнвентаряНаСкладахНУ=ТаблицаОстатков.КоличествоИнвентаряНаСкладахНУ-КоличествоОтгрузкиИнвентаряНУ;
				КоличествоОтгрузкиНУ=КоличествоОтгрузкиНУ-КоличествоОтгрузкиИнвентаряНУ;
			Иначе
				КоличествоОтгрузкиИнвентаряНУ=Количество;
                ТаблицаОстатков.КоличествоИнвентаряНаСкладахНУ=ТаблицаОстатков.КоличествоИнвентаряНаСкладахНУ-КоличествоОтгрузкиИнвентаряНУ;
				КоличествоОтгрузкиНУ=КоличествоОтгрузкиНУ-КоличествоОтгрузкиИнвентаряНУ;
			КонецЕсли;
			
			
			Если КоличествоОтгрузкиНУ >= ТаблицаОстатков.КоличествоНаСкладахНУ Тогда
				СуммаОтгрузки = ТаблицаОстатков.СуммаНаСкладахНУ;    
			Иначе
				СуммаОтгрузки = Окр(КоличествоОтгрузкиНУ*ТаблицаОстатков.СредняяСтоимостьНУ, 2, 1);
			КонецЕсли;
			
			// Уменьшим остаток материала на списанное количество.
			ТаблицаОстатков.СуммаНаСкладахНУ = ТаблицаОстатков.СуммаНаСкладахНУ - СуммаОтгрузки;
			ТаблицаОстатков.КоличествоНаСкладахНУ = ТаблицаОстатков.КоличествоНаСкладахНУ - КоличествоОтгрузкиНУ;
			
			// Если опредлен налоговый счет, то необходимо
			// сформировать проводку для целей налогового учета.
			Если КоличествоОтгрузкиНУ<>0 Тогда
				Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
				Операция.НомерЖурнала = "НУ";
				Операция.СодержаниеПроводки = ?(ВидМатериалов = 1, "Отражены расходы на спецодежду",?(ВидМатериалов=2, "Отражены расходы на спецоснастку","Отражены расходы на инвентарь и хоз.пр-ти"));
				Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
				Операция.Кредит.Счет = СчН02_01;
				Операция.Кредит.Субконто(1, Материал);
				Если ПустоеЗначение(СчетНУ) = 0 Тогда
					Операция.Дебет.Счет = СчетНУ;
					Для СчетчикЦикла = 1 По СчетНУ.КоличествоСубконто() Цикл
						ИдентификаторСубконто = СчетНУ.ВидСубконто(СчетчикЦикла,,).Идентификатор();
						Операция.Дебет.Субконто(СчетчикЦикла, ОбъектыАналитикиНУ.Получить(ИдентификаторСубконто));
					КонецЦикла;
				КонецЕсли;
				Операция.Сумма = СуммаОтгрузки;
				Операция.Количество = КоличествоОтгрузкиНУ; 
			КонецЕсли;
			//_____________________________________________________________________________   
			Если ВидМатериалов=3 Тогда
				Если КоличествоОтгрузкиИнвентаряНУ>0 Тогда
					Операция.НоваяПроводка(); Операция.РазделительУчета = ЮрЛицо;
					Операция.НомерЖурнала = "НУ";
					Операция.СодержаниеПроводки = "Отражено выбытие инвентаря и хоз.пр-тей";
					Операция.ПервичныйДокумент  = глПредставлениеПервичногоДокумента(Контекст);
					Операция.Кредит.Счет = СчН02_МЦ;
					Операция.Кредит.Субконто(1, Материал);
					Операция.Количество=КоличествоОтгрузкиИнвентаряНУ;
				КонецЕсли;
			КонецЕсли;
            //_____________________________________________________________________________            
		КонецЕсли;
	КонецЦикла;
	
	Операция.Записать();
	
	ЗафиксироватьТранзакцию();
	
	ТекстСообщения = "Документ проведен.";
	глСообщениеПроведения(ТекстСообщения, ТекущийДокумент(), 0);
	глПриПроведении(Контекст);
	
КонецПроцедуры // ОбработкаПроведения()

//******************************************************************************
// Предопределенная процедура.
//
Процедура ОбработкаУдаленияПроведения()
	
	Если ВыбратьСтроки() = 1 Тогда
		Пока ПолучитьСтроку() = 1 Цикл
			Сумма = 0;
		КонецЦикла;
	КонецЕсли;
	
	// Метятся на удаление все назначения использования, созданные этим документом.
	СпрНазначенияИспользования = СоздатьОбъект("Справочник.НазначенияИспользования");
	СпрНазначенияИспользования.ВыбратьЭлементыПоРеквизиту("ДокументПередачаВПроизводство", ТекущийДокумент(), 0);
	Пока СпрНазначенияИспользования.ПолучитьЭлемент() = 1 Цикл
		СпрНазначенияИспользования.Удалить(0);
	КонецЦикла;
	
КонецПроцедуры // ОбработкаУдаленияПроведения()