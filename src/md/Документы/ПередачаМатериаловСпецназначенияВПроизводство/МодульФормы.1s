////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//
Перем ТаблицаПечФорм;  // список печатных форм документа
Перем НомерТекущейФормы;
Перем НачальнаяДатаДокумента;
Перем СписокДействий;
Перем Новый;

//******************************************************************************
// ПриВыбореСчета()
// 
Процедура ПриВыбореСчета()
	Для А = 1 По 3 Цикл
		НазначитьТип("Субконто"+А,СчетЗатрат.ВидСубконто(А));
	КонецЦикла;
	
	Если (Константа.ИспользоватьСписокКорректныхПроводок = Да) Тогда
		Если ВидМатериалов=3 Тогда
			глПроверкаКорректныхПроводок(СчетЗатрат,СчетПоКоду("10.9"));    
		Иначе
			глПроверкаКорректныхПроводок(СчетЗатрат,СчетПоКоду("10.10"));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры //ПриВыбореСчета

//******************************************************************************
// 
Процедура ПечатьМ11()
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("М-11");
	
	ПодразделениеПолучатель = "";
	КорСчет = СчетЗатрат.Код;
	Если (ВидМатериалов = 1) или (ВидМатериалов = 3) Тогда // спецодежда (инвентарь)
		Для СчетчикЦикла = 1 По СчетЗатрат.КоличествоСубконто() Цикл 
			Субконто = ПолучитьАтрибут("Субконто" + СчетчикЦикла);
			Если Субконто.Вид() = "Подразделения" Тогда
				ПодразделениеПолучатель = Субконто;
				Прервать;
			КонецЕсли;
		КонецЦикла;	
		
	Иначе // спецоснастка
		ПодразделениеПолучатель = Подразделение;
	КонецЕсли;
	
	ПодразделениеОтправитель = МестоХранения;
	
	Если Проведен() = 0 Тогда
		НомерПовторяемойСтроки = 16;
		ТекстПредупреждения = "Для непроведенного документа графы ""Цена"" и ""Сумма"" не заполняются.";
		Таб.ВывестиСекцию("Предупреждение");
		Таб.ВывестиСекцию("Шапка");
		
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			Субсчет = Материал.СубСчет10;
			КоличествоЗатребовано = Количество;
			КоличествоОтпущено    = Количество;
			Если Материал.Выбран() = 1 Тогда
			    МатЦенность = Материал;
				Код = Материал.Код;
				ЕдиницаИзмерения = Материал.ЕдиницаИзмерения;
				ЕдиницаИзмеренияКод = Материал.ЕдиницаИзмерения.Код;
				МатериалСумма = 0;
				МатериалЦенаБезНДС = 0;
			КонецЕсли;
			Таб.ВывестиСекцию("Строка");
			Состояние("Выведено строк: " + НомерСтроки);
		КонецЦикла;
		
	ИначеЕсли Модифицированность() = 1 Тогда
		Предупреждение("Для печати документ необходимо перепровести.");
		Возврат;
		
	Иначе
		НомерПовторяемойСтроки = 15;
		Таб.ВывестиСекцию("Шапка");

		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
			КоличествоЗатребовано = Количество;
			КоличествоОтпущено    = Количество;
			Если Материал.Выбран() = 1 Тогда
				Субсчет = ?(Материал.Вид() = "Материалы",Материал.СубСчет10,"43");
				МатЦенность = Материал;
				Код = Материал.Код;
				ЕдиницаИзмерения = Материал.ЕдиницаИзмерения;
				ЕдиницаИзмеренияКод = Материал.ЕдиницаИзмерения.Код;
				МатериалСумма = Сумма;
				МатериалЦенаБезНДС = Сумма / Количество;
			КонецЕсли;
			Таб.ВывестиСекцию("Строка");
			Состояние("Выведено строк: " + НомерСтроки);
		КонецЦикла;
	КонецЕсли;

	Таб.ВывестиСекцию("Подвал");
  	Таб.Опции(0, 0, ?(Проведен() = 0, 1, 0), 0, "ОпцииПечатиМ11", "ОкноМ11");  	
	Таб.ОбластьПечати(?(Проведен() = 0, 2, 1), 2,,);
	Таб.ПовторятьПриПечатиСтроки(НомерПовторяемойСтроки, НомерПовторяемойСтроки);
	Таб.ТолькоПросмотр(1);
   	Таб.Показать("Требование-накладная (форма №М-11)");
КонецПроцедуры // ПечатьМ11
   
//******************************************************************************
// 
Процедура ПечатьМБ7()
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("МБ-7");
	
	ПодразделениеПолучатель = "";
	КорСчет = СчетЗатрат.Код;
	Если ВидМатериалов = 1 Тогда // спецодежда
		Для СчетчикЦикла = 1 По СчетЗатрат.КоличествоСубконто() Цикл 
			Субконто = ПолучитьАтрибут("Субконто" + СчетчикЦикла);
			Если Субконто.Вид() = "Подразделения" Тогда
				ПодразделениеПолучатель = Субконто;
				Прервать;
			КонецЕсли;
		КонецЦикла;	
		
	Иначе // спецоснастка
		ПодразделениеПолучатель = Подразделение;
	КонецЕсли;
	
	Таб.ВывестиСекцию("Шапка");
	
	ВыбратьСтроки();
	Пока ПолучитьСтроку() = 1 Цикл
		Если Материал.Выбран() = 1 Тогда
			СотрудникКод = Сотрудник.Код;
			МатериалКод = Материал.Код;
			ЕдиницаИзмерения = Материал.ЕдиницаИзмерения;
			ЕдиницаИзмеренияКод = Материал.ЕдиницаИзмерения.Код;
		КонецЕсли;
		
		Если СпособПогашенияСтоимости = Перечисление.СпособПогашенияСтоимости.ПропорциональноОбъемуПродукцииРабот Тогда
			СрокСлужбы = "";
		Иначе
			СрокСлужбы = СрокПолезногоИспользования;
		КонецЕсли;
		
		Таб.ВывестиСекцию("Строка");
		Состояние("Выведено строк: " + НомерСтроки);
	КонецЦикла;
		
	Таб.ВывестиСекцию("Подвал");
  	Таб.Опции(0, 0, 0, 0, "ОпцииПечатиМБ7", "ОкноМБ7");
	Таб.ПовторятьПриПечатиСтроки(15, 15);
	Таб.ТолькоПросмотр(1);
   	Таб.Показать("Ведомость учета выдачи спецодежды(форма №МБ-7)");
КонецПроцедуры // ПечатьМБ7
   
//******************************************************************************
// ПоКнопкеПечать()
// 
// Вызывается из формул элементов диалога:
//  Кнопка "кнПечать".
//
// Описание:
//  Определяется соответствующая печатная форма.
// 	
Процедура ПоКнопкеПечать(СразуНаПринтер = 0,КолЭкз = 1)
	
	Если  ПустоеЗначение(НомерТекущейФормы) = 1  Тогда
		НомерТекущейФормы = 1;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
	КонецЕсли;
	
	Если НомерТекущейФормы = 1  Тогда
		ПечатьМ11();
		
	ИначеЕсли (НомерТекущейФормы = 2) и (ВидМатериалов = 1) Тогда
		ПечатьМБ7();
		
	Иначе
		Параметры = СоздатьОбъект("СписокЗначений");
		Параметры.ДобавитьЗначение(Контекст, "Контекст");
		Параметры.ДобавитьЗначение(СразуНаПринтер, "Устройство");
		Параметры.ДобавитьЗначение(КолЭкз, "КоличествоКопий");
		
		ОткрытьФорму("Отчет", Параметры, глКаталогПечФорм+ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы,"Файл"));
	КонецЕсли;
	
КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// ПоКнопкеВыборПечатнойФормы()
//
// Вызывается из формул элементов диалога:
//  Кнопка "кнВыбПечать".
//
// Описание:
//  - открывает список для выбора способа печати. 
//  - формирует таблицу по выбранному способу.
//
Процедура ПоКнопкеВыборПечатнойФормы()
	
    ВыбНомер = глВыборПечатнойФормы("Документ." + Вид(), ТаблицаПечФорм);
	Если ВыбНомер > 0 Тогда
		НомерТекущейФормы = ВыбНомер;
		Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
		ПоКнопкеПечать();
	КонецЕсли;

КонецПроцедуры // ПоКнопкеВыборПечатнойФормы()

//******************************************************************************
//
Процедура УправлениеДиалогом()
	
	ВидимостьЭлементовДляСпецоснастки = ?(ВидМатериалов = 2, 1, 0);
	ВидимостьЭлементовДляСпецодежды   = ?((ВидМатериалов = 1) или (ВидМатериалов = 3), 1, 0);
	ВидимостьЭлементовДляИнвентаря	  = ?(ВидМатериалов=3,0,1);
	Форма.СтатикПодразделение.Видимость(ВидимостьЭлементовДляСпецоснастки);
	Форма.Подразделение.Видимость(ВидимостьЭлементовДляСпецоснастки);
	Форма.Сотрудник.Видимость(ВидимостьЭлементовДляСпецодежды);
	Форма.СтатикНазначениеИспользования.Видимость(ВидимостьЭлементовДляИнвентаря);
	Форма.НаименованиеНазначенияИспользования.Видимость(ВидимостьЭлементовДляИнвентаря);
	Форма.СрокПолезногоИспользования.Видимость(ВидимостьЭлементовДляИнвентаря);
	Форма.СпособПогашенияСтоимости.Видимость(ВидимостьЭлементовДляИнвентаря);
КонецПроцедуры // УправлениеДиалогом()

//_____________________________________________________________________________
//ПриСменеДатыДок()
//
Процедура ПриСменеДатыДок()
	Если ДатаДок>='01.01.2006' Тогда
		Если ВидМатериаловСписок.РазмерСписка()<3 Тогда
			ВидМатериаловСписок.ДобавитьЗначение("Инвентарь и хоз. принадлежности");    
		КонецЕсли;
	Иначе
		Если ВидМатериаловСписок.РазмерСписка()=3 Тогда
			ВидМатериаловСписок.УдалитьЗначение(3);
			Если ВидМатериаловСписок.ТекущаяСтрока() <> ВидМатериалов Тогда
				ВидМатериалов = ВидМатериаловСписок.ТекущаяСтрока();
				УправлениеДиалогом();
			КонецЕсли;
		КонецЕсли;			
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
// ПриВыбореВидаСпецматериалов()
//
Процедура ПриВыбореВидаСпецматериалов()
	
	Если ВидМатериаловСписок.ТекущаяСтрока() <> ВидМатериалов Тогда
		ВидМатериалов = ВидМатериаловСписок.ТекущаяСтрока();
		УправлениеДиалогом();
	КонецЕсли;
	                                                                       
	Если ВидМатериалов = 1 Тогда // спецодежда
		Если ТаблицаПечФорм.НайтиЗначение("Ведомость учета выдачи спецодежды МБ-7", "", "Название") = 0 Тогда
	        ТаблицаПечФорм.НоваяСтрока();
			ТаблицаПечФорм.Название     = "Ведомость учета выдачи спецодежды МБ-7";
			ТаблицаПечФорм.Кнопка       = "МБ-7";
		КонецЕсли;
		
	Иначе // спецоснастка
		НомерСтр = "";
		Если ТаблицаПечФорм.НайтиЗначение("Ведомость учета выдачи спецодежды МБ-7", НомерСтр, "Название") = 1 Тогда
			Если НомерТекущейФормы = НомерСтр Тогда
				НомерТекущейФормы = 1;
				Форма.кнПечать.Заголовок(ТаблицаПечФорм.ПолучитьЗначение(НомерТекущейФормы, "Кнопка"));
			КонецЕсли;
			ТаблицаПечФорм.УдалитьСтроку(НомерСтр);
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореВидаСпецматериалов()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, ФлагСтандОбр)
	
	Если (ИдентЭлемДиалога = "СчетЗатрат") и (Константа.ИспользоватьСписокКорректныхПроводок = Да) Тогда
		СписокКорректныхПроводок = СоздатьОбъект("СписокЗначений");
		Если ВидМатериалов=3 Тогда
			СписокКорректныхПроводок.Установить("Счет", СчетПоКоду("10.9"));
		Иначе
			СписокКорректныхПроводок.Установить("Счет", СчетПоКоду("10.10"));
		КонецЕсли;
		глЗначениеОтбора = СписокКорректныхПроводок;
		
	ИначеЕсли ИдентЭлемДиалога = "Материал" Тогда
		Если ВидМатериалов=3 Тогда
		    ОткрытьФорму("Справочник.Материалы.ДляВыбора", СчетПоКоду("10.9"));
			СпособПогашенияСтоимости=Перечисление.СпособПогашенияСтоимости.ПогашатьСтоимостьВМоментПередачиВПроизводство;
		Иначе
			ОткрытьФорму("Справочник.Материалы.ДляВыбора", СчетПоКоду("10.10"));
		КонецЕсли;
				
	ИначеЕсли ИдентЭлемДиалога = "СпособПогашенияСтоимости" Тогда
		
		// для спецодежды можно указывать не все способы погашения стоимости
		СписокСпособов = СоздатьОбъект("СписокЗначений");
		Если ВидМатериалов = 1 Тогда
			ФлагСтандОбр = 0;
			
			СписокСпособов.ДобавитьЗначение(Перечисление.СпособПогашенияСтоимости.Линейный);
			СписокСпособов.ДобавитьЗначение(Перечисление.СпособПогашенияСтоимости.ПогашатьСтоимостьВМоментПередачиВПроизводство);
			СписокСпособов.ВыбратьЗначение(СпособПогашенияСтоимости,,,, 2);
		ИначеЕсли ВидМатериалов=3 Тогда
			ФлагСтандОбр = 0;
			СписокСпособов.ДобавитьЗначение(Перечисление.СпособПогашенияСтоимости.ПогашатьСтоимостьВМоментПередачиВПроизводство);            
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеВыбораЗначения()

//******************************************************************************
//
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	Если Копирование = 1 Тогда //копирование документа
		
		// Если документ вводится путем копирования, то необходимо
		// чтобы не было указано назначение использовния в табличной части.
		ВыбратьСтроки();
		Пока ПолучитьСтроку() = 1 Цикл
		    НазначениеИспользования = 0;
		КонецЦикла;
		
	    Возврат;
	КонецЕсли;
                   
	ВидМатериалов = 1;
	МестоХранения = глЗначениеПоУмолчанию("ОсновнойСклад");
КонецПроцедуры

//******************************************************************************
// ПриВыбореВидаСпецМатериалов()
//
Процедура ПриОткрытии()
	
	ПриЗаписиПерепроводить(1);
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаПровести.Доступность(0);
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаОчистить.Доступность(0);
	КонецЕсли;
	                                
	ВидМатериаловСписок.ДобавитьЗначение("Спецодежда");
	ВидМатериаловСписок.ДобавитьЗначение("Спецоснастка");

	ПриСменеДатыДок();
	
	ВидМатериаловСписок.ТекущаяСтрока(ВидМатериалов);
	ПриВыбореВидаСпецматериалов();

	НачальнаяДатаДокумента = ДатаДок;
	УправлениеДиалогом();
	
	// Заполним таблицу для выбора печатной формы
	НомерТекущейФормы = глУстановкаКнопкиПечать(Контекст, "Документ." + Вид(),ТаблицаПечФорм);
	
КонецПроцедуры

//******************************************************************************
//
Процедура ПриЗаписи()
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	Если ВидМатериалов=3 Тогда
		Операция.Содержание = "Выдача инвентаря и хоз. принадлежностей";    
	Иначе
		Операция.Содержание = "Выдача спецодежды и спецоснастки";
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Новый = 0;

ТаблицаПечФорм		= СоздатьОбъект("ТаблицаЗначений");
ТаблицаПечФорм.НоваяКолонка("Название","Строка",,,,30);
ТаблицаПечФорм.НоваяКолонка("Файл","Строка",,,"Файл",10);
ТаблицаПечФорм.НоваяКолонка("Кнопка","Строка",,,,10); 
ТаблицаПечФорм.НоваяКолонка("ФайлОписания","Строка");
	
// добавим информацию о встроенной форме
ТаблицаПечФорм.НоваяСтрока();
ТаблицаПечФорм.Название     = "Требование накладная М-11";
ТаблицаПечФорм.Кнопка       = "М-11";

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");
СписокДействий.ДобавитьЗначение("Ввести на основании");
СписокДействий.ДобавитьЗначение("Перейти  в журнал");
