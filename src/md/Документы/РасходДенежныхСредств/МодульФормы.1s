////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
//

Перем НачальнаяДатаДокумента; 
Перем Новый;
Перем СписокДействий;
Перем СписокВидовДокументовДляВыбора;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//

//******************************************************************************
// ПриВыбореУсловияИлиВидаРасхода()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ПриВыбореУсловияИлиВидаРасхода()
	
    Если (УсловиеИлиВидРасхода = Перечисление.УсловияИлиВидыРасходов.ВзносыПоДобровольномуСтрахованию)
		или (УсловиеИлиВидРасхода = Перечисление.УсловияИлиВидыРасходов.ВзносыПоДобровольномуСтрахованиюНаОплатуМедицинскихРасходов)
		или (УсловиеИлиВидРасхода = Перечисление.УсловияИлиВидыРасходов.ВзносыПоДобровольномуСтрахованиюНаСлучайСмерти) Тогда
		Форма.СтатьяРасходовНаСтрахование.Доступность(1);
		Форма.Сотрудник.Доступность(1);

	Иначе
		Если ПустоеЗначение(СтатьяРасходовНаСтрахование) = 0 Тогда
		    СтатьяРасходовНаСтрахование = "";
		КонецЕсли;
		Форма.СтатьяРасходовНаСтрахование.Доступность(0);
		Если ПустоеЗначение(Сотрудник) = 0 Тогда
		    Сотрудник = "";
		КонецЕсли;
		Форма.Сотрудник.Доступность(0);
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореУсловияИлиВидаРасхода()

//******************************************************************************
// ПриИзмененииВалютнойСуммы()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Валютная сумма.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура ПриИзмененииВалютнойСуммы()
	
    Если ВалСумма = 0 Тогда
		Если ПустоеЗначение(Валюта) = 0 Тогда
		    Валюта = "";
		КонецЕсли;
		Форма.Валюта.Доступность(0);
		
	Иначе
		Форма.Валюта.Доступность(1);
	КонецЕсли;
	
КонецПроцедуры // ПриИзмененииВалютнойСуммы()

//*****************************************************************************
// ИтоговаяСтрока()
// 
// Вызывается из формул элементов диалога:
//  Текст ИтоговаяСтрока.
//
// Описание:
//  Формирует строку, содержащую итоговые суммы колонок табличной части.
//
Функция ИтоговаяСтрока()
	
	Возврат "Итого на сумму: " + СокрЛ(Формат(Итог("Сумма"),"Ч20.2-,"));
	
КонецФункции // ИтоговаяСтрока()

//******************************************************************************
// Заполнить()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Кнопка "Заполнить".
//
// Описание:
//  Заполняет табличную часть документа по данным учета.
//
Процедура Заполнить()

	СписокПараметров = СоздатьОбъект("СписокЗначений");
	СписокПараметров.Установить("КонтекстДокумента", Контекст);
	ОткрытьФормуМодально("Обработка.ФормированиеНалоговыхДокументов", СписокПараметров);

КонецПроцедуры // Заполнить()

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//*****************************************************************************
// Предопределенная процедура
//
Процедура ВводНового(Копирование)
	глЗаполнитьШапку(Контекст, Копирование);
	Новый = 1;
	Если Копирование = 1 Тогда //копирование документа
	    Возврат;
	КонецЕсли;
	
КонецПроцедуры // ВводНового()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Если глНовыеПравилаВеденияНУ(ДатаДок) = 1 Тогда
		Предупреждение("Данные, введенные в документ не будут влиять на
		               |налоговый учет, т.к. установлен способ ведения
					   |налогового учета - ""Одновременно с бухгалтерским учетом"" !");
	КонецЕсли;
				
	ПриЗаписиПерепроводить(1);
	НачальнаяДатаДокумента = ДатаДок;
	глПроверкаРазрешенияРедактирования(Контекст);
	Если Форма.ТолькоПросмотр() = 1 Тогда
		Форма.КнопкаЗаписать.Доступность(0);
		Форма.КнопкаОК.Доступность(0);
		Форма.КнопкаЗаполнить.Доступность(0);
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриРедактированииНовойСтроки()
	
	ПриВыбореУсловияИлиВидаРасхода();
	ПриИзмененииВалютнойСуммы();
	ДокументБУ.ВидыДляВыбора(СписокВидовДокументовДляВыбора);
	
КонецПроцедуры // ПриРедактированииНовойСтроки()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеРедактированияСтроки()
	
	
	Если (Форма.ТекущаяКолонка() = "ОткрытьДокумент") Тогда
		Если ДокументБУ.Выбран()=0 Тогда
			Предупреждение("Не могу открыть документ, отражающий данную операцию
			                |в бухгалтеском учете: документ не указан!",60);
		Иначе     
			ОткрытьФорму(ДокументБУ);
		КонецЕсли;
		
	Иначе
		ПриВыбореУсловияИлиВидаРасхода();
		ПриИзмененииВалютнойСуммы();
		ДокументБУ.ВидыДляВыбора(СписокВидовДокументовДляВыбора);
	КонецЕсли;
	
КонецПроцедуры // ПриНачалеРедактированияСтроки()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриЗаписи() //предопределенная
	
	Если глМожноЗаписатьДокумент(Контекст) = 0 Тогда
		СтатусВозврата(0);
		Возврат;
		
	ИначеЕсли глКонтрольДатыДокумента(Контекст, НачальнаяДатаДокумента) = 1 Тогда
		СтатусВозврата(0);
		Возврат;
	КонецЕсли;
	
	Операция.СуммаОперации = Итог("Сумма");
	Операция.Содержание = "Расход денежных средств";

КонецПроцедуры // ПриЗаписи()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриЗакрытии()
	
	глОткрытьЖурнал(Контекст, Новый);	
	
КонецПроцедуры // ПриЗакрытии()

//******************************************************************************
Новый = 0;

СписокДействий = СоздатьОбъект("СписокЗначений");
СписокДействий.ДобавитьЗначение("Отчет о движениях документа");
СписокДействий.ДобавитьЗначение("Структура подчиненности");       
СписокДействий.ДобавитьЗначение("Перейти  в журнал");

СписокВидовДокументовДляВыбора = "Выписка,РасходныйОрдер,Операция";
