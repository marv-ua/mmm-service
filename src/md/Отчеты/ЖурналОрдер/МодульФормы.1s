//-----------------------------------------------
Перем Т;
Перем ПоПроводке;
Перем ВидПериода;
Перем Расшифровка;
Перем Обновить;
Перем ПредставлениеРУ;

//-----------------------------------------------
Функция ПроверкаПериода()
	Если ПустоеЗначение(Дата1) = 1 Тогда
	    Предупреждение("Не указана дата начала периода отчета!");
		Возврат 0;
	КонецЕсли;
	Если Дата1 > Дата2 Тогда
		Предупреждение("Неправильно задан период отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если Дата2 > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		               "Расчет итогов выполняется в режиме"+РазделительСтрок+
					   """Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Процедура КорСчета(Т, Ит, Секция, Гр)

	Т.ВывестиСекцию(Секция+"|Секция_5");
	Если ПоПроводке <> 0 Тогда
		ЧЧ = 0;	ММ = 0;	СС = 0;
		Оп = Ит.Операция;
		Оп.ПолучитьВремя(ЧЧ, ММ, СС);
		Т.ПрисоединитьСекцию(Секция+"|Секция_14");
	КонецЕсли;

	Если ОстНачД = 1 Тогда
		Т.ПрисоединитьСекцию(Секция+"|Секция_6");
	КонецЕсли;

	Если ОстНачК = 1 Тогда
		Т.ПрисоединитьСекцию(Секция+"|Секция_7");
	КонецЕсли;

	Если ПоПроводке <> 1 Тогда
		Расшифровка.Установить("ДтКт", 1);
	КонецЕсли;
  	Если КорОбД = 1 Тогда
	  	Ит.ВыбратьКорСчета(Гр, 1);
	    Пока Ит.ПолучитьКорСчет() = 1 Цикл
			Если ПоПроводке <> 1 Тогда
				Расшифровка.Установить("КорСчет", Ит.КорСчет);
			КонецЕсли;
	    	Т.ПрисоединитьСекцию(Секция+"|Секция_8");
	    КонецЦикла;
		Если ПоПроводке <> 1 Тогда
			Расшифровка.Установить("КорСчет");
		КонецЕсли;
	КонецЕсли;

  	Если ОбД = 1 Тогда
		Т.ПрисоединитьСекцию(Секция+"|Секция_9");
	КонецЕсли;

	Если ПоПроводке <> 1 Тогда
		Расшифровка.Установить("ДтКт", 2);
	КонецЕсли;
  	Если КорОбК = 1 Тогда
	  	Ит.ВыбратьКорСчета(Гр, 2);
	    Пока Ит.ПолучитьКорСчет() = 1 Цикл
			Если ПоПроводке <> 1 Тогда
				Расшифровка.Установить("КорСчет", Ит.КорСчет);
			КонецЕсли;
	    	Т.ПрисоединитьСекцию(Секция+"|Секция_10");
	    КонецЦикла;
		Если ПоПроводке <> 1 Тогда
			Расшифровка.Установить("КорСчет");
		КонецЕсли;
	КонецЕсли;

  	Если ОбК = 1 Тогда
		Т.ПрисоединитьСекцию(Секция+"|Секция_11");
	КонецЕсли;

	Если ПоПроводке <> 1 Тогда
		Расшифровка.Установить("ДтКт", 3);
	КонецЕсли;
	Если ОстКонД = 1 Тогда
		Т.ПрисоединитьСекцию(Секция+"|Секция_12");
	КонецЕсли;

	Если ОстКонК = 1 Тогда
		Т.ПрисоединитьСекцию(Секция+"|Секция_13");
	КонецЕсли;

	Т.ПрисоединитьСекцию(Секция+"|Секция_15");
КонецПроцедуры

//-----------------------------------------------
Процедура Сформировать(Ручн = 0, ФлагЗакрытияФормы = 0)
	Если Счет.Выбран() <> 1 Тогда
		Предупреждение("Не указан счет!");
		Возврат;
	КонецЕсли;

	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;

	Если Ручн=1 Тогда
	    СохранитьЗначение("ОтчРабСчет",Счет);
	КонецЕсли;

	Если глФлагРасшифровки = 0 Тогда // вызвали для расшифровки другого отчета
		ВидПериода = Период.ПолучитьЗначение(Период.ТекущаяСтрока());
	КонецЕсли;

	Расшифровка = СоздатьОбъект("СписокЗначений");
	Если (ВидПериода = 2) или (ВидПериода = 3) Тогда
		ПоПроводке = 1;
	Иначе
		ПоПроводке = 0;
	КонецЕсли;

    Ит = СоздатьОбъект("БухгалтерскиеИтоги");
    Ит.ИспользоватьРазделительУчета(РазделительУчета);
    Ит.ВключатьСубсчета(0, ДанныеПоСубсчетам);
    Если ПоВалюте = 1 Тогда
		Если Ит.ВыполнитьЗапрос(Дата1, Дата2, Счет,, Валюта, 3, ВидПериода) = 0 Тогда
			Возврат;
		КонецЕсли;
	Иначе
    	Если Ит.ВыполнитьЗапрос(Дата1, Дата2, Счет,,, 3, ВидПериода) = 0 Тогда
			Возврат;
		КонецЕсли;
    КонецЕсли;

	Если (ТипЗначенияСтр(Т) <> "Таблица")ИЛИ(Обновить=0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
	Т.ИсходнаяТаблица("Таблица");
	
	НазваниеОрганизации = глНазваниеОрганизации(Контекст);

	Расшифровка.Установить("Отчет", "ЖурналОрдер");
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
	Расшифровка.Установить("Дата1", Дата1);
	Расшифровка.Установить("Дата2", Дата2);
	Расшифровка.Установить("Счет", Счет);
	Расшифровка.Установить("ДанныеПоСубсчетам", ДанныеПоСубсчетам);
    Если ПоВалюте = 1 Тогда
		Расшифровка.Установить("Валюта", Валюта);
		Расшифровка.Установить("ПоВалюте", 1);
		Секция_3 = "Секция_3_1";
		Секция_4 = "Секция_4_1";
	Иначе
		Секция_3 = "Секция_3";
		Секция_4 = "Секция_4";
	КонецЕсли;
	Расшифровка.Установить("ОстНачД", ОстНачД);
	Расшифровка.Установить("ОстНачК", ОстНачК);
	Расшифровка.Установить("ОбД", ОбД);
	Расшифровка.Установить("ОбК", ОбК);
	Расшифровка.Установить("КорОбД", КорОбД);
	Расшифровка.Установить("КорОбК", КорОбК);
	Расшифровка.Установить("ОстКонД", ОстКонД);
	Расшифровка.Установить("ОстКонК", ОстКонК);
	Расшифровка.Установить("ВидПериода", ВидПериода);
	Расшифровка.Установить("ПериодыСНулевымиОборотами", ПериодыСНулевымиОборотами);

	Т.ВывестиСекцию("Секция_16");
	Т.ВывестиСекцию("Секция_1");

	Если ПоПроводке = 1 Тогда
		Расшифровка.Установить("Отчет");
		Расшифровка.Установить("Счет");
		Расшифровка.Установить("Валюта");
		Расшифровка.Установить("ПоВалюте");
	Иначе
		Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");
		Расшифровка.Установить("ДтКт", 3);
	КонецЕсли;
	Расшифровка.Установить("Дата1");
	Расшифровка.Установить("Дата2");
	Расшифровка.Установить("ОстНачД");
	Расшифровка.Установить("ОстНачК");
	Расшифровка.Установить("ОбД");
	Расшифровка.Установить("ОбК");
	Расшифровка.Установить("КорОбД");
	Расшифровка.Установить("КорОбК");
	Расшифровка.Установить("ОстКонД");
	Расшифровка.Установить("ОстКонК");
	Расшифровка.Установить("ВидПериода");
	Расшифровка.Установить("ПериодыСНулевымиОборотами");
	Расшифровка.Установить("Обновить");

	КорСчета(Т, Ит,"Секция_2", -1); 		// заголовок
	Ит.ВыбратьПериоды(ПериодыСНулевымиОборотами);
	Пока Ит.ПолучитьПериод() = 1 Цикл
		Если ПоПроводке = 1 Тогда
			Опер = Ит.Операция;
			Расшифровка.Установить("Документ", Опер.Документ.ТекущийДокумент());
			Если ВидПериода = 2 Тогда
				Расшифровка.Установить("НомерПроводки", Опер.НомерПроводки());
				Расшифровка.Установить("НомерКорреспонденции", Опер.НомерКорреспонденции());
			КонецЕсли;
		Иначе
			Расшифровка.Установить("Дата1", Ит.НачДата);
			Расшифровка.Установить("Дата2", Ит.КонДата);
		КонецЕсли;
		КорСчета(Т, Ит, Секция_3, -2);    // по датам
	КонецЦикла;
	Если ПоПроводке = 1 Тогда
		ПоПроводке = 2;
		Расшифровка.Установить("Документ");
		Расшифровка.Установить("НомерПроводки");
		Расшифровка.Установить("НомерКорреспонденции");
		Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");
		Расшифровка.Установить("ДтКт", 3);
		Расшифровка.Установить("Счет", Счет);
	    Если ПоВалюте = 1 Тогда
			Расшифровка.Установить("Валюта", Валюта);
			Расшифровка.Установить("ПоВалюте", 1);
		КонецЕсли;
	КонецЕсли;
	Расшифровка.Установить("Дата1", Дата1);
	Расшифровка.Установить("Дата2", Дата2);
	КорСчета(Т, Ит, Секция_4, -1);        // итоги
	Ит = 0;
	
	НазваниеОрганизации = ?(ПустоеЗначение(НазваниеОрганизации)=1, "", " "+НазваниеОрганизации);
	ПериодИОрганизация = " ("+ПериодСтр(Дата1, Дата2)+")"+НазваниеОрганизации;
	ВерхнийКолонтитул = "Журнал-ордер по счету "+Счет+ПериодИОрганизация;
	НижнийКолонтитул = "Отчет сформирован "+ТекущаяДата()+" "+ТекущееВремя()+?(ПустоеЗначение(ИмяПользователя())=0,"  Пользователь: "+ИмяПользователя(),"");
	Т.ТолькоПросмотр(1);
    Т.Опции(0, 0, 6, 1, "ОпцииПечатиЖурналОрдер", "ЖурналОрдер");
	Т.ОбластьПечати(2);
	Т.ПовторятьПриПечатиСтолбцы(1,1);
	Т.ПовторятьПриПечатиСтроки(6,6);
	Т.ПараметрыСтраницы(2,,,,,,,,, 0);
    Т.Показать(ВерхнийКолонтитул, "");

	Если (ФлагЗакрытияФормы = 1) Или (Обновить = 2) Тогда
         СтрокаДействийФормы = "#Закрыть";
    КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВалюты()
	ПоВалюте = Валюта.Выбран();
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСчета()
	Если Счет.Валютный = 0 Тогда
	    ПоВалюте = 0;
		Если Метаданные.Валюта.Выбран() = 1 Тогда
	    	Валюта = ПолучитьПустоеЗначение("Справочник."+Метаданные.Валюта.Идентификатор);
		КонецЕсли;
	КонецЕсли;
	Форма.ПоВалюте.Доступность(Счет.Валютный);
	Форма.Валюта.Доступность(Счет.Валютный);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОткрытии()
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;

	Период.УдалитьВсе();
	Период.ДобавитьЗначение(2, "Проводка");
	Период.ДобавитьЗначение(3, "Операция");
	Период.ДобавитьЗначение(4, "Дата");
	Период.ДобавитьЗначение(5, "Неделя");
	Период.ДобавитьЗначение(6, "Декада");
	Период.ДобавитьЗначение(7, "Месяц");
	Период.ДобавитьЗначение(8, "Квартал");

	Если Метаданные.Валюта.Выбран() = 1 Тогда
		Форма.Валюта.НазначитьТип("Справочник."+Метаданные.Валюта.Идентификатор);
	Иначе
		Форма.ВалютаТекст.Доступность(0);
		Форма.Валюта.Доступность(0);
		Форма.ПоВалюте.Доступность(0);
	КонецЕсли;

	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		РУ = глРасшифровка.Получить("РазделительУчета");
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
		Дата1 = глРасшифровка.Получить("Дата1");
		Дата2 = глРасшифровка.Получить("Дата2");
		Счет = глРасшифровка.Получить("Счет");
		ПриВыбореСчета();
		ДанныеПоСубсчетам = глРасшифровка.Получить("ДанныеПоСубсчетам");
	    Валюта = глРасшифровка.Получить("Валюта");
	    ПоВалюте = глРасшифровка.Получить("ПоВалюте");
		Если Обновить <> 0 Тогда
			ОстНачД = глРасшифровка.Получить("ОстНачД");
			ОстНачК = глРасшифровка.Получить("ОстНачК");
			ОбД = глРасшифровка.Получить("ОбД");
			ОбК = глРасшифровка.Получить("ОбК");
			КорОбД = глРасшифровка.Получить("КорОбД");
			КорОбК = глРасшифровка.Получить("КорОбК");
			ОстКонД = глРасшифровка.Получить("ОстКонД");
			ОстКонК = глРасшифровка.Получить("ОстКонК");
			ВидПериода = глРасшифровка.Получить("ВидПериода");
			Период.ТекущаяСтрока(ВидПериода-1);
			ПериодыСНулевымиОборотами = глРасшифровка.Получить("ПериодыСНулевымиОборотами");
			Т = глТаблица;
		Иначе
		    ВидПериода = 2;
		    ОстНачД = 1;
		    ОстНачК = 1;
		    КорОбД  = 1;
		    ОбД     = 1;
		    КорОбК  = 1;
		    ОбК     = 1;
		    ОстКонД = 1;
		    ОстКонК = 1;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		ВыбРазделительУчета = 1;
		РУ = БухИтоги.ИспользоватьРазделительУчета();
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
		Если ОстНачД+ОстНачК+КорОбД+ОбД+КорОбК+ОбК+ОстКонД+ОстКонК = 0 Тогда
		    ОстНачД = 1;
		    ОстНачК = 1;
		    КорОбД  = 1;
		    ОбД     = 1;
		    КорОбК  = 1;
		    ОбК     = 1;
		    ОстКонД = 1;
		    ОстКонК = 1;
		КонецЕсли;
		Дата1 = НачалоПериодаБИ();
		Дата2 = КонецПериодаБИ();
		Если Счет.Выбран()=0 Тогда
		    ВосстановитьЗначение("ОтчРабСчет",Счет);
		КонецЕсли;
		ПриВыбореСчета();
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ВводНового()
	Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ();
	ПриВыбореСчета();
КонецПроцедуры