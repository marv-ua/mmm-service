//-----------------------------------------------
Перем ОтборСубк1, ОтборСубк2, ОтборСубк3;
Перем Субконто1, Субконто2, Субконто3;
Перем Т;
Перем Обновить;
Перем Расшифровка;
Перем ПредставлениеРУ;
Перем Количественный, Валютный;
Перем ВидПериода, НачалоПериода, КонецПериода;
Перем ДО, ДОКол, ДОВал, КО, КОКол, КОВал;
Перем НачДата, КонДата;

//-----------------------------------------------
Функция ПредставлениеПозицииДокумента(Поз)
	Перем ДокДата, Ч, М, С, Док;
	РазобратьПозициюДокумента(Поз, ДокДата, Ч, М, С, Док);
	ПредПозиции = "( "+Строка(ДокДата)+" "+Формат(Ч,"Ч(0)2")+":"+Формат(М,"Ч(0)2")+":"+Формат(С,"Ч(0)2")+" "+Док.представлениеВида()+" №"+Строка(Док.НомерДок)+" )";
	Возврат ПредПозиции;
КонецФункции

//-----------------------------------------------
Функция ПредставлениеИнтервалаОтчета(ДатаНачала, ДатаКонца)
	Если ((ТипЗначенияСтр(ДатаНачала) = "Дата") и (ТипЗначенияСтр(ДатаКонца) = "Дата")) Тогда
	    Возврат ПериодСтр(ДатаНачала, ДатаКонца);
	Иначе
		Если ТипЗначенияСтр(ДатаНачала) = "Документ" Тогда
			Интервал = ПредставлениеПозицииДокумента(ДатаНачала.ПолучитьПозицию());
		ИначеЕсли ТипЗначенияСтр(ДатаНачала) = "Строка" Тогда
			Интервал = ПредставлениеПозицииДокумента(ДатаНачала);
		ИначеЕсли ТипЗначенияСтр(ДатаНачала) = "Дата" Тогда
			Интервал = Строка(ДатаНачала);
		Иначе
			Интервал = """  .  .  """;
		КонецЕсли;
		
		Интервал = Интервал + " - ";
		Если ТипЗначенияСтр(ДатаКонца) = "Документ" Тогда
			Интервал = Интервал + ПредставлениеПозицииДокумента(ДатаКонца.ПолучитьПозицию());
		ИначеЕсли ТипЗначенияСтр(ДатаКонца) = "Строка" Тогда
			Интервал = Интервал + ПредставлениеПозицииДокумента(ДатаКонца);
		ИначеЕсли ТипЗначенияСтр(ДатаКонца) = "Дата" Тогда
			Интервал = Интервал + Строка(ДатаКонца);
		Иначе
			Интервал = Интервал + """  .  .  """;
		КонецЕсли;
		
		Возврат Интервал;
	КонецЕсли;	
КонецФункции

//-----------------------------------------------
Процедура ПриУстановкеДаты()
	Перем Ч, М, С, Док;
	
	Если ТипЗначенияСтр(НачДата) = "Документ" Тогда
		Дата1 = НачДата.ДатаДок;
	ИначеЕсли ТипЗначенияСтр(НачДата) = "Строка" Тогда
		РазобратьПозициюДокумента(НачДата, Дата1, Ч, М, С, Док);
	Иначе
		Дата1 = НачДата;
	КонецЕсли;
	
	Если ТипЗначенияСтр(КонДата) = "Документ" Тогда
		Дата2 = КонДата.ДатаДок;
	ИначеЕсли ТипЗначенияСтр(КонДата) = "Строка" Тогда
		РазобратьПозициюДокумента(КонДата, Дата2, Ч, М, С, Док);
	Иначе
		Дата2 = КонДата;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореДаты()
	НачДата = Дата1;
	КонДата = Дата2;
КонецПроцедуры 

//-----------------------------------------------
Процедура УстановитьВладельца(ПоВсем, РУ, Субконто)
	Назначить = 0;
	Если ТипЗначенияСтр(Субконто) = "Справочник" Тогда
 		МДСубконто = Метаданные.Справочник(Субконто.Вид());
		Если МДСубконто.Выбран() = 1 Тогда
	 		Назначить = 1;                      
			Если (ПоВсем = 0) И (ТипЗначенияСтр(РУ) = "Справочник") Тогда
				Если МДСубконто.Владелец.Выбран() = 1 Тогда
					Если МДСубконто.Владелец.Идентификатор = РУ.Вид() Тогда
						Назначить = 2;
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если Назначить = 1 Тогда
		Субконто.ИспользоватьВладельца("");
	ИначеЕсли Назначить = 2 Тогда
		Субконто.ИспользоватьВладельца(РУ);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Функция ПроверкаПериода()
	Если ПустоеЗначение(Дата1) = 1 Тогда
	    Предупреждение("Не указана дата начала периода отчета!");
		Возврат 0;
	КонецЕсли;
	Если Дата1 > Дата2 Тогда
		Предупреждение("Неправильно задан период отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если Дата2 > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		               "Расчет итогов выполняется в режиме"+РазделительСтрок+
					   """Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции

//-----------------------------------------------
Функция ЗначенияВСтроку(Значения)
	Если ТипЗначенияСтр(Значения) = "СписокЗначений" Тогда
		С = "";
		Для А=1 По Значения.РазмерСписка() Цикл
			Если А <> 1 Тогда
				С = С+" ";
			КонецЕсли;
			С = С+Значения.ПолучитьЗначение(А);
		КонецЦикла;
		Возврат СокрЛП(С);
	Иначе
		Возврат СокрЛП(Значения);
	КонецЕсли;
КонецФункции

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Функция НачПериода(Д)
Перем Д1;
	Если ВидПериода = 4 Тогда // День
		Д1 = Д;
	ИначеЕсли ВидПериода = 5 Тогда // Неделя
		Д1 = НачНедели(Д);
	ИначеЕсли ВидПериода = 6 Тогда // Декада
		Ч = ДатаЧисло(Д);
		Если Ч < 10 Тогда
			Ч = 1;
		ИначеЕсли Ч < 20 Тогда
			Ч = 10;
		Иначе
			Ч = 20;
		КонецЕсли;
		Д1 = Дата(ДатаГод(Д), ДатаМесяц(Д), Ч);
	ИначеЕсли ВидПериода = 7 Тогда // Месяц
		Д1 = НачМесяца(Д);
	ИначеЕсли ВидПериода = 8 Тогда // Квартал
		Д1 = НачКвартала(Д);
	ИначеЕсли ВидПериода = 9 Тогда // Год
		Д1 = НачГода(Д);                  
	КонецЕсли;
	Возврат Д1;
КонецФункции

//-----------------------------------------------
Функция КонПериода(Д)
Перем Д1;
	Если ВидПериода = 4 Тогда // День
		Д1 = Д;
	ИначеЕсли ВидПериода = 5 Тогда // Неделя
		Д1 = КонНедели(Д);
	ИначеЕсли ВидПериода = 6 Тогда // Декада
		Ч = ДатаЧисло(Д);
		Если Ч < 10 Тогда
			Ч = 9;
		ИначеЕсли Ч < 20 Тогда
			Ч = 19;
		Иначе
			Ч = ДатаЧисло(КонМесяца(Д));
		КонецЕсли;
		Д1 = Дата(ДатаГод(Д), ДатаМесяц(Д), Ч);
	ИначеЕсли ВидПериода = 7 Тогда // Месяц
		Д1 = КонМесяца(Д);
	ИначеЕсли ВидПериода = 8 Тогда // Квартал
		Д1 = КонКвартала(Д);
	ИначеЕсли ВидПериода = 9 Тогда // Год
		Д1 = КонГода(Д);                  
	КонецЕсли;
	Возврат Д1;
КонецФункции

//-----------------------------------------------
Процедура ДобавитьОбороты(Опер, Дт, Кт)
Перем Стр;
	Если Дт = 1 Тогда
		ДО = ДО+Опер.Сумма;
		Если Количественный = 1 Тогда
			ДОКол = ДОКол+Опер.Количество;
 		КонецЕсли;
		Если (Валютный = 1) И (ПоВалюте = 1) Тогда
	 		ДОВал = ДОВал+Опер.ВалСумма;
 		КонецЕсли;
	КонецЕсли;
	Если Кт = 1 Тогда
		КО = КО+Опер.Сумма;
		Если Количественный = 1 Тогда
			КОКол = КОКол+Опер.Количество;
 		КонецЕсли;
		Если (Валютный = 1) И (ПоВалюте = 1) Тогда
 			КОВал = КОВал+Опер.ВалСумма;
 		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ВывестиОбороты()
	Если ПустоеЗначение(НачалоПериода) = 1 Тогда
	    Возврат;
	КонецЕсли;
	Период = "Обороты за ";
	Если ВидПериода = 4 Тогда                  
		Период = Период+НачалоПериода;
	Иначе     
		Период = Период+ПериодСтр(НачалоПериода, КонецПериода);
	КонецЕсли;
	
	Секция = Т.ПолучитьСекцию("Секция_22");
	Если (Валютный = 1) И (ПоВалюте = 1) Тогда       
		Т.ВывестиСекцию(Секция);
		Секция = Т.ПолучитьСекцию("Секция_24");
	КонецЕсли;
	Если Количественный = 1 Тогда
		Т.ВывестиСекцию(Секция);
		Секция = Т.ПолучитьСекцию("Секция_23");
	КонецЕсли;                              
	Область = Секция.Область(1, 1, 1, 9);
	Область.РамкаСнизу(4);
	Т.ВывестиСекцию(Секция);
КонецПроцедуры                     

//-----------------------------------------------
Процедура ВывестиПроводку(Ит, Т, Опер, Дт, Кт)
	Расшифровка.Установить("НомерКорреспонденции", Опер.НомерКорреспонденции());

	СД = Ит.СКД()-Ит.СКК();
	Если Количественный = 1 Тогда
		Если (ОтборСубк1 <> 2) И (ОтборСубк2 <> 2) И (ОтборСубк3 <> 2) Тогда
			Если (Опер.ВалСумма <> 0) Или (ПоВалюте = 1) Тогда
				Если ПоВалюте = 1 Тогда
					СДВал = Ит.СКД(2)-Ит.СКК(2);
					Т.ВывестиСекцию("Секция_6_7_1");
				Иначе
					Т.ВывестиСекцию("Секция_6_1_7_1");
				КонецЕсли;
			Иначе
				Т.ВывестиСекцию("Секция_7_1");
			КонецЕсли;
		Иначе
			СДКол = Ит.СКД(3)-Ит.СКК(3);
			Если (Опер.ВалСумма <> 0) Или (ПоВалюте = 1) Тогда
				Если ПоВалюте = 1 Тогда
					СДВал = Ит.СКД(2)-Ит.СКК(2);
					Т.ВывестиСекцию("Секция_6_7");
				Иначе
					Т.ВывестиСекцию("Секция_6_1_7");
				КонецЕсли;
			Иначе
				Т.ВывестиСекцию("Секция_7");
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Опер.Количество <> 0 Тогда
		Если (Опер.ВалСумма <> 0) Или (ПоВалюте = 1) Тогда
			Если ПоВалюте = 1 Тогда
				СДВал = Ит.СКД(2)-Ит.СКК(2);
				Т.ВывестиСекцию("Секция_6_7_1");
			Иначе
				Т.ВывестиСекцию("Секция_6_1_7_1");
			КонецЕсли;
		Иначе
			Т.ВывестиСекцию("Секция_7_1");
		КонецЕсли;
	Иначе
		Если (Опер.ВалСумма <> 0) Или (ПоВалюте = 1) Тогда
			Если ПоВалюте = 1 Тогда
				СДВал = Ит.СКД(2)-Ит.СКК(2);
				Т.ВывестиСекцию("Секция_6");
			Иначе
				Т.ВывестиСекцию("Секция_6_1");
			КонецЕсли;
		Иначе
			Т.ВывестиСекцию("Секция_5");
		КонецЕсли;
	КонецЕсли;      
КонецПроцедуры

//-----------------------------------------------
Процедура СформироватьОтчет(ФлагЗакрытияФормы = 0)
	
	Если ((ТипЗначенияСтр(НачДата) = "Дата") и (ТипЗначенияСтр(КонДата) = "Дата")) Тогда
	    ПриВыбореДаты();
	КонецЕсли;
	
	Если Счет.Выбран() = 0 Тогда
		Предупреждение("Не указан счет!");
		Возврат;
	КонецЕсли;

	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;

	ОтборСубк1 = ?(ОтборСубконто1=1,2,3);
	ОтборСубк2 = ?(ОтборСубконто2=1,2,3);
	ОтборСубк3 = ?(ОтборСубконто3=1,2,3);
	    
	ВидПериода = 1;
	Если Периодичность.ТекущаяСтрока() <> 0 Тогда
		ВидПериода = Периодичность.ПолучитьЗначение(Периодичность.ТекущаяСтрока());
	КонецЕсли;
	
	Оп = СоздатьОбъект("Операция");
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ИспользоватьРазделительУчета(РазделительУчета);
	Если ВыбВидСубконто1.Выбран() = 0 Тогда
		ОтборСубк1 = 3;
	КонецЕсли;

	Если ВыбВидСубконто2.Выбран() = 0 Тогда
		ОтборСубк2 = 3;
	КонецЕсли;

	Если ВыбВидСубконто3.Выбран() = 0 Тогда
		ОтборСубк3 = 3;
	КонецЕсли;
	
	Если (ВыбВидСубконто1 = ВыбВидСубконто2) И (ОтборСубк1 <> 3) И (ОтборСубк2 <> 3) Или
		 (ВыбВидСубконто1 = ВыбВидСубконто3) И (ОтборСубк1 <> 3) И (ОтборСубк3 <> 3) Или
		 (ВыбВидСубконто3 = ВыбВидСубконто2) И (ОтборСубк3 <> 3) И (ОтборСубк2 <> 3) Тогда
		Предупреждение("Выбраны одинаковые виды субконто!");
		Возврат;
	КонецЕсли;

    Заголовок = "Карточка счета "+Счет;
    Заголовок1 = "";
    Если ОтборСубк1 <> 3 Тогда  // 3 - без учета субконто
    	Заголовок1 = Заголовок1+?(Заголовок1="","","; ")+Строка(ВыбВидСубконто1)+": "+ЗначенияВСтроку(Субконто1);
		Ит.ИспользоватьСубконто(ВыбВидСубконто1, Субконто1, ОтборСубк1);
	КонецЕсли;

    Если ОтборСубк2 <> 3 Тогда  // 3 - без учета субконто
    	Заголовок1 = Заголовок1+?(Заголовок1="","","; ")+Строка(ВыбВидСубконто2)+": "+ЗначенияВСтроку(Субконто2);
		Ит.ИспользоватьСубконто(ВыбВидСубконто2, Субконто2, ОтборСубк2);
	КонецЕсли;

    Если ОтборСубк3 <> 3 Тогда  // 3 - без учета субконто
    	Заголовок1 = Заголовок1+?(Заголовок1="","","; ")+Строка(ВыбВидСубконто3)+": "+ЗначенияВСтроку(Субконто3);
		Ит.ИспользоватьСубконто(ВыбВидСубконто3, Субконто3, ОтборСубк3);
	КонецЕсли;

	Если ПоВалюте = 1 Тогда
		Заголовок=Заголовок+?(Заголовок1="","","; ")+Валюта;
		Если Ит.ВыполнитьЗапрос(НачДата, КонДата, Счет,, Валюта,, "Проводка") = 0 Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Если Ит.ВыполнитьЗапрос(НачДата, КонДата, Счет,,,, "Проводка") = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если (ТипЗначенияСтр(Т) <> "Таблица") Или (Обновить = 0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
	Т.ИсходнаяТаблица("Таблица");
	
	НазваниеОрганизации = глНазваниеОрганизации(Контекст);

	ОбДт = 0;
	ОбКт = 0;

	Расшифровка.Установить("Отчет", "КарточкаСчета");
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
	Расшифровка.Установить("Дата1", НачДата);
	Расшифровка.Установить("Дата2", КонДата);
	Расшифровка.Установить("Счет", Счет);
	Расшифровка.Установить("ВидСубконто1", ВыбВидСубконто1);
	Расшифровка.Установить("Субконто1", Субконто1);
	Расшифровка.Установить("ОтборСубконто1", ОтборСубк1);
	Расшифровка.Установить("ВидСубконто2", ВыбВидСубконто2);
	Расшифровка.Установить("Субконто2", Субконто2);
	Расшифровка.Установить("ОтборСубконто2", ОтборСубк2);
	Расшифровка.Установить("ВидСубконто3", ВыбВидСубконто3);
	Расшифровка.Установить("Субконто3", Субконто3);
	Расшифровка.Установить("ОтборСубконто3", ОтборСубк3);
	Расшифровка.Установить("Валюта", Валюта);
	Расшифровка.Установить("ПоВалюте", ПоВалюте);
	Расшифровка.Установить("ВидПериода", ВидПериода);
	Т.ВывестиСекцию("Секция_13");
	Т.ВывестиСекцию("Секция_1_1");
	Если Заголовок1 <> "" Тогда
		Т.ВывестиСекцию("Секция_1_2");
	КонецЕсли;
	Т.ВывестиСекцию("Секция_1_3");

	Расшифровка.УдалитьВсе();
	Если ((Счет.Количественный = 1) И ((ОтборСубк1 = 2) Или (ОтборСубк2 = 2) Или (ОтборСубк3 = 2))) Или (ПоВалюте = 1) Тогда
		Т.ВывестиСекцию("Секция_2_1");
	Иначе
		Т.ВывестиСекцию("Секция_2");
	КонецЕсли;

	Если ПоВалюте = 1 Тогда
		Если (Счет.Количественный = 1) И ((ОтборСубк1 = 2) Или (ОтборСубк2 = 2) Или (ОтборСубк3 = 2)) Тогда
			Т.ВывестиСекцию("Секция_4_1");
		Иначе
			Т.ВывестиСекцию("Секция_4");
		КонецЕсли;
	КонецЕсли;

	Количественный = Счет.Количественный;
	Валютный = Счет.Валютный;

	Если (Количественный = 1) И ((ОтборСубк1 = 2) Или (ОтборСубк2 = 2) Или (ОтборСубк3 = 2)) Тогда
		Т.ВывестиСекцию("Секция_3");
	КонецЕсли;
	
	НачалоПериода = 0;
	Ит.ВыбратьПериоды();
	Пока Ит.ПолучитьПериод() = 1 Цикл
		Опер = Ит.Операция;
		Если ВидПериода <> 1 Тогда
			Если НачалоПериода <> НачПериода(Опер.ДатаОперации) Тогда
				Если НачалоПериода <> 0 Тогда
					ВывестиОбороты();
				КонецЕсли;
				ДО = 0;
				ДОКол = 0;
				ДОВал = 0;
				КО = 0;
				КОКол = 0;
				КОВал = 0;
				НачалоПериода = НачПериода(Опер.ДатаОперации);
				КонецПериода = КонПериода(Опер.ДатаОперации);
			КонецЕсли;
		КонецЕсли;
		
		Дт = Ит.ВыбранаПоДт();
		Кт = Ит.ВыбранаПоКт();

		Расшифровка.Установить("Документ", Опер.Документ.ТекущийДокумент());
		Расшифровка.Установить("НомерПроводки", Опер.НомерПроводки());
		ВывестиПроводку(Ит, Т, Опер, Дт, Кт);
		
		Если ВидПериода <> 1 Тогда
			ДобавитьОбороты(Опер, Дт, Кт);
		КонецЕсли;      
	КонецЦикла;      
	
	Если ВидПериода <> 1 Тогда
		Если (ДО <> 0) Или (ДОКол <> 0) Или (ДОВал <> 0) Или 
		     (КО <> 0) Или (КОКол <> 0) Или (КОВал <> 0) Тогда
			ВывестиОбороты();
		КонецЕсли;
	КонецЕсли;

	Т.ВывестиСекцию("Секция_8");
	Если ПоВалюте = 1 Тогда
		Т.ВывестиСекцию("Секция_14");
	КонецЕсли;
	Если (Количественный = 1) и ((ОтборСубк1 = 2) или (ОтборСубк2 = 2) или (ОтборСубк3 = 2)) Тогда
		Т.ВывестиСекцию("Секция_9");
	КонецЕсли;

	Т.ВывестиСекцию("Секция_10");
	Если ПоВалюте = 1 Тогда
		Т.ВывестиСекцию("Секция_15");
	КонецЕсли;
	Если (Количественный = 1) и ((ОтборСубк1 = 2) или (ОтборСубк2 = 2) или (ОтборСубк3 = 2)) Тогда
		Т.ВывестиСекцию("Секция_11");
	КонецЕсли;

	Т.ВывестиСекцию("Секция_12");
	Ит = 0;
	Т.ТолькоПросмотр(1);

	ФиксСтрок = 7;
	Если Заголовок1 <> "" Тогда
		ФиксСтрок = ФиксСтрок+1;
	КонецЕсли;

	НазваниеОрганизации = ?(ПустоеЗначение(НазваниеОрганизации)=1, "", " "+НазваниеОрганизации);
	ПериодИОрганизация = " ("+ПредставлениеИнтервалаОтчета(НачДата, КонДата)+")"+НазваниеОрганизации;
	ВерхнийКолонтитул = Заголовок+ПериодИОрганизация;
	НижнийКолонтитул = "Отчет сформирован "+ТекущаяДата()+" "+ТекущееВремя()+?(ПустоеЗначение(ИмяПользователя())=0,"  Пользователь: "+ИмяПользователя(),"");
	
	Т.Опции(0, 0, ФиксСтрок, 2, "ОпцииПечатиКарточкаСчета", "КарточкаСчета");
	Т.ОбластьПечати(2);
	Т.ПовторятьПриПечатиСтроки(ФиксСтрок-1, ФиксСтрок);
	Т.ПараметрыСтраницы(,,,,,,,,,1);
	Т.Показать(ВерхнийКолонтитул, "");

	Если (ФлагЗакрытияФормы = 1) Или (Обновить = 2) Тогда
         СтрокаДействийФормы = "#Закрыть";
    КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСчета()
	Если Счет.КоличествоСубконто() > 0 Тогда
		НазначитьСчет(ВыбВидСубконто1, Счет, 1);
		Форма.ВыбСубконто1.НазначитьТип(ВыбВидСубконто1);
		Доступность = 1;
	Иначе
		ВыбВидСубконто1 = "";
		ВыбСубконто1 = "";
		Доступность = 0;
		ОтборСубконто1=0;
	КонецЕсли;
	Форма.ВыбСубконто1.Доступность(Доступность);
	Форма.ОтборСубконто1.Доступность(Доступность);
	Форма.ВыбВидСубконто1.Доступность(Доступность);
	Форма.ОчиститьСубконто1.Доступность(Доступность);

	Если Счет.КоличествоСубконто() > 1 Тогда
		НазначитьСчет(ВыбВидСубконто2, Счет, 2);
		Форма.ВыбСубконто2.НазначитьТип(ВыбВидСубконто2);
		Доступность = 1;
	Иначе
		ВыбВидСубконто2 = "";
		ВыбСубконто2 = "";
		Доступность = 0;
		ОтборСубконто2=0;
	КонецЕсли;
	Форма.ВыбСубконто2.Доступность(Доступность);
	Форма.ОтборСубконто2.Доступность(Доступность);
	Форма.ВыбВидСубконто2.Доступность(Доступность);
	Форма.ОчиститьСубконто2.Доступность(Доступность);

	Если Счет.КоличествоСубконто() > 2 Тогда
		НазначитьСчет(ВыбВидСубконто3, Счет, 3);
		Форма.ВыбСубконто3.НазначитьТип(ВыбВидСубконто3);
		Доступность = 1;
	Иначе
		ВыбВидСубконто3 = "";
		ВыбСубконто3 = "";
		Доступность = 0;
		ОтборСубконто3=0;
	КонецЕсли;
	Форма.ВыбСубконто3.Доступность(Доступность);
	Форма.ОтборСубконто3.Доступность(Доступность);
	Форма.ВыбВидСубконто3.Доступность(Доступность);
	Форма.ОчиститьСубконто3.Доступность(Доступность);

	Если Счет.Валютный = 0 Тогда
	    ПоВалюте = 0;
		Если Метаданные.Валюта.Выбран() = 1 Тогда
	    	Валюта = ПолучитьПустоеЗначение("Справочник."+Метаданные.Валюта.Идентификатор);
		КонецЕсли;
	КонецЕсли;
	Форма.ПоВалюте.Доступность(Счет.Валютный);
	Форма.Валюта.Доступность(Счет.Валютный);

	УстановитьВладельца(ПоВсемРУ, РазделительУчета, ВыбСубконто1);
	УстановитьВладельца(ПоВсемРУ, РазделительУчета, ВыбСубконто2);
	УстановитьВладельца(ПоВсемРУ, РазделительУчета, ВыбСубконто3);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВалюты()
	Если Валюта.Выбран() = 1 Тогда
	    ПоВалюте = 1;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВидаСубконто1()
	Форма.ВыбСубконто1.НазначитьТип(ВыбВидСубконто1);
	УстановитьВладельца(ПоВсемРУ, РазделительУчета, ВыбСубконто1);
КонецПроцедуры
                      
//-----------------------------------------------
Процедура ПриВыбореВидаСубконто2()
	Форма.ВыбСубконто2.НазначитьТип(ВыбВидСубконто2);
	УстановитьВладельца(ПоВсемРУ, РазделительУчета, ВыбСубконто2);
КонецПроцедуры
                      
//-----------------------------------------------
Процедура ПриВыбореВидаСубконто3()
	Форма.ВыбСубконто3.НазначитьТип(ВыбВидСубконто3);
	УстановитьВладельца(ПоВсемРУ, РазделительУчета, ВыбСубконто3);
КонецПроцедуры
                      
//-----------------------------------------------
Процедура ПриВыбореСубконто1()
	Если ПустоеЗначение(ВыбСубконто1) = 0 Тогда
	    ОтборСубконто1 = 1;
	Иначе
	    ОтборСубконто1 = 0;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСубконто2()
	Если ПустоеЗначение(ВыбСубконто2) = 0 Тогда
	    ОтборСубконто2 = 1;
	Иначе
	    ОтборСубконто2 = 0;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСубконто3()
	Если ПустоеЗначение(ВыбСубконто3) = 0 Тогда
	    ОтборСубконто3 = 1;
	Иначе
	    ОтборСубконто3 = 0;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОчиститьСубконто1()
	ВыбСубконто1 = "";
    ОтборСубконто1 = 0;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОчиститьСубконто2()
	ВыбСубконто2 = "";
    ОтборСубконто2 = 0;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОчиститьСубконто3()
	ВыбСубконто3 = "";
    ОтборСубконто3 = 0;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
	УстановитьВладельца(ПоВсемРУ, РазделительУчета, ВыбСубконто1);
	УстановитьВладельца(ПоВсемРУ, РазделительУчета, ВыбСубконто2);
	УстановитьВладельца(ПоВсемРУ, РазделительУчета, ВыбСубконто3);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореРУ()
	УстановитьВладельца(ПоВсемРУ, РазделительУчета, ВыбСубконто1);
	УстановитьВладельца(ПоВсемРУ, РазделительУчета, ВыбСубконто2);
	УстановитьВладельца(ПоВсемРУ, РазделительУчета, ВыбСубконто3);
КонецПроцедуры	

//-----------------------------------------------
Процедура ПриОткрытии()
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;
	
	Периодичность.УдалитьВсе();
	Периодичность.ДобавитьЗначение(1, "За период");
	Периодичность.ДобавитьЗначение(4, "По дням");
	Периодичность.ДобавитьЗначение(5, "По неделям");
	Периодичность.ДобавитьЗначение(6, "По декадам");
	Периодичность.ДобавитьЗначение(7, "По месяцам");
	Периодичность.ДобавитьЗначение(8, "По кварталам");
	Периодичность.ДобавитьЗначение(9, "По годам");

	Если Метаданные.Валюта.Выбран() = 1 Тогда
		Форма.Валюта.НазначитьТип("Справочник."+Метаданные.Валюта.Идентификатор);
	Иначе
		Форма.ВалютаТекст.Доступность(0);
		Форма.Валюта.Доступность(0);
		Форма.ПоВалюте.Доступность(0);
	КонецЕсли;

	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		РУ = глРасшифровка.Получить("РазделительУчета");
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
		НачДата = глРасшифровка.Получить("Дата1");
		КонДата = глРасшифровка.Получить("Дата2");
		ПриУстановкеДаты();
		Счет = глРасшифровка.Получить("Счет");
	   	ПриВыбореСчета();
		
	   	Если ПустоеЗначение(глРасшифровка.Получить("ВидСубконто1")) = 0 Тогда
	   		ВыбВидСубконто1 = глРасшифровка.Получить("ВидСубконто1");
	   	КонецЕсли;
		Субконто1 = глРасшифровка.Получить("Субконто1");
		Если глРасшифровка.Получить("ОтборСубконто1") = 2 Тогда
			ОтборСубконто1 = 1;
		Иначе
			ОтборСубконто1 = 0;
		КонецЕсли;
		
		Если ПустоеЗначение(глРасшифровка.Получить("ВидСубконто2")) = 0 Тогда	
			ВыбВидСубконто2 = глРасшифровка.Получить("ВидСубконто2");
		КонецЕсли;
		Субконто2 = глРасшифровка.Получить("Субконто2");
		Если глРасшифровка.Получить("ОтборСубконто2") = 2 Тогда
			ОтборСубконто2 = 1;
		Иначе
			ОтборСубконто2 = 0;
		КонецЕсли;
		
		Если ПустоеЗначение(глРасшифровка.Получить("ВидСубконто3")) = 0 Тогда		
			ВыбВидСубконто3 = глРасшифровка.Получить("ВидСубконто3");
		КонецЕсли;
		Субконто3 = глРасшифровка.Получить("Субконто3");
		Если глРасшифровка.Получить("ОтборСубконто3") = 2 Тогда
			ОтборСубконто3 = 1;
		Иначе
			ОтборСубконто3 = 0;
		КонецЕсли;
		
		Валюта = глРасшифровка.Получить("Валюта");
		ПоВалюте = глРасшифровка.Получить("ПоВалюте");
		                               
		ВидПериода = глРасшифровка.Получить("ВидПериода");
		Позиция = Периодичность.НайтиЗначение(ВидПериода);
		Если Позиция = 0 Тогда
			Позиция = 1;
		КонецЕсли;                 
		Периодичность.ТекущаяСтрока(Позиция);

		Если Обновить <> 0 Тогда
			Т = глТаблица;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			СформироватьОтчет();
			СтатусВозврата(0);
			Возврат;
		Иначе
			ПриВыбореДаты();
			Если ТипЗначенияСтр(Субконто1) = "СписокЗначений" Тогда
			    Субконто1 = "";
				ОтборСубконто1 = 0;
			КонецЕсли;
			Если ТипЗначенияСтр(Субконто2) = "СписокЗначений" Тогда
			    Субконто2 = "";
				ОтборСубконто2 = 0;
			КонецЕсли;
			Если ТипЗначенияСтр(Субконто3) = "СписокЗначений" Тогда
			    Субконто3 = "";
				ОтборСубконто3 = 0;
			КонецЕсли;
		КонецЕсли;

		Форма.ВыбСубконто1.НазначитьТип(ВыбВидСубконто1);
		ВыбСубконто1 = Субконто1;
		Если ТипЗначенияСтр(Субконто1) = "СписокЗначений" Тогда
			Если Субконто1.РазмерСписка() = 1 Тогда
				ВыбСубконто1 = Субконто1.ПолучитьЗначение(0);
			КонецЕсли;
		КонецЕсли;

		Форма.ВыбСубконто2.НазначитьТип(ВыбВидСубконто2);
		ВыбСубконто2 = Субконто2;
		Если ТипЗначенияСтр(Субконто2) = "СписокЗначений" Тогда
			Если Субконто2.РазмерСписка() = 1 Тогда
				ВыбСубконто2 = Субконто2.ПолучитьЗначение(0);
			КонецЕсли;
		КонецЕсли;

		Форма.ВыбСубконто3.НазначитьТип(ВыбВидСубконто3);
		ВыбСубконто3 = Субконто3;
		Если ТипЗначенияСтр(Субконто3) = "СписокЗначений" Тогда
			Если Субконто3.РазмерСписка() = 1 Тогда
				ВыбСубконто3 = Субконто3.ПолучитьЗначение(0);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Обновить = 0;
		РУ = БухИтоги.ИспользоватьРазделительУчета();
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
		НачДата = НачалоПериодаБИ();
		КонДата = КонецПериодаБИ();
		ПриУстановкеДаты();
		Если Счет.Выбран()=0 Тогда
		    ВосстановитьЗначение("ОтчРабСчет", Счет);
		КонецЕсли;
		ПриВыбореСчета();
	КонецЕсли;
	
КонецПроцедуры

//-----------------------------------------------
Процедура Сформировать(ФлагЗакрытияФормы = 0)
    СохранитьЗначение("ОтчРабСчет",Счет);
	Субконто1 = ВыбСубконто1;
	Субконто2 = ВыбСубконто2;
	Субконто3 = ВыбСубконто3;
	СформироватьОтчет();
КонецПроцедуры

//-----------------------------------------------
Процедура ВводНового()
	НачДата = НачалоПериодаБИ();
	КонДата = КонецПериодаБИ();
	ПриУстановкеДаты();
	ПриВыбореСчета();
КонецПроцедуры

//-----------------------------------------------
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, Флаг)
	//Обработка выбора субконто типа "Документ"
	Если Лев(ИдентЭлемДиалога, 11) = "ВыбСубконто" Тогда
		Ном = Число(Прав(ИдентЭлемДиалога, 1));
		Если Ном = 1 Тогда
		    ВидСубконто = ВыбВидСубконто1;
		ИначеЕсли Ном = 2 Тогда
		    ВидСубконто = ВыбВидСубконто2;
		ИначеЕсли Ном = 3 Тогда
		    ВидСубконто = ВыбВидСубконто3;
		КонецЕсли;
		Если Лев(ВидСубконто.ТипСубконто(), 8) = "Документ" Тогда
			//Предусмотрена возможность выбора субконто типа "Документ" из специально созданного в конфигурации журнала
			//Идентификатор такого журнала должен быть такой же, как у вида субконто
			Журнал = Метаданные.Журнал(ВидСубконто.Идентификатор());
		    Если Журнал.Выбран() = 0 Тогда
		        Флаг = 1;
				Возврат;
			КонецЕсли;
			//Тип первой ссылки первой графы этого журнала может определять тип параметра,
			//передаваемого в форму журнала документов.
			//Данный параметр может использоваться, например, для установки отбора документов в открываемой форме
			Флаг = 0;
			Если Журнал.Графа(1).Выбран() = 0 Тогда
				ОткрытьФорму("Журнал."+Журнал.Идентификатор);
				Возврат;
			КонецЕсли;
			Если Журнал.Графа(1).Ссылки.Количество() > 0 Тогда
				РеквизитОтбора = Журнал.Графа(1).Ссылки.Получить(1).Тип+"."+Журнал.Графа(1).Ссылки.Получить(1).Вид;
				Если РеквизитОтбора = ВыбВидСубконто1.ТипСубконто() Тогда
					ОткрытьФорму("Журнал."+Журнал.Идентификатор, ?(ОтборСубконто1 = 1, ВыбСубконто1, ""));
				ИначеЕсли РеквизитОтбора = ВыбВидСубконто2.ТипСубконто() Тогда
			        ОткрытьФорму("Журнал."+Журнал.Идентификатор, ?(ОтборСубконто2 = 1, ВыбСубконто2, ""));
				ИначеЕсли РеквизитОтбора = ВыбВидСубконто3.ТипСубконто() Тогда
			        ОткрытьФорму("Журнал."+Журнал.Идентификатор, ?(ОтборСубконто3 = 1, ВыбСубконто3, ""));
				Иначе
					ОткрытьФорму("Журнал."+Журнал.Идентификатор);
				КонецЕсли;
			Иначе
				ОткрытьФорму("Журнал."+Журнал.Идентификатор);
			КонецЕсли;
		КонецЕсли;
		глРазделительУчета = РазделительУчета;
	КонецЕсли;
КонецПроцедуры