//-----------------------------------------------
Перем Расшифровка;
Перем Т;
Перем Обновить;
Перем ПредставлениеРУ;
Перем тИтСб, тИтКорСб, тРазворотСубконто;

//-----------------------------------------------
Функция ПроверкаПериода()
	Если ПустоеЗначение(Дата1) = 1 Тогда
	    Предупреждение("Не указана дата начала периода отчета!");
		Возврат 0;
	КонецЕсли;
	Если Дата1 > Дата2 Тогда
		Предупреждение("Неправильно задан период отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если Дата2 > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		               "Расчет итогов выполняется в режиме"+РазделительСтрок+
					   """Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции

//-----------------------------------------------
Функция РасшифровкаДтКт(ДтКт)
	Расшифровка.Установить("ДтКт", ДтКт);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//----------------------------------------------------------------------
Функция ИтогиПоСубконто(Ит)
	тИтСб = СоздатьОбъект("ТаблицаЗначений");
	тИтСб.НоваяКолонка("Счет");
	тИтСб.НоваяКолонка("Ит");
	тИтСб.НоваяКолонка("ВидыСубконто");
	Если ДанныеПоСубсчетам = 0 Тогда
		Возврат 1;
	КонецЕсли;
	
	Ит.ВыбратьСчета();
	Пока Ит.ПолучитьСчет() = 1 Цикл
		Стр = 0;
		Если тРазворотСубконто.НайтиЗначение(Ит.Счет, Стр, "Счет") = 0 Тогда
			Продолжить;
		КонецЕсли;    
		тРазворотСубконто.ПолучитьСтрокуПоНомеру(Стр);
		 
		спВидыСубконто = СоздатьОбъект("СписокЗначений");
		ИтСб = СоздатьОбъект("БухгалтерскиеИтоги");
		ИтСб.ИспользоватьРазделительУчета(РазделительУчета);

		Для А=1 По тРазворотСубконто.Список.РазмерСписка() Цикл
			Если тРазворотСубконто.Список.Пометка(А) = 1 Тогда
				ИтСб.ИспользоватьСубконто(тРазворотСубконто.Список.ПолучитьЗначение(А),, 1);
				спВидыСубконто.ДобавитьЗначение(тРазворотСубконто.Список.ПолучитьЗначение(А));
			КонецЕсли;
		КонецЦикла;
	
		Если спВидыСубконто.РазмерСписка() = 0 Тогда
			Продолжить;
		КонецЕсли;                             
		
		тИтСб.НоваяСтрока();
		тИтСб.Счет = Ит.Счет;
		тИтСб.Ит = ИтСб;
		тИтСб.ВидыСубконто = спВидыСубконто;

		ИтСб.ВключатьСубсчета(ДанныеПоСубсчетам, ДанныеПоКорСубсчетам);
		Если ИтСб.ВыполнитьЗапрос(Дата1, Дата2, Ит.Счет,,, 3) = 0 Тогда
			Возврат 0;
		КонецЕсли;
	КонецЦикла;
	Возврат 1;
КонецФункции

//----------------------------------------------------------------------
Функция ИтогиПоКорСубконто(Ит)
	тИтКорСб = СоздатьОбъект("ТаблицаЗначений");
	тИтКорСб.НоваяКолонка("КорСчет");
	тИтКорСб.НоваяКолонка("Ит");
	тИтКорСб.НоваяКолонка("ВидыКорСубконто");
	Если ДанныеПоКорСубсчетам = 0 Тогда
		Возврат 1;
	КонецЕсли;
	                         
	спВидыСубконто = СоздатьОбъект("СписокЗначений");
    Если ДанныеПоСубсчетам = 1 Тогда
		Стр = 0;
		Если тИтСб.НайтиЗначение(Ит.Счет, Стр, "Счет") = 1 Тогда
			тИтСб.ПолучитьСтрокуПоНомеру(Стр);
			спВидыСубконто = тИтСб.ВидыСубконто;
		КонецЕсли;
	КонецЕсли;
	
	Ит.ВыбратьКорСчета();
	Пока Ит.ПолучитьКорСчет() = 1 Цикл
		Стр = 0;
		Если тРазворотСубконто.НайтиЗначение(Ит.КорСчет, Стр, "Счет") = 0 Тогда
			Продолжить;
		КонецЕсли;    
		тРазворотСубконто.ПолучитьСтрокуПоНомеру(Стр);
		 
		спВидыКорСубконто = СоздатьОбъект("СписокЗначений");
		ИтСб = СоздатьОбъект("БухгалтерскиеИтоги");
		ИтСб.ИспользоватьРазделительУчета(РазделительУчета);

		Для А=1 По тРазворотСубконто.Список.РазмерСписка() Цикл
			Если тРазворотСубконто.Список.Пометка(А) = 1 Тогда
				ИтСб.ИспользоватьКорСубконто(тРазворотСубконто.Список.ПолучитьЗначение(А),, 1);
				спВидыКорСубконто.ДобавитьЗначение(тРазворотСубконто.Список.ПолучитьЗначение(А));
			КонецЕсли;
		КонецЦикла;
	
		Если спВидыКорСубконто.РазмерСписка() = 0 Тогда
			Продолжить;
		КонецЕсли;                             
		
		Для А=1 По спВидыСубконто.РазмерСписка() Цикл
			ИтСб.ИспользоватьСубконто(спВидыСубконто.ПолучитьЗначение(А),, 1);
		КонецЦикла;
		
		тИтКорСб.НоваяСтрока();
		тИтКорСб.КорСчет = Ит.КорСчет;
		тИтКорСб.Ит = ИтСб;
		тИтКорСб.ВидыКорСубконто = спВидыКорСубконто;

		ИтСб.ВключатьСубсчета(0, ДанныеПоКорСубсчетам);
		Если ИтСб.ВыполнитьЗапрос(Дата1, Дата2, Ит.Счет, Ит.КорСчет,, 3) = 0 Тогда
			Возврат 0;
		КонецЕсли;
	КонецЦикла;
	Возврат 1;
КонецФункции

//----------------------------------------------------------------------
Функция ПечататьПоСчету(Счет, ДанныеПоСубсчетам)
	Если (ДанныеПоСубсчетам = 0) И (Счет.Уровень() > 1) Тогда
		Возврат 0;
	КонецЕсли;            
	Сч1 = Счет.Родитель();
	Пока Сч1.Выбран() = 1 Цикл                                          
		Стр = 0;
	    Если тРазворотСубконто.НайтиЗначение(Сч1, Стр, "Счет") = 1 Тогда
			тРазворотСубконто.ПолучитьСтрокуПоНомеру(Стр);
			Если тРазворотСубконто.СубСчета = " " Тогда
				Возврат 0;
			КонецЕсли; 
		КонецЕсли;
		Сч1 = Сч1.Родитель();
	КонецЦикла;
	Возврат 1;
КонецФункции

//----------------------------------------------------------------------
Процедура ПоКорСубконто(Ит, КолСубконто, ПоВалюте)
	Стр = 0;
	Если тИтКорСб.НайтиЗначение(Ит.КорСчет, Стр, "КорСчет") = 0 Тогда
		Возврат;
	КонецЕсли; 
	тИтКорСб.ПолучитьСтрокуПоНомеру(Стр);
	
	ИтСб = тИтКорСб.Ит;
	спВидыКорСубконто = тИтКорСб.ВидыКорСубконто;
	
	Для А=1 По КолСубконто Цикл
		Если ИтСб.ПолучитьСубконто(А,, Ит.Субконто(А)) = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	Если ПоВалюте = 1 Тогда
		Если ИтСб.ПолучитьВалюту(, Ит.Валюта) = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если спВидыКорСубконто.РазмерСписка() > 0 Тогда
		КорВидСубконто1 = спВидыКорСубконто.ПолучитьЗначение(1);
		Расшифровка.Установить("КорВидСубконто1", КорВидСубконто1);
		ИтСб.ВыбратьКорСубконто(КорВидСубконто1);
		Пока ИтСб.ПолучитьКорСубконто(КорВидСубконто1) = 1 Цикл
			КорСубконто = ИтСб.КорСубконто(КорВидСубконто1);
			Расшифровка.Установить("КорСубконто1", КорСубконто);
			Т.ВывестиСекцию("Секция_8");
			
			Если спВидыКорСубконто.РазмерСписка() > 1 Тогда
				КорВидСубконто2 = спВидыКорСубконто.ПолучитьЗначение(2);
				Расшифровка.Установить("КорВидСубконто2", КорВидСубконто2);
				ИтСб.ВыбратьКорСубконто(КорВидСубконто2);
				Пока ИтСб.ПолучитьКорСубконто(КорВидСубконто2) = 1 Цикл
					КорСубконто = ИтСб.КорСубконто(КорВидСубконто2);
					Расшифровка.Установить("КорСубконто2", КорСубконто);
					Т.ВывестиСекцию("Секция_8");

					Если спВидыКорСубконто.РазмерСписка() > 2 Тогда
						КорВидСубконто3 = спВидыКорСубконто.ПолучитьЗначение(3);
						Расшифровка.Установить("КорВидСубконто3", КорВидСубконто3);
						ИтСб.ВыбратьКорСубконто(КорВидСубконто3);
						Пока ИтСб.ПолучитьКорСубконто(КорВидСубконто3) = 1 Цикл
							КорСубконто = ИтСб.КорСубконто(КорВидСубконто3);
							Расшифровка.Установить("КорСубконто3", КорСубконто);
							Т.ВывестиСекцию("Секция_8");
						КонецЦикла;
						Расшифровка.Установить("КорВидСубконто3");
						Расшифровка.Установить("КорСубконто3");
					КонецЕсли;
				КонецЦикла;
				Расшифровка.Установить("КорВидСубконто2");
				Расшифровка.Установить("КорСубконто2");
			КонецЕсли;
		КонецЦикла;
		Расшифровка.Установить("КорВидСубконто1");
		Расшифровка.Установить("КорСубконто1");
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПоКорСчетам(Ит, КолСубконто, ПоВалюте)
	Расшифровка.Установить("Отчет", "КарточкаСчета");
	Расшифровка.Установить("ДтКт");
	Если ПоВалюте = 1 Тогда
		Т.ВывестиСекцию("Секция_2_1");
	КонецЕсли;
	Т.ВывестиСекцию("Секция_2_2");

	Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");
   	Ит.ВыбратьКорСчета();
	Пока Ит.ПолучитьКорСчет() = 1 Цикл
		Если ПечататьПоСчету(Ит.КорСчет, ДанныеПоКорСубсчетам) = 0 Тогда
			Продолжить;
		КонецЕсли;
		Расшифровка.Установить("КорСчет", Ит.КорСчет);
		Т.ВывестиСекцию("Секция_3");
		Если ДанныеПоКорСубсчетам = 1 Тогда
			ПоКорСубконто(Ит, КолСубконто, ПоВалюте);
		КонецЕсли;
	КонецЦикла;
	Расшифровка.Установить("КорСчет");
	Т.ВывестиСекцию("Секция_4");
	Расшифровка.Установить("Отчет", "КарточкаСчета");
	Расшифровка.Установить("ДтКт");
	Т.ВывестиСекцию("Секция_5");
КонецПроцедуры       

//----------------------------------------------------------------------
Процедура ПоВалютам(Ит, КолСубконто)
	ПоКорСчетам(Ит, КолСубконто, 0);
    Если ДанныеПоВалютам = 1 Тогда;
		Ит.ВыбратьВалюты();
		Пока Ит.ПолучитьВалюту() = 1 Цикл
			Расшифровка.Установить("Валюта", Ит.Валюта);
			Расшифровка.Установить("ПоВалюте", 1);
			ПоКорСчетам(Ит, КолСубконто, 1);
	    КонецЦикла;
		Расшифровка.Установить("Валюта");
		Расшифровка.Установить("ПоВалюте");
	КонецЕсли;
КонецПроцедуры       

//----------------------------------------------------------------------
Процедура ПоСубконто(Ит)
	Если ДанныеПоСубсчетам = 0 Тогда
		Возврат;
	КонецЕсли;                 
	
	Стр = 0;
	Если тИтСб.НайтиЗначение(Ит.Счет, Стр, "Счет") = 0 Тогда
		Возврат;
	КонецЕсли; 
	тИтСб.ПолучитьСтрокуПоНомеру(Стр);
	
	ИтСб = тИтСб.Ит;
	спВидыСубконто = тИтСб.ВидыСубконто;
	
	Если спВидыСубконто.РазмерСписка() > 0 Тогда
		ВидСубконто1 = спВидыСубконто.ПолучитьЗначение(1);
		Расшифровка.Установить("ВидСубконто1", ВидСубконто1);
		ИтСб.ВыбратьСубконто(ВидСубконто1);
		Пока ИтСб.ПолучитьСубконто(ВидСубконто1) = 1 Цикл
			Субконто = ИтСб.Субконто(ВидСубконто1);
			Расшифровка.Установить("Субконто1", Субконто);           
			Расшифровка.Установить("ОтборСубконто1", 2);
			Т.ВывестиСекцию("Секция_10");
			Расшифровка.Установить("ОтборСубконто1");
			ПоВалютам(ИтСб, 1);
			
			Если спВидыСубконто.РазмерСписка() > 1 Тогда
				ВидСубконто2 = спВидыСубконто.ПолучитьЗначение(2);
				Расшифровка.Установить("ВидСубконто2", ВидСубконто2);
				ИтСб.ВыбратьСубконто(ВидСубконто2);
				Пока ИтСб.ПолучитьСубконто(ВидСубконто2) = 1 Цикл
					Субконто = ИтСб.Субконто(ВидСубконто2);
					Расшифровка.Установить("Субконто2", Субконто);
					Расшифровка.Установить("ОтборСубконто2", 2);
					Т.ВывестиСекцию("Секция_10");
					Расшифровка.Установить("ОтборСубконто2");
                    ПоВалютам(ИтСб, 2);
					
					Если спВидыСубконто.РазмерСписка() > 2 Тогда
						ВидСубконто3 = спВидыСубконто.ПолучитьЗначение(3);
						Расшифровка.Установить("ВидСубконто3", ВидСубконто3);
						ИтСб.ВыбратьСубконто(ВидСубконто3);
						Пока ИтСб.ПолучитьСубконто(ВидСубконто3) = 1 Цикл
							Субконто = ИтСб.Субконто(ВидСубконто3);
							Расшифровка.Установить("Субконто3", Субконто);
							Расшифровка.Установить("ОтборСубконто3", 2);
							Т.ВывестиСекцию("Секция_10");
							Расшифровка.Установить("ОтборСубконто3");
							ПоВалютам(ИтСб, 3);
						КонецЦикла;
						Расшифровка.Установить("ВидСубконто3");
						Расшифровка.Установить("Субконто3");
					КонецЕсли;
				КонецЦикла;
				Расшифровка.Установить("ВидСубконто2");
				Расшифровка.Установить("Субконто2");
			КонецЕсли;
		КонецЦикла;
		Расшифровка.Установить("ВидСубконто1");
		Расшифровка.Установить("Субконто1");
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПоСчетам(Ит)
   	Ит.ВыбратьСчета();
	Пока Ит.ПолучитьСчет() = 1 Цикл
		Если Ит.Счет <> Счет Тогда
			Если ПечататьПоСчету(Ит.Счет, ДанныеПоСубсчетам) = 0 Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ИтогиПоКорСубконто(Ит);
		
		Расшифровка.Установить("Счет", Ит.Счет);
		Если ДанныеПоСубсчетам = 1 Тогда
			Расшифровка.Установить("Отчет", "КарточкаСчета");
			Расшифровка.Установить("ДтКт");
	        Т.ВывестиСекцию("Секция_9");
		КонецЕсли;
		ПоВалютам(Ит, 0);
		Если ДанныеПоСубсчетам = 1 Тогда
			ПоСубконто(Ит);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры       

//-----------------------------------------------
Процедура Сформировать(Ручн = 0, ФлагЗакрытияФормы = 0)
	Если Счет.Выбран() = 0 Тогда
		Предупреждение("Не указан счет!");
		Возврат;
	КонецЕсли;

	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;

	Если Ручн=1 Тогда
	    СохранитьЗначение("ОтчРабСчет",Счет);
	КонецЕсли;

	Расшифровка = СоздатьОбъект("СписокЗначений");
    Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Если ПоВсемРУ = 0 Тогда
    	Ит.ИспользоватьРазделительУчета(РазделительУчета);
	КонецЕсли;

	Ит.ВключатьСубсчета(ДанныеПоСубсчетам, ДанныеПоКорСубсчетам);
  	Если Ит.ВыполнитьЗапрос(Дата1, Дата2, Счет,,, 3) = 0 Тогда
  		Возврат;
  	КонецЕсли;

	тРазворотСубконто = СоздатьОбъект("ТаблицаЗначений");
	тРазворотСубконто.НоваяКолонка("Счет",,,, "Счет", 16);
	тРазворотСубконто.НоваяКолонка("Субсчета",,,, "Субсчета", 9);
	тРазворотСубконто.НоваяКолонка("Субконто",,,, "Субконто", 9);
	тРазворотСубконто.НоваяКолонка("Список");
	тРазворотСубконто.ВидимостьКолонки("Список", 0);
	ВосстановитьЗначение("ОСВРазворотСубконто", тРазворотСубконто);

  	Если ИтогиПоСубконто(Ит) = 0 Тогда
		Возврат;
	КонецЕсли;

	Если (ТипЗначенияСтр(Т) <> "Таблица")ИЛИ(Обновить=0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
	Т.ИсходнаяТаблица("Таблица");
	Расшифровка.Установить("Дата1", Дата1);
	Расшифровка.Установить("Дата2", Дата2);
	Расшифровка.Установить("Счет", Счет);
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
	Расшифровка.Установить("ДанныеПоСубсчетам", ДанныеПоСубсчетам);
	Расшифровка.Установить("ДанныеПоКорСубсчетам", ДанныеПоКорСубсчетам);
   	Расшифровка.Установить("ДанныеПоВалютам", ДанныеПоВалютам);
   	Расшифровка.Установить("Отчет", "АнализСчета");
   	
   	НазваниеОрганизации = глНазваниеОрганизации(Контекст);
	
	Т.ВывестиСекцию("Секция_6");
	Т.ВывестиСекцию("Секция_1");
   	Расшифровка.Установить("ДанныеПоСубсчетам");
   	Расшифровка.Установить("ДанныеПоКорСубсчетам");
   	Расшифровка.Установить("ДанныеПоВалютам");
   	Расшифровка.Установить("Отчет");
	Расшифровка.Установить("Обновить");
	
	ПоСчетам(Ит);
	                                                                      
	НазваниеОрганизации = ?(ПустоеЗначение(НазваниеОрганизации)=1, "", " "+НазваниеОрганизации);
	ПериодИОрганизация = " ("+ПериодСтр(Дата1, Дата2)+")"+НазваниеОрганизации;
	ВерхнийКолонтитул = "Анализ счета: "+Счет+ПериодИОрганизация;
	НижнийКолонтитул = "Отчет сформирован "+ТекущаяДата()+" "+ТекущееВремя()+?(ПустоеЗначение(ИмяПользователя())=0,"  Пользователь: "+ИмяПользователя(),"");
	Ит = 0;
	Т.ТолькоПросмотр(1);
    Т.Опции(0, 0, 7, 0, "ОпцииПечатиАнализСчет", "АнализСчет");
	Т.ОбластьПечати(2);
	Т.ПовторятьПриПечатиСтроки(6,7);
	Т.ПараметрыСтраницы(1,,,,,,,,, 1);
    Т.Показать(ВерхнийКолонтитул, "");

	Если (ФлагЗакрытияФормы = 1) Или (Обновить = 2) Тогда
         СтрокаДействийФормы = "#Закрыть";
    КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСчета()
	Если Счет.Валютный = 0 Тогда
	    ДанныеПоВалютам = 0;
	КонецЕсли;
	Форма.ДанныеПоВалютам.Доступность(Счет.Валютный);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ОткрытьНастройку()              
	ОткрытьФормуМодально("Обработка.РазворотСубконто", 1);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОткрытии()
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;

	Если Метаданные.Валюта.Выбран() = 0 Тогда
		Форма.ДанныеПоВалютам.Доступность(0);
	КонецЕсли;

	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		РУ = глРасшифровка.Получить("РазделительУчета");
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
	   	Дата1 = глРасшифровка.Получить("Дата1");
	   	Дата2 = глРасшифровка.Получить("Дата2");
	   	Счет = глРасшифровка.Получить("Счет");
		ДанныеПоСубсчетам = глРасшифровка.Получить("ДанныеПоСубсчетам");
		ДанныеПоКорСубсчетам = глРасшифровка.Получить("ДанныеПоКорСубсчетам");
		ДанныеПоВалютам = глРасшифровка.Получить("ДанныеПоВалютам");
		Если Обновить <> 0 Тогда
			Т = глТаблица;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		РУ = БухИтоги.ИспользоватьРазделительУчета();
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
		Дата1 = НачалоПериодаБИ();
		Дата2 = КонецПериодаБИ();
		Если Счет.Выбран()=0 Тогда
		    ВосстановитьЗначение("ОтчРабСчет",Счет);
		КонецЕсли;
		ПриВыбореСчета();
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ВводНового()
	Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ();
	ПриВыбореСчета();
КонецПроцедуры

