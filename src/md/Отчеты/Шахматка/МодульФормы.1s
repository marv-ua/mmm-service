//-----------------------------------------------
Перем Т;
Перем Обновить;
Перем Расшифровка;
Перем ПланСчетов;
Перем ВыбПланСчетов;
Перем ПредставлениеРУ;

//-----------------------------------------------
Функция ПроверкаПериода()
	Если ПустоеЗначение(Дата1) = 1 Тогда
	    Предупреждение("Не указана дата начала периода отчета!");
		Возврат 0;
	КонецЕсли;
	Если Дата1 > Дата2 Тогда
		Предупреждение("Неправильно задан период отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если Дата2 > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		               "Расчет итогов выполняется в режиме"+РазделительСтрок+
					   """Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Процедура Сформировать(ФлагЗакрытияФормы = 0)
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;

	Расшифровка = СоздатьОбъект("СписокЗначений");
    Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	// запрос за период  фильтровать по валюте и считать только обороты
    Ит.ВключатьСубсчета(ДанныеПоСубсчетам, ДанныеПоСубсчетам);
    Если ВыбПланСчетов = 1 Тогда
        ПланСчетов = ВыбранныйПланСчетов();
    КонецЕсли;
    Ит.ИспользоватьПланСчетов(ПланСчетов);
    Ит.ИспользоватьРазделительУчета(РазделительУчета);
	Ит.Опции(0);
	Если ПоВалюте = 1 Тогда
		Если Ит.ВыполнитьЗапрос(Дата1, Дата2,,, Валюта, 2) = 0 Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Если Ит.ВыполнитьЗапрос(Дата1, Дата2,,,, 2) = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если (ТипЗначенияСтр(Т) <> "Таблица") Или (Обновить = 0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
	Т.ИсходнаяТаблица("Таблица");
	
	НазваниеОрганизации = глНазваниеОрганизации(Контекст);

	Расшифровка.Установить("Отчет", "Шахматка");
    Расшифровка.Установить("ПланСчетов", ПланСчетов);
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
	Расшифровка.Установить("Дата1", Дата1);
	Расшифровка.Установить("Дата2", Дата2);
   	Расшифровка.Установить("ДанныеПоСубсчетам", ДанныеПоСубсчетам);
	Расшифровка.Установить("Валюта", Валюта);
	Расшифровка.Установить("ПоВалюте", ПоВалюте);
	Расшифровка.Установить("ЗабалансовыеСчета", ЗабалансовыеСчета);

	Т.ВывестиСекцию("Секция_11");
	Т.ВывестиСекцию("Секция_1");
   	Расшифровка.Установить("ДанныеПоСубсчетам");
	Расшифровка.Установить("Обновить");
	Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");

    // вывести шапку таблицы
	Т.ВывестиСекцию("Секция_2|Секция_5");
	Расшифровка.Установить("ДтКт", 2);
	Ит.ВыбратьСчета(0, 2);
	Расшифровка.Установить("Валюта", Валюта);
	Расшифровка.Установить("ПоВалюте", ПоВалюте);
   	Пока Ит.ПолучитьСчет() = 1 Цикл
   		Если ЗабалансовыеСчета = 0 Тогда
   			Если (Ит.Счет.Забалансовый = 1) или (ПустоеЗначение(Ит.Счет) = 1)  Тогда
   			    Продолжить;
   			КонецЕсли;
		КонецЕсли;
   		
		Расшифровка.Установить("Счет", Ит.Счет);
   		Т.ПрисоединитьСекцию("Секция_2|Секция_6");
		Ит.ВыбратьВалюты(0, 2);
   		Пока Ит.ПолучитьВалюту() = 1 Цикл
			Расшифровка.Установить("Валюта", Ит.Валюта);
			Расшифровка.Установить("ПоВалюте", 1);
   			Т.ПрисоединитьСекцию("Секция_2|Секция_7");
   		КонецЦикла;
		Расшифровка.Установить("Валюта", Валюта);
		Расшифровка.Установить("ПоВалюте", ПоВалюте);
   	КонецЦикла;
	Т.ПрисоединитьСекцию("Секция_2|Секция_8");

   	// перебор счетов только с дебетовыми оборотами
	Ит.ВыбратьСчета(0, 1);
    Пока Ит.ПолучитьСчет() = 1 Цикл
		Если ЗабалансовыеСчета = 0 Тогда
   			Если (Ит.Счет.Забалансовый = 1) или (ПустоеЗначение(Ит.Счет) = 1)  Тогда
   			    Продолжить;
   			КонецЕсли;
		КонецЕсли;
   		
		Расшифровка.Установить("ДтКт", 1);
		Расшифровка.Установить("Счет", Ит.Счет);

		// показать обороты с кор счетами
	   	Т.ВывестиСекцию("Секция_3|Секция_5");
		Ит.ВыбратьКорСчета(1, 1);
	   	Пока Ит.ПолучитьКорСчет() = 1 Цикл
	   		Если ЗабалансовыеСчета = 0 Тогда
	   			Если (Ит.КорСчет.Забалансовый = 1) или (ПустоеЗначение(Ит.КорСчет) = 1)  Тогда
	   			    Продолжить;
	   			КонецЕсли;
			КонецЕсли;
	   		
			Расшифровка.Установить("КорСчет", Ит.КорСчет);
			Если Ит.КорДО() = 0 Тогда
	   			Т.ПрисоединитьСекцию("Секция_10|Секция_6");
	   		Иначе
	   			Т.ПрисоединитьСекцию("Секция_3|Секция_6");
	   		КонецЕсли;
	   		Если Ит.КорСчет.Валютный = 1 Тогда
	   			Ит.ВыбратьВалюты(-1, 1, 1);
	   			Пока Ит.ПолучитьВалюту(1) = 1 Цикл
					Расшифровка.Установить("Валюта", Ит.Валюта);
					Расшифровка.Установить("ПоВалюте", 1);
					Если (Ит.КорДО(2) = 0) И (Ит.КорДО() = 0) Тогда
			   			Т.ПрисоединитьСекцию("Секция_10|Секция_7");
			   		Иначе
		   				Т.ПрисоединитьСекцию("Секция_3|Секция_7");
		   			КонецЕсли;
	   			КонецЦикла;
				Расшифровка.Установить("Валюта", Валюта);
				Расшифровка.Установить("ПоВалюте", ПоВалюте);
	   		КонецЕсли;
	   	КонецЦикла;
		Расшифровка.Установить("КорСчет");
		Т.ПрисоединитьСекцию("Секция_3|Секция_8");

        // по валютам дебета
		Ит.ВыбратьВалюты(0, 1);
		Пока Ит.ПолучитьВалюту() = 1 Цикл
			Расшифровка.Установить("Валюта", Ит.Валюта);
			Расшифровка.Установить("ПоВалюте", 1);
		   	Т.ВывестиСекцию("Секция_3_1|Секция_5");
			Ит.ВыбратьКорСчета(1, 1, 1);
		   	Пока Ит.ПолучитьКорСчет(1) = 1 Цикл
		   		Если ЗабалансовыеСчета = 0 Тогда
		   			Если (Ит.КорСчет.Забалансовый = 1) или (ПустоеЗначение(Ит.КорСчет) = 1)  Тогда
		   			    Продолжить;
		   			КонецЕсли;
				КонецЕсли;
		   		
				Расшифровка.Установить("КорСчет", Ит.КорСчет);
				Если (Ит.КорДО(2) = 0) И (Ит.КорДО() = 0) Тогда
		   			Т.ПрисоединитьСекцию("Секция_10_1|Секция_6");
		   		Иначе
			   		Т.ПрисоединитьСекцию("Секция_3_1|Секция_6");
			   	КонецЕсли;
		   		Если Ит.КорСчет.Валютный = 1 Тогда
		   			Ит.ВыбратьВалюты(-1, 1, 2);
		   			Пока Ит.ПолучитьВалюту(2) = 1 Цикл
						Если Ит.КорДО(2) = 0 Тогда
				   			Т.ПрисоединитьСекцию("Секция_10_1|Секция_7");
				   		Иначе
			   				Т.ПрисоединитьСекцию("Секция_3_1|Секция_7");
					   	КонецЕсли;
		   			КонецЦикла;
		   		КонецЕсли;
		   	КонецЦикла;
			Расшифровка.Установить("КорСчет");
			Т.ПрисоединитьСекцию("Секция_3_1|Секция_8");
		КонецЦикла;
		Расшифровка.Установить("Валюта", Валюта);
		Расшифровка.Установить("ПоВалюте", ПоВалюте);
    КонецЦикла;

	// итоговая строка таблицы
   	Т.ВывестиСекцию("Секция_4|Секция_5");
	Расшифровка.Установить("ДтКт", 2);
	Ит.ВыбратьСчета(0, 2);
   	Пока Ит.ПолучитьСчет() = 1 Цикл
   		Если ЗабалансовыеСчета = 0 Тогда
   			Если (Ит.Счет.Забалансовый = 1) или (ПустоеЗначение(Ит.Счет) = 1)  Тогда
   			    Продолжить;
   			КонецЕсли;
		КонецЕсли;
   		
		Расшифровка.Установить("Счет", Ит.Счет);
   		Т.ПрисоединитьСекцию("Секция_4|Секция_6");
		Ит.ВыбратьВалюты(0, 2);
		Пока Ит.ПолучитьВалюту() = 1 Цикл
			Расшифровка.Установить("Валюта", Ит.Валюта);
			Расшифровка.Установить("ПоВалюте", 1);
			Т.ПрисоединитьСекцию("Секция_4|Секция_7");
		КонецЦикла;
		Расшифровка.Установить("Валюта", Валюта);
		Расшифровка.Установить("ПоВалюте", ПоВалюте);
   	КонецЦикла;
	Т.ПрисоединитьСекцию("Секция_4|Секция_8");
	
	НазваниеОрганизации = ?(ПустоеЗначение(НазваниеОрганизации)=1, "", " "+НазваниеОрганизации);
	ПериодИОрганизация = " ("+ПериодСтр(Дата1, Дата2)+")"+НазваниеОрганизации;
	ВерхнийКолонтитул = "Шахматка"+ПериодИОрганизация;
	НижнийКолонтитул = "Отчет сформирован "+ТекущаяДата()+" "+ТекущееВремя()+?(ПустоеЗначение(ИмяПользователя())=0,"  Пользователь: "+ИмяПользователя(),"");
    Ит = 0;
    Т.ТолькоПросмотр(1);
	Т.Опции(0, 0, 6, 1, "ОпцииПечатиШахматка", "Шахматка");
	Т.ОбластьПечати(2);
	Т.ПовторятьПриПечатиСтолбцы(1,1);
	Т.ПовторятьПриПечатиСтроки(6,6);
	Т.ПараметрыСтраницы(2,,,,,,,,, 0);
    Т.Показать(ВерхнийКолонтитул, "");

	Если (ФлагЗакрытияФормы = 1) Или (Обновить = 2) Тогда
         СтрокаДействийФормы = "#Закрыть";
    КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВалюты()
	ПоВалюте = Валюта.Выбран();
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОткрытии()
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;

	Если Метаданные.Валюта.Выбран() = 1 Тогда
		Форма.Валюта.НазначитьТип("Справочник."+Метаданные.Валюта.Идентификатор);
	Иначе
		Форма.ВалютаТекст.Доступность(0);
		Форма.Валюта.Доступность(0);
		Форма.ПоВалюте.Доступность(0);
	КонецЕсли;

	Если глФлагРасшифровки = 1 Тогда
		ВыбПланСчетов = 0;
		Обновить = глОбновить;
		Если Обновить <> 0 Тогда
			ПланСчетов = глРасшифровка.Получить("ПланСчетов");
			РУ = глРасшифровка.Получить("РазделительУчета");
			Если ТипЗначенияСтр(РУ) <> "" Тогда
				РазделительУчета = РУ;
				ПоВсемРУ = 0;
			Иначе
				Форма.РазделительУчета.НазначитьТип("");
				ПоВсемРУ = 1;
			КонецЕсли;
			Дата1 = глРасшифровка.Получить("Дата1");
			Дата2 = глРасшифровка.Получить("Дата2");
			ДанныеПоСубсчетам = глРасшифровка.Получить("ДанныеПоСубсчетам");
			Валюта = глРасшифровка.Получить("Валюта");
			ПоВалюте = глРасшифровка.Получить("ПоВалюте");
			ЗабалансовыеСчета = глРасшифровка.Получить("ЗабалансовыеСчета");
			Т = глТаблица;
		Иначе
			ПланСчетов = ВыбранныйПланСчетов();
			РУ = БухИтоги.ИспользоватьРазделительУчета();
			Если ТипЗначенияСтр(РУ) <> "" Тогда
				РазделительУчета = РУ;
				ПоВсемРУ = 0;
			Иначе
				Форма.РазделительУчета.НазначитьТип("");
				ПоВсемРУ = 1;
			КонецЕсли;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		ВыбПланСчетов = 1;
		ПланСчетов = ВыбранныйПланСчетов();
		РазделительУчета = БухИтоги.ИспользоватьРазделительУчета();
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ВводНового() //Считывание существующей настройки
	Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ();
КонецПроцедуры

