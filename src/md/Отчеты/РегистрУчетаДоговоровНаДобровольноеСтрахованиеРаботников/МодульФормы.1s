//******************************************************************************
// ПроверкаПериода()
//
// Возвращаемое значение:
//  1 - корректно выбран период в диалоге
//  0 - не корректно выбран период в диалоге
//
// Описание:
// Функция проверяет корректность ввода интервала дат в дилоге
//
Функция ПроверкаПериода()
	
	Если НачДата > КонДата Тогда
		Предупреждение("Неправильно задан период формирования отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	
	Возврат 1;
	
КонецФункции // ПроверкаПериода()

//******************************************************************************
Процедура Сформировать()
	
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СчН04_05 = СчетПоКоду("Н04.05");
	СчН04_06 = СчетПоКоду("Н04.06");
	СчН04_07 = СчетПоКоду("Н04.07");
	
	Таб = СоздатьОбъект("Таблица"); 
	СтрНалогоплательщик = Константа.ОфициальноеНазваниеОрганизации;
	Если ПустаяСтрока(СтрНалогоплательщик) = 1 Тогда
	СтрНалогоплательщик = Константа.НазваниеОрганизации;	    
	КонецЕсли;
	ИНН = Константа.ИННОрганизации;
	Таб.ВывестиСекцию("Шапка");
	
	Опер = СоздатьОбъект("Операция");  
	Опер.ВыбратьОперацииСПроводками(НачДата, КонДата, "Н04.05,*;Н04.06,*;Н04.07,*",,,);
	Пока Опер.ПолучитьПроводку() = 1 Цикл
		ДатаФормированияЗаписи = Опер.ДатаОперации;
		СчетДт = Опер.Дебет.Счет;
		
		Если СчетДт = СчН04_05 Тогда
			ВидСтрахования = "Долгосрочное страхование жизни работников, пенсионное страхование, негосударственное пенсионное обеспечение работников";
			
		ИначеЕсли СчетДт = СчН04_06 Тогда
			ВидСтрахования = "Добровольное личное страхование, предусматривающее оплату страховщиками медицинских расходов";

		ИначеЕсли СчетДт = СчН04_07 Тогда
			ВидСтрахования = "Добровольное личное страхование, заключаемое исключительно на случай наступления смерти или утраты трудоспособности";
		КонецЕсли;
		ВидСтрахования = ВидСтрахования + ", " + Опер.Дебет.РасходыБудущихПериодов;
		
		Сотрудник = "" + ФИО(Опер.Дебет.Сотрудники) + " (таб.№" + Опер.Дебет.Сотрудники.Код + ")";
		
		Если Опер.Дебет.Сотрудники.Подразделение.Получить(ДатаФормированияЗаписи).Административное = 1 Тогда
		    ВидПерсонала = "АУП";
		Иначе
		    ВидПерсонала = "Прочие";
		КонецЕсли;
		
		ДатаНачалаДействияДоговора = Опер.Дебет.РасходыБудущихПериодов.ДатаНачалаСписания;
		ДатаПрекращенияДействияДоговора = Опер.Дебет.РасходыБудущихПериодов.ДатаОкончанияСписания;
		
		Если (ПустоеЗначение(ДатаНачалаДействияДоговора) = 0) 
			и (ПустоеЗначение(ДатаПрекращенияДействияДоговора) = 0)
			и (ДатаНачалаДействияДоговора <= ДатаПрекращенияДействияДоговора) Тогда
				
			КоличествоМесяцевДействияДоговора = (ДатаГод(ДатаПрекращенияДействияДоговора) - ДатаГод(ДатаНачалаДействияДоговора)) * 12;
			КоличествоМесяцевДействияДоговора = КоличествоМесяцевДействияДоговора + ДатаМесяц(ДатаПрекращенияДействияДоговора) - ДатаМесяц(ДатаНачалаДействияДоговора);
			Если ДатаЧисло(ДатаНачалаДействияДоговора) - ДатаЧисло(ДатаПрекращенияДействияДоговора) <= 1 Тогда
			    КоличествоМесяцевДействияДоговора = КоличествоМесяцевДействияДоговора + 1;
			КонецЕсли;
				
		Иначе
			КоличествоМесяцевДействияДоговора = "";
		КонецЕсли;
		
		СуммаПлатежа = ФорматС(Опер.Сумма);

		Таб.ВывестиСекцию("Строка");
	КонецЦикла;

	ОтветственныйЗаСоставление = ФИО(Константа.ОтветственныйЗаСоставлениеРегистровНУ);
	
	Таб.ВывестиСекцию("Подвал");
	Таб.ТолькоПросмотр(1);
	Таб.ОбластьПечати(,2,,);
	Таб.Опции(0, 0, 0, 0, "ПечатьРегистрУчетаДоговоровНаДобровольноеСтрахованиеРаботников", "ОкноРегистрУчетаДоговоровНаДобровольноеСтрахованиеРаботников");
	Таб.Показать("Регистр учета договоров на добровольное страхование работников");

КонецПроцедуры // Сформировать    

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
    НачДата = НачГода(РабочаяДата());
	КонДата = КонКвартала(РабочаяДата()); 
	
КонецПроцедуры // ПриОткрытии()