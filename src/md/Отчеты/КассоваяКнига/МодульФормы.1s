//_____________________________________________________________________________
Перем Т;
Перем Обновить;
Перем Расшифровка;
//_____________________________________________________________________________
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции
//_____________________________________________________________________________
Процедура ДобавитьОбороты(ТЗ, Валюта, ВалПриход, Приход, ВалРасход, Расход)
	Перем Стр;
	ТЗ.НайтиЗначение(Валюта, Стр, "Валюта");
	Если Стр = 0 Тогда
		ТЗ.НоваяСтрока();
		ТЗ.Валюта = Валюта;
		ТЗ.ВалОстаток = 0;
		ТЗ.Остаток = 0;
		ТЗ.ВалПриход = ВалПриход; 
		ТЗ.Приход = Приход;
		ТЗ.ВалРасход = ВалРасход;
		ТЗ.Расход = Расход;
	Иначе
		ТЗ.ПолучитьСтрокуПоНомеру(Стр);
		ТЗ.ВалПриход = ТЗ.ВалПриход+ВалПриход; 
		ТЗ.Приход = ТЗ.Приход+Приход;
		ТЗ.ВалРасход = ТЗ.ВалРасход+ВалРасход;
		ТЗ.Расход = ТЗ.Расход+Расход;
	КонецЕсли;    
КонецПроцедуры                                                                 

//_____________________________________________________________________________
Функция ПроверкаПериода()
	Если НачДата > КонДата Тогда
		Предупреждение("Неправильно задан период формирования отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если КонДата > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		               "Расчет итогов выполняется в режиме"+РазделительСтрок+
					   """Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции // ПроверкаПериода()

//_____________________________________________________________________________
Процедура Сформировать()
	Перем ДатаЛиста;
    
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Обновить = 2 Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;

	Если (ТипЗначенияСтр(Т) <> "Таблица") Или (Обновить = 0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
    Т.ИсходнаяТаблица("Таблица");
              
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Расшифровка.Установить("Отчет", "КассоваяКнига");
	Расшифровка.Установить("НачДата", НачДата);
	Расшифровка.Установить("КонДата", КонДата);
	Расшифровка.Установить("ПоследнийЛист", ПоследнийЛист);
	Расшифровка.Установить("ВыводитьОснования", ВыводитьОснования);
    Т.ВывестиСекцию("Обновить");
              	
	НачалоГода = НачГода(НачДата);        
	
	// рассчет номера начального листа
	// осуществляется по проводкам так как не все операции по
	// кассе оформляются приходными/расходными ордерами
	ЛистовЗаГод = 0;
	ЛистовЗаМесяц = 0;
	Если ПересчитатьНомера = 1 Тогда
		П = СоздатьОбъект("Периодический");
		П.ИспользоватьОбъект("НомерЛистаКассовойКниги");
		П.ВыбратьЗначения(НачалоГода, КонГода(НачалоГода));
		Пока П.ПолучитьЗначение() = 1 Цикл
			П.Удалить();
		КонецЦикла;
	Иначе
		Если НачДата <> НачалоГода Тогда
			П = СоздатьОбъект("Периодический");
			П.ИспользоватьОбъект("НомерЛистаКассовойКниги");
			Если П.НайтиЗначение(НачДата-1, -1) = 1 Тогда
				Если П.ДатаЗнач >= НачалоГода Тогда
					ЛистовЗаГод = П.Значение;
					ЛистовЗаМесяц = П.Значение;
				КонецЕсли;     
			КонецЕсли;
			Если П.НайтиЗначение(НачМесяца(КонДата)-1, -1) = 1 Тогда
				Если П.ДатаЗнач >= НачалоГода Тогда
					ЛистовЗаМесяц = ЛистовЗаГод - П.Значение;
				КонецЕсли;     
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	НачЗапроса = ?(ПересчитатьНомера=1,НачалоГода,НачДата);
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ВыполнитьЗапрос(НачЗапроса, КонДата, "50.1,50.11",,,, "День", "СВ");

	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса =
	"//{{ЗАПРОС(КассоваяКнига)
	|Период с НачЗапроса по КонДата;
	|ОбрабатыватьДокументы Все;
	|Обрабатывать НеПомеченныеНаУдаление;
	|ПриходДок = Документ.ПриходныйОрдер.Сумма,Документ.ПриходныйОрдерТорг.Сумма;
	|РасходДок = Документ.РасходныйОрдер.Сумма,Документ.РасходныйОрдерТорг.Сумма;
	|Валюта = Документ.ПриходныйОрдер.Валюта, Документ.РасходныйОрдер.Валюта;
	|Функция Приход = Сумма(ПриходДок);
	|Функция Расход = Сумма(РасходДок);
	|Группировка День;
	|Группировка Документ;
	|"//}}ЗАПРОС
	;
    Запрос.Выполнить(ТекстЗапроса);         
	
	ТЗ = СоздатьОбъект("ТаблицаЗначений");
	ТЗ.НоваяКолонка("Валюта", "Справочник.Валюты");
	ТЗ.НоваяКолонка("ВалОстаток", "Число", 15, 2);
	ТЗ.НоваяКолонка("Остаток", "Число", 15, 2);
	ТЗ.НоваяКолонка("ВалПриход", "Число", 15, 2);
	ТЗ.НоваяКолонка("Приход", "Число", 15, 2);
	ТЗ.НоваяКолонка("ВалРасход", "Число", 15, 2);
	ТЗ.НоваяКолонка("Расход", "Число", 15, 2);
	
	Ит.ВыбратьВалюты();
	Пока Ит.ПолучитьВалюту() = 1 Цикл
		Если Ит.Валюта.Выбран() = 1 Тогда
			ТЗ.НоваяСтрока();
			ТЗ.Валюта = Ит.Валюта;
			ТЗ.Остаток = Ит.СНД("С");
			ТЗ.ВалОстаток = Ит.СНД("В");
		КонецЕсли;
	КонецЦикла;
	ТЗ.НоваяСтрока();
	ТЗ.Валюта = ПолучитьПустоеЗначение("Справочник.Валюты");
	ТЗ.Остаток = Ит.СНД("С")-ТЗ.Итог("Остаток");
	ТЗ.ВалОстаток = Ит.СНД("В")-ТЗ.Итог("ВалОстаток");
	ТЗ.Сортировать("Валюта");
	
	Ит.ВыбратьПериоды();
	ПоПроводкам = Ит.ПолучитьПериод();
	ПоДокументам = Запрос.Группировка("День");
	БылиОшибки = 0;
	ВывестиПодвал = 0;
	Документы = СоздатьОбъект("СписокЗначений");
	Валюты = СоздатьОбъект("СписокЗначений");
	
	Пока (ПоПроводкам = 1) Или (ПоДокументам = 1) Цикл
		Если ПоПроводкам = 0 Тогда
			ДатаЛиста = Запрос.День;
		ИначеЕсли ПоДокументам = 0 Тогда
			ДатаЛиста = Ит.НачДата;
		Иначе                 
			ДатаЛиста = Мин(Запрос.День, Ит.НачДата);
		КонецЕсли;    
		НомерЛиста = ЛистовЗаГод + 1;
		ЛистовЗаГод = ЛистовЗаГод + 1;
		Если ДатаЛиста >= НачМесяца(КонДата) Тогда
			ЛистовЗаМесяц = ЛистовЗаМесяц + 1;
		КонецЕсли;
		КоличествоПриходныхДокументов = 0;
		КоличествоРасходныхДокументов = 0;
		
		СчетКурсовыхРазниц = СчетПоКоду("91");
		Остаток = ТЗ.Итог("Остаток");
		Если ДатаЛиста >= НачДата Тогда
			Т.ВывестиСекцию("ВкладнойЛист|Отчет");
			Т.НоваяКолонка();
			Т.ПрисоединитьСекцию("ОтчетКассира|Отчет");
			Т.ВывестиСекцию("Шапка|Отчет");
			Т.ПрисоединитьСекцию("Шапка|Отчет");
			Т.ВывестиСекцию("ОстатокНаНД|Отчет");
			Т.ПрисоединитьСекцию("ОстатокНаНД|Отчет");
		КонецЕсли;
	
		ПоВалютам = 0;
		ТЗ.ВыбратьСтроки();
		Пока ТЗ.ПолучитьСтроку() = 1 Цикл    
			Если ((ТЗ.Остаток <> 0) Или (ТЗ.ВалОстаток <> 0)) И (ТЗ.Валюта.Выбран() = 1) Тогда
				ПоВалютам = 1;      
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
//		Высота каждой строки = 13 пунктов
//      На стандартную страницу помещается 59 строк

//		Высоты секций в строках:
//      Шапка = 5 стр
//		ВкладнойЛист = 3 стр
//		ОтчетКассира = 3 стр
//		ОстатокНаНачало = 1 стр
//		ВТомЧисле = 1 стр
//		Остаток = 1 стр
//		ВалОстаток = 2 стр
//		КурсовыеРазницы = 2 стр
//		КурсовыеРазницыПоВалюте = 1 стр
//		Строка = 2 стр
//		СтрокаШирокая = 4 стр
//		СтрокаВал = 2 стр
//		СтрокаВалШирокая = 4 стр
//		Перенос = 1 стр
//		Оборот = 1 стр
//		ОборотРуб = 1 стр
//		ОборотВал = 2 стр
//		КурсоваяРазница = 1 стр
//		КурсоваяРазницаПоВалюте = 1 стр
//		КонечныйОстаток = 1 стр
//		Подвал = 11 стр
//		ЛистовЗаМесяц = 1 стр
//		ЛистовЗаГод = 1 стр
		
		ВысотаСтроки = ?(ВыводитьОснования = 1, 4, 2); // высота секции документа
		СтрокШапки = 9;
		СтрокПодвала = 13;
		
		Если ПоВалютам = 1 Тогда
			Если ДатаЛиста >= НачДата Тогда
				Т.ВывестиСекцию("ВТомЧисле|Отчет");
				Т.ПрисоединитьСекцию("ВТомЧисле|Отчет");
			КонецЕсли;
			СтрокШапки = СтрокШапки + 1;
			ТЗ.ВыбратьСтроки();
			Пока ТЗ.ПолучитьСтроку() = 1 Цикл    
				Валюта = ТЗ.Валюта;
				РубОстаток = ТЗ.Остаток;
				ВалОстаток = ТЗ.ВалОстаток;
				Если ТЗ.Валюта.Выбран() = 0 Тогда
					Если ДатаЛиста >= НачДата Тогда
						Т.ВывестиСекцию("Остаток|Отчет");
						Т.ПрисоединитьСекцию("Остаток|Отчет");
					КонецЕсли;
					СтрокШапки = СтрокШапки + 1;
				Иначе
					Если (РубОстаток <> 0) Или (ВалОстаток <> 0) Тогда
						Если ДатаЛиста >= НачДата Тогда
							Т.ВывестиСекцию("ВалОстаток|Отчет");
							Т.ПрисоединитьСекцию("ВалОстаток|Отчет");
						КонецЕсли;
						СтрокШапки = СтрокШапки + 2;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Документы.УдалитьВсе();
		Валюты.УдалитьВсе();
		Если (ПоДокументам = 1) И (Запрос.День = ДатаЛиста) Тогда
			Пока Запрос.Группировка("Документ") = 1 Цикл
				Док = Запрос.Документ;
				Документы.ДобавитьЗначение(Док);
				Если (Док.Вид()="ПриходныйОрдер") или (Док.Вид()="РасходныйОрдер") Тогда
					Если Док.Валютный = 2 Тогда
						Если Валюты.НайтиЗначение(Док.Валюта) = 0 Тогда
							Валюты.ДобавитьЗначение(Док.Валюта);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
		    КонецЦикла;
		КонецЕсли;
		
		СумПриход = 0;
		СумРасход = 0;
		ПерваяСтрока = 1;
		ТЗ.ВыбратьСтроки();
		Пока ТЗ.ПолучитьСтроку() = 1 Цикл
			Если ТЗ.Валюта.Выбран() = 0 Тогда
				Продолжить;
			КонецЕсли;

			Если ДатаЛиста <> КонМесяца(ДатаЛиста) Тогда
				Если Валюты.НайтиЗначение(ТЗ.Валюта) = 0 Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Курс = ТЗ.Валюта.Курс.Получить(ДатаЛиста);
			Кратность = ТЗ.Валюта.Кратность.Получить(ДатаЛиста);
			Кратность = ?(Кратность=0, 1, Кратность);
			КурсоваяРазница = Окр(ТЗ.ВалОстаток*Курс/Кратность-ТЗ.Остаток, 2, 1);
			Если КурсоваяРазница <> 0 Тогда
				Если ПерваяСтрока = 1 Тогда
					Если ДатаЛиста >= НачДата Тогда
						Т.ВывестиСекцию("КурсовыеРазницы|Отчет");
						Т.ПрисоединитьСекцию("КурсовыеРазницы|Отчет");
					КонецЕсли;
					СтрокШапки = СтрокШапки + 2;
					ПерваяСтрока = 0;
				КонецЕсли;
				Валюта = ТЗ.Валюта;
				Приход = 0;
				Расход = 0;
				КоррСчет = СчетКурсовыхРазниц;
				Если КурсоваяРазница > 0 Тогда
					Приход = КурсоваяРазница;
					СумПриход = СумПриход+КурсоваяРазница;
				Иначе
					Расход = -КурсоваяРазница;
					СумРасход = СумРасход-КурсоваяРазница;
				КонецЕсли;   
				ТЗ.Приход = ТЗ.Приход+Приход;
				ТЗ.Расход = ТЗ.Расход+Расход;
				Если ДатаЛиста >= НачДата Тогда
					Т.ВывестиСекцию("КурсовыеРазницыПоВалюте|Отчет");
					Т.ПрисоединитьСекцию("КурсовыеРазницыПоВалюте|Отчет");
				КонецЕсли;
				СтрокШапки = СтрокШапки + 1;
			КонецЕсли;
		КонецЦикла;
		          
		// Для простоты настройки печатной формы примем следующие соглашения:
		//	-	высота строк в таблице печатной формы задана жестко,
		//		тогда известно, сколько строк помещается на странице;
		СтрокНаСтранице = 58;
		//	-	высота шапки и подвала задана жестко и кратна высоте строк таблицы,
		//		тогда можно указать, сколько строк занимают шапка и подвал
		//		в пересчете на строки таблицы;
		//  -   Количество строк шапки определено при выводе валюты в шапку;
		//  -   Количество строк подвала определим позже по количеству валют;
		//	-	для нормальной работы алгоритма необходимо, чтобы шапка и подвал
		//		могли поместиться на одной странице + хотя бы одна строка таблицы:
		//		СтрокНаСтранице >= СтрокШапки + СтрокПодвала + 1
		//	-	если подвал не помещается на странице, он переносится на другую
		//		страницу с последней строкой; исключение составляет случай,
		//		когда в таблице всего одна строка.
		
		// Резервирование строк для вывода сведений о валюте в подвале.
		// Найдем количество валют, по которым будет остаток в конце дня.
		КоличествоВалютВПодвале = ТЗ.КоличествоСтрок() - 1; // без рублей
		// Добавим валюты, по которым нет итогов, но по которым есть оборот,
		// введенный документами.
		Для А = 1 по Валюты.РазмерСписка() Цикл
		    НомСтроки = 0;
		    Если ТЗ.НайтиЗначение(Валюты.ПолучитьЗначение(А), НомСтроки, "Валюта") = 0 Тогда
				КоличествоВалютВПодвале = КоличествоВалютВПодвале + 1;
			КонецЕсли;
		КонецЦикла;
		// По каждой валюте добавляется:
		//  2 строки для оборота;
		//  2 строки для остатка;
		//  1 строка под курсовую разницу.
		// Для рублей добавляется:
		//  1 строка для оборота;
		//  1 строка для остатка.
		// Добавляет строка для секции "КурсоваяРазница" и "ВТомЧисле".
		СтрокПодвала = СтрокПодвала + ?(КоличествоВалютВПодвале > 0, КоличествоВалютВПодвале * 5 + 4, 0);
		
		// Резервирование строк подвала для секции "ЛистовЗаМесяц" и "ЛистовЗаГод".
		СтрокПодвала = СтрокПодвала + ?(ПоследнийЛист > 1, ПоследнийЛист, 0);
		
		// Если ПереноситьПоследнююСтроку = 1 - переносить,
		// если ПереноситьПоследнююСтроку = 0 - не надо переносить:
		Если (Документы.РазмерСписка()*ВысотаСтроки) <= (СтрокНаСтранице - СтрокШапки - СтрокПодвала) Тогда
			ПереноситьПоследнююСтроку = 0;
		Иначе
			ЦелыхСтраницСПодвалом = Цел(((СтрокШапки-6)+Документы.РазмерСписка()*ВысотаСтроки+СтрокПодвала-1)/(СтрокНаСтранице-6));
			ЦелыхСтраницБезПодвала = Цел(((СтрокШапки-6)+Документы.РазмерСписка()*ВысотаСтроки-1)/(СтрокНаСтранице-6));
			ПереноситьПоследнююСтроку = ЦелыхСтраницСПодвалом - ЦелыхСтраницБезПодвала;
		КонецЕсли;
		
		КоличествоСтраниц = 1;
		Для А=1 По Документы.РазмерСписка() Цикл
			Док = Документы.ПолучитьЗначение(А);
			Если ((Док.Вид()="ПриходныйОрдер") или (Док.Вид()="РасходныйОрдер")) Тогда
				ЕстьВалюта=?(Док.Валютный = 2,1,0);
			Иначе
				ЕстьВалюта=0;
			КонецЕсли;
			Если ЕстьВалюта = 1 Тогда
				Валюта = Док.Валюта;
				Курс = Валюта.Курс.Получить(Док.ДатаДок);
				Кратность = Валюта.Кратность.Получить(Док.ДатаДок);
				Кратность = ?(Кратность=0,1,Кратность);
			Иначе
				Валюта = ПолучитьПустоеЗначение("Справочник.Валюты");
				Курс = 1;
				Кратность = 1;
			КонецЕсли;
				
			Если (Док.Вид() = "РасходныйОрдер") или (Док.Вид() = "РасходныйОрдерТорг") Тогда
				Клиент = "Выдано " + СокрЛП(Док.Выдать) + ?(ВыводитьОснования = 1, " " + СокрЛП(Док.Основание), "");
				КоличествоРасходныхДокументов = КоличествоРасходныхДокументов + 1;
				ВалРасход = Док.Сумма;
				Расход = Окр(Док.Сумма*Курс/Кратность,2,1);
				ВалПриход = 0;
				Приход = 0;
			Иначе
				Клиент = "Принято от " + СокрЛП(Док.ПринятоОт) + ?(ВыводитьОснования = 1, " " + СокрЛП(Док.Основание), "");
				КоличествоПриходныхДокументов = КоличествоПриходныхДокументов + 1;
				ВалПриход = Док.Сумма;
				Приход = Окр(Док.Сумма*Курс/Кратность,2,1);
				ВалРасход = 0;
				Расход = 0;
			КонецЕсли;   
			
			НомерДокПечатнойФормы = глПреобразоватьНомерДок(Док.НомерДок, 0, 0);
			КоррСчет = Док.КоррСчет;
			
			// Начинаем новую страницу, если предыдущая строка была последней на странице
			// или пора переносить последнюю строку на последнюю страницу с подвалом.
			ЦелаяСтраница = Цел(((СтрокШапки-6)+(КоличествоСтраниц - 1)+(А*ВысотаСтроки)-1)/(СтрокНаСтранице-6));
			Если (ЦелаяСтраница = КоличествоСтраниц) или 
				 ((ПереноситьПоследнююСтроку = 1) и (А = Документы.РазмерСписка())) Тогда
				ПриходЗаДень = ТЗ.Итог("Приход");
				РасходЗаДень = ТЗ.Итог("Расход");
				// Остаток = Остаток+ПриходЗаДень-РасходЗаДень;
				Если ДатаЛиста >= НачДата Тогда
					Т.ВывестиСекцию("Перенос|Отчет");
					Т.ПрисоединитьСекцию("Перенос|Отчет");
				КонецЕсли;
				НомерЛиста = НомерЛиста + 1;
				КоличествоСтраниц = КоличествоСтраниц + 1;
				ЛистовЗаГод = ЛистовЗаГод + 1;
				Если ДатаЛиста >= НачМесяца(КонДата) Тогда
					ЛистовЗаМесяц = ЛистовЗаМесяц + 1;        
				КонецЕсли;
				Если ДатаЛиста >= НачДата Тогда
					Т.НоваяСтраница();
					Т.ВывестиСекцию("Шапка|Отчет");
					Т.ПрисоединитьСекцию("Шапка|Отчет");
				КонецЕсли;
			КонецЕсли;
			
			Если ДатаЛиста >= НачДата Тогда
				Если ЕстьВалюта = 1 Тогда
					Если ВыводитьОснования = 1 Тогда
						Т.ВывестиСекцию("СтрокаВалШирокая|Отчет");
						Т.ПрисоединитьСекцию("СтрокаВалШирокая|Отчет");
					Иначе
						Т.ВывестиСекцию("СтрокаВал|Отчет");
						Т.ПрисоединитьСекцию("СтрокаВал|Отчет");
					КонецЕсли;
				Иначе
					Если ВыводитьОснования = 1 Тогда
						Т.ВывестиСекцию("СтрокаШирокая|Отчет");
						Т.ПрисоединитьСекцию("СтрокаШирокая|Отчет");
					Иначе
						Т.ВывестиСекцию("Строка|Отчет");
						Т.ПрисоединитьСекцию("Строка|Отчет");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		             
			ДобавитьОбороты(ТЗ, Валюта, ВалПриход, Приход, ВалРасход, Расход);
		КонецЦикла;
		
		ПриходЗаДень = ТЗ.Итог("Приход");
		РасходЗаДень = ТЗ.Итог("Расход");
		Если ДатаЛиста >= НачДата Тогда
			Т.ВывестиСекцию("Оборот|Отчет");
			Т.ПрисоединитьСекцию("Оборот|Отчет");
			ПоВалютам = 0;
			ТЗ.ВыбратьСтроки();
			Пока ТЗ.ПолучитьСтроку() = 1 Цикл    
				Если ((ТЗ.ВалПриход <> 0) Или (ТЗ.Приход <> 0) Или 
					  (ТЗ.ВалРасход <> 0) Или (ТЗ.Расход <> 0)) И
					 (ТЗ.Валюта.Выбран() = 1) Тогда
					ПоВалютам = 1;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ПоВалютам = 1 Тогда
				ТЗ.ВыбратьСтроки();
				Пока ТЗ.ПолучитьСтроку() = 1 Цикл
					Валюта = ТЗ.Валюта;
					ВалПриходЗаДень = ТЗ.ВалПриход;
					РубПриходЗаДень = ТЗ.Приход;
					ВалРасходЗаДень = ТЗ.ВалРасход;
					РубРасходЗаДень = ТЗ.Расход;
					Если ТЗ.Валюта.Выбран() = 0 Тогда
						Т.ВывестиСекцию("ОборотРуб|Отчет");
						Т.ПрисоединитьСекцию("ОборотРуб|Отчет");
					Иначе           
						Если (ВалПриходЗаДень <> 0) Или (РубПриходЗаДень <> 0) Или 
							 (ВалРасходЗаДень <> 0) Или (РубРасходЗаДень <> 0) Тогда
							Т.ВывестиСекцию("ОборотВал|Отчет");
							Т.ПрисоединитьСекцию("ОборотВал|Отчет");
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		ПерваяСтрока = 1;
		Остаток = Остаток+ПриходЗаДень-РасходЗаДень;
		ТЗ.ВыбратьСтроки();
		Пока ТЗ.ПолучитьСтроку() = 1 Цикл
			Если ТЗ.Валюта.Выбран() = 0 Тогда
				Продолжить;
			КонецЕсли;     
			               
			Если ДатаЛиста <> КонМесяца(ДатаЛиста) Тогда
				Если Валюты.НайтиЗначение(ТЗ.Валюта) = 0 Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Курс = ТЗ.Валюта.Курс.Получить(ДатаЛиста);
			Кратность = ТЗ.Валюта.Кратность.Получить(ДатаЛиста);
			Кратность = ?(Кратность=0, 1, Кратность);
			КурсоваяРазница = Окр((ТЗ.ВалОстаток+ТЗ.ВалПриход-ТЗ.ВалРасход)*Курс/Кратность, 2, 1)-(ТЗ.Остаток+ТЗ.Приход-ТЗ.Расход);
			Если КурсоваяРазница <> 0 Тогда
				Если ПерваяСтрока = 1 Тогда
					Если ДатаЛиста >= НачДата Тогда
						Т.ВывестиСекцию("КурсоваяРазница|Отчет");
						Т.ПрисоединитьСекцию("КурсоваяРазница|Отчет");
					КонецЕсли;
					ПерваяСтрока = 0;
				КонецЕсли;
				Валюта = ТЗ.Валюта;
				ПриходЗаДень = 0;
				РасходЗаДень = 0;
				Если КурсоваяРазница > 0 Тогда
					ПриходЗаДень = КурсоваяРазница;
					ТЗ.Приход = ТЗ.Приход + КурсоваяРазница;
				Иначе
					РасходЗаДень = -КурсоваяРазница;
					ТЗ.Расход = ТЗ.Расход - КурсоваяРазница;
				КонецЕсли;
				Если ДатаЛиста >= НачДата Тогда
					Т.ВывестиСекцию("КурсоваяРазницаПоВалюте|Отчет");
					Т.ПрисоединитьСекцию("КурсоваяРазницаПоВалюте|Отчет");
				КонецЕсли;
				Остаток = Остаток + КурсоваяРазница;
			КонецЕсли;
		КонецЦикла;              
		
		Если ДатаЛиста >= НачДата Тогда
			Т.ВывестиСекцию("КонечныйОстаток|Отчет");
			Т.ПрисоединитьСекцию("КонечныйОстаток|Отчет");
		
			ПоВалютам = 0;
			ТЗ.ВыбратьСтроки();
			Пока ТЗ.ПолучитьСтроку() = 1 Цикл    
				Если ((ТЗ.Остаток+ТЗ.Приход-ТЗ.Расход <> 0) Или (ТЗ.ВалОстаток+ТЗ.ВалПриход-ТЗ.ВалРасход <> 0)) И (ТЗ.Валюта.Выбран() = 1) Тогда
					ПоВалютам = 1;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ПоВалютам = 1 Тогда
				Т.ВывестиСекцию("ВТомЧисле|Отчет");
				Т.ПрисоединитьСекцию("ВТомЧисле|Отчет");
				ТЗ.ВыбратьСтроки();
				Пока ТЗ.ПолучитьСтроку() = 1 Цикл    
					Валюта = ТЗ.Валюта;
					РубОстаток = ТЗ.Остаток+ТЗ.Приход-ТЗ.Расход;
					ВалОстаток = ТЗ.ВалОстаток+ТЗ.ВалПриход-ТЗ.ВалРасход;
					Если ТЗ.Валюта.Выбран() = 0 Тогда
						Т.ВывестиСекцию("Остаток|Отчет");
						Т.ПрисоединитьСекцию("Остаток|Отчет");
					Иначе
						Если (РубОстаток <> 0) Или (ВалОстаток <> 0) Тогда
							Т.ВывестиСекцию("ВалОстаток|Отчет");
							Т.ПрисоединитьСекцию("ВалОстаток|Отчет");
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;

			Т.ВывестиСекцию("Подвал|Отчет");
			Т.ПрисоединитьСекцию("Подвал|Отчет");
		КонецЕсли;
			             
		Ошибка = 0;
		Сортировать = 0;
		Ит.ВыбратьВалюты();
		Пока Ит.ПолучитьВалюту() = 1 Цикл
			Если Ит.Валюта.Выбран() = 1 Тогда
				Стр = 0;
				Если ТЗ.НайтиЗначение(Ит.Валюта, Стр, "Валюта") = 0 Тогда
					ТЗ.НоваяСтрока();
					ТЗ.Валюта = Ит.Валюта;
					Сортировать = 1;
				Иначе
					ТЗ.ПолучитьСтрокуПоНомеру(Стр);
				КонецЕсли;
				Если (ТЗ.ВалОстаток+ТЗ.ВалПриход-ТЗ.ВалРасход <> Ит.СКД("В")) Или
				     (ТЗ.Остаток+ТЗ.Приход-ТЗ.Расход <> Ит.СКД("С")) Тогда
					Ошибка = 1;
				КонецЕсли;
				ТЗ.ВалОстаток = Ит.СКД("В");
				ТЗ.Остаток = Ит.СКД("С");   
			КонецЕсли;
		КонецЦикла;
		
		Стр = 0;
		Если ТЗ.НайтиЗначение(ПолучитьПустоеЗначение("Справочник.Валюты"), Стр, "Валюта") = 0 Тогда
			ТЗ.НоваяСтрока();
			ТЗ.Валюта = ПолучитьПустоеЗначение("Справочник.Валюты");
			Сортировать = 1;
		Иначе
			ТЗ.ПолучитьСтрокуПоНомеру(Стр);
			ТЗ.Остаток = 0;
		КонецЕсли;
		
		Если (ДатаЛиста = Ит.НачДата) Тогда
			ТЗ.Остаток = Ит.СКД("С")-ТЗ.Итог("Остаток");
			Если Остаток <> Ит.СКД("С") Тогда
				Ошибка = 1;
			КонецЕсли;
			
		Иначе
			ТЗ.Остаток = Ит.СНД("С")-ТЗ.Итог("Остаток");
			Если Остаток <> Ит.СНД("С") Тогда
				Ошибка = 1;
			КонецЕсли;    
		КонецЕсли;
		
		Если Ошибка = 1 Тогда
			Сообщить("Обороты по документам и проводкам за "+ДатаЛиста+" не совпадают!");
		КонецЕсли;
		
		Если Сортировать = 1 Тогда
			ТЗ.Сортировать("Валюта");
		КонецЕсли;
		ТЗ.Заполнить(0,,, "ВалПриход, Приход, ВалРасход, Расход");
		
		Если (ПоПроводкам = 1) И (ДатаЛиста = Ит.НачДата) Тогда
			ПоПроводкам = Ит.ПолучитьПериод();
		КонецЕсли;
		Если (ПоДокументам = 1) И (ДатаЛиста = Запрос.День) Тогда
			ПоДокументам = Запрос.Группировка("День");
		КонецЕсли;
		Если (ПоПроводкам = 1) или (ПоДокументам = 1) Тогда
		    Т.НоваяСтраница();
		КонецЕсли;
		ВывестиПодвал = 1;
		Если Константа.НомерЛистаКассовойКниги.Получить(ДатаЛиста) <> НомерЛиста Тогда
			Константа.НомерЛистаКассовойКниги.Установить(ДатаЛиста, НомерЛиста);
		КонецЕсли;
	КонецЦикла;

	Если ВывестиПодвал = 1 Тогда
		Д = КонДата;
		Если (КонМесяца(ДатаЛиста) = ДатаЛиста) Или
			 (НачМесяца(ДатаЛиста) <> НачМесяца(КонДата)) Или
			 (ПоследнийЛист <> 1) Тогда
			Т.ВывестиСекцию("ЛистовЗаМесяц|Отчет");
			Т.ПрисоединитьСекцию("ЛистовЗаМесяц|Отчет");
		КонецЕсли;
		Если (КонГода(ДатаЛиста) = ДатаЛиста) Или 
			 (НачГода(ДатаЛиста) <> НачГода(КонДата)) Или 
			 (ПоследнийЛист = 3)	Тогда
			Т.ВывестиСекцию("ЛистовЗаГод|Отчет");
			Т.ПрисоединитьСекцию("ЛистовЗаГод|Отчет"); 
			
			// Печать обложки и титульного листа
			Т1= СоздатьОбъект("Таблица");
			Т1.ИсходнаяТаблица( "Обложка");
			Т1.ВывестиСекцию("Обложка");
	    	Т1.НоваяСтраница();
	    	Т1.ВывестиСекцию("Обложка");
            // Последний лист кассовой книги
			Т1.НоваяСтраница();
			Т1.ВывестиСекцию("ПослДеньГода");
	        Т1.ТолькоПросмотр(1);
			Т1.Опции(0,0,0,0,"ОпцииПечатиКО_4");
			Т1.ПараметрыСтраницы(1,,,10,10);
			Т1.Показать("Обложка и титульный лист кассовой книги","");
		КонецЕсли;
	КонецЕсли;         

	Т.ТолькоПросмотр(1);
	Т.Опции(0,0,1,0);
	Т.ОбластьПечати(2);
	Если НачДата = КонДата Тогда
		Т.Показать("Касса ("+НачДата+")","");
	Иначе
		Т.Показать("Касса ("+НачДата+"-"+КонДата+")","");
	КонецЕсли;
	
КонецПроцедуры
//_____________________________________________________________________________
Процедура ПриОткрытии()
	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		Если Обновить <> 0 Тогда
			НачДата = глРасшифровка.Получить("НачДата");
			КонДата = глРасшифровка.Получить("КонДата");
			ПоследнийЛист = глРасшифровка.Получить("ПоследнийЛист");
			ВыводитьОснования = глРасшифровка.Получить("ВыводитьОснования");
			Т = глТаблица;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		НачДата = РабочаяДата();
		КонДата = РабочаяДата();
	КонецЕсли;
	Форма.Кн_Видеокурс.Видимость(?(ТипЗначения(Видео_Компонента) = 0, 0, 1));
	ПоследнийЛист = 1;
КонецПроцедуры