
//******************************************************************************
// ПроверкаПериода(НачДата)
//
// Возвращаемое значение:
//  1 - корректно выбран период в диалоге
//  0 - не корректно выбран период в диалоге
//
// Описание:
// Функция проверяет корректность ввода интервала дат в дилоге
//
Функция ПроверкаПериода(НачДата)
	
	Если (НачДата > КонДата) Тогда
		Предупреждение("Неправильно дата формирования отчета!"+РазделительСтрок+
		               "Дата формирования меньше даты начала начисления штрафных санкций.", 60);
		Возврат 0;

	КонецЕсли;
	
	Возврат 1;
	
КонецФункции // ПроверкаПериода()

//*****************************************************************************
//
Процедура Сформировать()
	
	НачДата = ВыбрДоговор.ДатаНачалаНачисленияШтрафныхСанкций;
	Если ПустоеЗначение(НачДата) = 1 Тогда
		// отбираем все движения штрафов по данному договору, независимо от того когда они вводились 
		НачДата = Дата("01.01.0000");    
	КонецЕсли;

	Если ПроверкаПериода(НачДата) = 0 Тогда
		Возврат;
	КонецЕсли;

	Если ПустоеЗначение(ВыбрДоговор) = 1 Тогда
	    Предупреждение("Не указан договор!", 60);
		Возврат;
	КонецЕсли;

	СтрНалогоплательщик = Константа.ОфициальноеНазваниеОрганизации;
	Если ПустаяСтрока(СтрНалогоплательщик) = 1 Тогда
		СтрНалогоплательщик = Константа.НазваниеОрганизации;	    
	КонецЕсли;
	ИНН = Константа.ИННОрганизации;
	
	РеквизитыДоговора = "" + глПолноеНаименование(ВыбрКонтрагент) + ", " + ВыбрДоговор;
	
	Если ПустоеЗначение(ВыбрДоговор.ДатаНачалаНачисленияШтрафныхСанкций) = 0 Тогда
	    ДатаНачалаНачисленияШтрафныхСанкций = Формат(ВыбрДоговор.ДатаНачалаНачисленияШтрафныхСанкций, "Д ДДММММГГГГ");
	Иначе
		ДатаНачалаНачисленияШтрафныхСанкций = "";
	КонецЕсли;

	Если ПустоеЗначение(ВыбрДоговор.ДатаПрекращенияНачисленияШтрафныхСанкций) = 0 Тогда
	    ДатаПрекращенияНачисленияШтрафныхСанкций = Формат(ВыбрДоговор.ДатаПрекращенияНачисленияШтрафныхСанкций, "Д ДДММММГГГГ");
	Иначе
		ДатаПрекращенияНачисленияШтрафныхСанкций = "";
	КонецЕсли;
	
	Таб = СоздатьОбъект("Таблица"); 
	Таб.ВывестиСекцию("Шапка");
	
	СуммаДохода = 0;
	СуммаРасхода = 0;
	
	Если глНовыеПравилаВеденияНУ(КонДата) = 1 Тогда

		// ФОРМИРОВАНИЕ С УЧЕТОМ НОВЫХ ПРАВИЛ НАЛОГОВОГО УЧЕТА РЕДАКЦИИ 4.4

		ТабРасчет = СоздатьОбъект("ТаблицаЗначений");
		ТабРасчет.НоваяКолонка("ТекДок");
		ТабРасчет.НоваяКолонка("ПризнакДоходаРасхода");
		ТабРасчет.НоваяКолонка("Период"); 
		ТабРасчет.НоваяКолонка("База");
		ТабРасчет.НоваяКолонка("Ставка");
		ТабРасчет.НоваяКолонка("ВременнаяЕдиницаРасчета");
		ТабРасчет.НоваяКолонка("Сумма");
		
		БухИт = СоздатьОбъект("БухгалтерскиеИтоги");   	

		Для т = 1 По 2 Цикл	
			БухИт.ИспользоватьПланСчетов(ОсновнойПланСчетов());
			Если т =1 Тогда
				// ВНЕРЕАЛИЗАЦИОННЫЕ ДОХОДЫ
				БухИт.ИспользоватьСубконто(ВидыСубконто.ВнереализационныеДоходы, Перечисление.ВнереализационныеДоходы.Штрафы, 2);
				БухИт.ВыполнитьЗапрос(НачДата, КонДата, "Н08",,, 2, "Проводка", "С");
			Иначе
				// ВНЕРЕАЛИЗАЦИОННЫЕ РАСХОДЫ
				БухИт.ИспользоватьСубконто(ВидыСубконто.ВнереализационныеРасходы, Перечисление.ВнереализационныеРасходы.Штрафы, 2);
				БухИт.ВыполнитьЗапрос(НачДата, КонДата, "Н09",,, 2, "Проводка", "С");
			КонецЕсли;
	
			БухИт.ВыбратьПериоды();
			Пока БухИт.ПолучитьПериод() = 1 Цикл
				Опер = БухИт.Операция;
	            
				Если (Опер.Документ.Выбран() = 1)
				   и (Опер.Документ.Вид() = "НачислениеШтрафныхСанкций") Тогда
					// Штраф начислен при помощи документа "Начисление штрафных санкций"
					ДокНачШтраф = Опер.Документ.ТекущийДокумент();
					ДокНачШтраф.ПолучитьСтрокуПоНомеру(Опер.НомерСтрокиДокумента());
					
					Если ДокНачШтраф.Договор <> ВыбрДоговор Тогда
						// фильтруем по договорам
						Продолжить;    
					КонецЕсли;
					
					ТабРасчет.НоваяСтрока();
					ТабРасчет.ПризнакДоходаРасхода = ДокНачШтраф.ПризнакДоходаРасхода;
					ТабРасчет.Период = ПериодСтр(ДокНачШтраф.НачалоПериода, ДокНачШтраф.ОкончаниеПериода);
			        ТабРасчет.База = ФорматС(ДокНачШтраф.База);
					ТабРасчет.Ставка = ФорматС(ДокНачШтраф.Ставка);
					ТабРасчет.ВременнаяЕдиницаРасчета = ДокНачШтраф.ВременнаяЕдиницаРасчета;
					ТабРасчет.Сумма = ФорматС(ДокНачШтраф.Сумма);
	
				Иначе

			        Если т = 1 Тогда
						// Доходы по штрафам, отраженные ручными операциями невозможно отбирать по конкрентому договору
						// (на счете Н08 нет соответствующего субконто)
						Продолжить;
						
					ИначеЕсли Опер.Дебет.Основание = ВыбрДоговор Тогда
						ТабРасчет.НоваяСтрока();
						ТабРасчет.ПризнакДоходаРасхода = Перечисление.ПризнакДоходаРасхода.Расход;

					Иначе
						// Договор указанный в аналитике не соответствует отбираемому договору
						Продолжить;
						
					КонецЕсли;
					// В проводках не содержится никаких данных о расчете штрафа кроме его суммы
					ТабРасчет.Сумма = ФорматС(Опер.Сумма);
				   	
				КонецЕсли;
	
				ТабРасчет.ТекДок = Опер.Документ.ТекущийДокумент();
				Если т = 1 Тогда
					СуммаДохода = СуммаДохода + Опер.Сумма;
				Иначе
					СуммаРасхода = СуммаРасхода + Опер.Сумма;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;

		ТабРасчет.Сортировать("ТекДок");

		ТабРасчет.ВыбратьСтроки();
		Пока ТабРасчет.ПолучитьСтроку() = 1 Цикл
			ПризнакДоходаРасхода = ТабРасчет.ПризнакДоходаРасхода;
			Период = ТабРасчет.Период; 
			База = ТабРасчет.База;
			Ставка = ТабРасчет.Ставка;
			ВременнаяЕдиницаРасчета = ТабРасчет.ВременнаяЕдиницаРасчета;
			Сумма = ТабРасчет.Сумма;
			ТекДок = ТабРасчет.ТекДок;
			
			Таб.ВывестиСекцию("Строка");
		КонецЦикла;
	
	Иначе

		// ФОРМИРОВАНИЕ С УЧЕТОМ ПРАВИЛ НАЛОГОВОГО УЧЕТА, ДЕЙСТВОВАВШИХ ДО РЕДАКЦИИ 4.4

		Запрос = СоздатьОбъект("Запрос");
		ТекстЗапроса = 
		"//{{ЗАПРОС(Сформировать)
		|Обрабатывать НеПомеченныеНаУдаление;
		|Период с НачДата по КонДата;
		|
		|Контрагент = Документ.НачислениеШтрафныхСанкций.Контрагент;
		|Договор = Документ.НачислениеШтрафныхСанкций.Договор;
		|НачалоПериода = Документ.НачислениеШтрафныхСанкций.НачалоПериода;
		|ОкончаниеПериода = Документ.НачислениеШтрафныхСанкций.ОкончаниеПериода;
		|ПризнакДоходаРасхода = Документ.НачислениеШтрафныхСанкций.ПризнакДоходаРасхода;
		|База = Документ.НачислениеШтрафныхСанкций.База;
		|Ставка = Документ.НачислениеШтрафныхСанкций.Ставка;
		|ВременнаяЕдиницаРасчета = Документ.НачислениеШтрафныхСанкций.ВременнаяЕдиницаРасчета;
		|Сумма = Документ.НачислениеШтрафныхСанкций.Сумма;
		|
		|Группировка Документ; 
		|Группировка СтрокаДокумента; 
		|
		|Условие (Контрагент = ВыбрКонтрагент);
		|Условие (Договор = ВыбрДоговор);
		|
		|"//}}ЗАПРОС
		;
	
		Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
			Возврат;
		КонецЕсли;
	
		Состояние("Заполнение выходной таблицы...");
	
		Пока Запрос.Группировка("Документ") = 1 Цикл
			Пока Запрос.Группировка("СтрокаДокумента") = 1 Цикл
				ПризнакДоходаРасхода = Запрос.ПризнакДоходаРасхода;
				Период = ПериодСтр(Запрос.НачалоПериода, Запрос.ОкончаниеПериода);
				База = ФорматС(Запрос.База);
				Ставка = ФорматС(Запрос.Ставка);
				ВременнаяЕдиницаРасчета = Запрос.ВременнаяЕдиницаРасчета;
				Сумма = ФорматС(Запрос.Сумма);
				Таб.ВывестиСекцию("Строка"); 
	
				Если ПризнакДоходаРасхода = Перечисление.ПризнакДоходаРасхода.Доход Тогда
					СуммаДохода = СуммаДохода + Запрос.Сумма;
	
				ИначеЕсли ПризнакДоходаРасхода = Перечисление.ПризнакДоходаРасхода.Расход Тогда
					СуммаРасхода = СуммаРасхода + Запрос.Сумма;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;

	КонецЕсли;

	СуммаДохода = ФорматС(СуммаДохода);
	СуммаРасхода = ФорматС(СуммаРасхода);
                              
	ОтветственныйЗаСоставление = ФИО(Константа.ОтветственныйЗаСоставлениеРегистровНУ);
	
	Таб.ВывестиСекцию("Подвал");
	Таб.ТолькоПросмотр(1);
	Таб.ОбластьПечати(,2,,);
	Таб.Опции(0, 0, 0, 0, "ПечатьРегистрУчетаРасчетовПоШтрафнымСанкциям", "ОкноРегистрУчетаРасчетовПоШтрафнымСанкциям");
	Таб.Показать("Регистр учета расчетов по штрафным санкциям");

КонецПроцедуры // Сформировать()

//*****************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()

	КонДата = КонКвартала(РабочаяДата()); 
	
КонецПроцедуры