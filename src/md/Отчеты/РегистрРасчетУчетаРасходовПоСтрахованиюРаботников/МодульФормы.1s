////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ МОДУЛЯ
//
//******************************************************************************
// ПроверкаПериода()
//
// Возвращаемое значение:
//  1 - на указанную дату итоги рассчитаны
//  0 - на указанную дату итоги не рассчитаны
//
// Описание:
// Функция проверяет рассчитаны ли итоги за заданный период
//
Функция ПроверкаПериода()
	
	Если КонМесяца(КонДата) > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("На указанную дату итоги не рассчитаны!"+РазделительСтрок+
		"Расчет итогов выполняется в режиме"+РазделительСтрок+
		"""Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	
	Возврат 1;
	
КонецФункции // ПроверкаПериода()

//******************************************************************************
// ПолучитьСуммуРасходовНаОплатуТрудаСНачалаГода()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Число - сумма расходов на оплату труда,
//  которая принимается к налоговому учету.
//
// Описание:
//  Рассчитывается сумма расходов на оплату труда
//  исключая суммы расходов, которые не принимаются к налоговому учету.
//
Функция ПолучитьСуммуРасходовНаОплатуТрудаСНачалаГода(ДатаКонцаРасчета)
	
	БухИт = СоздатьОбъект("БухгалтерскиеИтоги");
	БухИт.ПериодМНГ(ДатаКонцаРасчета);
	
	СуммаРасходовНаОплатуТруда = БухИт.КО("Н14") + БухИт.КО("Н04.10");
	
	// Из общей суммы расходов необходимо исключить
	// расходы, не учитываемые в целях налогообложения.
	БухИт.ИспользоватьКорСубконто(ВидыСубконто.ГруппыВидыРасходов, Перечисление.ГруппыВидыРасходов.НеПринимаемые, 2);
	Если БухИт.ВыполнитьЗапрос(НачГода(ДатаКонцаРасчета), ДатаКонцаРасчета, "Н14, Н04.10", "Н07.04",, 2,,) = 1 Тогда
		СуммаРасходовНаОплатуТруда = СуммаРасходовНаОплатуТруда - БухИт.КорКО();    
	КонецЕсли;
	
	Возврат СуммаРасходовНаОплатуТруда;
	
КонецФункции // ПолучитьСуммуРасходовНаОплатуТрудаСНачалаГода()

//******************************************************************************
// РаспределитьРасходы(БухИтН03.КО(), КоэффициентРаспределения, ТаблицаРаспределения, "СуммаРасходовПоДолгосрочномуСтрахованию")
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается процедура.
//
Процедура РаспределитьРасходы(СуммаРасходов, КоэффициентРаспределения, ТаблицаРаспределения, НаименованиеКолонки)
	
	Если СуммаРасходов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СуммаРасходовАУП   = СуммаРасходов * КоэффициентРаспределения;
	СуммаРасходовНеАУП = СуммаРасходов - СуммаРасходовАУП;
	
	Если СуммаРасходовАУП <> 0 Тогда
		ТаблицаРаспределения.НоваяСтрока();
		ТаблицаРаспределения.ВидПерсонала = "АУП";
		ТаблицаРаспределения.УстановитьЗначение(ТаблицаРаспределения.НомерСтроки, НаименованиеКолонки, СуммаРасходовАУП);
	КонецЕсли;
		
	Если СуммаРасходовНеАУП <> 0 Тогда
		ТаблицаРаспределения.НоваяСтрока();
		ТаблицаРаспределения.ВидПерсонала = "Прочие";
		ТаблицаРаспределения.УстановитьЗначение(ТаблицаРаспределения.НомерСтроки, НаименованиеКолонки, СуммаРасходовНеАУП);
	КонецЕсли;
	
КонецПроцедуры // РаспределитьРасходы()
//******************************************************************************
//
Процедура Сформировать()                       
	
	Перем ТаблицаСотрудников;
	
	СчН03_05 = СчетПоКоду("Н03.05");
	СчН03_06 = СчетПоКоду("Н03.06");
	СчН03_07 = СчетПоКоду("Н03.07");
	СчН07_04 = СчетПоКоду("Н07.04.1"); 
	СчН01_06 = СчетПоКоду("Н01.06"); 
	СчН01_05 = СчетПоКоду("Н01.05");  
	
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Таб = СоздатьОбъект("Таблица"); 
	СтрНалогоплательщик = Константа.ОфициальноеНазваниеОрганизации;
	Если ПустаяСтрока(СтрНалогоплательщик) = 1 Тогда
		СтрНалогоплательщик = Константа.НазваниеОрганизации;	    
	КонецЕсли;
	ИНН = Константа.ИННОрганизации;
	Таб.ВывестиСекцию("Шапка");
	
	//Формируем таблицу распределения нарастающим итогом с начала года
	НачДата = НачГода(КонДата);
	ДатаФормированияЗаписи = КонМесяца(НачДата);
	
	БухИт    = СоздатьОбъект("БухгалтерскиеИтоги");
	БухИтН03 = СоздатьОбъект("БухгалтерскиеИтоги");
	
	ТаблицаРаспределения = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаРаспределения.НоваяКолонка("ВидПерсонала");
	ТаблицаРаспределения.НоваяКолонка("СуммаРасходовПоДолгосрочномуСтрахованию", "Число");
	ТаблицаРаспределения.НоваяКолонка("СуммаРасходовПоСтрахованиюНаОплатуМедицинскихРасходов", "Число");
	ТаблицаРаспределения.НоваяКолонка("СуммаРасходовПоСтрахованиюНаСлучайСмерти", "Число");
	
	Пока ДатаФормированияЗаписи <= КонДата Цикл 
		
		ТаблицаРаспределения.УдалитьСтроки();
		
		БухИт.ПериодМНГ(ДатаФормированияЗаписи);
		
		СуммаРасходовНаОплатуТруда = ПолучитьСуммуРасходовНаОплатуТрудаСНачалаГода(ДатаФормированияЗаписи);
		
		CуммаПлатежаПоДолгосрочномуСтрахованиюВПределахУстановленныхНорм = 0.12 * СуммаРасходовНаОплатуТруда;
		ФактическаяCуммаПлатежаПоДолгосрочномуСтрахованию = БухИт.ДО(СчН03_05);
		CуммаПлатежаПоДолгосрочномуСтрахованию = БухИт.ОБ(СчН07_04, СчН03_05) + БухИт.ОБ(СчН01_06, СчН03_05) + БухИт.ОБ(СчН01_05, СчН03_05);
		
		СуммаПлатежаПоСтрахованиюНаОплатуМедицинскихРасходовВПределахУстановленныхНорм = 0.03 * СуммаРасходовНаОплатуТруда;
		ФактическаяСуммаПлатежаПоСтрахованиюНаОплатуМедицинскихРасходов = БухИт.ДО(СчН03_06);
		СуммаПлатежаПоСтрахованиюНаОплатуМедицинскихРасходов = БухИт.ОБ(СчН07_04, СчН03_06) + БухИт.ОБ(СчН01_06, СчН03_06) + БухИт.ОБ(СчН01_05, СчН03_06);
		
		ФактическаяСуммаПлатежаПоСтрахованиюНаСлучайСмерти = БухИт.ДО(СчН03_07);
		СуммаПлатежаПоСтрахованиюНаСлучайСмерти = ФактическаяСуммаПлатежаПоСтрахованиюНаСлучайСмерти;
		
		// Если расходов на страхование не было, то и строк таблицы по этим месяцам не формируем
		Если (ФактическаяCуммаПлатежаПоДолгосрочномуСтрахованию = 0)
			и (ФактическаяСуммаПлатежаПоСтрахованиюНаОплатуМедицинскихРасходов = 0)
			и (ФактическаяСуммаПлатежаПоСтрахованиюНаСлучайСмерти = 0) Тогда
				
			ДатаФормированияЗаписи = КонМесяца(ДобавитьМесяц(ДатаФормированияЗаписи, 1));
		    Продолжить;
		КонецЕсли;
		
		БухИтН03.Опции(1,1);
		БухИтН03.ИспользоватьСубконто(ВидыСубконто.Сотрудники);
		БухИтН03.ВыполнитьЗапрос(НачГода(КонДата), ДатаФормированияЗаписи, "Н03.05, Н03.06, Н03.07","Н01.05, Н01.06, Н07.04",,2,,);
		БухИтН03.ВыбратьСубконто(ВидыСубконто.Сотрудники,, 2);
		Пока БухИтН03.ПолучитьСубконто(ВидыСубконто.Сотрудники) = 1 Цикл
			Сотрудник = БухИтН03.Субконто(ВидыСубконто.Сотрудники);
			СуммаРасходов = БухИтН03.КО();
			Если СуммаРасходов = 0 Тогда
			    Продолжить;
			КонецЕсли;

			ВидПерсонала = Сотрудник.Подразделение.Получить(ДатаФормированияЗаписи).Административное;

			БухИтН03.ВыбратьСчета();
			Пока БухИтН03.ПолучитьСчет() = 1 Цикл
			    Если БухИтН03.Счет = СчН03_05 Тогда
			        РаспределитьРасходы(БухИтН03.КО(), ВидПерсонала, ТаблицаРаспределения, "СуммаРасходовПоДолгосрочномуСтрахованию");
					
				ИначеЕсли БухИтН03.Счет = СчН03_06 Тогда
					РаспределитьРасходы(БухИтН03.КО(), ВидПерсонала, ТаблицаРаспределения, "СуммаРасходовПоСтрахованиюНаОплатуМедицинскихРасходов");
					
				ИначеЕсли БухИтН03.Счет = СчН03_07 Тогда
					РаспределитьРасходы(БухИтН03.КО(), ВидПерсонала, ТаблицаРаспределения, "СуммаРасходовПоСтрахованиюНаСлучайСмерти");
			    КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		ТаблицаРаспределения.Свернуть("ВидПерсонала", "СуммаРасходовПоДолгосрочномуСтрахованию, СуммаРасходовПоСтрахованиюНаОплатуМедицинскихРасходов, СуммаРасходовПоСтрахованиюНаСлучайСмерти");		
		
		ВидПерсонала = "";
		Если ТаблицаРаспределения.КоличествоСтрок() = 1 Тогда
			ТаблицаРаспределения.ПолучитьСтрокуПоНомеру(1);
			ВидПерсонала = ТаблицаРаспределения.ВидПерсонала;
			СуммаРасходовПоДолгосрочномуСтрахованию = ТаблицаРаспределения.СуммаРасходовПоДолгосрочномуСтрахованию;
			СуммаРасходовПоСтрахованиюНаОплатуМедицинскихРасходов = ТаблицаРаспределения.СуммаРасходовПоСтрахованиюНаОплатуМедицинскихРасходов;
			СуммаРасходовПоСтрахованиюНаСлучайСмерти = ТаблицаРаспределения.СуммаРасходовПоСтрахованиюНаСлучайСмерти;
		КонецЕсли;
		Таб.ВывестиСекцию("Строка");
		
		Если ТаблицаРаспределения.КоличествоСтрок() > 1 Тогда
			ТаблицаРаспределения.Сортировать("- ВидПерсонала");
			ТаблицаРаспределения.ВыбратьСтроки();
			Пока ТаблицаРаспределения.ПолучитьСтроку() = 1 Цикл
				ВидПерсонала = ТаблицаРаспределения.ВидПерсонала;
				СуммаРасходовПоДолгосрочномуСтрахованию = ТаблицаРаспределения.СуммаРасходовПоДолгосрочномуСтрахованию;
				СуммаРасходовПоСтрахованиюНаОплатуМедицинскихРасходов = ТаблицаРаспределения.СуммаРасходовПоСтрахованиюНаОплатуМедицинскихРасходов;
				СуммаРасходовПоСтрахованиюНаСлучайСмерти = ТаблицаРаспределения.СуммаРасходовПоСтрахованиюНаСлучайСмерти;
				Таб.ВывестиСекцию("ПоВидуПерсонала");
			КонецЦикла;
		КонецЕсли;
		
		ДатаФормированияЗаписи = КонМесяца(ДобавитьМесяц(ДатаФормированияЗаписи, 1));
	КонецЦикла;
	
	ИтогоПоСтрахованию = ТаблицаРаспределения.Итог("СуммаРасходовПоДолгосрочномуСтрахованию") + 
	                     ТаблицаРаспределения.Итог("СуммаРасходовПоСтрахованиюНаОплатуМедицинскихРасходов") +
						 ТаблицаРаспределения.Итог("СуммаРасходовПоСтрахованиюНаСлучайСмерти");
						 
	ОтветственныйЗаСоставление = ФИО(Константа.ОтветственныйЗаСоставлениеРегистровНУ);
	
	Таб.ВывестиСекцию("Подвал");
	Таб.ТолькоПросмотр(1);
	Таб.ОбластьПечати(,2,,);
	Таб.Опции(0, 0, 0, 0, "ПечатьРегистрРасчетУчетаРасходовПоСтрахованиюРаботников", "ОкноРегистрРасчетУчетаРасходовПоСтрахованиюРаботников");
	Таб.Показать("Регистр-расчет учета расходов по страхованию работников текущего периода");

КонецПроцедуры // Сформировать    

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	КонДата = КонКвартала(РабочаяДата()); 

КонецПроцедуры // ПриОткрытии()