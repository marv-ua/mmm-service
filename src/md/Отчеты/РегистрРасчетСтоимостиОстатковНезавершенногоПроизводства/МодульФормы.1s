//******************************************************************************
// ПриВыбореПериода()
//
// Параметры:
//  Нет.
//
// Вызывается из формул элементов диалога:
//  Да.
//
// Описание:
//  Дата начала формирования регистра - первый день месяца,
//  а дата конца - последняя дата месяца.
//
Процедура ПриВыбореПериода()
	
	НачДата = НачМесяца(НачДата);
	КонДата = КонМесяца(КонДата);
	
КонецПроцедуры // ПриВыбореПериода()
//******************************************************************************
// ПроверкаПериода()
//
// Возвращаемое значение:
//  1 - корректно выбран период в диалоге
//  0 - не корректно выбран период в диалоге
//
// Описание:
// Функция проверяет корректность ввода интервала дат в дилоге и рассчитаны ли итоги за заданный период
//
Функция ПроверкаПериода()
	
	Если НачДата > КонДата Тогда
		Предупреждение("Неправильно задан период формирования отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если КонДата > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		"Расчет итогов выполняется в режиме"+РазделительСтрок+
		"""Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	
	Возврат 1;
	
КонецФункции // ПроверкаПериода()

//******************************************************************************
Процедура Сформировать()                                               
	
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// создание объекта "Таблица"
	Таб = СоздатьОбъект("Таблица");
	Таб.ИспользоватьФормат("Ч017.2.,");
	
	// подготовка реквизитов шапки
	СтрНалогоплательщик = СокрЛП(Константа.ОфициальноеНазваниеОрганизации);
	Если ПустаяСтрока(СтрНалогоплательщик) = 1 Тогда
	СтрНалогоплательщик = СокрЛП(Константа.НазваниеОрганизации);	    
	КонецЕсли;
	ИНН = Константа.ИННОрганизации; 
	
	Таб.ВывестиСекцию("Шапка_1");  
	
	ТабЗатрат = СоздатьОбъект("ТаблицаЗначений");
	ТабЗатрат.НоваяКолонка("ВидЗатрат");
	ТабЗатрат.НоваяКолонка("Сумма", "Число", 12, 2);
	БухИтЗатрат = СоздатьОбъект("БухгалтерскиеИтоги");
	БухИтЗатрат.ИспользоватьСубконто(ВидыСубконто.ГруппыВидыРасходов);
	БухИтЗатрат.ВыполнитьЗапрос(НачДата, КонДата, "Н01.05",,,,"Месяц",);   
	БухИтЗатрат.ВыбратьСубконто(ВидыСубконто.ГруппыВидыРасходов);
	Пока БухИтЗатрат.ПолучитьСубконто(ВидыСубконто.ГруппыВидыРасходов) = 1 Цикл 
		ТабЗатрат.НоваяСтрока();
		ТабЗатрат.ВидЗатрат = БухИтЗатрат.Субконто(ВидыСубконто.ГруппыВидыРасходов); 
		ТабЗатрат.Сумма = 0;
	КонецЦикла;
	
				
	Таб.ВывестиСекцию("Шапка_2|Колонка_1");
	ТекНомер = 4;
	ТабЗатрат.ВыбратьСтроки();
	Пока ТабЗатрат.ПолучитьСтроку() = 1 Цикл
	Таб.ПрисоединитьСекцию("Шапка_2|Колонка_2");
	ТекНомер = ТекНомер + 1;
	КонецЦикла;
	Таб.ПрисоединитьСекцию("Шапка_2|Колонка_3"); 
	
	БухИт = СоздатьОбъект("БухгалтерскиеИтоги");
	БухИт.ИспользоватьСубконто(ВидыСубконто.ВидыНоменклатуры);
	БухИт.ИспользоватьСубконто(ВидыСубконто.ГруппыВидыРасходов);
	БухИт.ВыполнитьЗапрос(НачДата, КонДата, "Н01.05",,,,"Месяц",);
	БухИт.ВыбратьПериоды();
	Пока БухИт.ПолучитьПериод() = 1 Цикл
		МесяцГод = Формат(БухИт.НачДата,"Д ММММГГГГ");
		ИтогоСуммаМатериальныхРасходовЗаМесяц  = 0;
		ИтогоСуммаРасходовНаОплатуТрудаЗаМесяц = 0;
		ИтогоСуммаЕСНЗаМесяц                   = 0;
		ИтогоСуммаАмортизацииЗаМесяц           = 0;
		    
		БухИт.ВыбратьСубконто(ВидыСубконто.ВидыНоменклатуры);
		Пока БухИт.ПолучитьСубконто(ВидыСубконто.ВидыНоменклатуры) = 1 Цикл
			ВидНоменклатуры = БухИт.Субконто(ВидыСубконто.ВидыНоменклатуры);
			
			СуммаОстатокНЗПНаНачалоМесяца     = БухИт.СНД();
			СуммаПрямыхРасхдовЗаМесяц         = БухИт.ДО();
			СуммаПрямыхРасходовНаКонецМесяца  = СуммаОстатокНЗПНаНачалоМесяца + СуммаПрямыхРасхдовЗаМесяц;
			СуммаОстатокНЗПНаКонецМесяца      = БухИт.СКД();
			СуммаОтносящаясяКВыпускуПродукции = БухИт.КО();			
			
			СуммаМатериальныхРасходовЗаМесяц  = 0;
			СуммаРасходовНаОплатуТрудаЗаМесяц = 0;
			СуммаЕСНЗаМесяц                   = 0;
			СуммаАмортизацииЗаМесяц           = 0;
			
			Таб.ВывестиСекцию("Строка|Колонка_1");
			ТабЗатрат.ВыбратьСтроки();
			Пока ТабЗатрат.ПолучитьСтроку() = 1 Цикл
				Если БухИт.ПолучитьСубконто(ВидыСубконто.ГруппыВидыРасходов, , ТабЗатрат.ВидЗатрат) = 1 Тогда
					СуммаРасходовЗаМесяц = БухИт.ДО(); 
					ТабЗатрат.Сумма = ТабЗатрат.Сумма + СуммаРасходовЗаМесяц;
					Таб.ПрисоединитьСекцию("Строка|Колонка_2");
				Иначе  
					Таб.ПрисоединитьСекцию("Строка|Колонка_Пусто");
				КонецЕсли;
				
			КонецЦикла;  
			Таб.ПрисоединитьСекцию("Строка|Колонка_3");  
		КонецЦикла;
		
		ИтогоСуммаОстатокНЗПНаНачалоМесяца     = БухИт.СНД();
		ИтогоСуммаПрямыхРасхдовЗаМесяц         = БухИт.ДО();
		ИтогоСуммаПрямыхРасходовНаКонецМесяца  = ИтогоСуммаОстатокНЗПНаНачалоМесяца + ИтогоСуммаПрямыхРасхдовЗаМесяц;
		ИтогоСуммаОстатокНЗПНаКонецМесяца      = БухИт.СКД();
		ИтогоСуммаОтносящаясяКВыпускуПродукции = БухИт.КО();	
		
		Таб.ВывестиСекцию("Итого|Колонка_1");
		ТабЗатрат.ВыбратьСтроки();
		Пока ТабЗатрат.ПолучитьСтроку() = 1 Цикл
			Таб.ПрисоединитьСекцию("Итого|Колонка_2");
    		ТабЗатрат.Сумма = 0;
		КонецЦикла;
		Таб.ПрисоединитьСекцию("Итого|Колонка_3");   
		
	КонецЦикла;
	
	ОтветственныйЗаСоставление = ФИО(Константа.ОтветственныйЗаСоставлениеРегистровНУ);
	
	Таб.ВывестиСекцию("Подвал");    
	
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0, 0, 0, 0, "ПечатьРегистрРасчетСтоимостиОстатковНЗП", "ОкноРегистрРасчетСтоимостиОстатковНЗП");
	Таб.Показать("Регистр-расчет стоимости остатков незавершенного производства");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии() //предопределенная
	
	НачДата = НачМесяца(РабочаяДата());
	КонДата = КонМесяца(РабочаяДата());
	
	ТипВидовНоменклатуры = 1; // все
	
КонецПроцедуры //ПриОткрытии()