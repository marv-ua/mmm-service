//-----------------------------------------------
Перем Счет;
Перем КорСчет;
Перем Субконто1;
Перем ВидСубк1;
Перем КорСубконто1;
Перем КорВидСубк1;
Перем Субконто2;
Перем ВидСубк2;
Перем КорСубконто2;
Перем КорВидСубк2;
Перем Субконто3;
Перем ВидСубк3;
Перем КорСубконто3;
Перем КорВидСубк3;
Перем ДтКт;
Перем Т;
Перем Обновить;
Перем Расшифровка;
Перем ИспФильтр;
Перем ПланСчетов;
Перем ВыбПланСчетов;
Перем ПредставлениеРУ;
Перем НачДата, КонДата;

//-----------------------------------------------
Функция ПроверкаПериода(Дата1, Дата2)
	Если ПустоеЗначение(Дата1) = 1 Тогда
	    Предупреждение("Не указана дата начала периода отчета!");
		Возврат 0;
	КонецЕсли;
	Если Дата1 > Дата2 Тогда
		Предупреждение("Неправильно задан период отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если Дата2 > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		               "Расчет итогов выполняется в режиме"+РазделительСтрок+
					   """Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции
//-----------------------------------------------
Функция ЗначенияВСтроку(Значения)
	Если ТипЗначенияСтр(Значения) = "СписокЗначений" Тогда
		С = "";
		Для А=1 По Значения.РазмерСписка() Цикл
			Если А <> 1 Тогда
				С = С+" ";
			КонецЕсли;
			С = С+Значения.ПолучитьЗначение(А);
		КонецЦикла;
		Возврат С;
	Иначе
		Возврат Значения;
	КонецЕсли;
КонецФункции
//-----------------------------------------------
Функция ПредставлениеПозицииДокумента(Поз)
	Перем ДокДата, Ч, М, С, Док;
	РазобратьПозициюДокумента(Поз, ДокДата, Ч, М, С, Док);
	ПредПозиции = "( "+Строка(ДокДата)+" "+Формат(Ч,"Ч(0)2")+":"+Формат(М,"Ч(0)2")+":"+Формат(С,"Ч(0)2")+" "+Док.представлениеВида()+" №"+Строка(Док.НомерДок)+" )";
	Возврат ПредПозиции;
КонецФункции
//-----------------------------------------------
Функция ПредставлениеИнтервалаОтчета(ДатаНачала, ДатаКонца)
	Если ((ТипЗначенияСтр(ДатаНачала) = "Дата") и (ТипЗначенияСтр(ДатаКонца) = "Дата")) Тогда
	    Возврат ПериодСтр(ДатаНачала, ДатаКонца);
	Иначе
		Если ТипЗначенияСтр(ДатаНачала) = "Документ" Тогда
			Интервал = ПредставлениеПозицииДокумента(ДатаНачала.ПолучитьПозицию());
		ИначеЕсли ТипЗначенияСтр(ДатаНачала) = "Строка" Тогда
			Интервал = ПредставлениеПозицииДокумента(ДатаНачала);
		ИначеЕсли ТипЗначенияСтр(ДатаНачала) = "Дата" Тогда
			Интервал = Строка(ДатаНачала);
		Иначе
			Интервал = """  .  .  """;
		КонецЕсли;
		
		Интервал = Интервал + " - ";
		Если ТипЗначенияСтр(ДатаКонца) = "Документ" Тогда
			Интервал = Интервал + ПредставлениеПозицииДокумента(ДатаКонца.ПолучитьПозицию());
		ИначеЕсли ТипЗначенияСтр(ДатаКонца) = "Строка" Тогда
			Интервал = Интервал + ПредставлениеПозицииДокумента(ДатаКонца);
		ИначеЕсли ТипЗначенияСтр(ДатаКонца) = "Дата" Тогда
			Интервал = Интервал + Строка(ДатаКонца);
		Иначе
			Интервал = Интервал + """  .  .  """;
		КонецЕсли;
		
		Возврат Интервал;
	КонецЕсли;	
КонецФункции
//-----------------------------------------------
Процедура ПриУстановкеДаты()
	Перем Ч, М, С, Док;
	
	Если ТипЗначенияСтр(НачДата) = "Документ" Тогда
		Дата1 = НачДата.ДатаДок;
	ИначеЕсли ТипЗначенияСтр(НачДата) = "Строка" Тогда
		РазобратьПозициюДокумента(НачДата, Дата1, Ч, М, С, Док);
	Иначе
		Дата1 = НачДата;
	КонецЕсли;
	
	Если ТипЗначенияСтр(КонДата) = "Документ" Тогда
		Дата2 = КонДата.ДатаДок;
	ИначеЕсли ТипЗначенияСтр(КонДата) = "Строка" Тогда
		РазобратьПозициюДокумента(КонДата, Дата2, Ч, М, С, Док);
	Иначе
		Дата2 = КонДата;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореДаты()
	НачДата = Дата1;
	КонДата = Дата2;
КонецПроцедуры

//-----------------------------------------------
Функция ВидСубконтоСтр(ВидСубк, Субк, КорВидСубк, КорСубк)
	с = "";
	Если (ПустоеЗначение(Субк) = 1)  и (ПустоеЗначение(КорСубк) = 1) Тогда
	    Возврат "";
	КонецЕсли;
	Если (ТипЗначения(ВидСубк) = 16) Или (ТипЗначения(КорВидСубк) = 16) Тогда
		с = "; """;
		Если ТипЗначения(ВидСубк) = 16 Тогда
			Если ВидСубк.Выбран() = 1 Тогда
				с = с+Строка(ВидСубк)+?(ТипЗначения(Субк) > 0, ": "+ЗначенияВСтроку(Субк), "");
			Иначе
				с = с+"*";
			КонецЕсли;
		Иначе
			с = с+"*";
		КонецЕсли;
		с = с+", ";
		Если ТипЗначения(КорВидСубк) = 16 Тогда
			Если КорВидСубк.Выбран() = 1 Тогда
				с = с+Строка(КорВидСубк)+?(ТипЗначения(КорСубк) > 0, ": "+ЗначенияВСтроку(КорСубк), "")+"""";
			Иначе
				с = с+"*""";
			КонецЕсли;
		Иначе
			с = с+"*""";
		КонецЕсли;
	КонецЕсли;
	Возврат с;
КонецФункции

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Процедура Сформировать(Фильтр, ФлагЗакрытияФормы = 0)
	Перем ФильтрДебета[3], ФильтрКредита[3];
	
	Если ((ТипЗначенияСтр(НачДата) = "Дата") и (ТипЗначенияСтр(КонДата) = "Дата")) Тогда
	    ПриВыбореДаты();
	КонецЕсли;
	
	Если ПроверкаПериода(НачДата, НачДата) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Расшифровка = СоздатьОбъект("СписокЗначений");
	Опер = СоздатьОбъект("Операция");
	Сумма = 0;
	Количество = 0;
	ВалСумма = 0;

	Если (ТипЗначенияСтр(Т) <> "Таблица") Или (Обновить = 0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
 	Т.ИсходнаяТаблица("Таблица");
 	
 	НазваниеОрганизации = глНазваниеОрганизации(Контекст);

    Если ВыбПланСчетов = 1 Тогда
        ПланСчетов = ВыбранныйПланСчетов();
    КонецЕсли;
	Если ИспФильтр=1 Тогда
		Опер.ВыбратьОперацииСПроводками(НачДата, КонДата, Фильтр, ?(ПоВалюте=1,Валюта,""), ПланСчетов, РазделительУчета);
		Заголовок = Фильтр;
	Иначе
		Для а = 1 по 3 Цикл
			ФильтрДебета[а] = "";
			ФильтрКредита[а] = "";
		КонецЦикла;		
		
		Если МаксимальноеКоличествоСубконто() > 0 Тогда
			Если ТипЗначения(Субконто1) > 0 Тогда
				Опер.ИспользоватьСубконто(ВидСубк1, Субконто1);
				
			ИначеЕсли ТипЗначения(ВидСубк1) > 0 Тогда
				Если ДтКт = 1 Тогда
			    	ФильтрДебета[1] = ВидСубк1;
				Иначе
					ФильтрКредита[1] = ВидСубк1;
				КонецЕсли;
			КонецЕсли;
			
			Если ТипЗначения(КорСубконто1) > 0 Тогда
				Опер.ИспользоватьКорСубконто(КорВидСубк1, КорСубконто1);
				
			ИначеЕсли ТипЗначения(КорВидСубк1) > 0 Тогда
				Если ДтКт = 2 Тогда
			    	ФильтрДебета[1] = КорВидСубк1;
				Иначе
					ФильтрКредита[1] = КорВидСубк1;
				КонецЕсли;
			КонецЕсли;
			
			Если МаксимальноеКоличествоСубконто() > 1 Тогда
				Если ТипЗначения(Субконто2) > 0 Тогда
					Опер.ИспользоватьСубконто(ВидСубк2, Субконто2);
					
				ИначеЕсли ТипЗначения(ВидСубк2) > 0 Тогда
					Если ДтКт = 1 Тогда
				    	ФильтрДебета[2] = ВидСубк2;
					Иначе
						ФильтрКредита[2] = ВидСубк2;
					КонецЕсли;
				КонецЕсли;
				
				Если ТипЗначения(КорСубконто2) > 0 Тогда
					Опер.ИспользоватьКорСубконто(КорВидСубк2, КорСубконто2);
					
				ИначеЕсли ТипЗначения(КорВидСубк2) > 0 Тогда
					Если ДтКт = 2 Тогда
				    	ФильтрДебета[2] = КорВидСубк2;
					Иначе
						ФильтрКредита[2] = КорВидСубк2;
					КонецЕсли;
				КонецЕсли;
				
				Если МаксимальноеКоличествоСубконто() > 2 Тогда
					Если ТипЗначения(Субконто3) > 0 Тогда
						Опер.ИспользоватьСубконто(ВидСубк3, Субконто3);
						
					ИначеЕсли ТипЗначения(ВидСубк3) > 0 Тогда
						Если ДтКт = 1 Тогда
					    	ФильтрДебета[3] = ВидСубк3;
						Иначе
							ФильтрКредита[3] = ВидСубк3;
						КонецЕсли;
					КонецЕсли;
					
					Если ТипЗначения(КорСубконто3) > 0 Тогда
						Опер.ИспользоватьКорСубконто(КорВидСубк3, КорСубконто3);
						
					ИначеЕсли ТипЗначения(КорВидСубк3) > 0 Тогда
						Если ДтКт = 2 Тогда
					    	ФильтрДебета[3] = КорВидСубк3;
						Иначе
							ФильтрКредита[3] = КорВидСубк3;
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Опер.ВыбратьОперацииСПроводками(НачДата, КонДата, Счет, КорСчет, ДтКт, ?(ПоВалюте=1,Валюта,""), ПланСчетов, РазделительУчета);
		Если ДтКт = 1 Тогда
			Фильтр = ""+?(ТипЗначения(Счет)=0,"*",Счет)+","+?(ТипЗначения(КорСчет)=0,"*",КорСчет);
			Заголовок = Фильтр+ВидСубконтоСтр(ВидСубк1, Субконто1, КорВидСубк1, КорСубконто1);
			Заголовок = Заголовок+ВидСубконтоСтр(ВидСубк2, Субконто2, КорВидСубк2, КорСубконто2);
			Заголовок = Заголовок+ВидСубконтоСтр(ВидСубк3, Субконто3, КорВидСубк3, КорСубконто3);
		ИначеЕсли ДтКт = 2 Тогда
			Фильтр = ""+?(ТипЗначения(КорСчет)=0,"*",КорСчет)+","+?(ТипЗначения(Счет)=0,"*",Счет);
			Заголовок = Фильтр+ВидСубконтоСтр(КорВидСубк1, КорСубконто1, ВидСубк1, Субконто1);
			Заголовок = Заголовок+ВидСубконтоСтр(КорВидСубк2, КорСубконто2, ВидСубк2, Субконто2);
			Заголовок = Заголовок+ВидСубконтоСтр(КорВидСубк3, КорСубконто3, ВидСубк3, Субконто3);
		Иначе
			Фильтр = ""+?(ТипЗначения(Счет)=0,"*",Счет)+","+?(ТипЗначения(КорСчет)=0,"*",КорСчет)+"; "+?(ТипЗначения(КорСчет)=0,"*",КорСчет)+","+?(ТипЗначения(Счет)=0,"*",Счет);
			Заголовок = Фильтр+ВидСубконтоСтр(ВидСубк1, Субконто1, КорВидСубк1, КорСубконто1);
			Заголовок = Заголовок+ВидСубконтоСтр(КорВидСубк1, КорСубконто1, ВидСубк1, Субконто1);
			Заголовок = Заголовок+ВидСубконтоСтр(КорВидСубк3, КорСубконто3, ВидСубк3, Субконто3);
			Заголовок = Заголовок+ВидСубконтоСтр(ВидСубк2, Субконто2, КорВидСубк2, КорСубконто2);
			Заголовок = Заголовок+ВидСубконтоСтр(КорВидСубк2, КорСубконто2, ВидСубк2, Субконто2);
			Заголовок = Заголовок+ВидСубконтоСтр(КорВидСубк3, КорСубконто3, ВидСубк3, Субконто3);
		КонецЕсли;
	КонецЕсли;
	Если ПоВалюте=1 Тогда
		Заголовок = Заголовок+"; "+Валюта;
	КонецЕсли;
	Если ПустаяСтрока(ВыбЖурнал)=0 Тогда
		Заголовок = Заголовок+"; "+ВыбЖурнал;
	КонецЕсли;

	Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
    Расшифровка.Установить("ПланСчетов", ПланСчетов);
	Расшифровка.Установить("Дата1", НачДата);
	Расшифровка.Установить("Дата2", КонДата);
	Расшифровка.Установить("Фильтр", Фильтр);
	Расшифровка.Установить("Валюта", Валюта);
	Расшифровка.Установить("ПоВалюте", ПоВалюте);
	Расшифровка.Установить("НомерЖурнала", ВыбЖурнал);
	Если ИспФильтр=1 Тогда
		Расшифровка.Установить("ИспФильтр",1);
	Иначе
		Расшифровка.Установить("Счет",Счет);
		Расшифровка.Установить("КорСчет",КорСчет);
		Расшифровка.Установить("ДтКт",ДтКт);
		Расшифровка.Установить("ВидСубконто1",ВидСубк1);
		Расшифровка.Установить("Субконто1",Субконто1);
		Расшифровка.Установить("ВидСубконто2",ВидСубк2);
		Расшифровка.Установить("Субконто2",Субконто2);
		Расшифровка.Установить("ВидСубконто3",ВидСубк3);
		Расшифровка.Установить("Субконто3",Субконто3);
		Расшифровка.Установить("КорВидСубконто1",КорВидСубк1);
		Расшифровка.Установить("КорСубконто1",КорСубконто1);
		Расшифровка.Установить("КорВидСубконто2",КорВидСубк2);
		Расшифровка.Установить("КорСубконто2",КорСубконто2);
		Расшифровка.Установить("КорВидСубконто3",КорВидСубк3);
		Расшифровка.Установить("КорСубконто3",КорСубконто3);
	КонецЕсли;
	Т.ВывестиСекцию("Секция_4");
	Т.ВывестиСекцию("Секция_1");
	Расшифровка.УдалитьВсе();

	Пока Опер.ПолучитьПроводку() = 1 Цикл
		Если ИспФильтр <> 1 Тогда
			ФильтрНеПройден = 0;
			Для а = 1 по 3 Цикл
				Если ФильтрДебета[а] <> "" Тогда
					ВидСубконтоНайден = 0;
					Для б = 1 по 3 Цикл
						Если (ФильтрДебета[а] = Опер.Дебет.Счет.ВидСубконто(б)) Тогда
							ВидСубконтоНайден = 1;
						КонецЕсли;
					КонецЦикла;
					
					Если ВидСубконтоНайден = 0 Тогда
					    ФильтрНеПройден = 1;
					КонецЕсли;
				КонецЕсли;
				
				Если ФильтрКредита[а] <> "" Тогда
					ВидСубконтоНайден = 0;
					Для б = 1 по 3 Цикл
						Если (ФильтрКредита[а] = Опер.Кредит.Счет.ВидСубконто(б)) Тогда
							ВидСубконтоНайден = 1;
						КонецЕсли;
					КонецЦикла;
					
					Если ВидСубконтоНайден = 0 Тогда
					    ФильтрНеПройден = 1;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если ФильтрНеПройден = 1 Тогда
		    	Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ПустаяСтрока(ВыбЖурнал)=0 Тогда
		    Если Опер.НомерЖурнала<>ВыбЖурнал Тогда
		        Продолжить;
		    КонецЕсли;
		КонецЕсли;
		Если (Опер.СложнаяПроводка() = 0) Или (Опер.НомерКорреспонденции() > 1) Тогда
			Сумма = Сумма+Опер.Сумма;
			Количество = Количество+Опер.Количество;
			ВалСумма = ВалСумма+Опер.ВалСумма;
		КонецЕсли;
		Расшифровка.Установить("Документ", Опер.Документ.ТекущийДокумент());
		Расшифровка.Установить("НомерПроводки", Опер.НомерПроводки());
		Расшифровка.Установить("НомерКорреспонденции", Опер.НомерКорреспонденции());
		Т.ВывестиСекцию("Секция_2");
	КонецЦикла;
	Т.ВывестиСекцию("Секция_3");
	
	ФиксСтрок = 7;
	
	НазваниеОрганизации = ?(ПустоеЗначение(НазваниеОрганизации)=1, "", " "+НазваниеОрганизации);
	ПериодИОрганизация = " ("+ПредставлениеИнтервалаОтчета(НачДата, КонДата)+")"+НазваниеОрганизации;
	ВерхнийКолонтитул = "Журнал проводок"+ПериодИОрганизация;
	НижнийКолонтитул = "Отчет сформирован "+ТекущаяДата()+" "+ТекущееВремя()+?(ПустоеЗначение(ИмяПользователя())=0,"  Пользователь: "+ИмяПользователя(),"");
	Т.ТолькоПросмотр(1);
	Т.Опции(0, 0, ФиксСтрок, 0, "ОпцииПечатиОтчетыПоПроводкам", "ОтчетыПоПроводкам");
	Т.ОбластьПечати(2);
	Т.ПовторятьПриПечатиСтроки(ФиксСтрок,ФиксСтрок);
	Т.ПараметрыСтраницы(,,,,,,,,, 1);
	Т.Показать(ВерхнийКолонтитул, "");

	Если (ФлагЗакрытияФормы = 1) Или (Обновить = 2) Тогда
         СтрокаДействийФормы = "#Закрыть";
    КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВалюты()
	ПоВалюте = Валюта.Выбран();
КонецПроцедуры

//-----------------------------------------------
Процедура ВыбратьЖурнал()
	Журналы = СоздатьОбъект("СписокЗначений");
	ПолучитьЗначенияОтбора("НомерЖурнала", Журналы);
	Журналы.ВыбратьЗначение(ВыбЖурнал, "Выбор журнала",,, ?(Журналы.РазмерСписка()<20, 1, 0));
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОткрытии()
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;

	ИспФильтр=1;

	Если Метаданные.Валюта.Выбран() = 1 Тогда
		Форма.Валюта.НазначитьТип("Справочник."+Метаданные.Валюта.Идентификатор);
	Иначе
		Форма.Валюта.Доступность(0);
		Форма.ПоВалюте.Доступность(0);
	КонецЕсли;

	Если глФлагРасшифровки = 1 Тогда
		ВыбПланСчетов = 0;
		Обновить = глОбновить;
		ПланСчетов = глРасшифровка.Получить("ПланСчетов");
		РУ = глРасшифровка.Получить("РазделительУчета");
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
		НачДата = глРасшифровка.Получить("Дата1");
		КонДата = глРасшифровка.Получить("Дата2");
		ПриУстановкеДаты();
		Валюта = глРасшифровка.Получить("Валюта");
		ПоВалюте = глРасшифровка.Получить("ПоВалюте");
		ВыбЖурнал= глРасшифровка.Получить("НомерЖурнала");
		ИспФильтр=Число(глРасшифровка.Получить("ИспФильтр"));
		Если Обновить <> 0 Тогда
			Т = глТаблица;
		КонецЕсли;

		Если Обновить=2 Тогда   //Не поддерживается "Настройка для варианта без фильтра"
			ИспФильтр=1;
			ПриВыбореДаты();
		КонецЕсли;

		Если ИспФильтр=1 Тогда
			ВыбФильтр = глРасшифровка.Получить("Фильтр");
		Иначе
			Счет = глРасшифровка.Получить("Счет");
			КорСчет = глРасшифровка.Получить("КорСчет");
			ДтКт = глРасшифровка.Получить("ДтКт");
			ВидСубк1 = глРасшифровка.Получить("ВидСубконто1");
			Субконто1 = глРасшифровка.Получить("Субконто1");
			ВидСубк2 = глРасшифровка.Получить("ВидСубконто2");
			Субконто2 = глРасшифровка.Получить("Субконто2");
			ВидСубк3 = глРасшифровка.Получить("ВидСубконто3");
			Субконто3 = глРасшифровка.Получить("Субконто3");
			КорВидСубк1 = глРасшифровка.Получить("КорВидСубконто1");
			КорСубконто1 = глРасшифровка.Получить("КорСубконто1");
			КорВидСубк2 = глРасшифровка.Получить("КорВидСубконто2");
			КорСубконто2 = глРасшифровка.Получить("КорСубконто2");
			КорВидСубк3 = глРасшифровка.Получить("КорВидСубконто3");
			КорСубконто3 = глРасшифровка.Получить("КорСубконто3");
			ВыбФильтр = "";
		КонецЕсли;
		Если Обновить <> 2 Тогда
			Сформировать(ВыбФильтр);
			глРасшифровка = "";
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		ВыбПланСчетов = 1;
		ПланСчетов = ВыбранныйПланСчетов();
		РУ = БухИтоги.ИспользоватьРазделительУчета();
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
	    НачДата = НачалоПериодаБИ();
	    КонДата = КонецПериодаБИ();
		ПриУстановкеДаты();
	КонецЕсли;
	ВосстановитьЗначение("ОЖО_Фильтры",Фильтры);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриЗакрытии()
	СохранитьЗначение("ОЖО_Фильтры",Фильтры);
КонецПроцедуры

//-----------------------------------------------
Процедура Запомнить()        
	Если СокрЛП(ВыбФильтр) <> "" Тогда
		Фильтры.ДобавитьЗначение(ВыбФильтр);
		Фильтры.ТекущаяСтрока(Фильтры.РазмерСписка());
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура Удалить()
	Если Фильтры.ТекущаяСтрока()>0 Тогда
		Фильтры.УдалитьЗначение(Фильтры.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура Выбрать()
	Если Фильтры.ТекущаяСтрока()>0 Тогда
		ВыбФильтр=Фильтры.ПолучитьЗначение(Фильтры.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура Сформировать1()
	С = Фильтры.ТекущаяСтрока();
	Сформировать(Фильтры.ПолучитьЗначение(С));
КонецПроцедуры
