//-----------------------------------------------
Перем Т;
Перем Обновить;
Перем Расшифровка;
Перем ПланСчетов;
Перем ВыбПланСчетов;
Перем ПредставлениеРУ;

//-----------------------------------------------
Функция ПроверкаПериода()
	Если ПустоеЗначение(Дата1) = 1 Тогда
	    Предупреждение("Не указана дата начала периода отчета!");
		Возврат 0;
	КонецЕсли;
	Если Дата1 > Дата2 Тогда
		Предупреждение("Неправильно задан период отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если Дата2 > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		               "Расчет итогов выполняется в режиме"+РазделительСтрок+
					   """Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Процедура КорСчета(Т, Ит, Секция, Гр)
	Т.ВывестиСекцию(Секция+"|Секция_5");
	Если ОстНачД = 1 Тогда
		Т.ПрисоединитьСекцию(Секция+"|Секция_6");
	КонецЕсли;

	Если ОстНачК = 1 Тогда
		Т.ПрисоединитьСекцию(Секция+"|Секция_7");
	КонецЕсли;

	Расшифровка.Установить("ДтКт", 1);
	Если КорОбД = 1 Тогда
  		Ит.ВыбратьКорСчета(Гр, 1);
    	Пока Ит.ПолучитьКорСчет() = 1 Цикл
			Расшифровка.Установить("КорСчет", Ит.КорСчет);
    		Т.ПрисоединитьСекцию(Секция+"|Секция_8");
    	КонецЦикла;
		Расшифровка.Установить("КорСчет");
    КонецЕсли;

	Если ОбД = 1 Тогда
		Т.ПрисоединитьСекцию(Секция+"|Секция_9");
	КонецЕсли;

	Расшифровка.Установить("ДтКт", 2);
	Если КорОбК = 1 Тогда
  		Ит.ВыбратьКорСчета(Гр, 2);
    	Пока Ит.ПолучитьКорСчет() = 1 Цикл
			Расшифровка.Установить("КорСчет", Ит.КорСчет);
    		Т.ПрисоединитьСекцию(Секция+"|Секция_10");
    	КонецЦикла;
		Расшифровка.Установить("КорСчет");
    КонецЕсли;

	Если ОбК = 1 Тогда
		Т.ПрисоединитьСекцию(Секция+"|Секция_11");
	КонецЕсли;

	Расшифровка.Установить("ДтКт", 3);
	Если ОстКонД = 1 Тогда
		Т.ПрисоединитьСекцию(Секция+"|Секция_12");
	КонецЕсли;

	Если ОстКонК = 1 Тогда
		Т.ПрисоединитьСекцию(Секция+"|Секция_13");
	КонецЕсли;

    //Т.ПрисоединитьСекцию(Секция+"|Секция_14");
КонецПроцедуры

//-----------------------------------------------
Процедура Сформировать(Ручн = 0, ФлагЗакрытияФормы = 0)
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;

	Расшифровка = СоздатьОбъект("СписокЗначений");
    Ит = СоздатьОбъект("БухгалтерскиеИтоги");
    Ит.ИспользоватьРазделительУчета(РазделительУчета);
    Ит.ВключатьСубсчета(0, ДанныеПоСубсчетам);
    Если Счет.Выбран() = 0 Тогда
		Если Ручн=1 Тогда
		    Если Вопрос("Сформировать по всем счетам ?","ОК+Отмена")="Отмена" Тогда
				Возврат;
		    КонецЕсли;
		КонецЕсли;
	    Если ВыбПланСчетов = 1 Тогда
	        ПланСчетов = ВыбранныйПланСчетов();
	    КонецЕсли;
        Ит.ИспользоватьПланСчетов(ПланСчетов);
    Иначе
		Если Ручн=1 Тогда
		    СохранитьЗначение("ОтчРабСчет",Счет);
		КонецЕсли;
    КонецЕсли;
    Если ПоВалюте = 1 Тогда
		Если Ит.ВыполнитьЗапрос(Дата1, Дата2, Счет,, Валюта, 3, "Месяц", "СВ") = 0 Тогда
			Возврат;
		КонецЕсли;
		Расшифровка.Установить("Валюта", Валюта);
		Расшифровка.Установить("ПоВалюте", 1);
		Секция_3 = "Секция_3_1";
		Секция_4 = "Секция_4_1";
	Иначе
    	Если Ит.ВыполнитьЗапрос(Дата1, Дата2, Счет,,, 3, "Месяц", "С") = 0 Тогда
			Возврат;
		КонецЕсли;
		Секция_3 = "Секция_3";
		Секция_4 = "Секция_4";
    КонецЕсли;

	Если (ТипЗначенияСтр(Т) <> "Таблица")ИЛИ(Обновить=0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
	Т.ИсходнаяТаблица("Таблица");
	
	Расшифровка.Установить("Отчет", "ГлавнаяКнига");
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
    Расшифровка.Установить("ПланСчетов", ПланСчетов);
	Расшифровка.Установить("Дата1", Дата1);
	Расшифровка.Установить("Дата2", Дата2);
	Расшифровка.Установить("Счет", Ит.Счет);
	Расшифровка.Установить("ДанныеПоСубсчетам", ДанныеПоСубсчетам);
	Расшифровка.Установить("ОстНачД", ОстНачД);
	Расшифровка.Установить("ОстНачК", ОстНачК);
	Расшифровка.Установить("ОбД", ОбД);
	Расшифровка.Установить("ОбК", ОбК);
	Расшифровка.Установить("КорОбД", КорОбД);
	Расшифровка.Установить("КорОбК", КорОбК);
	Расшифровка.Установить("ОстКонД", ОстКонД);
	Расшифровка.Установить("ОстКонК", ОстКонК);
   	Т.ВывестиСекцию("Секция_15");
	Расшифровка.Установить("Дата1");
	Расшифровка.Установить("Дата2");
	Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");
	Расшифровка.Установить("ДанныеПоСубсчетам");
	Расшифровка.Установить("ОстНачД");
	Расшифровка.Установить("ОстНачК");
	Расшифровка.Установить("ОбД");
	Расшифровка.Установить("ОбК");
	Расшифровка.Установить("КорОбД");
	Расшифровка.Установить("КорОбК");
	Расшифровка.Установить("ОстКонД");
	Расшифровка.Установить("ОстКонК");
	Расшифровка.Установить("Обновить");
    
	НазваниеОрганизации = глНазваниеОрганизации(Контекст);
	
	Если Счет.Выбран() = 1 Тогда
		Заголовок = "Обороты по счету "+Ит.Счет;
		СекцияЗаголовкаСчета = "Секция_1";
		ФиксСтрок = 6;
		
	Иначе
		Заголовок = "Главная книга";
		СекцияЗаголовкаСчета = "Секция_1_1";
		Т.ВывестиСекцию("Секция_1");
		ФиксСтрок = 5;
	КонецЕсли;
	
	Ит.ВыбратьСчета();
    Пока Ит.ПолучитьСчет() = 1 Цикл
	   	Т.ВывестиСекцию(СекцияЗаголовкаСчета);
		Расшифровка.Установить("Счет", Ит.Счет);
		Расшифровка.Установить("ДтКт", 3);
	   	КорСчета(Т, Ит,"Секция_2", -1); 		// заголовок
	   	Ит.ВыбратьПериоды(1);
	   	Пока Ит.ПолучитьПериод() = 1 Цикл
			Расшифровка.Установить("Дата1", Ит.НачДата);
			Расшифровка.Установить("Дата2", КонМесяца(Ит.НачДата));
			КорСчета(Т, Ит, Секция_3, -2); // по датам
	    КонецЦикла;
		Расшифровка.Установить("Дата1", Дата1);
		Расшифровка.Установить("Дата2", Дата2);
	   	КорСчета(Т, Ит, Секция_4, -1);        // итоги
	КонецЦикла;
    Ит = 0;
	
    Т.ТолькоПросмотр(1);

    НазваниеОрганизации = ?(ПустоеЗначение(НазваниеОрганизации)=1, "", " "+НазваниеОрганизации);
	ПериодИОрганизация = " ("+ПериодСтр(Дата1, Дата2)+")"+НазваниеОрганизации;
	ВерхнийКолонтитул = Заголовок+ПериодИОрганизация;
	НижнийКолонтитул = "Отчет сформирован "+ТекущаяДата()+" "+ТекущееВремя()+?(ПустоеЗначение(ИмяПользователя())=0,"  Пользователь: "+ИмяПользователя(),"");
	Т.Опции(0, 0, ФиксСтрок, 1, "ОпцииПечатиГлавнаяКнига", "ГлавнаяКнига");
	Т.ОбластьПечати(2);
	Если Счет.Выбран() = 1 Тогда
		Т.ПовторятьПриПечатиСтолбцы(1,1);
		Т.ПовторятьПриПечатиСтроки(4,4);
	КонецЕсли;
	Т.ПараметрыСтраницы(2,,,,,,,,, 0);
    Т.Показать(ВерхнийКолонтитул, "");

	Если (ФлагЗакрытияФормы = 1) Или (Обновить = 2) Тогда
         СтрокаДействийФормы = "#Закрыть";
    КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВалюты()
	ПоВалюте = Валюта.Выбран();
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСчета()
	Если Счет.Валютный = 0 Тогда
	    ПоВалюте = 0;           
		Если Метаданные.Валюта.Выбран() = 1 Тогда
	    	Валюта = ПолучитьПустоеЗначение("Справочник."+Метаданные.Валюта.Идентификатор);
		КонецЕсли;
	КонецЕсли;
	Форма.ПоВалюте.Доступность(Счет.Валютный);
	Форма.Валюта.Доступность(Счет.Валютный);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОткрытии()
	Форма.Кн_Видеокурс.Видимость( ?(ТипЗначения(Видео_Компонента)=0,0,1 ));
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;

	Если Метаданные.Валюта.Выбран() = 1 Тогда
		Форма.Валюта.НазначитьТип("Справочник."+Метаданные.Валюта.Идентификатор);
	Иначе
		Форма.ВалютаТекст.Доступность(0);
		Форма.Валюта.Доступность(0);
		Форма.ПоВалюте.Доступность(0);
	КонецЕсли;

	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		ВыбПланСчетов = 0;
		ПланСчетов = глРасшифровка.Получить("ПланСчетов");
		РУ = глРасшифровка.Получить("РазделительУчета");
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
		Дата1 = глРасшифровка.Получить("Дата1");
		Дата2 = глРасшифровка.Получить("Дата2");
		Счет = глРасшифровка.Получить("Счет");
		ПриВыбореСчета();
		ДанныеПоСубсчетам = глРасшифровка.Получить("ДанныеПоСубсчетам");
	    Валюта = глРасшифровка.Получить("Валюта");
	    ПоВалюте = глРасшифровка.Получить("ПоВалюте");
		Если Обновить <> 0 Тогда
			ОстНачД = глРасшифровка.Получить("ОстНачД");
			ОстНачК = глРасшифровка.Получить("ОстНачК");
			ОбД = глРасшифровка.Получить("ОбД");
			ОбК = глРасшифровка.Получить("ОбК");
			КорОбД = глРасшифровка.Получить("КорОбД");
			КорОбК = глРасшифровка.Получить("КорОбК");
			ОстКонД = глРасшифровка.Получить("ОстКонД");
			ОстКонК = глРасшифровка.Получить("ОстКонК");
			Т = глТаблица;
		Иначе
		    ОстНачД = 1;
		    ОстНачК = 1;
		    КорОбД  = 1;
		    ОбД     = 1;
		    КорОбК  = 1;
		    ОбК     = 1;
		    ОстКонД = 1;
		    ОстКонК = 1;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		ВыбПланСчетов = 1;
		ПланСчетов = ВыбранныйПланСчетов();
		РУ = БухИтоги.ИспользоватьРазделительУчета();
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
		Дата1 = НачалоПериодаБИ();
		Дата2 = КонецПериодаБИ();
		Если Счет.Выбран()=0 Тогда
		    ВосстановитьЗначение("ОтчРабСчет",Счет);
		КонецЕсли;
		Обновить = 0;
		Если ОстНачД+ОстНачК+КорОбД+ОбД+КорОбК+ОбК+ОстКонД+ОстКонК = 0 Тогда
		    ОстНачД = 1;
		    ОстНачК = 1;
		    КорОбД  = 1;
		    ОбД     = 1;
		    КорОбК  = 1;
		    ОбК     = 1;
		    ОстКонД = 1;
		    ОстКонК = 1;
		КонецЕсли;
		ПриВыбореСчета();
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ВводНового()
	Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ();
	ПриВыбореСчета();
КонецПроцедуры