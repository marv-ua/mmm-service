//******************************************************************************
// ПроверкаПериода()
//
// Возвращаемое значение:
//  1 - корректно выбран период в диалоге
//  0 - не корректно выбран период в диалоге
//
// Описание:
// Функция проверяет корректность ввода интервала дат в дилоге
//
Функция ПроверкаПериода()
	
	Если НачДата > КонДата Тогда
		Предупреждение("Неправильно задан период формирования отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	
	Возврат 1;
	
КонецФункции // ПроверкаПериода()

//******************************************************************************
Процедура Сформировать()
	
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СчН03_05 = СчетПоКоду("Н03.05");
	СчН03_06 = СчетПоКоду("Н03.06");
	СчН03_07 = СчетПоКоду("Н03.07");
	
	Таб = СоздатьОбъект("Таблица"); 
	СтрНалогоплательщик = Константа.ОфициальноеНазваниеОрганизации;
	Если ПустаяСтрока(СтрНалогоплательщик) = 1 Тогда
	СтрНалогоплательщик = Константа.НазваниеОрганизации;	    
	КонецЕсли;
	ИНН = Константа.ИННОрганизации;
	Таб.ВывестиСекцию("Шапка");
	
	ИтогоCуммаПлатежаПоДолгосрочномуСтрахованию = 0;
	ИтогоСуммаПлатежаПоСтрахованиюНаОплатуМедицинскихРасходов = 0;
	ИтогоСуммаПлатежаПоСтрахованиюНаСлучайСмерти = 0;
	
	Опер = СоздатьОбъект("Операция");  
	Опер.ВыбратьОперацииСПроводками(НачДата, КонДата, "Н03.05,*;Н03.06,*;Н03.07,*",,,);
	Пока Опер.ПолучитьПроводку() = 1 Цикл
		ДатаФормированияЗаписи = Опер.ДатаОперации;
		Сотрудник = "" + ФИО(Опер.Дебет.Сотрудники) + " (таб.№" + Опер.Дебет.Сотрудники.Код + ")";

		Если Опер.Дебет.Сотрудники.Подразделение.Получить(ДатаФормированияЗаписи).Административное = 1 Тогда
		    ВидПерсонала = "АУП";
		Иначе
		    ВидПерсонала = "Прочие";
		КонецЕсли;

		СчетДт = Опер.Дебет.Счет;
		
		CуммаПлатежаПоДолгосрочномуСтрахованию = "";
		СуммаПлатежаПоСтрахованиюНаОплатуМедицинскихРасходов = "";
		СуммаПлатежаПоСтрахованиюНаСлучайСмерти = "";
		
		Если СчетДт = СчН03_05 Тогда
			CуммаПлатежаПоДолгосрочномуСтрахованию = ФорматС(Опер.Сумма);
			ИтогоCуммаПлатежаПоДолгосрочномуСтрахованию = ИтогоCуммаПлатежаПоДолгосрочномуСтрахованию + Опер.Сумма;
			
		ИначеЕсли СчетДт = СчН03_06 Тогда
			СуммаПлатежаПоСтрахованиюНаОплатуМедицинскихРасходов = ФорматС(Опер.Сумма);
			ИтогоСуммаПлатежаПоСтрахованиюНаОплатуМедицинскихРасходов = ИтогоСуммаПлатежаПоСтрахованиюНаОплатуМедицинскихРасходов + Опер.Сумма;

		ИначеЕсли СчетДт = СчН03_07 Тогда
			СуммаПлатежаПоСтрахованиюНаСлучайСмерти = ФорматС(Опер.Сумма);
			ИтогоСуммаПлатежаПоСтрахованиюНаСлучайСмерти = ИтогоСуммаПлатежаПоСтрахованиюНаСлучайСмерти + Опер.Сумма;
		КонецЕсли;
		
		Таб.ВывестиСекцию("Строка");
	КонецЦикла;

	ИтогоCуммаПлатежаПоДолгосрочномуСтрахованию = ФорматС(ИтогоCуммаПлатежаПоДолгосрочномуСтрахованию);
	ИтогоСуммаПлатежаПоСтрахованиюНаОплатуМедицинскихРасходов = ФорматС(ИтогоСуммаПлатежаПоСтрахованиюНаОплатуМедицинскихРасходов);
	ИтогоСуммаПлатежаПоСтрахованиюНаСлучайСмерти = ФорматС(ИтогоСуммаПлатежаПоСтрахованиюНаСлучайСмерти);
	
	ОтветственныйЗаСоставление = ФИО(Константа.ОтветственныйЗаСоставлениеРегистровНУ);
	
	Таб.ВывестиСекцию("Подвал");
	Таб.ТолькоПросмотр(1);
	Таб.ОбластьПечати(,2,,);
	Таб.Опции(0, 0, 0, 0, "ПечатьРегистрУчетаРасходовПоДобровольномуСтрахованиюРаботников", "ОкноРегистрУчетаРасходовПоДобровольномуСтрахованиюРаботников");
	Таб.Показать("Регистр учета расходов по добровольному страхованию работников");

КонецПроцедуры // Сформировать    

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
    НачДата = НачГода(РабочаяДата());
	КонДата = КонКвартала(РабочаяДата()); 
	
КонецПроцедуры // ПриОткрытии()