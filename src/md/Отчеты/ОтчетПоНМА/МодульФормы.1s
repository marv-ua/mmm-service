Перем Сч04, Сч05;
//_____________________________________________________________________________
Перем Т;
Перем Обновить;
Перем Расшифровка;
//_____________________________________________________________________________
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции //РасшифровкаОбновить  

//_____________________________________________________________________________
Функция ПроверкаПериода()
	Если ДатаНач > ДатаКон Тогда
		Предупреждение("Неправильно задан период формирования отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если ДатаКон > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		               "Расчет итогов выполняется в режиме"+РазделительСтрок+
					   """Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции // ПроверкаПериода()

//_____________________________________________________________________________
Процедура ПриВыбореВарианта()
    Если ВариантФормирования = 1 Тогда
        Форма.ПодписьДокумента.Доступность(0);
        Форма.ДокументНачисленияАмортизации.Доступность(0);
        Форма.ПодписьС.Доступность(1);
        Форма.ДатаНач.Доступность(1);
        Форма.ПодписьПо.Доступность(1);
        Форма.ДатаКон.Доступность(1);
        Форма.КнопкаПериода.Доступность(1);
    Иначе
        Форма.ПодписьС.Доступность(0);
        Форма.ДатаНач.Доступность(0);
        Форма.ПодписьПо.Доступность(0);
        Форма.ДатаКон.Доступность(0);
        Форма.КнопкаПериода.Доступность(0);
        Форма.ПодписьДокумента.Доступность(1);
        Форма.ДокументНачисленияАмортизации.Доступность(1);
    КонецЕсли;
КонецПроцедуры //ПриВыбореВарианта
//_____________________________________________________________________________
Процедура ПриВыбореГруппы()
	Если ГруппаНМА.Выбран() = 0 Тогда
	ИначеЕсли ОбъектНМА.Выбран() = 0 Тогда
	ИначеЕсли ОбъектНМА.Группа = ГруппаНМА Тогда
	Иначе ОбъектНМА = "";
	КонецЕсли;
КонецПроцедуры //ПриВыбореГруппы
//_____________________________________________________________________________
Процедура ПриВыбореОбъекта()
	Если ОбъектНМА.Выбран() = 0 Тогда
	ИначеЕсли ГруппаНМА.Выбран() = 0 Тогда
	ИначеЕсли ОбъектНМА.Группа = ГруппаНМА Тогда
	Иначе ГруппаНМА = "";
	КонецЕсли;
КонецПроцедуры //ПриВыбореОбъекта
//_____________________________________________________________________________
Процедура Сформировать()
	                    
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ВариантФормирования = 1 Тогда
	ИначеЕсли ДокументНачисленияАмортизации.Выбран() = 0 Тогда
		Предупреждение("Не выбран документ!");
		Возврат;
	КонецЕсли;

    Если Обновить = 2 Тогда
	    СтрокаДействийФормы = "#Закрыть";
	КонецЕсли;
	Если (ТипЗначенияСтр(Т) <> "Таблица") ИЛИ (Обновить = 0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
    Т.ИсходнаяТаблица( "Таблица" );

    Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "ОтчетПоНМА");
    Расшифровка.Установить("ВариантФормирования", ВариантФормирования);
    Расшифровка.Установить("ДатаНач", ДатаНач);
    Расшифровка.Установить("ДатаКон", ДатаКон);
    Расшифровка.Установить("ДокументНачисленияАмортизации", ДокументНачисленияАмортизации);
    Расшифровка.Установить("ГруппаНМА", ГруппаНМА);
    Расшифровка.Установить("ОбъектНМА", ОбъектНМА);

	Т.ВывестиСекцию("Шапка");
    Т.ИспользоватьФормат("Ч15.2-,");

    Расшифровка = СоздатьОбъект("СписокЗначений");
	Если ВариантФормирования = 1 Тогда
		Расшифровка.Установить("Отчет", "АнализСубконто");
		Расшифровка.Установить("Обновить", 0);
		Расшифровка.Установить("ПланСчетов", ВыбранныйПланСчетов());
		Расшифровка.Установить("РазделительУчета", БухИтоги.ИспользоватьРазделительУчета());
		Расшифровка.Установить("Дата1", ДатаНач);
		Расшифровка.Установить("Дата2", ДатаКон);
		Расшифровка.Установить("ВидСубконто1", ВидыСубконто.НематериальныеАктивы);
		Расшифровка.Установить("ОтборСубконто1", 1);
		Расшифровка.Установить("ПоГруппам1", 0);
		Расшифровка.Установить("ДанныеПоСубсчетам", 1);
	Иначе
		Расшифровка.Установить("Документ", ДокументНачисленияАмортизации);
	КонецЕсли;

	БИ = СоздатьОбъект("БухгалтерскиеИтоги");
	НачДата = ?(ВариантФормирования = 1, ДатаНач, ДокументНачисленияАмортизации);
	КонДата = ?(ВариантФормирования = 1, ДатаКон, ДокументНачисленияАмортизации);
	Если ОбъектНМА.Выбран() = 1 Тогда
	    БИ.ИспользоватьСубконто(ВидыСубконто.НематериальныеАктивы,ОбъектНМА);
	Иначе
		БИ.ИспользоватьСубконто(ВидыСубконто.НематериальныеАктивы);
	КонецЕсли;
	Если Дата(КонДата) > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За данный период бухгалтерские итоги не рассчитаны!");
		Возврат;
	КонецЕсли;
	Если БИ.ВыполнитьЗапрос(НачДата,КонДата,"04.1,05") = 0 Тогда
		Предупреждение("Не удается выполнить запрос к бухгалтерским итогам!");
		Возврат;
	КонецЕсли;
	//Выбранные бухгалтерские итоги поместим в таблицу значений
	Результат = СоздатьОбъект("ТаблицаЗначений");
	Результат.НоваяКолонка("ГруппаНМА");
	Результат.НоваяКолонка("НМА");
	Результат.НоваяКолонка("нБаланс");
	Результат.НоваяКолонка("кБаланс");
	Результат.НоваяКолонка("оБаланс");
	Результат.НоваяКолонка("нАмортизация");
	Результат.НоваяКолонка("кАмортизация");
	Результат.НоваяКолонка("оАмортизация");

	БИ.ВыбратьСубконто(1,0,0,0,0,"Группа",0);
    Пока БИ.ПолучитьСубконто(1,0,) = 1 Цикл
		Если БИ.Субконто().ЭтоГруппа() = 0 Тогда
			Если ((ГруппаНМА.Выбран() = 0) или (БИ.Субконто().Группа = ГруппаНМА)) Тогда
			    Результат.НоваяСтрока();     
				Результат.ГруппаНМА = БИ.Субконто().Группа;
				Результат.НМА = БИ.Субконто();
				Результат.нБаланс = 0;
				Результат.кБаланс = 0;
				Результат.оБаланс = 0;
				Результат.нАмортизация = 0;
				Результат.кАмортизация = 0;
				Результат.оАмортизация = 0;
				БИ.ВыбратьСчета(-1,0,1,0);
				Если Результат.НМА.ПорядокНачисленияАмортизации < 3 Тогда
					Если БИ.ПолучитьСчет(1,Сч04) = 1 Тогда
						Результат.нБаланс = БИ.СНД();
						Результат.кБаланс = БИ.СКД();
						Результат.оБаланс = БИ.ДО()-БИ.КО();
					КонецЕсли;
					Если БИ.ПолучитьСчет(1,Сч05) = 1 Тогда
						Результат.нАмортизация = БИ.СНК();
						Результат.кАмортизация = БИ.СКК();
						Результат.оАмортизация = БИ.КО()-БИ.ДО();         
					КонецЕсли;
				Иначе
					Если БИ.ПолучитьСчет(1,Сч04) = 1 Тогда
						Если Результат.НМА.Состояние.Получить(НачДата) = Перечисление.СостоянияНМА.ПринятКУчету Тогда
							ПервоначальнаяСтоимостьНМА = Результат.НМА.ПервоначальнаяСтоимость ;
							НачальнаяАмортизацияНМА = ПервоначальнаяСтоимостьНМА - БИ.СНД();
						Иначе
							ПервоначальнаяСтоимостьНМА = 0;
							НачальнаяАмортизацияНМА = 0;
						КонецЕсли;
						Если Результат.НМА.Состояние.Получить(КонДата) = Перечисление.СостоянияНМА.Списан Тогда
							КонечнаяСтоимостьНМА = 0;
							КонечнаяАмортизацияНМА = 0;
						Иначе
							КонечнаяСтоимостьНМА = ПервоначальнаяСтоимостьНМА;
							КонечнаяАмортизацияНМА = ПервоначальнаяСтоимостьНМА - БИ.СКД();
						КонецЕсли;
						Результат.кАмортизация = КонечнаяАмортизацияНМА;
						Результат.нАмортизация = НачальнаяАмортизацияНМА;
						Результат.оАмортизация = КонечнаяАмортизацияНМА - НачальнаяАмортизацияНМА;
						
						Результат.кБаланс = КонечнаяСтоимостьНМА;
						Результат.оБаланс = КонечнаяСтоимостьНМА - ПервоначальнаяСтоимостьНМА;
						Результат.нБаланс = ПервоначальнаяСтоимостьНМА;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

    //Вставим в результирую таблицу итоги по группам субконто
	грРезультат = СоздатьОбъект("ТаблицаЗначений");
	грРезультат.Загрузить(Результат);
	грРезультат.Свернуть("ГруппаНМА","нБаланс,кБаланс,оБаланс,нАмортизация,кАмортизация,оАмортизация");
    
	грРезультат.ВыбратьСтроки();
	Результат.ВыбратьСтроки();
	Результат.ПолучитьСтроку();
    Пока грРезультат.ПолучитьСтроку() = 1 Цикл
		Если ((ОбъектНМА.Выбран() = 0) или (ОбъектНМА.ЭтоГруппа() = 1)) Тогда
			нБалансоваяСтоимость = грРезультат.нБаланс;
	    	кБалансоваяСтоимость = грРезультат.кБаланс;
	    	оБалансоваяСтоимость = грРезультат.оБаланс;
	    	нАмортизация = грРезультат.нАмортизация;
	    	кАмортизация = грРезультат.кАмортизация;
	    	оАмортизация = грРезультат.оАмортизация;
	    	нОстаточнаяСтоимость = нБалансоваяСтоимость - нАмортизация;
	    	кОстаточнаяСтоимость = кБалансоваяСтоимость - кАмортизация;
	   		Т.ВывестиСекцию("Группа");
		КонецЕсли;
        Пока Результат.ГруппаНМА = грРезультат.ГруппаНМА Цикл
			нБалансоваяСтоимость = Результат.нБаланс;
    		кБалансоваяСтоимость = Результат.кБаланс;
    		оБалансоваяСтоимость = Результат.оБаланс;
    		нАмортизация = Результат.нАмортизация;
    		кАмортизация = Результат.кАмортизация;
    		оАмортизация = Результат.оАмортизация;                     
			нОстаточнаяСтоимость = нБалансоваяСтоимость - нАмортизация;
    		кОстаточнаяСтоимость = кБалансоваяСтоимость - кАмортизация;
    		Если ВариантФормирования = 1 Тогда
    			Расшифровка.Установить("Субконто1", Результат.НМА);
			КонецЕсли;
			НаименованиеНМА = Результат.НМА.Наименование + ", инв.№" + Результат.НМА.Код;
    		Т.ВывестиСекцию("НМА");	
			Если Результат.ПолучитьСтроку() = 0 Тогда
			    Прервать;
			КонецЕсли;
    	КонецЦикла;
	КонецЦикла;
	ИТОГОнБалансоваяСтоимость = грРезультат.Итог("нБаланс");
    ИТОГОкБалансоваяСтоимость = грРезультат.Итог("кБаланс");
    ИТОГОоБалансоваяСтоимость = грРезультат.Итог("оБаланс");
    ИТОГОнАмортизация = грРезультат.Итог("нАмортизация");
    ИТОГОкАмортизация = грРезультат.Итог("кАмортизация");
    ИТОГОоАмортизация = грРезультат.Итог("оАмортизация");
    ИТОГОнОстаточнаяСтоимость = ИТОГОнБалансоваяСтоимость - ИТОГОнАмортизация;
    ИТОГОкОстаточнаяСтоимость = ИТОГОкБалансоваяСтоимость - ИТОГОкАмортизация;  
    Если ((ГруппаНМА.Выбран()=0) И ((ОбъектНМА.Выбран()=0) или (ОбъектНМА.ЭтоГруппа() = 1))) Тогда
		Т.ВывестиСекцию("Итого");
	Иначе
		Т.ВывестиСекцию("Подвал");
    КонецЕсли;


	Т.Опции(0,0,6,0,"ОпцииПечатиОтчетаПоНМА");
	Т.ОбластьПечати(3);
	Т.ТолькоПросмотр(1);
	Т.Показать("Отчет по нематериальным активам");
КонецПроцедуры
//_____________________________________________________________________________
Процедура ПриОткрытии()

	Сч04 = СчетПоКоду("04.1");
	Сч05 = СчетПоКоду("05");

	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		ВариантФормирования = глРасшифровка.Получить("ВариантФормирования");
		ДатаНач = глРасшифровка.Получить("ДатаНач");
		ДатаКон = глРасшифровка.Получить("ДатаКон");
		ДокументНачисленияАмортизации = глРасшифровка.Получить("ДокументНачисленияАмортизации");
		ГруппаНМА = глРасшифровка.Получить("ГруппаНМА");
		ОбъектНМА = глРасшифровка.Получить("ОбъектНМА");

		Если ДатаНач = Дата(0) Тогда
			ДатаНач = НачалоПериодаБИ();
		КонецЕсли;
		Если ДатаКон = Дата(0) Тогда
			ДатаКон = КонецПериодаБИ();
		КонецЕсли;

		Если Обновить <> 0 Тогда
			Т = глТаблица;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		ВариантФормирования = 1;
		ДатаНач = НачалоПериодаБИ();
		ДатаКон = КонецПериодаБИ();
		ДокументНачисленияАмортизации = "";
	КонецЕсли;
	ПриВыбореВарианта();
КонецПроцедуры //ПриОткрытии

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(ИдентЭлемДиалога, ФлагСтандОбр)
	
	Если ИдентЭлемДиалога = "ОбъектНМА" Тогда
		ОткрытьФорму("Справочник.НематериальныеАктивы.ДляВыбора", "НМА");
		ФлагСтандОбр = 0;
	КонецЕсли;
	
КонецПроцедуры
