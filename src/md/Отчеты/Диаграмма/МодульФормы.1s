Перем Ит, ВидДиаграммы, ДтКт, ТекущийСлой;
Перем СписокСубконто, ДобавитьСубконто;
Перем Серии, ПоТаблице, ПоТочкам, ВидСерий, ВидТочек;
Перем ПроверятьЗначения;
Перем ПланСчетов;
Перем ВыбПланСчетов;
Перем ПредставлениеРУ;
Перем Расшифровка;
Перем Т;
Перем Обновить;

//-----------------------------------------------
Функция ПолучитьВидСуммы()
	Если ВыбВидСуммы = 1 Тогда
		Возврат 1;
	ИначеЕсли ВыбВидСуммы = 2 Тогда
		Возврат 2;
	ИначеЕсли ВыбВидСуммы = 3 Тогда
		Возврат 4;
	КонецЕсли;
КонецФункции

//-----------------------------------------------
Процедура УстановитьВладельца(ПоВсем, РУ, Вид, Форма)
	Назначить = 0;
	Если ТипЗначенияСтр(РУ) = "Справочник" Тогда
		Если ПоВсем = 0 Тогда
			МДСубконто = Метаданные.Справочник(Вид);
			Если МДСубконто.Выбран() = 1 Тогда
				Если МДСубконто.Владелец.Выбран() = 1 Тогда
					Если МДСубконто.Владелец.Идентификатор = РУ.Вид() Тогда
						Назначить = 1;
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если Назначить = 1 Тогда
		Форма.ИспользоватьВладельца(РУ);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Функция ПроверкаПериода(Дата1, Дата2)
	Если ПустоеЗначение(Дата1) = 1 Тогда
	    Предупреждение("Не указана дата начала периода отчета!");
		Возврат 0;
	КонецЕсли;
	Если Дата1 > Дата2 Тогда
		Предупреждение("Неправильно задан период отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если Дата2 > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		               "Расчет итогов выполняется в режиме"+РазделительСтрок+
					   """Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Функция НайтиМесто(Значение)
	Н = 1;
	К = Серии.КоличествоСтрок();
	Пока Н<=К Цикл
		С = Цел((Н+К)/2);
		Если Серии.ПолучитьЗначение(С, "Значение") > Значение Тогда
			Н = С+1;
		ИначеЕсли Серии.ПолучитьЗначение(С, "Значение") < Значение Тогда
			К = С-1;
		Иначе
			Возврат С;
		КонецЕсли;
	КонецЦикла;
	Возврат Н;
КонецФункции

//-----------------------------------------------
Функция ИмяСерии(Имя)
	Если СтрДлина(Имя) > 20 Тогда
		Имя = Лев(Имя, 17)+"...";
	КонецЕсли;
	Возврат Имя;
КонецФункции

//-----------------------------------------------
Функция ПолучитьЗначение()
	Если ПоТаблице = 1 Тогда
		Если ПоТочкам = 0 Тогда
			Возврат Серии.Значение;
		ИначеЕсли Серии.Точки.НомерСтроки < 1 Тогда
			Возврат Серии.Значение;
		Иначе
			Возврат Серии.Точки.Значение;
		КонецЕсли;
	ИначеЕсли ВыбДтКт = 1 Тогда
		Если ВыбВидИтогов = 1 Тогда
			Возврат Ит.СНД(ВыбВидСуммы);
		ИначеЕсли ВыбВидИтогов = 2 Тогда
			Возврат Ит.ДО(ВыбВидСуммы);
		Иначе
			Возврат Ит.СКД(ВыбВидСуммы);
		КонецЕсли;
	ИначеЕсли ВыбДтКт = 2 Тогда
		Если ВыбВидИтогов = 1 Тогда
			Возврат Ит.СНК(ВыбВидСуммы);
		ИначеЕсли ВыбВидИтогов = 2 Тогда
			Возврат Ит.КО(ВыбВидСуммы);
		Иначе
			Возврат Ит.СКК(ВыбВидСуммы);
		КонецЕсли;
	Иначе
		Если ВыбВидИтогов = 1 Тогда
			Возврат Ит.СНД(ВыбВидСуммы)-Ит.СНК(ВыбВидСуммы);
		ИначеЕсли ВыбВидИтогов = 2 Тогда
			Возврат Ит.ДО(ВыбВидСуммы)-Ит.КО(ВыбВидСуммы);
		Иначе
			Возврат Ит.СКД(ВыбВидСуммы)-Ит.СКК(ВыбВидСуммы);
		КонецЕсли;
	КонецЕсли;
КонецФункции

//-----------------------------------------------
Процедура ВыбратьСерии()
	Если ПоТаблице = 1 Тогда
		Серии.ВыбратьСтроки();
	ИначеЕсли ВидСерий = 1 Тогда
		Ит.ВыбратьСчета(, ДтКт);
	ИначеЕсли ВидСерий = 2 Тогда
		Ит.ВыбратьВалюты(, ДтКт);
	ИначеЕсли ВидСерий = 3 Тогда
		Ит.ВыбратьПериоды(, ДтКт);
	Иначе
		Ит.ВыбратьСубконто(ВыбВидСубконтоСерий, , ДтКт);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Функция ПолучитьСерию()
	Если ПоТаблице = 1 Тогда
		Возврат Серии.ПолучитьСтроку();
	ИначеЕсли ВидСерий = 1 Тогда
		Возврат Ит.ПолучитьСчет();
	ИначеЕсли ВидСерий = 2 Тогда
		Возврат Ит.ПолучитьВалюту();
	ИначеЕсли ВидСерий = 3 Тогда
		Возврат Ит.ПолучитьПериод();
	Иначе
		Возврат Ит.ПолучитьСубконто(ВыбВидСубконтоСерий);
	КонецЕсли;
КонецФункции

//-----------------------------------------------
Функция ЗначениеСерии()
	Если ПоТаблице = 1 Тогда
		Возврат Серии.Серия;
	ИначеЕсли ВидСерий = 1 Тогда
		Возврат Ит.Счет;
	ИначеЕсли ВидСерий = 2 Тогда
		Возврат Ит.Валюта;
	ИначеЕсли ВидСерий = 3 Тогда
		Возврат ПериодСтр(Ит.НачДата, Ит.КонДата);
	Иначе
		Возврат Ит.Субконто(ВыбВидСубконтоСерий);
	КонецЕсли;
КонецФункции

//-----------------------------------------------
Процедура ВыбратьТочки()
	Если ПоТаблице = 1 Тогда
		Серии.Точки.ВыбратьСтроки();
	ИначеЕсли ВидТочек = 1 Тогда
		Ит.ВыбратьСчета(1, ДтКт);
	ИначеЕсли ВидТочек = 2 Тогда
		Ит.ВыбратьВалюты(1, ДтКт);
	ИначеЕсли ВидТочек = 3 Тогда
		Ит.ВыбратьПериоды(1, ДтКт);
	Иначе
		Ит.ВыбратьСубконто(ВыбВидСубконтоТочек, 1, ДтКт);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Функция ПолучитьТочку()
	Если ПоТаблице = 1 Тогда
		Возврат Серии.Точки.ПолучитьСтроку();
	ИначеЕсли ВидТочек = 1 Тогда
		Возврат Ит.ПолучитьСчет();
	ИначеЕсли ВидТочек = 2 Тогда
		Возврат Ит.ПолучитьВалюту();
	ИначеЕсли ВидТочек = 3 Тогда
		Возврат Ит.ПолучитьПериод();
	Иначе
		Возврат Ит.ПолучитьСубконто(ВыбВидСубконтоТочек);
	КонецЕсли;
КонецФункции

//-----------------------------------------------
Функция ЗначениеТочки()
	Если ПоТаблице = 1 Тогда
		Возврат Серии.Точки.Точка;
	ИначеЕсли ВидТочек = 1 Тогда
		Возврат Ит.Счет;
	ИначеЕсли ВидТочек = 2 Тогда
		Возврат Ит.Валюта;
	ИначеЕсли ВидТочек = 3 Тогда
		Возврат ПериодСтр(Ит.НачДата, Ит.КонДата);
	Иначе
		Возврат Ит.Субконто(ВыбВидСубконтоТочек);
	КонецЕсли;
КонецФункции

//-----------------------------------------------
Процедура ЗаполнитьТочки(Точки)
	Точки = СоздатьОбъект("ТаблицаЗначений");
	Точки.НоваяКолонка("Точка");
	Точки.НоваяКолонка("Значение");
	ВыбратьТочки();
	Пока ПолучитьТочку() = 1 Цикл
		Состояние("Построение графика: "+ЗначениеСерии()+"  "+ЗначениеТочки());
		Точки.НоваяСтрока();
		Точки.Точка = ЗначениеТочки();
		Точки.Значение = ПолучитьЗначение();
	КонецЦикла;
КонецПроцедуры

//-----------------------------------------------
Процедура СложитьЗначенияТочек(Точки1, Точки2)
	Если Точки1.КоличествоСтрок() = 0 Тогда
		Точки1.Загрузить(Точки2);
	Иначе
		Точки1.ВыбратьСтроки();
		Точки2.ВыбратьСтроки();
		Пока (Точки1.ПолучитьСтроку()= 1) И (Точки2.ПолучитьСтроку()= 1) Цикл
			Точки1.Значение = Точки1.Значение+Точки2.Значение;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ЗаполнитьТаблицу()
	Перем Точки;
	ПоТаблице = 0;
	Если ВыбКоличествоСерий > 0 Тогда
		Серии = СоздатьОбъект("ТаблицаЗначений");
		Серии.НоваяКолонка("Серия");
		Серии.НоваяКолонка("Значение");
		ПрочиеЗначения = 1;
		ПрочиеЗначения = 0;
		Если ПоТочкам = 1 Тогда
			Серии.НоваяКолонка("Точки");
			ПрочиеЗначенияТочек = СоздатьОбъект("ТаблицаЗначений");
			ПрочиеЗначенияТочек.НоваяКолонка("Точка");
			ПрочиеЗначенияТочек.НоваяКолонка("Значение");
		КонецЕсли;

		ВключитьПрочие = 0;
        ВыбратьСерии();
		Пока ПолучитьСерию() = 1 Цикл
			Значение = ПолучитьЗначение();
			Серия = ЗначениеСерии();
			Состояние("Построение графика: "+Серия);
			Если Серии.КоличествоСтрок() < ВыбКоличествоСерий Тогда
				Серии.НоваяСтрока(НайтиМесто(Значение));
				Серии.Серия = Серия;
				Серии.Значение = Значение;
				Если ПоТочкам = 1 Тогда
					ЗаполнитьТочки(Точки);
					Серии.Точки = Точки;
				КонецЕсли;
			Иначе
				ВключитьПрочие = 1;
				А = НайтиМесто(Значение);
				Если А <= ВыбКоличествоСерий Тогда
					Серии.НоваяСтрока(А);
					Серии.Серия = Серия;
					Серии.Значение = Значение;
					Если ПоТочкам = 1 Тогда
						ЗаполнитьТочки(Точки);
						Серии.Точки = Точки;
						Точки = Серии.ПолучитьЗначение(ВыбКоличествоСерий+1, "Точки");
						СложитьЗначенияТочек(ПрочиеЗначенияТочек, Точки);
					КонецЕсли;
					Значение = Серии.ПолучитьЗначение(ВыбКоличествоСерий+1, "Значение");
				    ПрочиеЗначения = ПрочиеЗначения+Значение;
					Серии.УдалитьСтроку(ВыбКоличествоСерий+1);
				Иначе
					ПрочиеЗначения = ПрочиеЗначения+Значение;
					Если ПоТочкам = 1 Тогда
						ЗаполнитьТочки(Точки);
						СложитьЗначенияТочек(ПрочиеЗначенияТочек, Точки);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если ВключитьПрочие = 1 Тогда
			Серии.НоваяСтрока();
			Серии.Серия = "Прочие";
			Серии.Значение = ПрочиеЗначения;
			Если ПоТочкам = 1 Тогда
				Серии.Точки = ПрочиеЗначенияТочек;
			КонецЕсли;
		КонецЕсли;
		ПоТаблице = 1;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПроверитьЗначение(Значение)
	Если ПроверятьЗначения = 1 Тогда
		Если Значение < 0 Тогда
			Сообщить("В процессе построения графика встретились отрицательные значения!");
			Сообщить("График будет построен в виде гистограммы!");
			ПроверятьЗначения = 0;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура График_Диаграмма(Диаграмма)
	Диаграмма.Обновление(0);
	ПоТочкам = ?((ВидДиаграммы = "Круговая") Или (ВидТочек = 0), 0, 1);
	Если (ВидДиаграммы = "Круговая") Или (ВидДиаграммы = "Многоярусная") Тогда
		ПроверятьЗначения = 1;
	Иначе
		ПроверятьЗначения = 0;
	КонецЕсли;
	ЗаполнитьТаблицу();
	Серия = 1;
	ВыбратьСерии();
	Пока ПолучитьСерию() = 1 Цикл
		ЗначСерии = ЗначениеСерии();
		Состояние("Построение графика: "+ЗначСерии);
		Диаграмма.УстановитьИмяСерии(Серия, ИмяСерии(""+ЗначСерии));
		Если ПоТочкам = 0 Тогда
			Значение = ПолучитьЗначение();
			ПроверитьЗначение(Значение);
			Диаграмма.УстановитьЗначение(1, Серия, Значение, ""+ЗначСерии+": "+Значение);
		Иначе
			Точка = 1;
			ВыбратьТочки();
			Пока ПолучитьТочку() = 1 Цикл
				ЗначТочки = ЗначениеТочки();
				Состояние("Построение графика: "+ЗначСерии+"  "+ЗначТочки);
				Если Серия = 1 Тогда
					Диаграмма.УстановитьИмяТочки(Точка, ""+ЗначТочки);
				КонецЕсли;
				Значение = ПолучитьЗначение();
				ПроверитьЗначение(Значение);
				Диаграмма.УстановитьЗначение(Точка, Серия, Значение, ""+ЗначСерии+" "+ЗначТочки+": "+Значение);
				Точка = Точка + 1;
			КонецЦикла;
		КонецЕсли;
		Серия = Серия + 1;
	КонецЦикла;
	Серии = "";
	Диаграмма.Обновление(1);
КонецПроцедуры

//-----------------------------------------------
Процедура График(ФлагЗакрытияФормы = 0)
	Перем НачПериода, КонПериода, Валюта, Заголовок;

	Если ВыбВидСерий.ТекущаяСтрока() = 0 Тогда
		Предупреждение("Не указан вид разреза для серий графика!");
		Возврат;
	КонецЕсли;


	Ит = СоздатьОбъект("БухгалтерскиеИтоги");

	Периодичность = "Период";

	ВидСерий = ВыбВидСерий.ПолучитьЗначение(ВыбВидСерий.ТекущаяСтрока());
	Если ВыбВидТочек.ТекущаяСтрока() = 0 Тогда
		ВидТочек = 0;
	Иначе
		ВидТочек = ВыбВидТочек.ПолучитьЗначение(ВыбВидТочек.ТекущаяСтрока());
	КонецЕсли;

	ДтКт = ВыбДтКт;
	Если ВыбВидИтогов <> 2 Тогда
		ДтКт = 0;
	КонецЕсли;
	
	ВидДиаграммы = ВыбВидДиаграммы.ПолучитьЗначение(ВыбВидДиаграммы.ТекущаяСтрока());
	Если ВидДиаграммы <> "Круговая" Тогда
		Если (ВидСерий = 4) и (ВидТочек = 4) Тогда
			Если ВыбВидСубконтоТочек = ВыбВидСубконтоСерий Тогда
				Предупреждение("Выбраны одинаковые виды субконто!");
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

    Если (ВидСерий = 1) Тогда
		Ит.ВключатьСубсчета(-1);
	ИначеЕсли ВидСерий = 3 Тогда
		Периодичность = ВыбПериодичностьСерий.ПолучитьЗначение(ВыбПериодичностьСерий.ТекущаяСтрока());
	ИначеЕсли ВидСерий = 4 Тогда
		Ит.ИспользоватьСубконто(ВыбВидСубконтоСерий, ВыбСубконтоСерий, 1);
	КонецЕсли;

	Если ВидДиаграммы <> "Круговая" Тогда
		Если ВидТочек = 1 Тогда
			Ит.ВключатьСубсчета(-1);
		ИначеЕсли ВидТочек = 3 Тогда
			Периодичность = ВыбПериодичностьТочек.ПолучитьЗначение(ВыбПериодичностьТочек.ТекущаяСтрока());
		ИначеЕсли ВидТочек = 4 Тогда
			Ит.ИспользоватьСубконто(ВыбВидСубконтоТочек, ВыбСубконтоТочек, 1);
		КонецЕсли;
	КонецЕсли;

	Заголовок = "";
	Если ВыбВидИтогов = 1 Тогда
		Если ВыбДтКт = 1 Тогда
			Заголовок = "Начальные дебетовые остатки";
		ИначеЕсли ВыбДтКт = 2 Тогда
			Заголовок = "Начальные кредитовые остатки";
		ИначеЕсли ВыбДтКт = 3 Тогда
			Заголовок = "Начальные остатки";
		КонецЕсли;
	ИначеЕсли ВыбВидИтогов = 2 Тогда
		Если ВыбДтКт = 1 Тогда
			Заголовок = "Дебетовые обороты";
		ИначеЕсли ВыбДтКт = 2 Тогда
			Заголовок = "Кредитовые обороты";
		ИначеЕсли ВыбДтКт = 3 Тогда
			Заголовок = "Обороты";
		КонецЕсли;
	ИначеЕсли ВыбВидИтогов = 3 Тогда
		Если ВыбДтКт = 1 Тогда
			Заголовок = "Конечные дебетовые остатки";
		ИначеЕсли ВыбДтКт = 2 Тогда
			Заголовок = "Конечные кредитовые остатки";
		ИначеЕсли ВыбДтКт = 3 Тогда
			Заголовок = "Конечные остатки";
		КонецЕсли;
	КонецЕсли;

	Если ВыбСчет.Выбран() = 1 Тогда
		Заголовок = Заголовок + " по счету "+ВыбСчет;
	КонецЕсли;

	Если ВыбВалюта.Выбран() = 1 Тогда
		Валюта = ВыбВалюта;
		Заголовок = Заголовок + " по валюте "+ВыбВалюта;
	КонецЕсли;
	
	Если (ВыбВидИтогов = 1) И (Периодичность = "Период") Тогда
		НачПериода = Дата1;
		Заголовок = Заголовок+" на "+Дата1;
		Если ПроверкаПериода(НачПериода, НачПериода) = 0 Тогда
			Возврат;
		КонецЕсли;
	ИначеЕсли (ВыбВидИтогов = 3) И (Периодичность = "Период") Тогда
		КонПериода = Дата2;
		Заголовок = Заголовок+" на "+Дата2;
		Если ПроверкаПериода(КонПериода, КонПериода) = 0 Тогда
			Возврат;
		КонецЕсли;
	Иначе
		НачПериода = Дата1;
		КонПериода = Дата2;
		Заголовок = Заголовок+" за "+ПериодСтр(Дата1, Дата2);
		Если ПроверкаПериода(НачПериода, КонПериода) = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

    Если ВыбПланСчетов = 1 Тогда
        ПланСчетов = ВыбранныйПланСчетов();
    КонецЕсли;
    Ит.ИспользоватьПланСчетов(ПланСчетов);
    Ит.ИспользоватьРазделительУчета(РазделительУчета);
	Если Ит.ВыполнитьЗапрос(НачПериода, КонПериода, ВыбСчет,, Валюта, 1, Периодичность, ПолучитьВидСуммы()) = 0 Тогда
		Возврат;
	КонецЕсли;

	Если (ТипЗначенияСтр(Т) <> "Таблица") Или (Обновить = 0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
	Т.ИсходнаяТаблица("График");

	Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "Диаграмма");
	Расшифровка.Установить("Дата1", Дата1);
	Расшифровка.Установить("Дата2", Дата2);
	Расшифровка.Установить("Счет", ВыбСчет);
   	Расшифровка.Установить("Валюта", ВыбВалюта);
   	Расшифровка.Установить("ВидИтогов", ВыбВидИтогов);
   	Расшифровка.Установить("ДтКт", ВыбДтКт);
   	Расшифровка.Установить("ВидСуммы", ВыбВидСуммы);

   	Расшифровка.Установить("ВидДиаграммы", ВыбВидДиаграммы.ПолучитьЗначение(ВыбВидДиаграммы.ТекущаяСтрока()));

   	Расшифровка.Установить("ВидСерий", ВыбВидСерий.ПолучитьЗначение(ВыбВидСерий.ТекущаяСтрока()));
   	Расшифровка.Установить("ВидСубконтоСерий", ВыбВидСубконтоСерий);
   	Расшифровка.Установить("СубконтоСерий", ВыбСубконтоСерий);
   	Расшифровка.Установить("ПериодичностьСерий", ВыбПериодичностьСерий.ПолучитьЗначение(ВыбПериодичностьСерий.ТекущаяСтрока()));
    Расшифровка.Установить("КоличествоСерий", ВыбКоличествоСерий);

   	Расшифровка.Установить("ВидТочек", ВыбВидТочек.ПолучитьЗначение(ВыбВидТочек.ТекущаяСтрока()));
   	Расшифровка.Установить("ВидСубконтоТочек", ВыбВидСубконтоТочек);
   	Расшифровка.Установить("СубконтоТочек", ВыбСубконтоТочек);
   	Расшифровка.Установить("ПериодичностьТочек", ВыбПериодичностьТочек.ПолучитьЗначение(ВыбПериодичностьТочек.ТекущаяСтрока()));

   	Расшифровка.Установить("РазделительУчета", РазделительУчета);
   	Расшифровка.Установить("ПланСчетов", ПланСчетов);
   	
   	НазваниеОрганизации = глНазваниеОрганизации(Контекст);
	Т.ВывестиСекцию("Заголовок");
	Т.ВывестиСекцию(ВидДиаграммы);
	
	НазваниеОрганизации = ?(ПустоеЗначение(НазваниеОрганизации)=1, "", " "+НазваниеОрганизации);
	ВерхнийКолонтитул = "Диаграмма: "+Заголовок+НазваниеОрганизации;
	НижнийКолонтитул = "Отчет сформирован "+ТекущаяДата()+" "+ТекущееВремя()+?(ПустоеЗначение(ИмяПользователя())=0,"  Пользователь: "+ИмяПользователя(),"");
	Т.Опции(0, 0, 1, 0, "ОпцииПечатиДиаграмма");
	Т.ОбластьПечати(2);
	Т.ТолькоПросмотр(1);
	Т.Показать(ВерхнийКолонтитул, "");
	Ит = "";
	Если (ФлагЗакрытияФормы = 1) Или (Обновить = 2) Тогда
         СтрокаДействийФормы = "#Закрыть";
    КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВидаСерий() Далее
Процедура ПриВыбореВидаТочек() Далее

Процедура ПриВыбореСчета()
	НазначитьСчет(ВыбВидСубконтоСерий, ВыбСчет, 1);
	Форма.ВыбСубконтоСерий.НазначитьТип(ВыбВидСубконтоСерий);
	НазначитьСчет(ВыбВидСубконтоТочек, ВыбСчет, 2);
	Форма.ВыбСубконтоТочек.НазначитьТип(ВыбВидСубконтоТочек);


    Если ((ВыбСчет.КоличествоСубконто() > 0) ИЛИ (ВыбСчет.Выбран() = 0)) Тогда
		Если ВыбВидСерий.РазмерСписка() < 4 Тогда 
			ВыбВидСерий.ДобавитьЗначение(4, "Субконто");
		КонецЕсли;
	Иначе
		Если ВыбВидСерий.РазмерСписка() = 4 Тогда 
			ТС = ВыбВидСерий.ТекущаяСтрока();
			ВыбВидСерий.УдалитьЗначение(4);
			Если ТС = 4 Тогда
				ВыбВидСерий.ТекущаяСтрока(1);
				ПриВыбореВидаСерий();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
    Если ((ВыбСчет.КоличествоСубконто() > 0) ИЛИ (ВыбСчет.Выбран() = 0)) Тогда
		Если ВыбВидТочек.РазмерСписка() < 5 Тогда 
			ВыбВидТочек.ДобавитьЗначение(5, "Субконто");
		КонецЕсли;
	Иначе
		Если ВыбВидТочек.РазмерСписка() = 5 Тогда 
			ТС = ВыбВидТочек.ТекущаяСтрока();
			ВыбВидТочек.УдалитьЗначение(5);
			Если ТС = 5 Тогда
				ВыбВидТочек.ТекущаяСтрока(1);
				ПриВыбореВидаТочек();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВидаСубконтоСерий()
	Форма.ВыбСубконтоСерий.НазначитьТип(ВыбВидСубконтоСерий);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВидаСубконтоТочек()
	Форма.ВыбСубконтоТочек.НазначитьТип(ВыбВидСубконтоТочек);
КонецПроцедуры

//-----------------------------------------------
Процедура ОбработкаПодбора(ВыбранноеЗначение)
	Если (ДобавитьСубконто = 1) Или (СписокСубконто.РазмерСписка() = 0) Тогда
		СписокСубконто.ДобавитьЗначение(ВыбранноеЗначение);
		СписокСубконто.ТекущаяСтрока(СписокСубконто.РазмерСписка());
	Иначе
		Позиция = СписокСубконто.ТекущаяСтрока();
		СписокСубконто.УстановитьЗначение(Позиция, ВыбранноеЗначение);
		СписокСубконто.ТекущаяСтрока(Позиция);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ВводСубконто(ВыбВидСубконто, ВыбСубконто, РежимПодбора, ДобавитьСубк)
	Перем Субк, Форма;
	Если (ВыбСубконто.РазмерСписка() > 0) И (ВыбСубконто.ТекущаяСтрока() > 0)  Тогда
		Субк = ВыбСубконто.ПолучитьЗначение(ВыбСубконто.ТекущаяСтрока());
	КонецЕсли;
	
	Если ВыбВидСубконто.Выбран() = 0 Тогда
	    Предупреждение("Укажите вид субконто");
		Возврат;
	КонецЕсли;

	СписокСубконто = ВыбСубконто;
	ДобавитьСубконто = ДобавитьСубк;

	МД = Метаданные.ВидСубконто(ВыбВидСубконто.Идентификатор());
	Тип = МД.Тип;
	Если (Тип = "Справочник") Или (Тип = "Документ") Или (Тип = "Счет") Тогда
		ТипВид = Тип;
		Если МД.Вид <> "" Тогда
			ТипВид = ТипВид+"."+МД.Вид;
		Иначе
			СписокВидов = СоздатьОбъект("СписокЗначений");
			Если Тип = "Справочник" Тогда
				Для а = 1 по Метаданные.Справочник() Цикл
					СписокВидов.ДобавитьЗначение(Метаданные.Справочник(а).Идентификатор, Метаданные.Справочник(а).Представление());
				КонецЦикла;
				
			ИначеЕсли Тип = "Документ" Тогда
				Для а = 1 по Метаданные.Документ() Цикл
					СписокВидов.ДобавитьЗначение(Метаданные.Документ(а).Идентификатор, Метаданные.Документ(а).Представление());
				КонецЦикла;
				
			КонецЕсли;
			ВыбВид = "";
			Если СписокВидов.ВыбратьЗначение(ВыбВид, "Укажите вид объекта") = 0 Тогда
				Возврат;
			КонецЕсли;
			ТипВид = ТипВид+"."+ВыбВид;
		КонецЕсли;
		глРазделительУчета = РазделительУчета;
		ОткрытьПодбор(ТипВид,,Форма, РежимПодбора, Субк);
		Если (Тип = "Справочник") Или (Тип = "Счет") Тогда
			Форма.ВыборГруппы(1);
			Если Тип = "Справочник" Тогда
				УстановитьВладельца(ПоВсемРУ, РазделительУчета, МД.Вид, Форма);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Тип = "Перечисление" Тогда
		СписокЗначений = СоздатьОбъект("СписокЗначений");
		П = Перечисление.ПолучитьАтрибут(МД.Вид);
		Для А=1 По П.КоличествоЗначений() Цикл
			СписокЗначений.ДобавитьЗначение(П.ЗначениеПоНомеру(А));
			Если РежимПодбора = 1 Тогда
				Если ВыбСубконто.НайтиЗначение(П.ЗначениеПоНомеру(А)) <> 0 Тогда
				    СписокЗначений.Пометка(А, 1);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если РежимПодбора = 1 Тогда
			Если СписокЗначений.ОтметитьЗначения(, "Выберите "+МД.Синоним) = 1 Тогда
				ВыбСубконто.УдалитьВсе();
				Для А=1 По СписокЗначений.РазмерСписка() Цикл
					Если СписокЗначений.Пометка(А) = 1 Тогда
						ВыбСубконто.ДобавитьЗначение(СписокЗначений.ПолучитьЗначение(А));
					КонецЕсли;
				КонецЦикла;
			КонецЕсли
		Иначе
			Если СписокЗначений.ВыбратьЗначение(Субк, "Выберите "+МД.Синоним) = 1 Тогда
				ОбработкаПодбора(Субк);
			КонецЕсли
		КонецЕсли;
	Иначе
		ВвестиЗначение(Субк, "Введите "+Тип, Тип, МД.Длина, МД.Точность);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриРедактированииСубконто(ВыбВидСубконто, ВыбСубконто)
	ВводСубконто(ВыбВидСубконто, ВыбСубконто, 0, 0);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореСубконто(ВыбВидСубконто, ВыбСубконто)
	ВводСубконто(ВыбВидСубконто, ВыбСубконто, 0, 1);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриДобавленииСубконто(ВыбВидСубконто, ВыбСубконто)
	ВводСубконто(ВыбВидСубконто, ВыбСубконто, 1, 1);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриУдаленииСубконто(ВыбСубконто)
	Если (ВыбСубконто.РазмерСписка() > 0) И
		 (ВыбСубконто.ТекущаяСтрока() > 0) Тогда
		ВыбСубконто.УдалитьЗначение(ВыбСубконто.ТекущаяСтрока());
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВидаСерий()
	Перем Доступность, ВидСерий;

	Если ВыбВидСерий.ТекущаяСтрока() = 0 Тогда
		ВидСерий = 0;
	Иначе
		ВидСерий = ВыбВидСерий.ПолучитьЗначение(ВыбВидСерий.ТекущаяСтрока());
	КонецЕсли;

	Если ВидСерий <> 4 Тогда
		Доступность = 0;
	Иначе
		Доступность = 1;
	КонецЕсли;
	Форма.ВыбВидСубконтоСерий.Видимость(?(ТекущийСлой = "Диаграмма", Доступность, 0));
	Форма.ВыбСубконтоСерий.Доступность(Доступность);
	Форма.ВыбратьСубконтоСерий.Доступность(Доступность);
	Форма.ДобавитьСубконтоСерий.Доступность(Доступность);
    Форма.УдалитьСубконтоСерий.Доступность(Доступность);
    Форма.УдалитьВсеСубконтоСерий.Доступность(Доступность);

	Форма.ВыбПериодичностьСерий.Видимость(?((ВидСерий = 3) И (ТекущийСлой = "Диаграмма"), 1, 0));
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВидаТочек()
	Перем Доступность, ВидТочек;

	Если ВыбВидТочек.ТекущаяСтрока() = 0 Тогда
		ВидТочек = 0;
	Иначе
		ВидТочек = ВыбВидТочек.ПолучитьЗначение(ВыбВидТочек.ТекущаяСтрока());
	КонецЕсли;

	Если ВидТочек <> 4 Тогда
		Доступность = 0;
	Иначе
		Доступность = 1;
	КонецЕсли;
	Форма.ВыбВидСубконтоТочек.Видимость(?(ТекущийСлой = "Диаграмма", Доступность, 0));
	Форма.ВыбСубконтоТочек.Доступность(Доступность);
	Форма.ВыбратьСубконтоТочек.Доступность(Доступность);
	Форма.ДобавитьСубконтоТочек.Доступность(Доступность);
    Форма.УдалитьСубконтоТочек.Доступность(Доступность);
    Форма.УдалитьВсеСубконтоТочек.Доступность(Доступность);

	Форма.ВыбПериодичностьТочек.Видимость(?((ВидТочек = 3) И (ТекущийСлой = "Диаграмма"), 1, 0));
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВидаДиаграммы()
	Перем Доступность, ВидТочек;

	Если ВыбВидТочек.ТекущаяСтрока() = 0 Тогда
		ВидТочек = 0;
	Иначе
		ВидТочек = ВыбВидТочек.ПолучитьЗначение(ВыбВидТочек.ТекущаяСтрока());
	КонецЕсли;

	Если ВыбВидДиаграммы.ПолучитьЗначение(ВыбВидДиаграммы.ТекущаяСтрока()) = "Круговая" Тогда
		Доступность = 0;
	Иначе
		Доступность = 1;
	КонецЕсли;

	Форма.ВыбВидСубконтоТочек.Доступность(Доступность);
	Форма.ВыбВидТочек.Доступность(Доступность);
	Форма.ВыбПериодичностьТочек.Доступность(Доступность);

	Если ВидТочек <> 4 Тогда
		Доступность = 0;
	КонецЕсли;

	Форма.ВыбСубконтоТочек.Доступность(Доступность);
	Форма.ВыбратьСубконтоТочек.Доступность(Доступность);
	Форма.ДобавитьСубконтоТочек.Доступность(Доступность);
    Форма.УдалитьСубконтоТочек.Доступность(Доступность);
    Форма.УдалитьВсеСубконтоТочек.Доступность(Доступность);
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореЗакладки(Номер,Значение)
	Форма.ИспользоватьСлой(Значение+",Основной", 2);
	Форма.Кн_Видеокурс.Видимость(?(ТипЗначения(Видео_Компонента)=0, 0, 1));
	ТекущийСлой = Значение;
	ПриВыбореВидаСерий();
	ПриВыбореВидаТочек();
	Если Метаданные.РазделительУчета.Выбран() = 0 Тогда
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОткрытии(ФлагЧтенияНастройки)
	Форма.ИспользоватьЗакладки(1);
	Форма.Закладки.ДобавитьЗначение("Данные", "Данные");
	Форма.Закладки.ДобавитьЗначение("Диаграмма", "Диаграмма");
	Форма.ИспользоватьСлой("Данные,Основной", 2);
	Форма.Кн_Видеокурс.Видимость(?(ТипЗначения(Видео_Компонента)=0, 0, 1));

	ТекущийСлой = "Данные";
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;

	Если Метаданные.Валюта.Выбран() = 1 Тогда
		Форма.ВыбВалюта.НазначитьТип("Справочник."+Метаданные.Валюта.Идентификатор);
	Иначе
		Форма.ВалютаТекст.Доступность(0);
		Форма.ВыбВалюта.Доступность(0);
	КонецЕсли;

	Если ФлагЧтенияНастройки = 0 Тогда
		ВыбВидДиаграммы.УдалитьВсе();
		ВыбВидДиаграммы.ДобавитьЗначение("Гистограмма", "Гистограмма");
		ВыбВидДиаграммы.ДобавитьЗначение("Многоярусная", "Многоярусная");
		ВыбВидДиаграммы.ДобавитьЗначение("Круговая", "Круговая");
		ВыбВидДиаграммы.ДобавитьЗначение("График", "График");
		ВыбВидДиаграммы.ДобавитьЗначение("Областями", "Областями");

		ВыбПериодичностьСерий.УдалитьВсе();
		ВыбПериодичностьСерий.ДобавитьЗначение("Проводка");
		ВыбПериодичностьСерий.ДобавитьЗначение("Операция");
		ВыбПериодичностьСерий.ДобавитьЗначение("День");
		ВыбПериодичностьСерий.ДобавитьЗначение("Неделя");
		ВыбПериодичностьСерий.ДобавитьЗначение("Декада");
		ВыбПериодичностьСерий.ДобавитьЗначение("Месяц");
		ВыбПериодичностьСерий.ДобавитьЗначение("Квартал");
		ВыбПериодичностьСерий.ДобавитьЗначение("Год");
		ВыбПериодичностьСерий.ТекущаяСтрока(6);

		ВыбПериодичностьТочек.УдалитьВсе();
		ВыбПериодичностьТочек.ДобавитьЗначение("Проводка");
		ВыбПериодичностьТочек.ДобавитьЗначение("Операция");
		ВыбПериодичностьТочек.ДобавитьЗначение("День");
		ВыбПериодичностьТочек.ДобавитьЗначение("Неделя");
		ВыбПериодичностьТочек.ДобавитьЗначение("Декада");
		ВыбПериодичностьТочек.ДобавитьЗначение("Месяц");
		ВыбПериодичностьТочек.ДобавитьЗначение("Квартал");
		ВыбПериодичностьТочек.ДобавитьЗначение("Год");
		ВыбПериодичностьТочек.ТекущаяСтрока(6);

		ВыбВидСерий.УдалитьВсе();
		ВыбВидСерий.ДобавитьЗначение(1, "Субсчетам");
		ВыбВидСерий.ДобавитьЗначение(2, "Валютам");
		ВыбВидСерий.ДобавитьЗначение(3, "Периодам");
		ВыбВидСерий.ДобавитьЗначение(4, "Субконто");

		ВыбВидТочек.УдалитьВсе();
		ВыбВидТочек.ДобавитьЗначение(0, "<Не использовать>");
		ВыбВидТочек.ДобавитьЗначение(1, "Субсчетам");
		ВыбВидТочек.ДобавитьЗначение(2, "Валютам");
		ВыбВидТочек.ДобавитьЗначение(3, "Периодам");
		ВыбВидТочек.ДобавитьЗначение(4, "Субконто");

		ВыбВидСерий.ТекущаяСтрока(4);
		ВыбВидТочек.ТекущаяСтрока(4);
		ВыбКоличествоСерий = 8;
		ВыбВидИтогов = 3;
		ВыбВидСуммы = 1;
		ВыбДтКт = 3;

		ПриВыбореВидаСерий();
		ПриВыбореВидаТочек();
	КонецЕсли;

	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		ВыбПланСчетов = 0;
		Если Обновить <> 0 Тогда
			ПланСчетов = глРасшифровка.Получить("ПланСчетов");
			РУ = глРасшифровка.Получить("РазделительУчета");
			Если ТипЗначенияСтр(РУ) <> "" Тогда
				РазделительУчета = РУ;
				ПоВсемРУ = 0;
			Иначе
				Форма.РазделительУчета.НазначитьТип("");
				ПоВсемРУ = 1;
			КонецЕсли;
			Дата1 = глРасшифровка.Получить("Дата1");
			Дата2 = глРасшифровка.Получить("Дата2");
			ВыбСчет = глРасшифровка.Получить("Счет");
			ПриВыбореСчета();
		   	ВыбВалюта = глРасшифровка.Получить("Валюта");
		   	ВыбВидИтогов = глРасшифровка.Получить("ВидИтогов");
		   	ВыбДтКт = глРасшифровка.Получить("ДтКт");
		   	ВыбВидСуммы = глРасшифровка.Получить("ВидСуммы");

		   	ВыбВидДиаграммы.ТекущаяСтрока(ВыбВидДиаграммы.НайтиЗначение(глРасшифровка.Получить("ВидДиаграммы")));
		   	ПриВыбореВидаДиаграммы();

		   	ВыбВидСерий.ТекущаяСтрока(ВыбВидСерий.НайтиЗначение(глРасшифровка.Получить("ВидСерий")));
		   	ПриВыбореВидаСерий();
		   	ВыбВидСубконтоСерий = глРасшифровка.Получить("ВидСубконтоСерий");
		   	ВыбСубконтоСерий.УдалитьВсе();
		   	СубконтоСерий = глРасшифровка.Получить("СубконтоСерий");
		   	Если ТипЗначенияСтр(СубконтоСерий) = "СписокЗначений" Тогда
		   		Для А=1 По СубконтоСерий.РазмерСписка() Цикл
		   			ВыбСубконтоСерий.ДобавитьЗначение(СубконтоСерий.ПолучитьЗначение(А));
		   		КонецЦикла;
		   	КонецЕсли;
		   	ВыбПериодичностьСерий.ТекущаяСтрока(ВыбПериодичностьСерий.НайтиЗначение(глРасшифровка.Получить("ПериодичностьСерий")));
		    ВыбКоличествоСерий = глРасшифровка.Получить("КоличествоСерий");

		   	ВыбВидТочек.ТекущаяСтрока(ВыбВидТочек.НайтиЗначение(глРасшифровка.Получить("ВидТочек")));
		   	ПриВыбореВидаТочек();
		   	ВыбВидСубконтоТочек = глРасшифровка.Получить("ВидСубконтоТочек");
		   	ВыбСубконтоТочек.УдалитьВсе();
		   	СубконтоТочек = глРасшифровка.Получить("СубконтоТочек");
		   	Если ТипЗначенияСтр(СубконтоТочек) = "СписокЗначений" Тогда
		   		Для А=1 По СубконтоТочек.РазмерСписка() Цикл
		   			ВыбСубконтоТочек.ДобавитьЗначение(СубконтоТочек.ПолучитьЗначение(А));
		   		КонецЦикла;
		   	КонецЕсли;
		   	ВыбПериодичностьТочек.ТекущаяСтрока(ВыбПериодичностьТочек.НайтиЗначение(глРасшифровка.Получить("ПериодичностьТочек")));

		   	Т = глТаблица;
		Иначе
			ПланСчетов = ВыбранныйПланСчетов();
			РУ = БухИтоги.ИспользоватьРазделительУчета();
			Если ТипЗначенияСтр(РУ) <> "" Тогда
				РазделительУчета = РУ;
				ПоВсемРУ = 0;
			Иначе
				Форма.РазделительУчета.НазначитьТип("");
				ПоВсемРУ = 1;
			КонецЕсли;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			График();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		ВыбПланСчетов = 1;
		ПланСчетов = ВыбранныйПланСчетов();
		РУ = БухИтоги.ИспользоватьРазделительУчета();
		Если ТипЗначенияСтр(РУ) <> "" Тогда
			РазделительУчета = РУ;
			ПоВсемРУ = 0;
		Иначе
			Форма.РазделительУчета.НазначитьТип("");
			ПоВсемРУ = 1;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ВводНового()
	Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ();
	ВидСубконтоСерий = ВыбВидСубконтоСерий;
	ВидСубконтоТочек = ВыбВидСубконтоТочек;
	ПриВыбореСчета();
	ВыбВидСубконтоСерий = ВидСубконтоСерий;
	ВыбВидСубконтоТочек = ВидСубконтоТочек;
	ПриВыбореВидаСерий();
	ПриВыбореВидаТочек();
КонецПроцедуры