//-----------------------------------------------
Перем Т;
Перем Обновить;
Перем Расшифровка;
Перем ПланСчетов;
Перем ВыбПланСчетов;
Перем ПредставлениеРУ;

//-----------------------------------------------
Функция ПроверкаПериода()
	Если ПустоеЗначение(Дата1) = 1 Тогда
	    Предупреждение("Не указана дата начала периода отчета!");
		Возврат 0;
	КонецЕсли;
	Если Дата1 > Дата2 Тогда
		Предупреждение("Неправильно задан период отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если Дата2 > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		               "Расчет итогов выполняется в режиме"+РазделительСтрок+
					   """Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	Возврат 1;
КонецФункции

//-----------------------------------------------
Функция РасшифровкаОбновить(Обновить)
	Расшифровка.Установить("Обновить", Обновить);
	Возврат Расшифровка;
КонецФункции

//-----------------------------------------------
Процедура Сформировать(ФлагЗакрытияФормы = 0)
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;

	Расшифровка = СоздатьОбъект("СписокЗначений");
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	Ит.ВключатьСубсчета(ДанныеПоСубсчетам, ДанныеПоСубсчетам);
    Если ВыбПланСчетов = 1 Тогда
        ПланСчетов = ВыбранныйПланСчетов();
    КонецЕсли;
    Ит.ИспользоватьПланСчетов(ПланСчетов);
    Ит.ИспользоватьРазделительУчета(РазделительУчета);
	Если ПоВалюте = 1 Тогда
		Если Ит.ВыполнитьЗапрос(Дата1, Дата2,,, Валюта, 2) = 0 Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Если Ит.ВыполнитьЗапрос(Дата1, Дата2,,,, 2) = 0 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если (ТипЗначенияСтр(Т) <> "Таблица")ИЛИ(Обновить=0) Тогда
	   	Т = СоздатьОбъект("Таблица");
	Иначе
	 	Т.Очистить();
	КонецЕсли;
	Т.ИсходнаяТаблица("Таблица");
	
	НазваниеОрганизации = глНазваниеОрганизации(Контекст);

	Расшифровка.Установить("Отчет", "СводныеПроводки");
    Расшифровка.Установить("ПланСчетов", ПланСчетов);
    Расшифровка.Установить("РазделительУчета", РазделительУчета);
	Расшифровка.Установить("Дата1", Дата1);
	Расшифровка.Установить("Дата2", Дата2);
	Расшифровка.Установить("ДтКт", Порядок);

   	Расшифровка.Установить("ДанныеПоСубсчетам", ДанныеПоСубсчетам);
	Расшифровка.Установить("Валюта", Валюта);
	Расшифровка.Установить("ПоВалюте", ПоВалюте);
	Т.ВывестиСекцию("Секция_6");
	Т.ВывестиСекцию("Секция_1");
	Расшифровка.Установить("Обновить");
   	Расшифровка.Установить("ДанныеПоСубсчетам");
	Расшифровка.Установить("Отчет", "ОтчетПоПроводкам");
	Ит.ВыбратьСчета();
	Пока Ит.ПолучитьСчет() = 1 Цикл
		Расшифровка.Установить("Счет", Ит.Счет);
		Ит.ВыбратьКорСчета();
		Пока Ит.ПолучитьКорСчет() = 1 Цикл
			Если ?(Порядок=1,Ит.КорДО(),Ит.КорКО()) <> 0 Тогда
				Расшифровка.Установить("КорСчет", Ит.КорСчет);
				Если ПоВалюте = 0 Тогда
					Т.ВывестиСекцию("Секция_2");
					Если (Ит.Счет.Валютный = 1) или (Ит.КорСчет.Валютный = 1) Тогда
						Расшифровка.Установить("ПоВалюте", 1);
						Ит.ВыбратьВалюты();
						Пока Ит.ПолучитьВалюту() = 1 Цикл
							Если (?(Порядок=1,Ит.КорДО(),Ит.КорКО()) <> 0) или (?(Порядок=1,Ит.КорДО(2),Ит.КорКО(2)) <> 0) Тогда
								Расшифровка.Установить("Валюта", Ит.Валюта);
								Т.ВывестиСекцию("Секция_3");
							КонецЕсли;
						КонецЦикла;
						Расшифровка.Установить("Валюта", Валюта);
						Расшифровка.Установить("ПоВалюте", ПоВалюте);
					КонецЕсли;
				Иначе
					Т.ВывестиСекцию("Секция_4");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Расшифровка.Установить("КорСчет");
	КонецЦикла;
	Т.ВывестиСекцию("Секция_5");
    Ит = 0;
	
	НазваниеОрганизации = ?(ПустоеЗначение(НазваниеОрганизации)=1, "", " "+НазваниеОрганизации);
	ПериодИОрганизация = " ("+ПериодСтр(Дата1, Дата2)+")"+НазваниеОрганизации;
	ВерхнийКолонтитул = "Сводные проводки"+?(ПоВалюте=1," "+Валюта, "")+ПериодИОрганизация;
	НижнийКолонтитул = "Отчет сформирован "+ТекущаяДата()+" "+ТекущееВремя()+?(ПустоеЗначение(ИмяПользователя())=0,"  Пользователь: "+ИмяПользователя(),"");
    Т.ТолькоПросмотр(1);
	Т.Опции(0, 0, 6, 0, "ОпцииПечатиСводныеПроводки", "СводныеПроводки");
	Т.ОбластьПечати(2);
	Т.ПовторятьПриПечатиСтроки(6,6);
	Т.ПараметрыСтраницы(1,,,,,,,,, 1);
	Т.Показать(ВерхнийКолонтитул, "");

	Если (ФлагЗакрытияФормы = 1) Или (Обновить = 2) Тогда
         СтрокаДействийФормы = "#Закрыть";
    КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореВалюты()
	ПоВалюте = Валюта.Выбран();
КонецПроцедуры

//-----------------------------------------------
Процедура ПриВыбореПоВсемРУ()
	Если ПоВсемРУ = 1 Тогда
		Форма.РазделительУчета.НазначитьТип("");
	Иначе
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ПриОткрытии(ИспНастройки)
	Если Метаданные.РазделительУчета.Выбран() = 1 Тогда
		ПредставлениеРУ = Метаданные.РазделительУчета.Представление();
		Форма.РазделительУчета.НазначитьТип(Метаданные.РазделительУчета);
		Форма.Текст.Видимость(0);
	Иначе
		Форма.РазделительУчета.Видимость(0);
		Форма.ТекстРУ.Видимость(0);
		Форма.ПоВсемРУ.Видимость(0);
	КонецЕсли;

	Если ИспНастройки=0 Тогда
		ДанныеПоСубсчетам=1;
		Дата1 = НачалоПериодаБИ();
		Дата2 = КонецПериодаБИ();
		Порядок = 1;
	КонецЕсли;
	
	Если Метаданные.Валюта.Выбран() = 1 Тогда                 
		Форма.Валюта.НазначитьТип("Справочник."+Метаданные.Валюта.Идентификатор);
	Иначе
		Форма.ВалютаТекст.Доступность(0);
		Форма.Валюта.Доступность(0);
		Форма.ПоВалюте.Доступность(0);
	КонецЕсли;

	Если глФлагРасшифровки = 1 Тогда
		Обновить = глОбновить;
		ВыбПланСчетов = 0;
		Если Обновить <> 0 Тогда
			ПланСчетов = глРасшифровка.Получить("ПланСчетов");
			РУ = глРасшифровка.Получить("РазделительУчета");
			Если ТипЗначенияСтр(РУ) <> "" Тогда
				РазделительУчета = РУ;
				ПоВсемРУ = 0;
			Иначе
				Форма.РазделительУчета.НазначитьТип("");
				ПоВсемРУ = 1;
			КонецЕсли;
			Дата1 = глРасшифровка.Получить("Дата1");
			Дата2 = глРасшифровка.Получить("Дата2");
		   	ДанныеПоСубсчетам = глРасшифровка.Получить("ДанныеПоСубсчетам");
			Валюта = глРасшифровка.Получить("Валюта");
			ПоВалюте = глРасшифровка.Получить("ПоВалюте");
			Порядок = глРасшифровка.Получить("ДтКт");
			Т = глТаблица;
		Иначе
			ПланСчетов = ВыбранныйПланСчетов();
			РУ = БухИтоги.ИспользоватьРазделительУчета();
			Если ТипЗначенияСтр(РУ) <> "" Тогда
				РазделительУчета = РУ;
				ПоВсемРУ = 0;
			Иначе
				Форма.РазделительУчета.НазначитьТип("");
				ПоВсемРУ = 1;
			КонецЕсли;
		КонецЕсли;

		Если Обновить <> 2 Тогда
			Сформировать();
			СтатусВозврата(0);
			Возврат;
		КонецЕсли;
	Иначе
		Обновить = 0;
		ВыбПланСчетов = 1;
		ПланСчетов = ВыбранныйПланСчетов();
		РазделительУчета = БухИтоги.ИспользоватьРазделительУчета();
	КонецЕсли;
КонецПроцедуры

//-----------------------------------------------
Процедура ВводНового() //Считывание существующей настройки
	Дата1 = НачалоПериодаБИ();
	Дата2 = КонецПериодаБИ();
КонецПроцедуры
