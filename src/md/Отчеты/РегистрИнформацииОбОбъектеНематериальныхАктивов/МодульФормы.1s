//******************************************************************************
// ПроверкаПериода()
//
// Возвращаемое значение:
//  1 - корректно выбран период в диалоге
//  0 - не корректно выбран период в диалоге
//
// Описание:
// Функция проверяет корректность ввода интервала дат в дилоге и рассчитаны ли итоги за заданный период
//
Функция ПроверкаПериода()                                                                             
	
	Если НачДата > КонДата Тогда
		Предупреждение("Неправильно задан период формирования отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если КонДата > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		"Расчет итогов выполняется в режиме"+РазделительСтрок+
		"""Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	
	Возврат 1;
	
КонецФункции // ПроверкаПериода()

//******************************************************************************
Процедура Сформировать()
	
	Если ПустоеЗначение(НематериальныйАктив) = 1 Тогда
	    Предупреждение("Не указан нематериальный актив!", 60);
		Возврат;
	КонецЕсли;
	
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;

	Состояние("Заполнение выходной таблицы...");

	Таб = СоздатьОбъект("Таблица");
    
	СтрНалогоплательщик = Константа.ОфициальноеНазваниеОрганизации;
	Если ПустаяСтрока(СтрНалогоплательщик) = 1 Тогда
	СтрНалогоплательщик = Константа.НазваниеОрганизации;	    
	КонецЕсли;
	ИНН = Константа.ИННОрганизации;
	
	// I. Общая информация об объекте нематериальных активов
	
	// Наименование объекта
	Наименование = НематериальныйАктив.Наименование + ", Инв. №" + НематериальныйАктив.Код;

	// Дата приобретения
	ДатаПриобретения = НематериальныйАктив.ДатаПриобретения;

	// Первоначальная стоимость                                                
	ДатаНачалаНачисленияАмортизации = Макс(НематериальныйАктив.ДатаПринятияКУчету, '01.01.2002');
	ПервоначальнаяСтоимость = НематериальныйАктив.ПервоначальнаяСтоимостьН;
	
	Если ПервоначальнаяСтоимость = 0 Тогда
		Сообщить("Не задана первоначальная стоимость для целей налогового учета.", "!");
	КонецЕсли;
	
	ДатаГраница = Макс(ДатаНачалаНачисленияАмортизации, НачДата);
	
	// Базовая стоимость нематериального актива
	БазоваяСтоимость = НематериальныйАктив.БазоваяСтоимость.Получить(КонДата);

	// Срок полезного использования
	СрокПолезногоИспользования = НематериальныйАктив.СрокПолезногоИспользованияН;

	// Метод начисления амортизации
	МетодНачисленияАмортизации = НематериальныйАктив.МетодНачисленияАмортизации;

	// Дата снятия объекта с учета
	ДатаСнятияОбъектаСУчета = ?(НематериальныйАктив.ДатаСписания <= КонДата,НематериальныйАктив.ДатаСписания,"");

	// Основание для снятия объекта с учета
	ОснованиеДляСнятияОбъектаСУчета = "";

	Если  ПустоеЗначение(ДатаСнятияОбъектаСУчета) = 0 Тогда
		СостояниеНМА = СоздатьОбъект("Периодический");
		СостояниеНМА.ИспользоватьОбъект("Состояние", НематериальныйАктив);
		СостояниеНМА.ВыбратьЗначения();
		Пока СостояниеНМА.ПолучитьЗначение() = 1 Цикл
			
			Если (СостояниеНМА.Значение = Перечисление.СостоянияНМА.Списан) Тогда
				ОснованиеДляСнятияОбъектаСУчета = ?(ПустоеЗначение(СостояниеНМА.ТекущийДокумент()) = 1, "", глПредставлениеДокумента(СостояниеНМА.ТекущийДокумент()));
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;   
		
	КонецЕсли;
   
	Таб.ВывестиСекцию("Шапка1");
	ОснованиеНачалаНачисленияАмортизации = "";
	
	// II. Применение понижающего коэффициента
	Если ДатаГраница <= КонДата Тогда                                           
		Если ДатаГраница = НачДата Тогда
			ДатаНачалаПримененияПонижающегоКоэффициента = "На дату начала регистра";
			
		Иначе
			ДатаНачалаПримененияПонижающегоКоэффициента = ДатаГраница;	
		КонецЕсли;
	
		// Применение специального коэффициента
		СпецКоэффициент = СоздатьОбъект("Периодический");
		СпецКоэффициент.ИспользоватьОбъект("СпециальныйКоэффициент",НематериальныйАктив);
		
		//Первое значение
		ПонижающийКоэффициент = Число(СпецКоэффициент.ЗначениеНаДату(ДатаГраница));
		ПредСпециальныйКоэффициент = ПонижающийКоэффициент; //запоминаем предыдущее значение
		Если ПонижающийКоэффициент = 1 Тогда
			ПонижающийКоэффициент = "---";
		КонецЕсли;                       
		Таб.ВывестиСекцию("Строка1");
	    
		СпецКоэффициент.ВыбратьЗначения(ДатаГраница + 1,КонДата);
		Пока СпецКоэффициент.ПолучитьЗначение() = 1 Цикл
			ПонижающийКоэффициент = СпецКоэффициент.Значение;
			Если ПредСпециальныйКоэффициент <> ПонижающийКоэффициент Тогда
				ДатаНачалаПримененияПонижающегоКоэффициента = СпецКоэффициент.ДатаЗнач;
				Таб.ВывестиСекцию("Строка1");
			КонецЕсли;
			ПредСпециальныйКоэффициент = ПонижающийКоэффициент;
		КонецЦикла;
	КонецЕсли;
    
	// III. Суммы начисленной амортизации
	
	Если ДатаГраница <= КонДата Тогда
		
		БИ = СоздатьОбъект("БухгалтерскиеИтоги");
		БИ.ИспользоватьСубконто(ВидыСубконто.НематериальныеАктивы, НематериальныйАктив);
		БИ.ВыполнитьЗапрос(НачМесяца(ДатаНачалаНачисленияАмортизации), КонДата, "Н05.04", , , , "Месяц");
		
		// Подсчет кол-ва месяцев полезного использования и суммы начисленной амортизации на начало регистра
		КоличествоМесяцевПолезногоИспользованияНаНач = 0;
		СуммаНакопленнойАмортизацииНаНач = 0;
	    БИ.ВыбратьПериоды();
		Пока БИ.ПолучитьПериод() = 1 Цикл
			Если БИ.НачДата >= НачДата Тогда
				Прервать;
			КонецЕсли;
			СуммаНачисленнойАмортизации = БИ.КО();
			Если СуммаНачисленнойАмортизации <> 0 Тогда
				СуммаНакопленнойАмортизацииНаНач = СуммаНакопленнойАмортизацииНаНач + СуммаНачисленнойАмортизации;
				КоличествоМесяцевПолезногоИспользованияНаНач = КоличествоМесяцевПолезногоИспользованияНаНач + 1;
			КонецЕсли;
		КонецЦикла;
		Таб.НоваяСтраница();
		Таб.ВывестиСекцию("Шапка2");    
	
		МесяцПолезногоИспользования = КоличествоМесяцевПолезногоИспользованияНаНач;
		СуммаНачисленнойАмортизацииНарастающимИтогом = СуммаНакопленнойАмортизацииНаНач;
		НачалоМесяца = НачМесяца(ДатаГраница);
		Пока НачалоМесяца < КонДата Цикл
			СуммаНачисленнойАмортизации = 0;
			МесяцГодНачисленияАмортизации = Формат(НачалоМесяца,"Д ММММГГГГ");
			Если БИ.ПолучитьПериод(, НачалоМесяца) = 1 Тогда
				СуммаНачисленнойАмортизации = БИ.КО(); 
				СуммаНачисленнойАмортизацииНарастающимИтогом = СуммаНачисленнойАмортизацииНарастающимИтогом + СуммаНачисленнойАмортизации;
				Если СуммаНачисленнойАмортизации <> 0 Тогда
					МесяцПолезногоИспользования = МесяцПолезногоИспользования + 1;
					Таб.ВывестиСекцию("Строка2");
				КонецЕсли;
			КонецЕсли;                           
			
			НачалоМесяца = ДобавитьМесяц(НачалоМесяца, 1);
		КонецЦикла
	КонецЕсли;
	
	ОтветственныйЗаСоставление = ФИО(Константа.ОтветственныйЗаСоставлениеРегистровНУ);
	
    Таб.ВывестиСекцию("Подвал");

	Таб.ТолькоПросмотр(1);
	Таб.Опции(0, 0, 0, 0, "ПечатьРегистрИнформацииОбОбъектеНематериальногоАктива", "ОкноРегистрИнформацииОбОбъектеНематериальногоАктива");
	Таб.Показать("Регистр информации об объекте нематериальных активов");

КонецПроцедуры // Сформировать  

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
    НачДата = НачалоПериодаБИ();
	КонДата = КонецПериодаБИ();
	НематериальныйАктив.ВыборГруппы(0);
КонецПроцедуры // ПриОткрытии() 

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриНачалеВыбораЗначения(Элемент, Флаг)
	Если Элемент = "НематериальныйАктив" Тогда                     
	    ОткрытьФорму("Справочник.НематериальныеАктивы.ДляВыбора", "НМА");
		Флаг = 0;
	КонецЕсли;
КонецПроцедуры