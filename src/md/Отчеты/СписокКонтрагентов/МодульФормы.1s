//*******************************************
Перем Конт;
Перем ВсегоСтрок;
// __________________________________________________________________________
//
Процедура Сформировать()
	
	Дог = СоздатьОбъект("Справочник.Договоры");
	РС = СоздатьОбъект("Справочник.РасчетныеСчета");	
   	Таб = СоздатьОбъект("Таблица");
	Таб.ВывестиСекцию("Шапка");
	ВсегоСтрок = 5;
	НачДата = РабочаяДата();

	ТЗ = "
		|Период С НачДата По НачДата;
		|Контрагент = Справочник.Контрагенты.ТекущийЭлемент;";
	Если ПустоеЗначение(ФильтроватьПо) = 0 Тогда
		ТЗ = ТЗ + "
		|Рек = Справочник.Контрагенты."+Фильтр.ПолучитьЗначение(Фильтр.ТекущаяСтрока())+";";
	КонецЕсли;
	Если Сортировать = 1 Тогда
	    ТЗ = ТЗ + "
		|Группировка Контрагент Упорядочить по Контрагент.Код;";
	Иначе
		ТЗ = ТЗ + "
		|Группировка Контрагент Упорядочить по Контрагент.Наименование;";
	КонецЕсли;
	Если ВыбРодитель.Выбран() = 1 Тогда
	    ТЗ = ТЗ + "
		|Условие (Контрагент в ВыбРодитель);";		
	КонецЕсли;
	Если ПустоеЗначение(ФильтроватьПо) = 0 Тогда
		Если ТипЗначенияСтр(ФильтроватьПо) = "Строка" Тогда
			ТЗ = ТЗ + "
			|Условие (Найти(НРег(Рек),НРег(ФильтроватьПо)) > 0);";
		Иначе
			ТЗ = ТЗ + "
			|Условие (Рек = ФильтроватьПо);";
		КонецЕсли;
	КонецЕсли;
	
	Запрос = СоздатьОбъект("Запрос");
	Запрос.Выполнить(ТЗ);
		
    Пока Запрос.Группировка(1) = 1 Цикл
		Конт = Запрос.Контрагент;
	    Если Конт.ПометкаУдаления() = 0 Тогда
	        Если Конт.ЭтоГруппа() = 1 Тогда
				Таб.ВывестиСекцию("Группа");
				ВсегоСтрок = ВсегоСтрок + 1;
			Иначе
				Паспорт = СокрЛП(Конт.ДокументСерия)+"-"+СокрЛП(Конт.ДокументНомер);
				Если Паспорт = "-" Тогда
				    Паспорт = "";
				Иначе
					Паспорт = ", паспорт № " + Паспорт;
				КонецЕсли;
				Паспорт = СокрЛП(Конт.ПолнНаименование) + Паспорт;
				РС.ИспользоватьВладельца(Конт);
				Дог.ИспользоватьВладельца(Конт);
				Счет = ""; Банк = ""; Договор = ""; ДатаОплаты = "";
				РС_Основной = глРасчетныйСчетКонтрагента(Конт);
				Если РС_Основной <> "" Тогда
					Счет = "р/с № "+РС_Основной.Номер;
					Если СокрЛП(РС_Основной.БанкОрганизации) <> "" Тогда
						Счет = Счет + " в " + СокрЛП(РС_Основной.БанкОрганизации);    
					КонецЕсли;                      
					Если СокрЛП(РС_Основной.БанкОрганизации.Местонахождение) <> "" Тогда
						Счет = Счет + ", г."+СокрЛП(РС_Основной.БанкОрганизации.Местонахождение);
					КонецЕсли;
				КонецЕсли;
				Если Дог.НайтиЭлемент(Конт.ОсновнойДоговор) = 1 Тогда
					Договор = Дог.ТекущийЭлемент().Наименование;
					Если Дог.ДатаВозникновенияОбязательства <> ПолучитьПустоеЗначение("Дата") Тогда
					    Договор = Договор + "   дата возникновения обязательства :  " + Строка(Дог.ДатаВозникновенияОбязательства);
					КонецЕсли;                        
				КонецЕсли;
				Таб.ВывестиСекцию("Контрагент");
				ВсегоСтрок = ВсегоСтрок + 2;
				Если ПоказатьДоговора = 1 Тогда
					НачВсегоСтрок = ВсегоСтрок;
					Если Сортировать = 1 Тогда
						Дог.ПорядокКодов();
					Иначе
						Дог.ПорядокНаименований();
					КонецЕсли;
					Дог.ВыбратьЭлементы();
					Пока Дог.ПолучитьЭлемент() = 1 Цикл       
						Если ((Дог.ПометкаУдаления() = 0) и (Дог.ТекущийЭлемент() <> Конт.ОсновнойДоговор)) Тогда
							Договор = Дог.ТекущийЭлемент().Наименование;
							Если Дог.ДатаВозникновенияОбязательства <> ПолучитьПустоеЗначение("Дата") Тогда
							    Договор = Договор + "   дата возникновения обязательства :  " + Строка(Дог.ДатаВозникновенияОбязательства);
							КонецЕсли;
							Таб.ВывестиСекцию("Договор");
							ВсегоСтрок = ВсегоСтрок + 1;
						КонецЕсли;
					КонецЦикла;
					Если НачВсегоСтрок < ВсегоСтрок Тогда
					    Таб.ВывестиСекцию("Разделитель");
						ВсегоСтрок = ВсегоСтрок + 1;
					КонецЕсли;
				КонецЕсли;
				
				Если ПоказатьРС = 1 Тогда 
					НачВсегоСтрок = ВсегоСтрок;
					Если Сортировать = 1 Тогда
						РС.ПорядокКодов();
					Иначе
						РС.ПорядокНаименований();
					КонецЕсли;
					РС.ВыбратьЭлементы();
					Счет = "";
					Пока РС.ПолучитьЭлемент() = 1  Цикл
						Если ((РС.ПометкаУдаления() = 0) и (РС.ТекущийЭлемент() <> РС_Основной)) Тогда
							Счет = "р/с № "+РС.Номер;
							Если СокрЛП(РС.БанкОрганизации) <> "" Тогда
								Счет = Счет + " в " + СокрЛП(РС.БанкОрганизации);    
							КонецЕсли;                      
							Если СокрЛП(РС.БанкОрганизации.Местонахождение) <> "" Тогда
								Счет = Счет + ", г."+СокрЛП(РС.БанкОрганизации.Местонахождение);
							КонецЕсли;
							Таб.ВывестиСекцию("РС");
							ВсегоСтрок = ВсегоСтрок + 1;
						КонецЕсли;
					КонецЦикла;
					Если НачВсегоСтрок < ВсегоСтрок Тогда
					    Таб.ВывестиСекцию("Разделитель");
						ВсегоСтрок = ВсегоСтрок + 1;
					КонецЕсли;
				КонецЕсли;
	        КонецЕсли;
	    КонецЕсли;	
	КонецЦикла;
	
	Таб.ТолькоПросмотр(1);
 	Таб.Опции(0,0,5,3);
 	Таб.Показать("Список контрагентов");
    Таб.ПараметрыСтраницы(2);

КонецПроцедуры
// __________________________________________________________________________
//
Процедура НовыйТипФильтра()
	Если Фильтр.ТекущаяСтрока() > 2 Тогда
		Атрибут = Метаданные.Справочник("Контрагенты").Реквизит(Фильтр.ПолучитьЗначение(Фильтр.ТекущаяСтрока()));
	    Если ((Атрибут.Тип<>"Число") и (Атрибут.Тип<>"Дата") и (Атрибут.Тип<>"Строка")) Тогда
		    НовыйТип = Строка(Атрибут.Тип) + "." + Строка(Атрибут.Вид);
		Иначе
			НовыйТип = Строка(Атрибут.Тип);
		КонецЕсли;
	Иначе 
		НовыйТип = "Строка";
	КонецЕсли;
	Форма.ФильтроватьПо.НазначитьТип(НовыйТип);
	ФильтроватьПо = ПолучитьПустоеЗначение(НовыйТип);
КонецПроцедуры
// __________________________________________________________________________
//
Процедура ПриОткрытии()
	Если Фильтр.ТекущаяСтрока() < 1 Тогда
		Фильтр.УдалитьВсе();
		Фильтр.ДобавитьЗначение("Код");
		Фильтр.ДобавитьЗначение("Наименование");
		Для Инд = 1 По Метаданные.Справочник("Контрагенты").Реквизит() Цикл
			КонтрМД = Метаданные.Справочник("Контрагенты").Реквизит(Инд);
			Если СокрЛП(КонтрМД.Идентификатор) <> "ПолнНаименование" Тогда
			    Фильтр.ДобавитьЗначение(СокрЛП(КонтрМД.Идентификатор),КонтрМД.Представление());
			КонецЕсли;
		КонецЦикла;
		Фильтр.ТекущаяСтрока(Фильтр.НайтиЗначение("Наименование"));
		НовыйТипФильтра();
	КонецЕсли;
	Если глФлагРасшифровки = 0 Тогда
		Если ПустоеЗначение(Сортировать) = 1 Тогда
			Сортировать = 2;    
		КонецЕсли;
	Иначе
		П = глРасшифровка;
		Фильтр.ТекущаяСтрока(П.Получить("ТекФильтр"));
		НовыйТипФильтра();
		ФильтроватьПо = П.Получить("ФильтроватьПо");
		ПоказатьРС = П.Получить("ПоказатьРС");
		ПоказатьДоговора = П.Получить("ПоказатьДоговора");
		ВыбРодитель = П.Получить("ВыбРодитель");
		Сортировать = П.Получить("Сортировать");
	КонецЕсли;
КонецПроцедуры
// __________________________________________________________________________
//
Функция СтрокаФильтра()
	Перем Синоним;
	Значение = Фильтр.ПолучитьЗначение(Фильтр.ТекущаяСтрока(), Синоним);
	Синоним = ?(ПустоеЗначение(Синоним) = 1, Значение, Синоним);
	Рез = ?(ПустоеЗначение(ФильтроватьПо)=0,("Установлен фильтр """+Синоним+""" по значению """+Строка(ФильтроватьПо))+"""","");
	Возврат Рез;
КонецФункции
// __________________________________________________________________________
//