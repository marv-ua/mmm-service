Процедура Сформировать()
	Перем Запрос, ТекстЗапроса;
	
	КолСтрок = 16;
	
	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("Таблица");
	
	НечетнаяСтраница = СоздатьОбъект("ТаблицаЗначений");
	НечетнаяСтраница.НоваяКолонка("ОстСтоимость");
	НечетнаяСтраница.НоваяКолонка("ВыбытиеДок");
	НечетнаяСтраница.НоваяКолонка("ВыбытиеМОЛ");
	НечетнаяСтраница.НоваяКолонка("ПодразделениеДвиж");
	НечетнаяСтраница.НоваяКолонка("ВыбытиеПричина");
	
	Ит = СоздатьОбъект("БухгалтерскиеИтоги");
	ИтАм = СоздатьОбъект("БухгалтерскиеИтоги");
		
	//Создание объекта типа Запрос
	Запрос = СоздатьОбъект("Запрос");
	ТекстЗапроса = 
	"//{{ЗАПРОС(Сформировать)
	|Период с НачДата по КонДата;
	|ОС = Документ.ВводВЭксплуатациюОС.ОсновноеСредство, Документ.ПередачаОС.ОсновноеСредство, Документ.СписаниеОС.ОсновноеСредство, Документ.ПеремещениеОС.ОсновноеСредство;
	|Док = Документ.ВводВЭксплуатациюОС.ТекущийДокумент, Документ.ПередачаОС.ТекущийДокумент, Документ.СписаниеОС.ТекущийДокумент, Документ.ПеремещениеОС.ТекущийДокумент;
	|Группировка Док;
	|Группировка ОС без групп;
	|Условие(ПустоеЗначение(ОС.МатериалДляОтраженияВСоставеМПЗ) = 1);";
	
	ПечМОЛ = "";
	ПечДолжностьМОЛ = "";
	Если МОЛ.Выбран()=1 Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|Условие(ОС.МОЛ.Получить(Док.ДатаДок) = МОЛ);
		|";              
		ПечМОЛ = ФИО(МОЛ);
		ПечДолжностьМОЛ = МОЛ.Должность;
	КонецЕсли; 
	
	ВыбПодразделение = "";
	Если Подразделение.Выбран()=1 Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|Условие((ОС.Подразделение.Получить(Док.ДатаДок) = Подразделение)или((Док.Вид() <> ""ВводВЭксплуатациюОС"") и (ОС.Подразделение.Получить(Док.ДатаДок-1) = Подразделение)));
		|";              
		ВыбПодразделение = Подразделение;
	КонецЕсли;
	// Если ошибка в запросе, то выход из процедуры
	Если Запрос.Выполнить(ТекстЗапроса) = 0 Тогда
		Возврат;
	КонецЕсли;
    Организация = Константа.НазваниеОрганизации;
	Таб.ВывестиСекцию("Обложка");
	Таб.НоваяСтраница();
	
	НечетнаяСтраница.УдалитьСтроки();
	НомерСтраницы = 2;
	Номер = 1;
	Таб.ВывестиСекцию("Шапка_2");
	Пока Запрос.Группировка(1) = 1 Цикл 
		Если Запрос.Док.Вид() = "ВводВЭксплуатациюОС" Тогда
		Иначе
			Если Запрос.Док.Вид() = "ПередачаОС" Тогда
				ДокЗапроса = ?(Запрос.Док.ДокументПодготовкаКПередаче.Выбран() = 1, Запрос.Док.ДокументПодготовкаКПередаче, Запрос.Док);
			Иначе
				ДокЗапроса = Запрос.Док;
			КонецЕсли;
			
			Ит.ИспользоватьСубконто(ВидыСубконто.ОсновныеСредства);
			Ит.ВыполнитьЗапрос(ДокЗапроса,,"01.1, 03.1, 03.2, 03.3, 03.4, 02.1, 02.2, 001, 010, 011",,,3);
			ИТ.ВыбратьСубконто(,,,,1);
			
			ИтАм.ИспользоватьСубконто(ВидыСубконто.ОсновныеСредства);
			ИтАм.ВыполнитьЗапрос(ДокЗапроса,ДокЗапроса,"02.1, 02.2, 010",,,1);
			ИТАм.ВыбратьСубконто(,,,,1);
        КонецЕсли;
		Пока Запрос.Группировка(2) = 1 Цикл
			Если 2*Номер/(НомерСтраницы) > КолСтрок Тогда
				Таб.НоваяСтраница(); 
				НомерСтраницы = НомерСтраницы +1;
				Таб.ВывестиСекцию("Шапка_3");
				НечетнаяСтраница.ВыбратьСтроки();
				Пока НечетнаяСтраница.ПолучитьСтроку()=1 Цикл
					ОстСтоимость = НечетнаяСтраница.ОстСтоимость;
					ДатаНомерДокДвиж = НечетнаяСтраница.ВыбытиеДок;
					МОЛДвиж = НечетнаяСтраница.ВыбытиеМОЛ;
					Причина = НечетнаяСтраница.ВыбытиеПричина;
					Таб.ВывестиСекцию("Строка_3");
				КонецЦикла;
				НечетнаяСтраница.УдалитьСтроки();
				Таб.НоваяСтраница(); 
				НомерСтраницы = НомерСтраницы +1;
				Таб.ВывестиСекцию("Шапка_2");
			КонецЕсли;
			НечетнаяСтраница.НоваяСтрока();
            НаимОС = Запрос.ОС.Наименование;
			ИнвНомер = Запрос.ОС.Код;
			НачСтоимость = Запрос.ОС.ПервоначальнаяСтоимость.Получить(Запрос.Док.ДатаДок);
			НачСтоимость = Формат(НачСтоимость,"Ч015.2");
			СрокПолезнИсп = Запрос.ОС.СрокПолезногоИспользования.Получить(Запрос.Док.ДатаДок);
			Если Запрос.Док.Вид() = "ВводВЭксплуатациюОС" Тогда
				ДокПоступления = Запрос.Док.ПредставлениеВида()+" №"+Запрос.Док.НомерДок+" от "+Запрос.Док.ДатаДок;
				ДатаВвода = Запрос.ОС.ДатаВводаВЭксплуатацию;
				ПоступлениеМОЛ = ФИО(Запрос.ОС.МОЛ.Получить(Запрос.Док.ДатаДок));
				ПодразделениеПост = Запрос.ОС.Подразделение.Получить(Запрос.Док.ДатаДок);
				НачАмортизация = 0;
				НечетнаяСтраница.ОстСтоимость = ?(Запрос.Док.СписатьНаЗатраты = 1, 0, НачСтоимость);
				НечетнаяСтраница.ВыбытиеДок = "";
				НечетнаяСтраница.ВыбытиеМОЛ = "";
				НечетнаяСтраница.ПодразделениеДвиж = "";
				НечетнаяСтраница.ВыбытиеПричина = "";
			Иначе
				ДокПоступления = "";
				ДатаВвода = "";
				ПоступлениеМОЛ = "";
				ПодразделениеПост = "";
				НечетнаяСтраница.ВыбытиеДок = Запрос.Док.ПредставлениеВида()+" №"+Запрос.Док.НомерДок+" от "+Запрос.Док.ДатаДок;
				Если Запрос.Док.Вид() = "ПеремещениеОС" Тогда
					Если (Подразделение.Выбран()=1)и(Запрос.ОС.Подразделение.Получить(Запрос.Док.ДатаДок)=Подразделение) Тогда
						ДокПоступления = Запрос.Док.ПредставлениеВида()+" №"+Запрос.Док.НомерДок+" от "+Запрос.Док.ДатаДок;
						ДатаВвода = Запрос.ОС.ДатаВводаВЭксплуатацию;
						ПоступлениеМОЛ = ФИО(Запрос.ОС.МОЛ.Получить(Запрос.Док.ДатаДок));
						ПодразделениеПост = Запрос.ОС.Подразделение.Получить(Запрос.Док.ДатаДок);
						НечетнаяСтраница.ВыбытиеПричина = "Перемещение";
						НечетнаяСтраница.ПодразделениеДвиж = Запрос.ОС.Подразделение.Получить(Запрос.Док.ДатаДок-1);
						НечетнаяСтраница.ВыбытиеМОЛ = ФИО(Запрос.ОС.МОЛ.Получить(Запрос.Док.ДатаДок-1));
					Иначе
						НечетнаяСтраница.ВыбытиеПричина = "Перемещение";
						НечетнаяСтраница.ПодразделениеДвиж = Запрос.ОС.Подразделение.Получить(Запрос.Док.ДатаДок);
						НечетнаяСтраница.ВыбытиеМОЛ = ФИО(Запрос.ОС.МОЛ.Получить(Запрос.Док.ДатаДок));
					КонецЕсли;
				Иначе
					НечетнаяСтраница.ВыбытиеПричина = ?(Запрос.Док.Вид() = "СписаниеОС", "Списание", Запрос.Док.ПричинаВыбытия);
					НечетнаяСтраница.ПодразделениеДвиж = Запрос.ОС.Подразделение.Получить(Запрос.Док.ДатаДок);
					НечетнаяСтраница.ВыбытиеМОЛ = ФИО(Запрос.ОС.МОЛ.Получить(Запрос.Док.ДатаДок));
				КонецЕсли;
								
				ЕстьИтогиИТ = Ит.ПолучитьСубконто(,,Запрос.ОС);
				ЕстьИтогиИТАм = ИтАм.ПолучитьСубконто(,,Запрос.ОС);
				
				Если (ЕстьИтогиИТ = 1) или (ЕстьИтогиИТАм = 1) Тогда
					НачАмортизация = Формат((?(ЕстьИтогиИТ = 1, ИТ.СНКРС("С"), 0) + ?(ЕстьИтогиИТАм = 1, ИТАм.КО("С"),0)), "Ч015.2");
					НечетнаяСтраница.ОстСтоимость = Формат((?(ЕстьИтогиИТ = 1, ИТ.СНДРС("С") - ИТ.СНКРС("С"), 0) - ?(ЕстьИтогиИТАм = 1, ИТАм.КО("С"),0)),"Ч015.2");
				Иначе 
					НачАмортизация = "";
					НечетнаяСтраница.ОстСтоимость = "";
				КонецЕсли;
				
			КонецЕсли;
			Таб.ВывестиСекцию("Строка_2");
			Номер = Номер+1;
		КонецЦикла;
	КонецЦикла;
	Таб.НоваяСтраница(); 
	НомерСтраницы = НомерСтраницы +1;
	Таб.ВывестиСекцию("Шапка_3");
	НечетнаяСтраница.ВыбратьСтроки();
	Пока НечетнаяСтраница.ПолучитьСтроку()=1 Цикл
		ОстСтоимость = НечетнаяСтраница.ОстСтоимость;
		ДатаНомерДокДвиж = НечетнаяСтраница.ВыбытиеДок;
		МОЛДвиж = НечетнаяСтраница.ВыбытиеМОЛ;
		ПодразделениеДвиж = НечетнаяСтраница.ПодразделениеДвиж;
		Причина = НечетнаяСтраница.ВыбытиеПричина;
		Таб.ВывестиСекцию("Строка_3");
	КонецЦикла;
				
	Таб.ТолькоПросмотр(1);
	Таб.ПараметрыСтраницы(2,,,,,,,,,,,);
	Таб.Опции(0,0,0,0,"ОпцииПечатиОС6б","ОкноОС6б");
	Таб.Показать("Инвентарная книга");
КонецПроцедуры 

Процедура ПриОткрытии()
	НачДата = НачалоПериодаБИ();
	КонДата = КонецПериодаБИ();
КонецПроцедуры

