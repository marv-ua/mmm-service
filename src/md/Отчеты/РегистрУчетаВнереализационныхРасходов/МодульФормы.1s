//******************************************************************************
// ПроверкаПериода()
//
// Возвращаемое значение:
//  1 - корректно выбран период в диалоге
//  0 - не корректно выбран период в диалоге
//
// Описание:
// Функция проверяет корректность ввода интервала дат в дилоге и рассчитаны ли итоги за заданный период
//
Функция ПроверкаПериода()
	
	Если НачДата > КонДата Тогда
		Предупреждение("Неправильно задан период формирования отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если КонДата > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		"Расчет итогов выполняется в режиме"+РазделительСтрок+
		"""Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	
	Возврат 1;
	
КонецФункции // ПроверкаПериода()

//******************************************************************************
//	Установить(Режим="") 
//
//	Параметры: Режим - строка, режим работы: "Все" - отметить все строки
//					   "Сброс" - снять отметку всех строк
//					   "" - инвертировать отметку всех строк
//
//	Описание: Устанавливает, снимает или инвертирует отметки выбора строк 
//
Процедура Установить(Список, Режим="") 
	Для Н = 1 По Список.РазмерСписка() Цикл
		Если Режим="Все" Тогда
			Список.Пометка(Н, 1);
		ИначеЕсли Режим="Сброс" Тогда
			Список.Пометка(Н, 0); 
		Иначе
			Список.Пометка(Н, ?(Список.Пометка(Н) = 1, 0, 1)); 
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры  //  Установить()

//******************************************************************************
// ПолучитьСписокОбъектовФильтра()
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Список значений - список, объектов, которые должны попасть в регистр.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается функция.
//
Функция ПолучитьСписокОбъектовФильтра(ПолныйСписокОбъектов)
	
	СписокОтбираемыхОбъектов = СоздатьОбъект("СписокЗначений");
	РазмерСписка = ПолныйСписокОбъектов.РазмерСписка();
	Для СчетчикЦикла = 1 По РазмерСписка Цикл
		Если ПолныйСписокОбъектов.Пометка(СчетчикЦикла) = 1 Тогда
		    СписокОтбираемыхОбъектов.ДобавитьЗначение(ПолныйСписокОбъектов.ПолучитьЗначение(СчетчикЦикла));
		КонецЕсли;	
	КонецЦикла;
	
	Возврат СписокОтбираемыхОбъектов;
	
КонецФункции // ПолучитьСписокОбъектовФильтра()

//******************************************************************************
// СостояниеОтбора()
//
// Параметры:
//  Список - список значенний - список который необходимо проанализировать.
//
// Возвращаемое значение:
//  Строка - отражает, помечены ли в списке все элементы, помечены некоторые или вообще все пометки сняты.
//
// Вызывается из формул элементов диалога:
//  Нет.
//
// Описание:
//  Здесь описывается функция.
//
Функция СостояниеОтбора(Список)
	
	СписокОтбираемыхОбъектов = ПолучитьСписокОбъектовФильтра(Список);
	Если СписокОтбираемыхОбъектов.РазмерСписка() = 0 Тогда
		ТипФильтра = "Не выбраны";
		
	ИначеЕсли СписокОтбираемыхОбъектов.РазмерСписка() = Список.РазмерСписка() Тогда
		ТипФильтра = "По всем";
		
	Иначе
		ТипФильтра = "Выборочно";
	КонецЕсли;                 
	
	Возврат ТипФильтра;
	
КонецФункции // СостояниеОтбора()

//******************************************************************************
Процедура Сформировать()                                               
	
	Перем СтатьяРасхода;
	
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Таб = СоздатьОбъект("Таблица");
	БухИт = СоздатьОбъект("БухгалтерскиеИтоги");
	
	// подготовка реквизитов шапки
	СтрНалогоплательщик = СокрЛП(Константа.ОфициальноеНазваниеОрганизации);
	Если ПустаяСтрока(СтрНалогоплательщик) = 1 Тогда
		СтрНалогоплательщик = СокрЛП(Константа.НазваниеОрганизации);	    
	КонецЕсли;
	ИНН = Константа.ИННОрганизации;                                              
	Таб.ВывестиСекцию("Шапка");
	                                  
	// Сформруем список только выбранных видов расхода.
	СписокФильтрСтатейРасхода = ПолучитьСписокОбъектовФильтра(СписокВидовРасходов);
	Если СписокФильтрСтатейРасхода.РазмерСписка() = 0 Тогда                   
		Предупреждение("Регистр не может быть сформирован,
						|т.к. на закладке ""Виды расхода"" не установлен ни один вид расхода,
						|которому соответствует включаемая в регистр операция.");
		Возврат;
	КонецЕсли;
	
	БухИт.ИспользоватьСубконто(ВидыСубконто.ВнереализационныеРасходы, СписокФильтрСтатейРасхода, 2);	
	БухИт.ВыполнитьЗапрос(НачДата, КонДата, "Н09",,,, "Проводка",);
	БухИт.ВыбратьПериоды(, 1);
	Пока БухИт.ПолучитьПериод() = 1 Цикл
		Опер = БухИт.Операция;
		Док = БухИт.Операция.Документ;
		СтатьяРасхода = Опер.Дебет.Субконто(ВидыСубконто.ВнереализационныеРасходы);
		НаименованиеОперации = Опер.СодержаниеПроводки;                               
		ДатаОперации = БухИт.НачДата;
		Сумма        = БухИт.ДО();
		Таб.ВывестиСекцию("Строка");	
	КонецЦикла;
	
    ОбщаяСумма = БухИт.ДО();
	
	ОтветственныйЗаСоставление = ФИО(Константа.ОтветственныйЗаСоставлениеРегистровНУ);
	
	Таб.ВывестиСекцию("Подвал");    
	
	Таб.ТолькоПросмотр(1);
	Таб.Опции(0, 0, 0, 0, "ПечатьРегистрУчетаВнереализационныхРасходовТекущегоПериода", "ОкноРегистрУчетаВнереализационныхРасходовТекущегоПериода");
	Таб.Показать("Регистр учета внереализационных расходов текущего периода");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ
//

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии(ФлагЧтенияНастройки) //предопределенная
	
	Форма.ИспользоватьЗакладки(1);
	Форма.ИспользоватьСлой("Параметры, Основной", 2);
	Форма.Закладки.ДобавитьЗначение("Параметры", "Параметры");
	Форма.Закладки.ДобавитьЗначение("Виды расхода");
	
	Если ФлагЧтенияНастройки = 1 Тогда
		
		Если СписокВидовРасходов.РазмерСписка() <> 0 Тогда
			
			// отразим состояние выбранных условий формирования регистра в диалоге на первой закладке
			Форма.ВидыРасходаФильтр.Заголовок(СостояниеОтбора(СписокВидовРасходов));
			Возврат;    
		КонецЕсли;
	КонецЕсли;
	
	НачДата = НачГода(РабочаяДата());
	КонДата = КонКвартала(РабочаяДата());
	
	// Заполним список видов расхода.
	Всего = Перечисление.ВнереализационныеРасходы.КоличествоЗначений();
	Для СчетчикЦикла = 1 По Всего Цикл
		СписокВидовРасходов.ДобавитьЗначение(Перечисление.ВнереализационныеРасходы.ЗначениеПоНомеру(СчетчикЦикла));
	КонецЦикла;
	Установить(СписокВидовРасходов, "Все"); // установить пометки у всех элементов списка
	Форма.ВидыРасходаФильтр.Заголовок(СостояниеОтбора(СписокВидовРасходов));
	
КонецПроцедуры //ПриОткрытии()                                                    

//******************************************************************************
Процедура ПриВыбореЗакладки(Номер, Значение)                                    

	Если Значение = "Параметры" Тогда
		
		// при переходе на первую закладку отразим на ней состояние выбранных условий формирования регистра
		Форма.ИспользоватьСлой("Параметры, Основной", 2);
		Форма.ВидыРасходаФильтр.Заголовок(СостояниеОтбора(СписокВидовРасходов));

	ИначеЕсли Значение = "Виды расхода" Тогда
		Форма.ИспользоватьСлой("ВидыРасхода, Основной", 2);
	КонецЕсли;
	
КонецПроцедуры