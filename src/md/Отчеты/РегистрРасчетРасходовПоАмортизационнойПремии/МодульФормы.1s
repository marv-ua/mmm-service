//******************************************************************************
// ПроверкаПериода()
//
// Возвращаемое значение:
//  1 - корректно выбран период в диалоге
//  0 - не корректно выбран период в диалоге
//
// Описание:
// Функция проверяет корректность ввода интервала дат в дилоге и рассчитаны ли итоги за заданный период
//
Функция ПроверкаПериода()
	
	Если НачДата > КонДата Тогда
		Предупреждение("Неправильно задан период формирования отчета!"+РазделительСтрок+
		               "Дата начала больше даты окончания периода.");
		Возврат 0;
	КонецЕсли;
	Если КонДата > КонецРассчитанногоПериодаБИ() Тогда
		Предупреждение("За выбранный период итоги не рассчитаны!"+РазделительСтрок+
		"Расчет итогов выполняется в режиме"+РазделительСтрок+
		"""Операции - Управление бухгалтерскими итогами"".");
		Возврат 0;
	КонецЕсли;
	
	Возврат 1;
	
КонецФункции // ПроверкаПериода()

//*****************************************************************************
Процедура КорректировкаПериода()
	НачДата = НачМесяца(НачДата);
	КонДата = КонМесяца(КонДата);
КонецПроцедуры //ВводПериода

//*****************************************************************************
Процедура ВводПериода()
	ВвестиПериод(НачДата,КонДата);
	КорректировкаПериода();
КонецПроцедуры //ВводПериода

//*****************************************************************************
Процедура СформироватьСтрокиПоГруппировкам(ТабОС,Таб,ТекОбъект,ИтогСуммаКапитальныхВложений,ИтогСуммаАммортизационнойПремии)
	Если ТабОС.КоличествоСтрок() = 0 Тогда
	    Возврат;
	КонецЕсли;
	Если (ТекОбъект <> ТабОС.Объект) и (ГруппироватьПоОбъектам = 1) Тогда
		Если (ТабОС.НомерСтроки <> 1) Тогда
			ИтогСуммаАммортизационнойПремииПоГруппировке = ИтогСуммаАммортизационнойПремии;
			ИтогСуммаАммортизационнойПремии = 0;
			ИтогСуммаКапитальныхВложенийПоГруппировке = ИтогСуммаКапитальныхВложений;
			ИтогСуммаКапитальныхВложений = 0;
			Таб.ВывестиСекцию("ИтогПоГруппировке|Общая1");
			Если ГруппироватьПоОбъектам = 0 Тогда
				Таб.ПрисоединитьСекцию("ИтогПоГруппировке|Объект");
			КонецЕсли;
			Таб.ПрисоединитьСекцию("ИтогПоГруппировке|Общая2");
		КонецЕсли;
		Если ТабОС.НомерСтроки > 0 Тогда
			ЗначениеГруппировки = "Наименование объекта: " + ТабОС.Объект;
			Таб.ВывестиСекцию("Группировка|Общая1");
			Если ГруппироватьПоОбъектам = 0 Тогда
				Таб.ПрисоединитьСекцию("Группировка|Объект");
			КонецЕсли;
			Таб.ПрисоединитьСекцию("Группировка|Общая2");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры //СформироватьСтрокиПоГруппировкам

//******************************************************************************
//
Процедура Сформировать()
	
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;

	Состояние("Заполнение выходной таблицы...");
	
	Таб = СоздатьОбъект("Таблица");
	СтрНалогоплательщик = Константа.ОфициальноеНазваниеОрганизации;
	Если ПустаяСтрока(СтрНалогоплательщик) = 1 Тогда
		СтрНалогоплательщик = Константа.НазваниеОрганизации;	    
	КонецЕсли;
	ИНН = Константа.ИННОрганизации; 
	НазваниеАмортизационнойГруппы = ?(ПустоеЗначение(АмортизационнаяГруппа) = 1, "по всем", АмортизационнаяГруппа);
	
	Таб.ВывестиСекцию("Шапка1");
	
	Таб.ВывестиСекцию("Шапка2|Общая1");
	НК = 1;
	Если ГруппироватьПоОбъектам = 0 Тогда
		НК = 2;
		Таб.ПрисоединитьСекцию("Шапка2|Объект");
	КонецЕсли;
	Таб.ПрисоединитьСекцию("Шапка2|Общая2");
	
	//Таблица документов
	ТабДокументов = СоздатьОбъект("ТаблицаЗначений");
	ТабДокументов.НоваяКолонка("Документ");	

	//таблица ОС и документов, при проведении которых оражены амортизационная премия и объем кап. вложений
	ТабОС = СоздатьОбъект("ТаблицаЗначений");
	ТабОС.НоваяКолонка("Объект","Справочник.ОсновныеСредства");
	ТабОС.НоваяКолонка("ИнвентарныйНомер");
	ТабОС.НоваяКолонка("ДатаОперации","Дата");
	ТабОС.НоваяКолонка("ДатаПризнания","Дата");
	ТабОС.НоваяКолонка("ДокументОснование");	
	ТабОС.НоваяКолонка("КонтрольнаяКолонка");	
	ТабОС.НоваяКолонка("СуммаКапитальныхВложений","Число");
	ТабОС.НоваяКолонка("СуммаАммортизационнойПремии","Число");
	
	БИН05КВ = СоздатьОбъект("БухгалтерскиеИтоги");
	БИН05КВ.Опции(1,1);
	БИН05КВ.ИспользоватьСубконто(ВидыСубконто.ОсновныеСредства,,1,0); 
	
	БИН0501 = СоздатьОбъект("БухгалтерскиеИтоги");
	БИН0501.Опции(1,1);
	БИН0501.ИспользоватьСубконто(ВидыСубконто.ОсновныеСредства,,1,0);
	БИН0501.ИспользоватьКорСубконто(ВидыСубконто.ГруппыВидыРасходов,,1);
	
	
	БИН0501.ВыполнитьЗапрос(НачДата, КонДата, "Н05.01,Н05.МЦ", "Н07.04.1,Н07.04.2",,2, "Проводка", "С");
	
    //Определим дату начала выборки документов, оражающих возникновение амортизационной премии
	НачПериодаОтчета = '01.01.2006';
	БИН0501.ВыбратьСубконто(ВидыСубконто.ОсновныеСредства);
	Пока БИН0501.ПолучитьСубконто(ВидыСубконто.ОсновныеСредства) = 1 Цикл
		ДатаВводаВЭксплуатацию = БИН0501.Субконто(ВидыСубконто.ОсновныеСредства).ДатаВводаВЭксплуатацию;
		Если ПустоеЗначение(ДатаВводаВЭксплуатацию) = 0 Тогда
			НачПериодаОтчета = Мин(НачПериодаОтчета, ДатаВводаВЭксплуатацию);
		КонецЕсли;
	КонецЦикла;
	
    БИН05КВ.ВыполнитьЗапрос(НачПериодаОтчета, КонДата,"Н05.КВ",,,2,"Проводка","С");
	
	БИН0501.ВыбратьСубконто(ВидыСубконто.ОсновныеСредства);
	Пока БИН0501.ПолучитьСубконто(ВидыСубконто.ОсновныеСредства) = 1 Цикл
		ОсновноеСредство = БИН0501.Субконто(ВидыСубконто.ОсновныеСредства);
		
		// фильтр по указанной в диалоге амортизационной группе (если она выбрана).
		Если (ПустоеЗначение(АмортизационнаяГруппа) = 0) и (АмортизационнаяГруппа <> ОсновноеСредство.АмортизационнаяГруппа) Тогда
			Продолжить;
		КонецЕсли;  

		БИН0501.ПолучитьКорСубконто(ВидыСубконто.ГруппыВидыРасходов, , Перечисление.ГруппыВидыРасходов.АмортизационнаяПремия);
		БИН0501.ВыбратьПериоды(1, );
		Пока БИН0501.ПолучитьПериод() = 1 Цикл 
			Если БИН0501.ВыбранаПоКт() = 1 Тогда
				Если БИН0501.КорСубконто() = Перечисление.ГруппыВидыРасходов.АмортизационнаяПремия Тогда
					АмортизационнаяПремия = БИН0501.Операция.Сумма;
					Если АмортизационнаяПремия  > 0 Тогда						
						//отберем документы, которые отразили амотризационную премию
						ТабДокументов.УдалитьСтроки();
						
						БИН05КВ.ПолучитьСубконто(ВидыСубконто.ОсновныеСредства, , ОсновноеСредство);
						БИН05КВ.ВыбратьПериоды(1, );
						Пока БИН05КВ.ПолучитьПериод() = 1 Цикл 
							ТабДокументов.НоваяСтрока();
							Если (БИН05КВ.ВыбранаПоДт() = 1) и (БИН05КВ.Операция.Документ.ДатаДок < НачМесяца(КонДата))
							и (БИН05КВ.Операция.Документ.ДатаДок < НачМесяца(БИН0501.Операция.Документ.ДатаДок)) Тогда
								ТабДокументов.Документ = БИН05КВ.Операция.Документ.ТекущийДокумент();								
							Иначе
								ТабДокументов.УдалитьСтроку();
							КонецЕсли;
						КонецЦикла;
						ТабДокументов.Свернуть("Документ",);
						ТабДокументов.Сортировать("-Документ",1); 
						
						ТабДокументов.ВыбратьСтроки();
						Пока ТабДокументов.ПолучитьСтроку() = 1 Цикл
							
							ДокументОснование = ТабДокументов.Документ;
							КонтрольнаяКолонка = ЗначениеВСтрокуВнутр(ОсновноеСредство) + ЗначениеВСтроку(ДокументОснование);
							
							СуммаКапитальныхВложенийПоДокументу = 0;
							АмортизационнаяПремияПоДокументу	= 0;
							ПроводкиДокумента = СоздатьОбъект("Операция");
							ПроводкиДокумента.НайтиОперацию(ДокументОснование);
							ПроводкиДокумента.ВыбратьПроводки();
							Пока ПроводкиДокумента.ПолучитьПроводку()=1 Цикл  
								Если (ПустоеЗначение(Строка(ПроводкиДокумента.Дебет.Счет.Код)) = 0)
								и (Найти("Н05.КВ, Н05.01, Н05.МЦ",Строка(ПроводкиДокумента.Дебет.Счет.Код)) > 0) Тогда
									Если ПроводкиДокумента.Дебет.Субконто(1) = ОсновноеСредство Тогда
										Если ПроводкиДокумента.Дебет.Счет = СчетПоКоду("Н05.КВ") Тогда
											АмортизационнаяПремияПоДокументу		=АмортизационнаяПремияПоДокументу + ПроводкиДокумента.Сумма;
										ИначеЕсли (ПроводкиДокумента.Дебет.Счет = СчетПоКоду("Н05.01"))
										или (ПроводкиДокумента.Дебет.Счет = СчетПоКоду("Н05.МЦ")) Тогда
											СуммаКапитальныхВложенийПоДокументу = СуммаКапитальныхВложенийПоДокументу + ПроводкиДокумента.Сумма;
										КонецЕсли;
									КонецЕсли;
								КонецЕсли; 
							КонецЦикла;
							
							СтрДок = 0;
							ТабОС.НоваяСтрока();
							Если ((АмортизационнаяПремия - АмортизационнаяПремияПоДокументу) >= 0) 
								и (ТабОС.НайтиЗначение(КонтрольнаяКолонка, СтрДок, "КонтрольнаяКолонка") = 0) Тогда							
								ТабОС.ДокументОснование = ДокументОснование ;
								ТабОС.ДатаОперации = ДокументОснование.ДатаДок;				
								ТабОС.Объект = ОсновноеСредство;												
								ТабОС.СуммаКапитальныхВложений = СуммаКапитальныхВложенийПоДокументу;								                                								
								ТабОС.ДатаПризнания  = БИН0501.Операция.Документ.ДатаДок;
								ТабОС.Объект = ОсновноеСредство;
								ТабОС.ИнвентарныйНомер = ОсновноеСредство.Код;							
								ТабОС.СуммаАммортизационнойПремии = АмортизационнаяПремияПоДокументу;							
								ТабОС.КонтрольнаяКолонка = КонтрольнаяКолонка;
								АмортизационнаяПремия  = АмортизационнаяПремия - АмортизационнаяПремияПоДокументу;								   
							Иначе
								ТабОС.УдалитьСтроку();
							КонецЕсли;
						КонецЦикла;					      
					КонецЕсли;
				КонецЕсли;
			КонецЕсли; 
		КонецЦикла;
	КонецЦикла;
	
	ТабОС.Свернуть("Объект, ДатаПризнания, ДатаОперации, ДокументОснование, ИнвентарныйНомер, СуммаКапитальныхВложений, СуммаАммортизационнойПремии",); 
	ТабОС.Сортировать("Объект, +ДокументОснование");

	ИтогСуммаКапитальныхВложений = 0;
	ИтогСуммаАммортизационнойПремии = 0; 
	
	ТабОС.ВыбратьСтроки();
	ТекОбъект = "";
	Пока ТабОС.ПолучитьСтроку() = 1 Цикл
				
		СформироватьСтрокиПоГруппировкам(ТабОС,Таб,ТекОбъект,ИтогСуммаКапитальныхВложений,ИтогСуммаАммортизационнойПремии);
		
		НаименованиеОбъекта 		= ТабОС.Объект.Наименование + ", Инв. №" + ТабОС.Объект.Код;
		ДатаОперации 				= Формат(ТабОС.ДатаОперации,"Д ММММГГГГ");
		ДатаПризнания 				= Формат(ТабОС.ДатаПризнания,"Д ММММГГГГ");
        ДокументОснование	 		= ТабОС.ДокументОснование;
		СуммаКапитальныхВложений 	= ТабОС.СуммаКапитальныхВложений;		
		СуммаАммортизационнойПремии = ТабОС.СуммаАммортизационнойПремии;
		ПроцентКапитальныхВложений 	= ?(СуммаКапитальныхВложений = 0, 0,(СуммаАммортизационнойПремии/СуммаКапитальныхВложений)*100);		
	
		ИтогСуммаКапитальныхВложений 	= ИтогСуммаКапитальныхВложений + СуммаКапитальныхВложений;
		ИтогСуммаАммортизационнойПремии = ИтогСуммаАммортизационнойПремии + СуммаАммортизационнойПремии; 

		
		Таб.ВывестиСекцию("Строка|Общая1");
		Если ГруппироватьПоОбъектам = 0 Тогда
			Таб.ПрисоединитьСекцию("Строка|Объект");
		КонецЕсли;
		Таб.ПрисоединитьСекцию("Строка|Общая2");
		ТекОбъект = ТабОС.Объект;

	КонецЦикла;

	ТекОбъект = 0;
	СформироватьСтрокиПоГруппировкам(ТабОС,Таб,ТекОбъект,ИтогСуммаКапитальныхВложений,ИтогСуммаАммортизационнойПремии);
	Таб.Область(Таб.ВысотаТаблицы(),2,Таб.ВысотаТаблицы(), 8 - ГруппироватьПоОбъектам).РамкаСнизу(3);
	
	ВсегоСуммаКапитальныхВложений 	 = ТабОС.Итог("СуммаКапитальныхВложений");
	ВсегоСуммаАммортизационнойПремии = ТабОС.Итог("СуммаАммортизационнойПремии");

	Таб.ВывестиСекцию("Всего|Общая1");
	Если ГруппироватьПоОбъектам = 0 Тогда
		Таб.ПрисоединитьСекцию("Всего|Объект");
	КонецЕсли;
	Таб.ПрисоединитьСекцию("Всего|Общая2");
	
	ОтветственныйЗаСоставление = ФИО(Константа.ОтветственныйЗаСоставлениеРегистровНУ);
	
	Таб.ВывестиСекцию("Подвал");
   
	Таб.ТолькоПросмотр(1);
	Таб.ОбластьПечати(,2,,);
	Таб.Опции(0, 0, 0, 0, "ПечатьРасходовПоАмортизационнойПремии", "ОкноРасходовПоАмортизационнойПремии");
	Таб.Показать("Регистр - расчет расходов по амортизационной премии");
	
КонецПроцедуры // Сформировать


//*****************************************************************************
//
// Предопределенная процедура
//
Процедура ПриОткрытии()
	НачДата = НачалоПериодаБИ();
	КонДата = КонецПериодаБИ();
КонецПроцедуры //ПриОткрытии
