#Область ПрограммныйИнтерфейс

// Функция - Запрос прокси
// 
// Возвращаемое значение:
//  Стукрура - структура с параметрами ТекстЗапроса, Параметры
//
Функция ЗапросПрокси() Экспорт
	
	Возврат Новый Структура("ТекстЗапроса, Параметры", "", Новый Структура);
	
КонецФункции	

// Функция - Получить прокси
//
// Параметры:
//  Сервер		 - Строка	 - 
//  База		 - Строка	 - 
//  Пользователь - Строка	 - 
//  Пароль		 - Строка	 - 
// 
// Возвращаемое значение:
// 	WSПрокси
//
Функция ПолучитьПрокси(Сервер, База, Пользователь = "Robot", Пароль = "bot") Экспорт
	
	ПараметрыПрокси = ОбщегоНазначения.ПараметрыПодключенияWSПрокси();
	ПараметрыПрокси.АдресWSDL = "http://"+Сервер+"/"+База+"/ws/MMMservice.1cws?wsdl";
	ПараметрыПрокси.URIПространстваИмен = "http://www.1c.ru";
	ПараметрыПрокси.ИмяСервиса = "MMMservice";
	ПараметрыПрокси.Пароль = Пароль;
	ПараметрыПрокси.ИмяПользователя = Пользователь;
	ПараметрыПрокси.ИмяСервиса = "MMMservice";
	ПараметрыПрокси.Таймаут = Константы.ТаймаутПодключенияКВебсервису.Получить();
	ПараметрыПрокси.Местоположение = "http://"+Сервер+"/"+База+"/ws/MMMservice.1cws";
	  
	Возврат ОбщегоНазначения.СоздатьWSПрокси(ПараметрыПрокси);	
	
КонецФункции

// Функция - Сериализовать
//
// Параметры:
//  Об	 - Объект	 - 
// 
// Возвращаемое значение:
//   Строка 
//
Функция Сериализовать(Об) Экспорт
	
	Возврат СериализоватьВСтрокуJSON(Об);
	
КонецФункции

// Функция - Де сериализовать
//
// Параметры:
//  Стр	 - Строка	 - 
// 
// Возвращаемое значение:
//   Объект 
//
Функция ДеСериализовать(Стр) Экспорт
	
	Возврат ДеСериализоватьИзСтрокиJSON(Стр);
	
КонецФункции	

// Функция - Получить таблицу серверов
//
// Параметры:
//  Период			 - Дата - Период актуальности
//  ОтборОрганизация - СправочникСсыка.Организации, Массив - 
// 
// Возвращаемое значение:
//  ТаблицаЗначений - 
//
Функция ПолучитьТаблицуСерверов(Период, ОтборОрганизация = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.Текст = "ВЫБРАТЬ
	               |	Организации.Ссылка КАК Организация,
				   |	Организации.Ссылка КАК Ссылка,
	               |	РасположениеФирм.Сервер КАК Сервер,
	               |	РасположениеФирм.ИмяБазы КАК База,
	               |	РасположениеФирм.Логин КАК Пользователь,
	               |	РасположениеФирм.Пароль КАК Пароль,
	               |	ЕСТЬNULL(СвязьОрганизаций.Счет7, ЗНАЧЕНИЕ(Справочник.Счета7.ПустаяСсылка)) КАК Счет7,
	               |	ЕСТЬNULL(Актуальность.Активна, ЛОЖЬ) КАК Пометка,
	               |	Организации.Бухгалтер КАК Бухгалтер,
	               |	Организации.БазаУТП КАК БазаУТП,
	               |	Организации.ЮридическоеФизическоеЛицо КАК ЮридическоеФизическоеЛицо,
	               |	Организации.Наименование КАК Наименование,
	               |	Организации.НаименованиеПолное КАК НаименованиеПолное
	               |ИЗ
	               |	Справочник.Организации КАК Организации
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасположениеФирм.СрезПоследних(&Период, ) КАК РасположениеФирм
	               |		ПО Организации.Ссылка = РасположениеФирм.Организация
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АктуальностьОрганизаций.СрезПоследних(&Период, ) КАК Актуальность
	               |		ПО Организации.Ссылка = Актуальность.Организация
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвязьОрганизаций8ИСчетов7.СрезПоследних(&Период, ) КАК СвязьОрганизаций
	               |		ПО Организации.Ссылка = СвязьОрганизаций.Организация8
	               |ГДЕ
	               |	НЕ Организации.ЭтоГруппа
	               |	И ИСТИНА";

	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ИСТИНА", "И Организации.Ссылка В ИЕРАРХИИ(&Организация)");
		Запрос.УстановитьПараметр("Организация", ОтборОрганизация);
	КонецЕсли;

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ПрочитатьФайлНаСервере(АдресФайла, ТабДок, РасширениеФайла) Экспорт
		
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
	ПолноеИмяФайла = ПолучитьИмяВременногоФайла(".xls");
	
	Если НЕ ЗначениеЗаполнено(ДвоичныеДанные) Тогда
		Возврат Ложь;
	КонецЕсли;	
		
	ДвоичныеДанные.Записать(ПолноеИмяФайла);	
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.Очистить();
	Попытка
		ТабДок.Прочитать(ПолноеИмяФайла);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция ПодключитьсяКБазеМММСервер(Пользователь = "Robot", Пароль = "bot") Экспорт

	ПутьКБазе = мммСервер.ЗначениеКонстанты("ПутьКБазеМММ");
	Если ПустаяСтрока(ПутьКБазе) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		База = Новый COMОбъект("v77.Application");
		//База = Новый COMОбъект("V1СEnterprise.Application");
	Исключение
		#Если Клиент Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ОписаниеОшибки());
		#КонецЕсли
		Возврат Неопределено;
	КонецПопытки;
	
	СтрокаПодключения = "/D""" + СокрЛП(ПутьКБазе) + """ /N""" + СокрЛП(Пользователь) + """ /P""" + СокрЛП(Пароль) + """";
	
	// Попытка 1
	РезультатПодключения = База.Initialize(База.RMTrade, СтрокаПодключения, "NO_SPLASH_SHOW");
	Если РезультатПодключения Тогда
		Возврат База;
	КонецЕсли;
	
	// Попытка 2
	РезультатПодключения = База.Initialize(База.RMTrade, СтрокаПодключения, "NO_SPLASH_SHOW");
	Если РезультатПодключения Тогда
		Возврат База;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция УдалитьДублиСтрокТаблицыЗначений(Таблица, Колонки = "") Экспорт

	Если ПустаяСтрока(Колонки) Тогда
		
		// Просто свернем таблицу по всем колонкам
		
		Для каждого Колонка Из Таблица.Колонки Цикл
			Колонки = Колонки + ?(ПустаяСтрока(Колонки), "", ",") + Колонка.Имя;
		КонецЦикла;
		
		ТаблицаРезультат = Таблица.Скопировать();
		ТаблицаРезультат.Свернуть(Колонки);
		
	Иначе
		
		КолонкиЗапроса = "ТЗ." + СтрЗаменить(Колонки, ",", ",ТЗ.");
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
					   |	" + КолонкиЗапроса + "
					   |ПОМЕСТИТЬ ВТ_ТЗ
					   |ИЗ
					   |	&ТЗ КАК ТЗ
					   |;
					   |
					   |////////////////////////////////////////////////////////////////////////////////
					   |ВЫБРАТЬ РАЗЛИЧНЫЕ
					   |	" + СтрЗаменить(КолонкиЗапроса, "ТЗ.", "ВТ_ТЗ.") + "
					   |ИЗ
					   |	ВТ_ТЗ КАК ВТ_ТЗ
					   |";

		Запрос.УстановитьПараметр("ТЗ", Таблица);
		РезультатЗапроса = Запрос.Выполнить();
		ТаблицаЗапроса = РезультатЗапроса.Выгрузить();
		ТаблицаРезультат = Таблица.СкопироватьКолонки();
		Для каждого СтрокаТЗ Из ТаблицаЗапроса Цикл
		
			СтруктураОтбора = Новый Структура(Колонки);
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТЗ);
			
			РезультатОтбора = Таблица.НайтиСтроки(СтруктураОтбора);
			Для каждого СтрокаОтбора Из РезультатОтбора Цикл
				
				НоваяСтрока = ТаблицаРезультат.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОтбора);
				Прервать;
				
			КонецЦикла;
		
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТаблицаРезультат;

КонецФункции // УдалитьДублиСтрокТаблицыЗначений()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗначениеКонстанты(ИмяКонстанты) Экспорт
	
	Возврат Константы[ИмяКонстанты].Получить();
	
КонецФункции	

Функция СериализоватьВСтрокуJSON(Об)
	
	ЗаписьJSON = Новый ЗаписьJSON;
	параметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	ЗаписьJSON.УстановитьСтроку(параметрыЗаписиJSON);
	СериализаторXDTO.ЗаписатьJSON(ЗаписьJSON, Об, НазначениеТипаXML.Явное);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции


Функция ДеСериализоватьИзСтрокиJSON(Стр)
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(Стр);
	Данные = СериализаторXDTO.ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	
	Возврат Данные;
	
КонецФункции

#КонецОбласти

Процедура ВнешниеНаборыДанныхЗапускВыполнения(Параметры, АдресХранилища) Экспорт
	
	Результат = Отчеты.ВНД.ВнешниеНаборыДанных(Параметры.ДатаНач, Параметры.ДатаКон, Параметры.Организация);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры
